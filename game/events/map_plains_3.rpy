# random map events that happen on plains
init python:

    ##### Plains event: Lost Flock ####
    event('lost_flock', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #### Plains Event: The Hanged Man ####
    event('the_hanged_man', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)


label lost_flock:
##### Plains event: Lost Flock ####
$ released_block_rollback()
scene bg31 with fade
show rowan necklace neutral behind black

"На холмах впереди Роуэн увидел большое стадо овец, что было необычно, поскольку поблизости не было никаких поселений."
"Потерянная собственость, убитых фермеров, или результат  небрежности? Невозможно сказать наверняка."

menu:
    "Окружить их.":
        $ released_fix_rollback()
        ro "Я нашел кое-что, что может оказаться полезным. Пришлите мне несколько орков."
        "Вскоре прибыл небольшой отряд орков."
        "Поначалу задача, поставленная перед ними, вызывала у них отвращение, но как только Роуэн рассказал, что у него на уме, они принялись за нее с большим рвением."
        "Примерно через час большинство овец были согнаны и отправлены в замок через портал."
        "Вернувшись в Бладмин, гарнизон облизал губы, наблюдая, как безмозглый шерстистый поезд прибывает и направляется к скотобойне."
        cook "Сегодня вечером, подонки, мы пируем!"
        orcs "УРА!"
        "Роуэн почувствовала укол вины за этот акт овцеводства. Но он знал, что армия лучше он накормит свою армию и продаст излишки мяса, чем отара достанется бандитам."
        #gain 5 xp
        #lose 1 move point, small guilt gain, small morale gain, small treasury gain
        $ add_exp(5)
        $ avatar.mp -= 1
        $ change_base_stat('g', 2)
        $ change_morale(3)
        $ change_treasury(15)
        return

    "Попробывать их вернуть.":
        $ released_fix_rollback()
        "Это заняло у него некоторое время, но в конце концов Роуэн смог найти крошечную деревушку и спросить, кому могут принадлежать овцы."
        "Жители сказали, что это, скорее всего, собственность другой деревни, уничтоженной орками."
        "Неужели они говорят правду? Так или иначе, Роуэн сделал жизнь этих крестьян неизмеримо лучше, помогая загнать им заблудших овец в их загоны."
        "Он ушел, а в ушах у него звенели их похвалы и благодарность."
        #lose 1 move point, small infany loss
        $ avatar.mp -= 1
        $ change_base_stat('f', -2)
        return

    "Игнорировать их":
        $ released_fix_rollback()
        "Овцы были чьей-то проблемой или благом. Так или иначе, Роуэн больше не беспокоился о судьбе этих глупых созданий."
        #no effect
        return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label the_hanged_man:
#### Plains Event: The Hanged Man ####
scene bg31 with fade

"Большую часть дня Роуэн бродил по полям, когда заметил вдали одинокое дерево и одинокую фигуру на зеленом фоне."
"Его сердце немного упало, когда он подошел ближе; что-то большое свисало с одной из самых крепких ветвей дерева, качаясь взад и вперед на ветру."
"Герою оставалось только надеяться, что его предположения о том, что это были неверны."
"Час спустя его опасения подтвердились. Он увидел человека, висящего на дереве, веревку, обвязанную вокруг шеи, оставляя лицо, с извращенной усмешкой трупного окоченения, на ветру."
"Как долго труп провисел здесь, он не мог сказать, но он пока не вошел стадию разложения, поэтому можно предположить, что не слишком долго."
"Роуэн, также не был уверен в событиях, которые привели этого человека к этому особенно жестокому концу. "
"Хотя всегда существовала вероятность того, что этот человек был преступником, возможно, даже особо мерзким."
"Хотя, в такие темные времена, жители деревень,  могли с такой же вероятностью могли повесить незнакомца посчитав его подозрительным."
"Единственный вопрос, на который он действительно мог ответить сейчас, что же он собирался сделать с телом бедняги."

menu:
    "Помародёрствовать.":
        $ released_fix_rollback()
        # gain guilt
        $ change_base_stat('g', 3)
        "Во время войны Роуэн понял важность выживания, и в его нынешнем затруднительном положении это умение было более необходимым, чем когда-либо."
        "Ему было стыдно за то, что он делал, но что бы ни было у этого человека сейчас, это ему уже не пригодится."
        #climb check dc12
        if not check_skill(12, 'climb')[0]:
        #fail
            "Роуэн проворно взобрался по стволу дерева и осторожно двинулся вдоль относительно крепкой ветки, на которой висело тело."
            "Лишний вес сделал ветку шаткой, и когда герой двинулся к тому месту, где висел труп, он поскользнулся и упал."
            "Резкий укол боли сопровождался хрустом кости, когда он приземлился левой ногой на землю, похоже, что его лодыжка сломалась."
            "В его теперешнем состоянии не было никакой возможности взобраться на дерево, и все, что он мог сделать, это захромать прочь, оставив дерево и его странный плод позади."
            #gain a wound and 5 xp
            $ avatar.take_damage(1)
            $ add_exp(5)
            return
        else:
        #pass
            "Роуэн проворно взобрался по стволу дерева и осторожно двинулся вдоль относительно крепкой ветки, на которой висело тело."
            "Лишний вес делал ветку шаткой, и она грозила сломаться с каждым новым шагом, но в конце концов она выдержала и герой смог добратся до веревки."
            "Вытащив меч из ножен, он разрезал веревку, освобождая человека."

            #choose one of the following three outcomes at random
            $ temp = dice(3)
            #1
            if temp == 1:
                "Вернувшись на землю, Роуэн приступил к неприятному занятию - обыску тела."
                "Проведя рукой по подкладке его туники, он обнаружил, что тот, кто проделал это до него, недостаточно хорошо справился со своей работой."
                "Снова вытащив меч, он разрезал шов рубашки, чтобы открыть потайной карман, в котором был спрятан маленький кошелек с монетами."
                #gain 1d20 + 10 gold
                $ change_treasury(dice(2)+10)
                menu:
                    "Похоронить тело.":
                        $ released_fix_rollback()
                        "Роуэн приступил к приготовлению могилы для лежащего человека. Без лопаты ему приходилось копать только руками, и это была долгая и трудная работа."
                        "В конце концов Роуэн выкопал достаточно глубокую яму для мужчины и осторожно положил в нее тело."
                        "Произнеся молитву Солансии за душу этого человека, он приступил к работе и закидал яму."
                        "К тому времени, когда он закончил, наступила ночь, но он чувствовал себя намного лучше после того, что сделал, несмотря на потерянное время."
                        "В конце концов, нам всем нужен кто-то другой, чтобы сделать это за нас."
                        #lose some guilt (less than gained for plundering) and 2 move points, gain 10 xp
                        $ change_base_stat('g', -2)
                        $ avatar.mp -= 2
                        $ add_exp(10)
                        return
                    "Оставить труп.":
                        $ released_fix_rollback()
                        "Было уже поздно, и Роуэн решил, что у него больше нет времени возиться с телом. Природа и звери сами позаботяться об остальном."
                        #gain 10 xp
                        $ add_exp(10)
                        return
            #2
            elif temp == 2:
                "Вернувшись на землю, Роуэн приступил к неприятному занятию - обыску тела."
                "Однако то, что он принял за обычное посмертное вздутие, оказалось чем-то гораздо худшим - трупными червями!"
                "Как только он прикоснулся к тунике мертвеца, тело взорвалось, и из  его груди потек отвратительный газ."
                "Роуэн отшатнулся, желчь подступила к горлу, как естественная реакция его тела, чтобы избежать заражения от отвратительных паразитов."
                "Когда он почувствовал, что добрался до безопасного места, его скрутило так, что его вырвало прямо на мягкую траву."
                "Ему очень повезло, что он пережил встречу с трупными червями, не заразившись, но он все еще будет чувствовать себя очень плохо в течение долгого времени."
                #gain a wound and 10 xp
                $ avatar.take_damage(1)
                $ add_exp(10)
                return
            #3
            else:
                "Вернувшись на землю, Роуэн приступил к неприятному занятию - обыску тела. Он осторожно провел руками по телу, ища что-нибудь, что могло быть упущено, но все было напрасно."
                "Кто бы его ни повесил, он уже очистил его от всего, что могло бы оказаться полезным, если у этого человека было хоть что-то."
                #gain additional guilt
                $ change_base_stat('g', 1)
                menu:
                    "Похоронить тело.":
                        $ released_fix_rollback()
                        "Роуэн приступил к приготовлению могилы для лежащего человека. Без лопаты ему приходилось копать только руками, и это была долгая и трудная работа."
                        "В конце концов Роуэн выкопал достаточно глубокую яму для мужчины и осторожно положил в нее тело."
                        "Произнеся молитву Солансии за душу этого человека, он приступил к работе и закидал яму."
                        "К тому времени, когда он закончил, наступила ночь, но он чувствовал себя намного лучше после того, что сделал, несмотря на потерянное время."
                        "В конце концов, нам всем нужен кто-то другой, чтобы сделать это за нас."
                        #lose some guilt (less than gained for plundering) and 2 move points, gain 10 xp
                        $ change_base_stat('g', -2)
                        $ avatar.mp -= 2
                        $ add_exp(10)
                        return
                    "Оставить труп.":
                        $ released_fix_rollback()
                        "Было уже поздно, и Роуэн решил, что у него больше нет времени возиться с телом. Природа и звери сами позаботяться об остальном."
                        #gain 10 xp
                        $ add_exp(10)
                        return
    "Похоронить его.":
        $ released_fix_rollback()
        "Роуэн не мог просто оставить этого человека там, он заслуживал достойного погребения, как и все остальные, независимо от причин, по которым его могли повесить."
        "Никого нельзя оставлять гнить, и пройдет совсем немного времени, прежде чем тело начнет привлекать падаль."
        #climb check dc12
        if check_skill(12, 'climb')[0]:
        #pass
            "Роуэн проворно взобрался по стволу дерева и осторожно двинулся вдоль относительно крепкой ветки, на которой висело тело."
            "Лишний вес делал ветку шаткой, и она грозила сломаться с каждым новым шагом, но в конце концов она выдержала достаточно долго, чтобы герой смог добратся до веревки."
            "Вытащив меч из ножен, он разорвал веревку, освобождая человека."
            "Вернувшись на землю, Роуэн принялся готовить могилу для лежащего человека. Без лопаты ему приходилось копать только руками, и это была долгая и трудная работа."
            "В конце концов Роуэн выкопал достаточно глубокую яму для мужчины и осторожно положил в нее тело."
            "Произнеся молитву Солансии за душу этого человека, он приступил к работе и закидал яму."
            "К тому времени, когда он закончил, наступила ночь, но он чувствовал себя намного лучше после того, что сделал, несмотря на потерянное время."
            "В конце концов, нам всем нужен кто-то другой, чтобы сделать это за нас."
            #lose guilt and 2 move points, and gain 10 xp
            $ change_base_stat('g', -3)
            $ avatar.mp -= 2
            $ add_exp(10)
            return
        else:
        #fail
            "Роуэн проворно взобрался по стволу дерева и осторожно двинулся вдоль относительно крепкой ветки, на которой висело тело."
            "Лишний вес сделал ветку шаткой, и когда герой двинулся к тому месту, где висел труп, он поскользнулся и упал."
            "Резкий укол боли сопровождался хрустом кости, когда он приземлился левой ногой на землю, похоже, что его лодыжка сломалась."
            "В его теперешнем состоянии не было никакой возможности взобраться на дерево, и все, что он мог сделать, это захромать прочь, оставив дерево и его странный плод позади."
            #gain a wound and 5 xp
            $ avatar.take_damage(1)
            $ add_exp(5)
            return
    "Оставить его.":
        $ released_fix_rollback()
        "Роуэн почувствовал себя неловко, но, в конце концов, он не знал, что за человек висел на дереве. Возможно, это наказание могло быть заслуженным."
        "Поскольку близнецы были суровыми мастерами, а цена неудачи была так высока и для Роуэна, и для его жены, он решил оставить тело в покое."
        "Возможно, что кто-нибудь еще придет и похоронит беднягу."
        #gain a small amount of guilt
        $ change_base_stat('g', 2)
        return
