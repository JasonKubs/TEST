label delaneRescuePlan:

if delane_plan == False:

    scene bg26 with fade

    "И вот он здесь: посреди недружелюбного лагеря орков, слуга пары маниакальных полудемонов. Когда-то герой, теперь угнетатель."
    "Это ненадолго."
    "Спасение Делейн будет нелегкой задачей. Ее палатка, в которую легко было попасть, тем не менее находилась в центре лагеря Улькро."
    "Роуэн должен был вывести ее из палатки к краю лагеря, мимо сторожевых башен и орочьих патрулей, и в лес."
    "Чтобы отвлечь внимание от палатки Делейн и пути его отступления, потребуется отвлекающий маневр. Чем меньше орков будет вокруг, тем лучше."
    "Возможно, кто-нибудь из знакомых ему орков сумеет ему помочь, не задавая лишних вопросов о ее цели."
    "Орки Батри всегда жаждут драки, так что, пока он укрепляет их доверие к нему, он может использовать их для достижения своих целей."
    "Кроме отвлекающего маневра, ему нужен был надежный выход из лагеря. Две фигуры в плащах, идущие в лес, были бы слишком подозрительны."
    "Возможно, был кто-то, кто мог бы ему в этом помочь, но лучше всего было, если бы он внимательно изучал периметр, чтобы бы найти слабые места, которые можно было бы использовать."
    "При наличии обоих этих элементов победа будет почти гарантирована."

    $ delane_plan = True

    jump escapePlanMenu

else:
    
    jump escapePlanMenu
    

label escapePlanMenu:

$ journal.add_quest_note('orciad', 'note30')


menu:
    "Подойдите к людям Батри." if delane_status == "tent" and batri_raid_count > 1 and delane_plan == True and delaneDistraction == False:
        $ released_fix_rollback()
        jump batriBoysFight
         
    "Посмотрим, сможет ли Улган помочь." if delane_status == "tent" and rowan_has_met_ulgan == True and delane_plan == True and delaneDistraction == False:
        $ released_fix_rollback()
        jump ulganHelp
        
    "Обратить внимание на периметр." if delane_status == "tent" and delane_plan == True and delaneEscapePerimeter < 1:
        $ released_fix_rollback()
        jump observePerimeter
    
    "Обратитесь к Матрон за помощью." if (delane_status == "tent" and delane_plan == True and delaneDistraction == False and orcMatronState == 2 and delaneEscapePerimeter < 1) or  (delane_status == "tent" and delane_plan == True and delaneDistraction == False and orcMatronState == 3 and delaneEscapePerimeter < 1):
        $ released_fix_rollback()
        jump orcMatronHelp
        
    "Спасти Делейн." if delane_status == "tent" and delane_plan == True and delaneDistraction == True and delane_trust > 2 and delaneEscapePerimeter > 0:
        $ released_fix_rollback()
        jump orciadEscape
        
    "Оставить планирование на потом." if delane_status == "tent" and delane_plan == True:
        $ released_fix_rollback()
        jump campMenu



label batriBoysFight:

scene bg26 with fade

if batriBoysFirst == True:
    "После того, как показал себя в рейдах, использовать орков должно быть легко. Было несколько способов заставить их сотрудничать, и он должен был решить, какой из них является наилучшим."
    "У него был только один шанс."
    $ batriBoysFirst = False
    
else:
    pass

label batriPlanMenu:
scene bg26 with fade

menu:
    "Подкупить их алкоголем. (100 золотых)" if avatar.gold > 99:
        $ released_fix_rollback()
        "Найти алкоголь не было проблемой."
        if all_actors['cla_min'].relation > 49:
            "Он планировал поговорить с некоторыми купцами внутри лагеря, но после того, как через амулет Кла-Мин он сообщил об этом Клэму, экономный гоблин организовал быструю доставку в лагерь, именно для него."
            "Она все еще заставляла его платить, хотя и мало, чтобы только покрыть расходы."
            $ avatar.gold -= 60
        else:
            "В лагере было несколько купцов, готовых предоставить баррель-другой за нужную цену."
            "Роуэн не имел возможности вести переговоры, но, по крайней мере, вся сделка прошла гладко."
            $ avatar.gold -= 100
        "Он встретил рейдоров в обычном месте, и его прибытие было встречено несколькими восторженными криками, но в основном его присутствие было бы проигнорировано."
        "Можно было бы подумать, что они будут более приветливы к нему после всех сражений, которые он воевал на их стороне, но глубоко укоренившееся негодование к человеку так легко не преодолеть."
        "Для этого и был нужен алкоголь. Трудно было поставить себя выше кого-то другого, когда вы оба в дерьме и лежите в грязи."
        show rowan necklace happy at center with dissolve
        show wild orc neutral at edgeleft with moveinleft
        show orc soldier neutral at edgeright with moveinright
        ro "Давайте отпразднуем! За весь успешные рейды до сих пор, и за все рейды в будущем!"
        ro "И за верховного Батри!"
        ro "Эль за мой счет!"
        "Среди орков раздались неистовые крики радости. Им трудно было угодить, но опять же, за чужой счет пьют и язвеники и трезвеники"
        hide wild orc with moveoutleft
        hide orc soldier with moveoutright
        "В течение следующих двух часов Роуэн переходил от группы к группе, смеялся и пил вместе с ними, выражая свое восхищение молодыми рейдарам и презрение к Улькро."
        "Алкоголь и пламенная политика действовали на них легко, но Роуэн старался не переусердствовать. Он хотел, чтобы они восприняли его слова, а не затеяли драку."
        jump batriBoysMain
        
    "Сладко  ними поговорить." if batriSweetFail == False:
        $ released_fix_rollback()
        if check_skill(15, 'diplomacy')[0]:
            "Он встретил орков-налетчиков на обычном месте, и его прибытие было встречено восторженными криками, многие орки махали ему, когда он приблизился к группе."
            "Как дипломат, он старался быть хорошей компанией во время набегов. После нескольких из них ему удалось довольно хорошо вжиться в их привычную компанию." 
            show rowan necklace happy at center with dissolve
            ro "Тог, ты выиграл конкурс на благосклонность Шарна?"
            show wild orc neutral at edgeleft with moveinleft
            wo "Ба, нет. Глупый Улаг победил, жонглируя."
            wo "Жонглирование!"
            if avatar.corruption > 70:
                ro "Сломай ему пальцы, пока он спит. Это послужит ему уроком!"
            else:
                show rowan necklace concerned at center with dissolve
                ro "Это не честно. Может быть в следующий раз."
            show rowan necklace neutral at center with dissolve
            hide wild orc with moveoutleft
            show orc soldier neutral at edgeright with moveinright
            ro "Крил, тебе нужно залатать броню. Следующий Розарианский рыцарь, которого мы встретим, проткнет тебя, как рыбу, если ты этого не сделаешь."
            os "Заткись, или тебе понадобится залатать лицо!"
            hide orc solder with moveoutright
            "Роуэн неодобрительно покачал головой, зная,  он должен был продолжать приставать к нему с этим, иначе орк никогда не будет слушать."
            "Он шел среди них как равный. Заставить их выполнять его приказы было бы легко."
            "Если он жаловался на то, какие ублюдки являются сторонниками Улькро и как он хочет поставить их на место, он был уверен, что они одолжат ему уши." 
            "И он сделал именно это. Более двух часов он работал с ними, и постепенно настроение менялось, и гнев на Улькро рос."
            jump batriBoysMain
        else:
            "Он встретил рейдоров в обычном месте, и его прибытие было встречено несколькими восторженными криками, но в основном его присутствие было бы проигнорировано."
            "Можно было бы подумать, что они будут более приветливы к нему после всех сражений, которые он воевал на их стороне, но глубоко укоренившееся негодование к человеку так легко не преодолеть."
            "К счастью для него, их неприязнь к людям Улькро была гораздо сильнее."
            if check_skill(10, 'bluff')[0]:
                "В течение следующих двух часов он шел среди них, ведя светскую беседу."
                "Он бы упомянул, как тонко, как он мог, как он слышал, что люди Улькро говорили дерьмо о Батри и его рейдерах."
                "Что они считают его последователи недисциплинированными, слабыми маленькими гоблинами."
                if all_actors['cla_min'].relation > 49:
                    show rowan necklace concerned behind bg26
                    ro "(Кла-Мин, прости меня.)"
                    hide rowan
                else:
                    pass
                "Он рассказал им, что видел рабов Улькро, и они, очевидно, отбирали всех симпатичных самок."
                "Очевидно, это была ложь, но трава всегда была зеленее на другой стороне."
                "В течение следующих двух часов он рассказывал все больше и больше фантастических историй о реальных и воображаемых преступлениях людей Улькро, совершенных против орков Батри."
                "Больше всего им нравилось мочиться в ту же реку, из которой они пили. Он был совершенно уверен, что все они поступали так. Орк понятия не имел о гигиене."
                "Но это их разожгло, и это самое главное." 
                jump batriBoysMain
            else:
                "В течение следующего часа он ходил от группы к группе, пытаясь завязать светскую беседу, и натравливал их на Улькро и его людей."
                "Но через некоторое время он понял, что переигрывает. Он говорил слишком резко, слишком неуважительно, и в редком проявлении единства орков люди Батри начали защищать Улькро."
                "Усвоив урок, он попытался спасти ситуацию, но было уже слишком поздно. Он растратил доверие, которое приобрел во время набегов, и не было никакой возможности вернуть его."
                "Ему придется найти другой способ отвлечь орков."
                show rowan necklace angry behind bg26
                ro "(Черт возьми!)"
                hide rowan
                $ batriSweetFail = True
                jump batriPlanMenu
                
    "Насмехайтесь над ними." if batriTauntFail == False:
        $ released_fix_rollback()
        "Он встретил рейдоров в обычном месте, и его прибытие было встречено несколькими восторженными криками, но в основном его присутствие было бы проигнорировано."
        "Это не продлится долго. Он будет играть здесь и если его гамбит сработает, они все скоро будут есть из его рук."
        show rowan necklace angry at center with dissolve
        show wild orc neutral at edgeleft with moveinleft
        show orc soldier neutral at edgeright with moveinright
        ro "Я вкалывал как проклятый, помогая Батри показать всем, что Улькро и его люди кучка слабаков."
        ro "А вы, ребята, только и делаете, что раз в неделю отправляетесь в рейд и называете его Днем Траха!"
        "Теперь вокруг него собралось небольшое сборище. Все, что ему было нужно…"
        wo "Эй, ты хочешь, чтобы тебя убили?!"
        show rowan necklace happy at center with dissolve
        ro "(Так предсказуемо.)"
        show rowan necklace angry at center with dissolve
        ro "Нет, я пытаюсь заставить орков, которые должны быть моими союзниками, делать это!"
        "Орк зарычал на него и начал медленно приближаться. Роуэн встретил его яростный взгляд своим холодным взглядом."
        if check_skill(15, 'intimidate')[0]:
            "Он остановился в нескольких шагах от него."
            "Все они видели, как Роуэн сражался во время набегов. Они знали, что он не был тем, через труп которого они могли просто перешагнуть."
            wo "Да ну?"
            wo "Так что ты предлагаешь нам сделать?"
            wo "Или ты здесь только для того, чтобы нести чушь?"
            show rowan necklace happy at center with dissolve
            "Он собрал их вокруг себя и начал объяснять свою идею. Гнев, однажды вызванный,легко было перенаправить. Ей нужно было только немного времени, чтобы поговорить с ними."
            jump batriBoysMain
        else:
            "Все они видели, как Роуэн сражается. Они знали, что он не тот человек, с которым можно шутить"
            "Но гнев затуманил рассудок орка. С устрашающим ревом он сократил расстояние между ними и нацелил удар в лицо."
            $ event_tmp['combat roll'] = check_combat(10)[1]
            if event_tmp['combat roll'] >= 10:
                show rowan necklace angry at midright with moveoutright
                "Роуэн увернулся от удара легко и ударил нападавшего в живот. Он последовал с быстрым апперкотом, а затем еще один удар, прямо в лицо."
                hide orc soldier with dissolve
                show rowan necklace angry at center with moveinright
                ro "Продолжим, или мы можем перейти к тому, что важно для всех нас?"
                ro "Показать оркам Улькро, где их место."
                "Группа одобрила это с энтузиазмом, ворча одобрения. Мораль была низкой, но это не имеет значения."
                "Роуэну потребовалось некоторое время, чтобы поговорить с ними, и после того, как его позиция была восстановлена, он был свободен, чтобы творить свою магию."
                jump batriBoysMain
            else:
                "Он поднял руки, чтобы блокировать удар, но грубая сила удара заставила его пошатнуться."
                show rowan necklace angry at midleft with moveoutleft
                hide rowan with dissolve
                show orc soldier neutral at center with moveinright
                "Он рухнул на землю, орк навис над ним. Он быстро вскочил и положил руку на рукоять своего меча, на случай, если орк двинется к нему."
                "Но воин лишь презрительно посмотрел на него сверху вниз."
                os "Ты хороший рейдер."
                os "Придерживайся этого."
                "Окружающие орки согласно закивали, и группа медленно начала рассеиваться."
                "То уважение, которое Роуэн приобрел за последние недели, растворилось в воздухе. Теперь они ничем не смогут ему помочь."
                "Ему придется найти другой способ."
                show rowan necklace angry behind bg26
                ro "(Черт возьми! Хам Дуралям!)"
                hide rowan
                $ batriTauntFail = True
                jump batriPlanMenu

label batriBoysMain:

scene bg26 with fade

"Его усилия не остались незамеченными. Один из лейтенантов Батри, который часто руководил рейдами, внимательно наблюдал за ним в течение всего вечера."
"Теперь он счел нужным подойти к нему."

show wild orc neutral at edgeright with moveinright

wo "Хьюми!"

show wild orc neutral at center with moveinright
show rowan necklace neutral at center with dissolve

"Орк-молодой, худощавый, почти человекоподобный боец-обнял его за плечи, явно стараясь быть дружелюбным."

show rowan necklace happy at center with dissolve

wo "Так приятно тебя видеть."

show rowan necklace neutral at center with dissolve

wo "Что ты задумал?"

"Из того, что Роуэн помнил, этот был одним из наиболее агрессивных сторонников, не уклоняющихся от насилия и коварной тактики."
"Возможно, он мог бы быть ему полезен."

show rowan necklace happy at midleft with moveoutleft
show wild orc neutral at midright with moveoutright

"Он высвободился из его объятий и посмотрел на него с дружелюбной улыбкой."

ro "Маленькую интрижку"
ro "Я просто подумал, что надо бы немного завести ребят."

show rowan necklace happy at midleft with dissolve

ro "Мы уже несколько недель совершаем набеги, а Улькро и его люди все еще думают, что могут смотреть на нас сверху вниз."
ro "Так что я подумал... что, может быть, мы могли бы немного потрепать их ... поставить их на место."

"Роуэн увидел, как глаза орка оживились. Ничто так не радовало орков, как перспектива оскорблять других."

show rowan necklace neutral at midleft with dissolve

if avatar.corruption < 31:
    "Правда это отвратительно, ."
else:
    pass
    
show orc soldier neutral at edgeright with moveinright

"Роуэн еще раз объяснил свою идею. У него уже была поддержка регулярных рейдеров,и как только командир заглотил наживку, вопрос был решен."

os "Ха-ха-ха! Ты мне нравишься, Роуэн."
os "Скажи нам, когда и где."

show rowan necklace happy at midleft with dissolve

ro "(Отвлекающий маневр приобретен.)"

$ delaneDistraction = True

jump campMenu


label ulganHelp:

scene bg26 with fade 

if ulganHelpFirst == True:

    "Для того, что он должен был сделать, ему нужна была помощь."
    "По большей части, у него не было достаточно хорошо влияния в племени, чтобы кто-то начал драку, только потому, что он сказал им." 
    "Ему нужно было убедить их. Подкупать их. И к счастью для него, был один орк, которого он знал, что сможет купить его, несмотря на ограниченные ресурсы."
    "Улган, низкорослый, трудолюбивый, недооцененный орк, которого он встретил, когда тренировал свои рефлексы с помощниками жреца Хароса."
    "Умный и честолюбивый, он станет идеальной правой рукой для того, что задумал Роуэн."
    "Он нашел его после того, как увидел, что последователи Хароса вернулись с очередной тренировки. Он отвел его в сторону и коротко объяснил, что ему от него нужно и что он предлагает взамен:"
    "Офицерская должность среди орков Бладмина, как только все это закончится." 
    "Он надеялся, что этого будет достаточно, но Улган не согласился."
    "Он получит от Роуэна то, что ему нужно, и не будет задавать никаких вопросов. Но это потребует от него нескольких услуг для его друзей."
    "Все они должны были получить офицерские должности в Бладмине. Это не обсуждалось." 
    "Он заключил выгодную сделку, и Роуэн мог это устроить. Андрас не заботился о продвижении отдельных орков по службе, поэтому он сомневался, что здесь был какой-то риск."
    "Но это был бы серьезный удар по моральному духу орков. Все они верили, что сила - это право, и такой явный фаворитизм должен был вызвать ответную реакцию."

else:
    "Возможность заключить сделку с Улганом все еще доступна, даже если это означает потенциальную обратную реакцию с другими орками."


menu:
    "Согласиться на его условия.":
        $ released_fix_rollback()
        if serveChoice == 4:
            "Если моральный дух станет проблемой, он найдет какой-то способ справиться с этим позже. На данный момент, это был самый быстрый способ, чтобы обеспечить отвлекающий маневр."
        else:
            "Конечно, Роуэн не мог меньше заботиться о моральном состоянии орков. Они должны были выступить достаточно хорошо, чтобы Андрас не отрубил ему голову."
        "Роуэн пожал ему руку и рассказал все подробности.  Они нашли место в Розарии, где Роуэн встретится с ними через месяц после того, как все разрешится." 
        "Быстро и легко. Удивительно, как наличие подходящего человека для работы ускорило события."
        $ delaneDistraction = True
        $ ulganBloodmeen = True
        jump campMenu

    "Отклонить сделку.":
        $ released_fix_rollback()
        "Привести в замок Бладмин одного орка, которого могли бы заподозрить в причастности к спасению Делейн, было рискованно. Целую группу их было безумием." 
        "Он найдет другой способ."
        jump campMenu

     
label observePerimeter:

scene bg26 with fade 

if observePerimeterFirst == True:
    "Найти дыру в орочьем периметре было несложно."
    "Их патрули были редкими, обычно не появлялись на своих сторожевых постах вовремя, и большинство из них редко обращали внимание на что-либо."
    "Но с Делейн за спиной он не мог позволить себе роскошь ждать, пока появится свободное место. Ему нужна была слабость, которую он мог бы надежно использовать, когда придет время."
    "И чтобы найти это, требовалась ночи тщательных наблюдений. Определение того, какие охранники занимали какие места, когда они прибыли, когда они ушли, и что они делали до и после своей смены."
    "Дурманящая работа, но не было другого способа."
    "Не имея ничего, чтобы сосредоточиться на своих мыслях, он отвлекся на другие дела. К замку Бладмин и людям в нем."
    if all_actors['alexia'].relation > 30:
        "He wondered how his wife was doing."
        if get_actor_job('alexia')=='maid':
            "Разве это нормально, что он позволил ей работать под началом Изеры? Роуэн не сомневалась, что демонесса воспользуется случаем и будет делать это как можно чаще."
            "Алексия не была героем. Сможет ли она выдержать?"
            "Он надеялся, что сможет."
        elif get_actor_job('alexia')=='research_assistant' and alexia_knows_magic == True:
            "Это была удача, действительно, узнать, что Алексия обладала таким магическим потенциалом."
            "Он не был уверен, какое влияние эльфийская колдунья окажет на его жену, но это не могло быть хуже того, что сделали бы с ней близнецы, если бы у нее не было такого защитника, как Клиона."
            "И возможно, со временем ее вновь обретенная магия окажется полезной, так или иначе."
        elif get_actor_job('alexia')=='forge':
            "Ему повезло, что Андрас проявил интерес к Грейхайду. Кузнец-Минотавр, несомненно, был подарком богини."
            "Пока Алексия была рядом с ним, он был уверен, что ничего плохого с ней не случится."
        elif get_actor_job('alexia')=='breeding':
            "Он не был уверен, что племенные загоны так хорошо подходят для Алексии. Он знал, что Дрейт не станет устраивать никаких шуток, но если драйдер вырвется на свободу…"
            "Он не хотел думать об этом."
        elif get_actor_job('alexia')=='barmaid':
            "Возможно, быть девкой из таверны не было вершиной мечтаний Алексии, но это было самое меньшее, что он мог сделать для нее."
            "Дайте ей возможность встретиться, поговорить с кем-нибудь, кроме близнецов и их рабов."
            "… А если она услышит пару полезных слухов, это будет еще лучше."
        else:
            "Жить в замке Бладмин, должно быть, было тяжело для нее. На самом деле она ни на что не жаловалась, но…"
            "Если что-то пойдет не так, с Андрасом или с Изерой, скажет ли она ему? Или она будет молча терпеть оскорбления, не желая усугублять его беспокойство?"
            "Он не мог этого знать."
    else:
        if helayna_escape_method == "no escape":
            "Как поживает Хелейна? Неужели кольцо снова сработало? Были ли близнецы причиной ее проблем?"
            "Неужели Алексия причинила ей неприятности?"
            "..."
            "Давным-давно он пообещал, что не возьмет в жены другую женщину, кроме Алексии. Сейчас…"
            "Теперь, казалось, его мысли были заняты кем угодно, только не ею. Он знал, что это неправильно, но ничего не мог с собой поделать. "
        if helayna_escape_method == "rowan":
            "Была ли Хелейна в безопасности? С тех пор как она сбежала, у него не было никаких новостей о ее местонахождении."
            "По крайней мере, это доказывало, что Близнецы еще не прикоснулись к ней, и Роуэн находил в этом утешение."
            "Ее побег был серьезным ударом по их авторитету, так что если они когда-нибудь ее поймают, их наказание будет столь же публичным, сколь и жестоким."
            "… Если Хелейна доберется до Протеи целой и невредимой, а он сумеет спасти Делейн, это будет доказательством того, что он все еще может принести какую-то пользу на службе близнецов."
            "Хотя Хелейне никогда не пришлось бы бежать из замка Бладмин, если бы он не помог Близнецам захватить Рейв."
        else:
            if rowanJezSex > rowanAndrasSex:
                "… Что сделает с ним Изера, если, да защитит его богиня, она узнает, что он решил спасти Делейн, а не вербовать орков?"
                "Неужели она его свяжет? Заствит целовать ее ноги? Будет держать его голову между своих ног и приказывать ему лизать, пока она не кончит столько раз, сколько недель Роуэн потратил впустую в лагере орков?"
                show rowan necklace concerned behind bg26
                ro "…"
                hide rowan
                "Он тяжело сглотнул. Может быть, ему не стоит проводить так много времени с демонессой…"
            elif rowanAndrasSex > rowanJezSex:
                "… Что Андрас сделает с ним, если, да защитит его богиня, он узнает, что решил спасти Делейн, а не вербовать орков?"
                "Будет ли он трахать его рот своим членом, заставит его съесть свою сперму? Возьмет ли он его задницу, не заботясь о его протестах?"
                show rowan necklace concerned behind bg26
                ro "…"
                hide rowan
                "Он тяжело сглотнул. Может быть, ему не стоит проводить так много времени с демоном…"
            elif rowanJezSex > 0 and rowanJezSex == rowanAndrasSex:
                "… Что сделают с ним близнецы, если, да защитит его богиня, они узнают, что он решил спасти Делейн, а не вербовать орков?"
                "Неужели она его свяжет? Заствит целовать ее ноги? Будет держать его голову между своих ног и приказывать ему лизать, пока она не кончит столько раз, сколько недель Роуэн потратил впустую в лагере орков?"
                "Будет ли он трахать его рот своим членом, заставит его съесть свою сперму? Возьмет ли он его задницу, не заботясь о его протестах?"
                "Или они оба возьмут его - Изера заставит его есть ее киску, пока Андрас трахает его задницу?"
                show rowan necklace concerned behind bg26
                ro "…"
                hide rowan
                "Он тяжело сглотнул. Может быть, ему не стоит проводить так много времени с Близнецами…"
            else:
                "… Когда в последний раз он делал что-то приятное своей жене? Он старался быть верным, насколько мог, но чувствовал, как она отдаляется от него."
                "Он спасал одну женщину, хотя медленно, но верно терял ту, которую поклялся защищать."
                show rowan necklace angry behind bg26
                ro "(Черт подери...)"
                hide rowan
                "У Алексии в замке не было никого, кроме него. Он должен уделять ей больше внимания, если хочет, чтобы она оставалась рядом с ним, когда все это закончится."

    "Эти и другие мысли не давали ему покоя, пока он изучал распорядок дня охраны в течение недели."
    "Он обратил внимание на нескольких орков, которые не явились вовремя или ушли рано. Во время следующего визита он должен будет проверить, было ли этоявление постоянным."
    "На данный момент его работа здесь была закончена."

    $ observePerimeterFirst = False
    $ avatar.mp = 0
    return
        
else:
    if check_skill(15, 'spot')[0] or perimeterSearchCount == 2:
        "Наконец, Роуэн нашел то, что искал."
        "Одинокий орк, возвышающийся над одной из северо-восточных сторожевых башен, рядом с областью, где группировки Тариша и Улькро граничили друг с другом."
        "Все остальные башни были довольно далеко, так что он был в значительной степени ответственен за наблюдение за этой частью выхода из лагеря."
        "Жаль, что у него была тягость к азартным играм и склонность пить во время игры. Из трех смен, которые Роуэн наблюдал за ним, он опоздал на все."
        "Всегда опаздывал по крайней мере на полчаса. И башня была пустой, так как другой стражник уходил потеряв терпение."
        "Это был бы их путь. Это было немного за пределами проторенной дороги, но у него было достаточно свободы действий во время самого побега."
        if delaneDistraction == True:
            "Теперь у него есть все необходимое для побега."
        else:
            "Теперь все, что ему было нужно, это отвлечь охрану, и они были готовы идти."
        $ delaneEscapePerimeter = 1
        jump campMenu

            
    else:
        "Роуэн провел всю неделю в поисках дыры в обороне орков."
        "Ни одно из проверенных им до сих пор отверстий не оказалось надежным. Один раз промахнулся, и все. Разочаровывает, но, этого и следовало ожидать."
        "Возможно, на следующей неделе ему повезет больше."
        $ perimeterSearchCount += 1
        $ avatar.mp = 0
        return


label orcMatronHelp:

scene bg26 with fade

"Роуэн снова нашел Матрону в лагере Тариш, которая учила орчанку-подростка готовить настоящее рагу."
"Любопытствуя, он решил остаться на некоторое время вне поля их зрения, чтобы посмотреть, как работает пожилая женщина."
"Матрона проявляла удивительное терпение по отношению к своей протеже, объясняя все в мельчайших подробностях, лишь изредка фыркая."
"Но младшая была не из тех, кто стоически переносит это вечно, невзирая на разницу в возрасте, и в конце концов в какой-то момент укусила ее."
"Старшая сестра-орк ударила ее по голове так сильно, что Роуэн была уверен, что у нее сотрясение мозга. Молодая только пробормотала слова извинения и вернулся к котлу."
"Не совсем его любимый метод обучения, и это было не его место, чтобы говорить. Кроме того, здесь у него была иная цель."
"Матрона была одной из немногих орков, которые смогли перерасти врожденное стремление орка к разрушению."
"И все, что мог бы противопоставить Роуэн - замок Бладмин - лагерю орков, было в ее интересах."
"Заставить ее помочь должно быть легко."
"Не желая привлекать слишком много внимания, он тихонько переместился так, чтобы старшая сестра орка могла его видеть."
"Ее глаза быстро нашли его – и почти мгновенно сузились от гнева. Для нее он был 'Воробьем'."
"Его присутствие никогда не предвещало ничего хорошего. Роуэн сделал ей знак следовать за ним, и после некоторого колебания она последовала за ним."

show rowan necklace neutral at midleft with dissolve

orcm "Воробей. Что тебе надо?"

"Он повел ее прочь от центра лагеря, к хорошему уединенному месту, где они могли свободно поговорить."

ro "Я пришел попросить тебя об одолжении."

"Матрона оскалила зубы. Роуэн не мог понять, улыбается она ему или насмехается. Возможно и то и другое."

orcm "Как это не похоже на тебя и твоего хозяина. Ваш вид редко просит нас о чем-либо. Обычно вы просто берете."

ro "И вы даете его нам свободно. Ты всегда так делаешь."

show rowan necklace concerned at midleft with dissolve

ro "Мы это уже проходили."

"Ее насмешливая улыбка дрогнула."

orcm "Да. Да, это так. Что заставляет меня задуматься ... что тебе от меня нужно?"
orcm "Мы оба знаем, чем это закончится. Зачем ты мучаешь меня своим присутствием?"

show rowan necklace neutral at midleft with dissolve

ro "Это не совсем так."
ro "На этот раз исход не предопределен."

if orcMatronState == 2:
    "Ее глаза сузились, и она закрыла их, рыча. Роуэн почувствовал на лице ее неприятное дыхание, но не отступил ни на шаг."
    orcm "Так ли это? Вы были совершенно уверены, что это был последний раз, когда мы гуляли."
    orcm "Что мы станем рабами твоего хозяина."
    "Роуэн был не из тех, кто допускает, чтобы такая дешевая тактика запугивания, подобная этой, подействовала на него, поэтому он держал свой взгляд спокойным, а голос твердым."
    ro "И это все еще может случиться."
    ro "А может, и нет."
    ro "Есть... кое-что, что мне нужно сделать."
    ro "Я не могу вдаваться в детали, но если я добьюсь успеха, все может обернуться для вас по-другому."
    ro "Может быть, вы все-таки не станете рабами моего господина."
    ro "… Может быть, ты еще обретешь покой."
    if avatar.corruption > 70:
        "Он не верил ни одному своему слову. Даже если по какой-то случайности орки избежат ярма Близнецов, они просто окажутся втянутыми в другой конфликт достаточно скоро."
        "Они рождаются зверями, и они умрут зверями."
        if check_skill(15, 'bluff')[0]:
            jump matronHelpGranted
        else:
            jump matronHelpDenied
    else:
        "Шансы на это были невелики. Орки были порочной расой, и даже если им удастся избежать ярма Близнецов, они, скорее всего, вскоре окажутся втянутыми в какой-нибудь другой конфликт."
        "Но у них будет шанс. А иногда это было все, что нужно людям."
        if check_skill(15, 'diplomacy')[0]:
            jump matronHelpGranted
        else:
            jump matronHelpDenied

else:
    show rowan necklace concerned at midleft with dissolve
    ro "Мы не должны быть рабами. Мы не должны просто позволить судьбе решить нашу роль во всем этом."
    "С ее губ сорвалось короткое сухое дыхание."
    orcm "Высокие слова, учитывая твое положение. Что-то изменилось, Воробей? Ты волен поступать, как заблагорассудится?"
    "В последний раз, когда они разговаривали, в минуту слабости Роуэн проговорился, что его собственная роль во всем этом была не совсем добровольной."
    "Теперь он проклинал себя за это. Но это будет мост, который поможет сократить разрыв между ними."
    ro "Нет, это не так."
    if serveChoice == 2:
        ro "Но я устал бояться."
    elif serveChoice == 1:
        ro "Но я не могу вечно ждать подходящего момента для удара."
    elif serveChoice == 3:
        ro "Но я не могу обречь одну женщину на то, чтобы защитить другую."
    else:
        pass
    jump matronHelpMain




label matronHelpGranted:

orcm "Но тебе нужна моя помощь, чтобы добиться успеха."

"Роуэн кивнул в ответ, и надзирательница недовольно заворчала. Она сердито расхаживала перед ним, не желая кричать."

orcm "Пустое слово. Ложная надежда, погруженная в красивую риторику!"
orcm "… Вот что я хочу сказать."

show rowan necklace happy at midleft with dissolve

ro "Возможно, красивая риторика."

show rowan necklace neutral at midleft with dissolve

ro "Но это не пустые обещания."
ro "То, что я собираюсь сделать, очень опасно, но поверьте мне, ты захочешь, чтобы я добился успеха."

orcm "Но ты не можешь сказать больше, пока я не соглашусь помочь. Да, да, я понимаю."

"Она отвернулась от него и перевела взгляд на лагерь орков."
"Роуэн мгновенно узнала этот взгляд. Он знал его наизусть. Он видел это каждый раз, когда приказывал молодым людям из деревень Розарии поднять оружие против Карнаса."
"Это смесь страха и тоски. Дом, в котором они выросли, увидят ли они его снова? Остановят ли они врага прежде, чем его когти достигнут того, что им дороже всего?"
"Выдержит ли он без них, или рассыплется в прах, как только они отвернутся от него? А если она рассыплется в прах, найдут ли они когда-нибудь другой такой же?"
"В скольких лагерях побывала Матрона за свою долгую жизнь?"

orcm "… Хорошо."

"Одно слова было достаточно."

orcm "Что тебе нужно?"

jump matronHelpMain

label matronHelpDenied:

orcm "Пустое слово. Ложная надежда, погруженная в красивую риторику, должна была подчинить меня твоей воле."
orcm "Вы хотите изменить результат?"
orcm "Уходи и никогда не возвращайся. Скажи своему хозяину, что ты потерпел неудачу. Скажи им, что орки никогда не будут служить вашему роду."

show rowan necklace angry at midleft with dissolve

ro "Дело не только в тебе..."

orcm "Конечно, нет."
orcm "Мы здесь просто пешки. Быть использованным и выброшенным."

"Она вздохнула, слишком уставшая, чтобы продолжать."
"Раньше Роуэн полагал, что ей было за пятьдесят, но когда она опустила плечи в знак поражения, чувство безнадежности добавило к ее телу еще двадцать лет."

show rowan necklace concerned at midleft with dissolve

orcm "Просто уйди."
orcm "Делай, что должен, и больше не приставай ко мне."

"… Здесь он не найдет никакой помощи."

$ orcMatronState = 5

jump campMenu


label matronHelpMain:

ro "Мне нужно, чтобы ты помогла мне тайно вывезти кое-кого из лагеря."
ro "Мне нужно, чтобы ты помогла мне спасти Элеонору Делейн."

"Он увидел, как ее глаза расширились. Это имя было известно тем, кто держал ухо востро."

orcm "Делейн ... аристократка, из-за которой все потеряли голову."
orcm "Они просили тебя забрать ее, верно? Тариш? Батри, а? В обмен на их поддержку."

show rowan necklace happy at midleft with dissolve

ro "И Улькро хотел, чтобы я убедил ее полюбить его."

orcm "В обмен на поддержку."
ro "В обмен на поддержку."

orcm "…"
orcm "Все племя."

show rowan necklace concerned at midleft with dissolve

orcm "Все мужчины, женщины и дети."
orcm "Продается за одну человеческую киску."
orcm "Невероятно."

show rowan necklace happy at midleft with dissolve

"Матрона издала болезненный стон, и Роуэн не могла не посочувствовать ей. Он также не мог понять пирамиду приоритетов близнецов."

ro "Ты сможешь провести ее через периметр лагеря?"

"Матрона кивнула, все еще ошеломленная этим открытием."
"Как бы то ни было, он мог оставить это дело в ее руках."

$ delaneEscapePerimeter = 2
jump campMenu


label orciadEscape:

$ journal.complete_quest_note('orciad', 'note14')
$ journal.complete_quest_note('orciad', 'note15')
$ journal.complete_quest_note('orciad', 'note16')
$ journal.complete_quest_note('orciad', 'note17')
$ journal.complete_quest_note('orciad', 'note18')
$ journal.complete_quest_note('orciad', 'note20')
$ journal.complete_quest_note('orciad', 'note21')
$ journal.complete_quest_note('orciad', 'note22')
$ journal.complete_quest_note('orciad', 'note23')
$ journal.complete_quest_note('orciad', 'note24')
$ journal.complete_quest_note('orciad', 'note25')
$ journal.complete_quest_note('orciad', 'note29')
$ journal.complete_quest_note('orciad', 'note30')
$ journal.complete_quest_note('orciad', 'note31')


scene bg30 with fade
show rowan necklace neutral at midleft with dissolve
show eleanor rags happy at midright with dissolve

ele "Роуэн, как всегда твое присутствие украшает мой день. Какие новости вы принесли?"

ro "Те, которые вы ждали все это время."

show rowan necklace happy at midleft with dissolve
show eleanor rags concerned at midright with dissolve

ro "Все на месте, Миледи. Мы покинем лагерь сегодня вечером."

"Сначала Делейн, казалось, не понял его слов. Так долго перспектива свободы казалась такой далекой." 
"Она держалась за нее, держалась за эту надежду, хотя и знала, что, скорее всего, просто обманывает себя." 
"И теперь Роуэн сказал ей, что к концу ночи она снова станет свободной женщиной." 

show eleanor rags happy at midright with dissolve

ele "Роуэн, Я..."

if delane_corruption_occurred == True:
    show eleanor rags concerned at midright with dissolve
    ele "Спасибо, большое спасибо."
    show rowan necklace concerned at midleft with dissolve
    ele "По правде говоря, я не знаю, как долго еще смогу сохранять рассудок."
    ele "Меня это уже посещало... "
    show eleanor rags concerned at midright with dissolve
    "Густой румянец залил ее лицо, и она поняла, что не может продолжить."
    ele "Нет, это уже не имеет значения. Главное-наконец выбраться из этого адского кошмара."

elif (delane_corruption_occurred == False) and (ulcro_path > 1):
    ele "По правде говоря, я уже начала терять надежду."
    ele "Судя по тому, как ты говорил об Улькро, я боялась, что ты уже не сможешь найти способ освободить меня."
    ele "Я прошу прощения за то, что сомневалась в тебе. Ты действительно человек слова."
    ele "Спасибо тебе за все."
    
else:
    ele "Спасибо тебе за все."
    ele "Я прошу прощения за то, что сомневалась в твоих намерениях. Ты действительно герой."

show eleanor rags neutral at midright with dissolve

ele "..."
ele "Но я не должна праздновать так рано. Я так понимаю, побег будет нелегким?"

show rowan necklace neutral at midleft with dissolve

ro "Я объясню план вкратце, но сначала…"
ro "Боюсь, вам придется пережить еще одно унижение."

if delane_abduction == True:
    "Он положил перед ними мешок, полученный от Тариш, и показал дворянке его содержимое."

else:
    "Он положил перед ними заранее приготовленный мешок и показал дворянке его содержимое."
    
"Рваные, тяжелые одежды. Банки с грязью, жиром. Мейк-ап."

show eleanor rags angry at midright with dissolve

"Фурнитура."

ro "Даже ночью я могу быть в лагере, не беспокоясь орков."
ro "Но я не могу гарантировать, это, если меня будет сопровождать фигура в плаще, идущая позади меня."

show eleanor rags neutral at midright with dissolve

ele "Так что вы заставите меня играть роль скромного раба, только что захваченного в плен."

ro "Боюсь, что да."

show eleanor rags happy at midright with dissolve

ele "Небольшая цена за свободу."
ele "Когда мы сбежим?"

ro "В течение следующего часа. Мои союзники начнут отвлекать, когда представится такая возможность."

ele "Тогда я немедленно начну готовиться."

#hide delane
"Делейн схватила мешок и исчезла за решеткой. Роуэн опустился перед ней на колени и, подняв булавку, начала ковыряться в замке камеры." 
"Это был далеко не его онавык, но Роуэна это не волновало. Давным-давно старый друг объяснил ему основные принципы этого искусства, так что он не был полным новичком." 
"Для грубых замков, которые использовали орки, ему требовалось только время." 


if delane_abduction == True:
    "Его пальцы неустанно работали, а мысли блуждали."
    "К тому времени, как взойдет солнце, Делейн будет знать, что герой Карста предал ее."
    "Она снова окажется в плену, на этот раз во власти того, чьи планы были гораздо более коварны, чем планы Улькро."
    "Ее разденут догола и используют как общественную игрушку, чтобы успокоить орков лагеря." 
    "И зелье Изеры сделает так, что она будет наслаждаться каждым моментом этого."
    if avatar.corruption > 69:
        "Это, безусловно, будет настоящее шоу, хотя он ожидал, что оно будет эксклюзивным для орков."
        "Немного обидно, если честно."
    else:
        ro "…"
        "То, что он делал на службе у Близнецов…"
        
else:
    #todo
    pass #for now
    
"К тому времени, как далекий крик пронесся по воздуху, Роуэн успел отпереть двери, и времени у него было предостаточно." 
"Камера была открыта. Отвлекающий маневр начался." 
"Это был он."


ro "Вы готовы?"

ele "Да."

#escape outfit sprite        

"Делейн вышла из камеры, ее маскировка была готова."
"С нелестным, тяжелым халатом, скрывающим ее фигуру, с ее спутанными волосамии с ее лицом, скрытым под слоем грязи, она была почти неузнаваема для гордой леди которую Роуэн знал."  
"Она даже использовала макияж, чтобы подделать себе глаза." 

show rowan necklace shock at midleft with dissolve

ro "Впечатляет. Я верю, что это одурачит большинство орков, с которыми мы встретимся."

show rowan necklace happy at midleft with dissolve

ro "Молодец"

#escape outfit angry
show eleanor rags angry at midright with dissolve

ele "Пожалуйста, не хвали меня за то, что я могу выставить себя избитым рабом."

ro "Мои глубочайшие извинения."

"Делейн усмехнулась, явно не веря ни единому его слову. "

show rowan necklace neutral at midleft with dissolve
#escape outfit neutral
show eleanor rags neutral at midright with dissolve

"Мгновение передышки, прежде чем они оба рискнут своей жизнью."
"Не желая больше терять времени, Элеонора протянула руки, и Роуэн надел кожаные 'браслеты'."
"От нее все равно не было бы никакого толку в драке, а таким образом ее маскировка была гораздо более убедительной."

ro "Пойдем."

hide rowan with moveoutleft
hide eleanor with moveoutleft
scene black with fade

scene bg26 with fade
show eleanor rags neutral at edgeright with dissolve
show rowan necklace neutral at midright with dissolve

"Они никогда не понимали, насколько наполненным жизнью может быть ночной лагерь, пока каждый проходящий мимо человек, каждый встреченный взгляд предвещал безвременную гибель." 
"Они с Делейн пересекли лагерь под покровом темноты, подальше от факелов и костров." 
"Раньше Роуэн боялся, что Делейн выдаст их – оплошает и споткнется, скорее всего, о какой-нибудь случайный колышек в земле."
"Но аристократка оказалась более компетентной, чем он ожидал. Она шла легко, без единого звука."
"Предвкушая этот день, она, должно быть, тренировалась в своей палатке, и Роуэн был глубоко благодарен ей за это. Поэтому он мог уводить ее в тень, когда это было возможно."
"Но это получалась не всегда. Время от времени им приходилось пересекать открытую местность или часто посещаемую лагерную тропу."
"Всякий раз, когда они это делали, Роуэн выпрямлял спину и надевал самое свирепое выражение лица, шагая с полной властью, которая была присуща сенешалю Бладмина." 
"Делейн опустила голову и расправила плечи, изо всех сил стараясь казаться испуганной и застенчивой, как от нее и ожидали."

if check_skill(18, 'intimidate')[0]:
    "Очевидно, они привлекли к себе некоторое внимание. Однажды Роуэн увидел, как два орка с большим интересом разглядывают грудь Делейн."
    "Один из них даже шагнул к ним – но, увидев холодный взгляд Роуэна, заколебался и попятился."
    "Не желая давать оркам возможность передумать, Роуэн схватил Делейн за руку и поспешил дальше."
    hide rowan with moveoutright
    hide eleanor with moveoutright
    
else:
    "В большинстве случаев их обман срабатывал. Орки проходили мимо них, проявляя некоторый интерес к рабу Роуэна,но его холодный взгляд обычно отпугивал их."
    "Обычно."
    "Двое орков проявили большой интерес к ее груди и, несмотря на хмурый взгляд Роуэна, начали приближаться к ним."
    if delane_abduction == True:
        "Только далеко они не дошли, пьяная оркская женщина, налетела на них."
        wo "Эй, смотри куда идешь!"
        femorc "Смотри, где стоишь!"
        "Воспользовавшись этим моментом, Роуэн схватил Делейн за руку и быстро поспешил дальше."
        hide rowan with moveoutright
        hide eleanor with moveoutright
    else:
        "Героя накрыл холодный пот. Это может быть очень опастным."
        ro "Сюда"
        "Он повернул направо, потащив с собой Делейн. Он пытался скрытся от их за большой палаткой..."
        wo "Эй! Ты!"
        "... но не успел."
        ro "(Черт возьми!)"
        "Бежать-это не выход, это только привлечет еще больше внимания." 
        "Ему придется избавиться от них по-тихому." 
        show wild orc neutral at midleft with moveinleft
        show orc soldier neutral at edgeleft with moveinleft
        ro "Что вы хотите. Разве не видите, что я занят?"
        wo "Хорошенькая у тебя рабыня, Хьюми."
        wo "Для кого она?"
        show rowan necklace neutral at midleft with moveinleft
        show wild orc neutral at midright with moveoutright
        "Один из орков подошел к нему, а другой потянулся к Делейн. Он грубо схватил ее за лицо, но Делейн не доставила ему удовольствия показав свою боль."
        wo "Выносливая. Хорошенькая. Все зубы."
        wo "Должно быть, стоит много."
        "Ситуация быстро выходила из-под контроля. Он должен был что-то сделать прямо сейчас."
        
        menu:
            "Блефовать.":
                $ released_fix_rollback()
                show rowan necklace happy at midleft with dissolve
                ro "О, она стоит многого."
                ro "Вот почему она принадлежит вождю Улькро."
                ro "Вы, два идиота, ощупываете его собственность."
                if check_skill(10, 'bluff')[0]:
                    show wild orc neutral at midleft with moveoutleft
                    show rowan necklace neutral at midright with moveoutright
                    "Орк отдернул руку, словно Делейн внезапно загорелась. Иногда правда рождает лучшую ложь."
                    wo "Я не связываюсь с рабом Улькро."
                    os "Да. Двигаем дальше."
                    show rowan necklace happy at midright with dissolve
                    ro "Спасибо. Приятного вечера, господа."
                    hide wild orc with moveoutleft
                    hide orc soldier with moveoutleft
                    "Он пожалел, что у него нет шляпы, которую он мог бы насмешливо приподнять."
                    show rowan necklace neutral at midright with dissolve
                    "Но это было близко. Слишком близко."
                    ele "Не будет ли это потом неприятностью? Они могут сказать Улькро, что видели тебя со мной."
                    ro "И признать, что они видели, как вы сбежали, и ничего не сделали? "
                    show rowan necklace happy at midright with dissolve
                    ro "Я думаю, у нас все будет хорошо."
                    show rowan necklace neutral at midright with dissolve
                    "Он позволил себе немного улыбнутся и сосредоточился на предстоящей задаче. Они еще не были близки к тому, чтобы выйти к лесу."
                    ro "Пойдем."
                    hide rowan with moveoutright
                    hide eleanor with moveoutright
                    
                else:
                    show rowan necklace angry at midleft with dissolve
                    "Два орка посмотрели друг на друга и расхохотались."
                    wo "Да, и вождь позволил тебе гулять со своими рабами." 
                    "Задача усложнилась."
                    $ event_tmp['combat roll'] = check_combat(15)[1]
                    if event_tmp['combat roll'] >= 15:
                        os "Атт... "
                        "Прежде чем второй орк успел крикнуть тревогу, Роуэн выхватил из-за пояса нож и метнул его в горло."
                        show bg26 with sshake
                        os "Гррлгх!"
                        "В яблочко."
                        "Удача была на его стороне. Лезвие ударило точно в цель, остановив его крик на полпути."
                        hide orc soldier with dissolve
                        show rowan necklace neutral at center with moveinright
                        "Роуэн быстро выхватил меч и прикончил обоих орков парой быстрых ударов. Делейн молча смотрела в сторону, бледная от страха."
                        ro "Быстро, помоги мне оттащить их в сторону."
                        "Она молча кивнула. Так как теперь они были в тени, то могли надеяться, что никто не обнаружит тела, пока они не окажутся далеко от лагеря."
                        ro "Хорошо, пошли. Вперед!"
                        hide rowan with moveoutright
                        hide eleanor with moveoutright
                        
                    else:
                        "Он выхватил меч и повернулся к другому орку."
                        os "Атака! На пом...Гах!"
                        show rowan necklace angry at midleft with moveinright
                        "Быстрым ударом он перерезал ему горло. Но тревога была поднята."
                        hide orc soldier with dissolve
                        ro "(Черт!)"
                        show bg26 with redflash
                        hide wild orc with dissolve
                        "Он быстро прикончил другого орка. Нельзя было позволить им допросить его."
                        ele "И что теперь?"
                        "План провалился.Они были слишком далеко от намеченного места выхода, чтобы успеть добраться до него прежде, чем их остановят." 
                        "Настало время для плана Б." 
                        ro "Не волнуйся об этом, просто следуй за мной."
                        "Он вложил клинок в ножны и быстро изменил направление их пути."
                        hide rowan with moveoutright
                        hide eleanor with moveoutright
                        jump desperateEscape
                    
                    
            "Убить их.":
                $ released_fix_rollback()
                "Ни секунды не колеблясь, Роуэн ударил первого орка в лицо, мгновенно вырубив его."
                hide wild orc with dissolve
                $ event_tmp['combat roll'] = check_combat(10)[1]
                if event_tmp['combat roll'] >= 10:
                    os "Атт... "
                    "Прежде чем второй орк успел крикнуть тревогу, Роуэн выхватил из-за пояса нож и метнул его в горло."
                    show bg26 with sshake
                    os "Гррлгх!"
                    "В яблочко."
                    "Удача была на его стороне. Лезвие ударило точно в цель, остановив его крик на полпути."
                    hide orc soldier with dissolve
                    show rowan necklace neutral at center with moveinright
                    "Роуэн быстро выхватил меч и прикончил обоих орков парой быстрых ударов. Делейн молча смотрела в сторону, бледная от страха."
                    ro "Быстро, помоги мне оттащить их в сторону."
                    "Она молча кивнула. Так как теперь они были в тени, то могли надеяться, что никто не обнаружит тела, пока они не окажутся далеко от лагеря."
                    ro "Хорошо, пошли. Вперед!"
                    hide rowan with moveoutright
                    hide eleanor with moveoutright
                        
                else:
                   "Он выхватил меч и повернулся к другому орку."
                   os "Атака! На пом...Гах!"
                   show rowan necklace angry at midleft with moveinright
                   "Быстрым ударом он перерезал ему горло. Но тревога была поднята."
                   hide orc soldier with dissolve
                   ro "(Черт!)"
                   show bg26 with redflash
                   hide wild orc with dissolve
                   "Он быстро прикончил другого орка. Нельзя было позволить им допросить его."
                   ele "И что теперь?"
                   "План провалился.Они были слишком далеко от намеченного места выхода, чтобы успеть добраться до него прежде, чем их остановят." 
                   "Настало время для плана Б." 
                   ro "Не волнуйся об этом, просто следуй за мной."
                   "Он вложил клинок в ножны и быстро изменил направление их пути."
                   hide rowan with moveoutright
                   hide eleanor with moveoutright
                   jump desperateEscape

            "Пусть они пощупают Делейн.":
                $ released_fix_rollback()
                $ change_base_stat('c', +2)   
                show rowan necklace happy at midleft with dissolve
                ro "Ты должен проверить ее сиськи. Она прячет большое вымя за этой одеждой."
                show eleanor rags angry at edgeright with dissolve
                ele "Что...?!"
                ele "А!"
                "Не нуждаясь ни в поощрении, ни, честно говоря, в разрешении, орк быстро засунул лапы Делейн под одежду, грубо ощупывая ее грудь."
                show wild orc neutral at midright with moveoutright
                show rowan necklace happy at midleft with moveoutleft
                wo "Ха-ха, здорово!"
                wo "Эй, зацени это!"
                "Орк окликнул своего друга,и тот быстро обернулся."
                show rowan necklace neutral at midleft with dissolve
                show bg26 with redflash
                "{i}~взмах~{/i}"
                hide wild orc with dissolve
                show bg26 with redflash
                "Первый орк даже не понял, что происходит, когда Роуэн перерезал ему горло."
                "Второй успел открыть рот, чтобы выкрикнуть предупреждение, но обнаружил, что его голос слегка ослаблен восемью дюймами острой стали."
                hide orc soldier with dissolve
                ro "Помоги мне спрятать тела. Мы в тени, так что они не должны их найти, пока не станет слишком поздно."
                ele "..."
                show rowan necklace concerned at midleft with dissolve
                ro "… Прости, что использовал тебя в качестве приманки."
                ele "Извинения приняты. Давайте никогда больше не будем говорить об этом."
                show rowan necklace neutral at midleft with dissolve
                show eleanor rags neutral at edgeright with dissolve
                "У них не было времени, чтобы обменяться своими мнениями на это. Время было очень важно."
                hide rowan with moveoutright
                hide eleanor with moveoutright


        
scene black with fade
scene bg26 with fade

"Через полчаса, прошедших в полном напряжении, внешние башни лагеря оказались в пределах их видимости."
    
    
if delane_abduction == True:
    "Они приближались к краю лагеря со стороны Улькро. Земля, где все три фракции граничили друг с другом, служила своего рода неофициальной “ничейной землей”."
    "Ближе к центру лагеря происходили большинство фракционных споров. Но по краям лагеря местность в основном оставалась без присмотра." 
    "Именно там охранники были наименее внимательны, если вообще были."  
    "Именно там ограждение были либо самыми слабыми, либо несуществовали вообще."
    "Как только они доберутся до него, агенты Тариш смогут вывести их из лагеря без помех."

    show rowan necklace happy at midleft with dissolve
    "Роуэн повернулся так, чтобы Делейн могла видеть его лицо, и ободряюще улыбнулся ей. - Почти приехали, - произнес он одними губами."

    #escape outfit happy
    show eleanor rags happy at midright with dissolve

    "Он увидел, как она улыбнулась в ответ, и в ее глазах появились слезы."
    "Она всегда делала все возможное, чтобы сохранить самообладание, и, учитывая ее происхождение, у нее не было особых проблем с этим." 
    "Но свобода была так близко, что даже она не могла сдержать свои эмоции."
    "Она быстро вытерла слезы и снова прошептала “Спасибо” своему герою. Ее рыцарю в сияющих доспехах." 
    "Который собирался передать ее соперничающему вождю орков." 

    ro "(...)"

    "Их ждали агенты Тариш."

    menu:
        "Доставь Делейн к Тариш.":
            $ released_fix_rollback()
            jump tarishEscapeFinale
            
        "Спаси Делейн, найди другой выход." if avatar.corruption < 80:
           $ released_fix_rollback()
           "Он не мог этого сделать. Передача Делейн Тариш противоречила всему, что он когда-либо защищал."
           if avatar.corruption > 50:
               "Как бы низко он ни падал, как бы много своей души не отдавал в жертву Близнецам, некоторые вещи просто заходили слишком далеко."
           else:
                pass
           ro "Изменение планов. Мы пойдем другим путем."
           "Он быстро сменил направление движения, увлекая за собой Делейн. Это было глупо. Он знал, что ведет себя глупо." 
           "Но он скорее будет дураком, чем безмозглым орудием в руках зла."
           $ tarishBetrayed = True
           jump desperateEscape


else:
    "Они приближались к краю лагеря Улькро, рядом с землей Тариш."
    "Рядом с центром, это было бы довольно оживленное место. Своего рода 'ничейная земля', где все три фракции решали свои споры." 
    "Но по краям лагеря местность в основном оставалась без присмотра."
    if delaneEscapePerimeter ==1:
        "И именно здесь стояла пустая сторожевая башня."
        jump escapeSoloFinale
    else:
        "Вот куда Матрона велела им идти."
        jump escapeMatronFinale
         
        

label tarishEscapeFinale:

if avatar.corruption > 69:
    "'Пожалуйста' - одними губами ответил он и продолжил путь."
        
else:
    "Роуэн сжал челюсти и отвернулся, чтобы эмоции не выдали его."
    "Все это было ради общего блага."

"Ничейная земля была последней частью их побега. С другой стороны Роуэн увидел две фигуры в плащах-шпионов Тариш, посланных убедиться, что все в порядке."  
"Они были не нужны. Они дошли, и никто из орков Улькро не обратил на них внимания."  
"Орки в плащах продолжали смотреть прямо перед собой. Когда они прошли мимо них, один из орков развернулся и пошел за ними."
"Делейн, заметив это, в панике дернула Роуэна за плащ."

#escape outfit concerned
show eleanor rags concerned at midright with dissolve

ro "Расслабься. Они же союзники."

"Она не выглядела успокоенной его словами, возможно подсознательно понимая, что что-то было не так."
"На краю лагеря он встретил еще одного агента, на этот раз поджидавшего его с четырьмя лошадьми.  Роуэн посадил Делейн на одну и взял другую." 
"Они должны были держать Делейн подальше от лагеря, на случай, если Улькро решит перевернуть все вверх дном, прежде чем бросить вызов Батри."

femorc "Какие-нибудь осложнения?"

ro "Нет, все прошло гладко."

femorc "Хорошо."

"Делейн выглядела так, словно хотела задать еще несколько вопросов, но в конце концов промолчала." 
"Они покинули лагерь без спешки. Ни тревоги, ни шума."
"Судя по тому, что Тариш рассказала ему, пройдет несколько часов, прежде чем Улькро узнает, что Делейн похищена - Батри, как скажут ему агенты, которые будут рядом с ним."
"Делейн по-прежнему держалась особняком, ее энтузиазм угас. Роуэн ехал впереди, а два агента рядом, и ей оставалось только следовать за ними в лес."

if avatar.corruption > 69:
    "Она знала. Она видела, как ее 'герой'' ухмыляется, когда они покидают лагерь, и видела, как их “союзники” наблюдают за ней. Это не было спасением."
    "Ее обманули." 

else:
    "Она знала. Она сразу поняла, потому как Роуэн избегает ее взгляда. Это не было спасением." 
    "Это было обманом." 

"У леса их ждали еще два орка. С цепями и кляпом наготове."

ele "В этом нет необходимости. Теперь я вижу, что никто не придет мне на помощь, даже если я закричу."

#escape outfit neutral
show eleanor rags neutral at midright with dissolve

"Она не повысила голоса, только поправила осанку, расправив плечи и высоко подняв голову."
"Когда ее надежда рухнула, достоинство было последним, что у нее еще оставалось, и она не собиралась отказываться от него, даже при таких обстоятельствах."
"Проявление благородства было последним, что она еще могла сделать." 
"Теперь, как бы ни хотелось Роуэну провести остаток ночи, чувствуя на своей спине ее осуждающий взгляд, его миссия была завершена. Все остальное было в руках Тариш." 

ro "Скажи своему боссу, что я буду ждать ее сообщения."

femorc "Да, пока что держись подальше от лагеря. Там вот-вот станет горячо."

ro "Мягко говоря."

"Он был не в настроении шутить. Роуэн направил лошадь вперед и оставил Делейн на произвол судьбы."

$ delane_status = "tarish"
$ change_base_stat('c', +10)   

return

label escapeSoloFinale:

scene bg26 with fade

"Каждый шаг приближал их к свободе. 100 метров. 80 метров. 50 метров." 
"Башня была видна Роуэну совершенно отчетливо. Она была пуста." 

show rowan necklace neutral at midleft with moveinleft
show eleanor rags neutral at edgeleft with moveinleft

ro "Не высовывайся. Нас все еще могут заметить другие охранники."

"Она кивнула, все ее тело напряглось. Так близко."

hide rowan with moveoutright
hide eleanor with moveoutright

"Но им незачем беспокоиться. Никакой тревоги не прозвучало. Орки были болезненно небрежны, как и всегда."
"Они покинули лагерь, и никто даже глазом не моргнул в их сторону."
"..."

jump escapeSex


label escapeMatronFinale:

"Матрона ждала их у одной из башен на краю лагеря Улькро."
"Ее сопровождала группа женщин."

show rowan necklace neutral at midleft with moveinleft
show eleanor rags concerned at edgeleft with moveinleft

"Делейн напряглась, когда внимание орков сосредоточилось на ней, без сомнения опасаясь, что их обнаружат."

ro "Не беспокойся. Они на нашей стороне."

"План 'побега' Матроны оказался очень простым. Она приказала им обоим не высовываться, и вместе с остальными вывела группу из лагеря."
"Орки не имели понятия о КПП. До тех пор, пока они видели знакомое лицо, которому могли доверять, они не обращали внимания на то, что то лицо делало." 
"И вот Роуэн увидел, что охранники наблюдают за их шествием, но ни один из них не проявил к ним хоть малейшего интереса." 

ro "Не могу поверить, что это так просто...."

orcm "Только потому, что ты со мной."

"Матрона бросила короткий взгляд на дворянку. С ее рабской маскировкой она выглядела не очень впечатляюще, но ее не обмануть такими трюками." 
"Ее глаза сузились и она продолжала пялиться на нее." 

orcm "Я не знаю, что смешнее."
orcm "Что ее киска стоит целого племени для демонов."
orcm "Или то, что для тебя она еще важнее."

ele "Что она сказала?"

show rowan necklace concerned at midleft with dissolve

ro "Ничего!"
ro "(Боже правый....) "

scene black with fade
"..."

scene bg3 with fade
show rowan necklace neutral at midleft with moveinleft
show eleanor rags neutral at edgeleft with moveinleft

orcm "Мы расстаемся здесь."
orcm "Мои спутницы сделают несколько ложных следов. Поможет сбить с толку людей Улькро."

show rowan necklace happy at midleft with dissolve

ro "Я думаю, ты недооцениваешь, как влюбленный Улькро относится к Делейн."

show rowan necklace neutral at midleft with dissolve

ro "Он может послать за нами весь лагерь."

orcm "Возможно."

"Она выглядела не слишком обеспокоенной."

ro "(Если они поймают нас, ей будет еще лучше.)"

"Им повезло, что его оценка ее доказала свою правоту. Она была женщиной чести. Она должна была довести дело до конца."

orcm "Не пойми меня неправильно, но я надеюсь, мы больше никогда не увидимся."

show rowan necklace happy at midleft with dissolve

ro "Это чувство взаимно." 

orcm "Удачи, Роуэн."

show rowan necklace concerned at midleft with dissolve

ro "… ?"
ro "(Когда я...?)"

ele "Что-то не так, Роуэн?"

ro "..."

show rowan necklace neutral at midleft with dissolve

ro "Нет. Пошли."

hide rowan with moveoutright
hide eleanor with moveoutright

scene black with fade

"..."

jump escapeSex


label desperateEscape:

$ desperateEscape = True

scene bg26 with fade
show rowan necklace angry at midleft with dissolve
show eleanor rags concerned at edgeleft with dissolve

ro "(Черт, черт, черт!)"

"Благодаря организованному отвлечению внимания и позднему часу, их путь был в основном пуст. Роуэн ускорил шаг, едва не сбившись с правильного пути."

if tarishBetrayed == True:
    "Каждая секунда на счету. Кто-то забил тревогу, и орки уже покидали свои палатки. Тариш не позволит ему так легко сбежать."
    
else:
    "Сигнализация в самом разгаре, каждая секунда на счету. Многие орки уже покинули свои палатки, проверяя, что происходит." 
    "Никто из них еще не остановил Роуэна и Делейн, но это был лишь вопрос времени, когда кто-нибудь это сделает."  

"Неважно. Его цель уже была видна: одна из лагерных конюшен." 
"Орки редко использовали кавалерию, но в их распоряжении было несколько лошадей. Пока... " 

ele "Роуэн!"

if tarishBetrayed == True:
    "В последний момент он заметил женщину-орка. С кинжалом в руке она прыгнула на него, пытаясь ударить прямо в сердце."
    if check_stat(25, 'strength')[0]:
        show bg26 with sshake
        "Не успев увернуться, он должен был блокировать атаку."
        "Несмотря на огромную разницу в силе между ними, он каким-то образом ухитрился схватить ее запястье обеими руками, остановив лезвие всего в дюйме от своего тела." 
        "Орк зарычала от ярости и бросилась на него всем телом, но теперь, когда элемент неожиданности исчез, у нее не было ни единого шанса." 
        ro "Вы...! "
        "Он вывернул ей руку и ударил под подбородок. Ее защита ослабла, и он быстро воспользовался моментом, чтобы вырвать кинжал из ее руки и вонзить его в шею."
        ele "Роуэн, ты..."
        ro "Я в порядке, вперед, быстрее!"
        
    else:
        "Не успев увернуться, он должен был блокировать атаку. Он схватил ее за запястье в самый последний момент..." 
        show bg26 with sshake
        show bg26 with redflash
        "Только для того, чтобы орк толкнул его всем своим телом вперед, преодолевая его блок и нанося удар в плечо."
        ro "Нгггх...!"
        ele "Роуэн!"
        femorc "Неужели ты действительно думаешь, что ты... Арррхх!"
        "Он без всякого раскаяния сунул пальцы ей в глаза. Она не отпустила его, но после того, как последовал удар под подбородком, ее хватка наконец ослабла."
        show bg26 with redflash
        "Ему нужно было лишь мгновение, чтобы вытащить свой собственный кинжал и закончить работу."
        ele "Роуэн, у тебя кровь идет!"
        "Рана горела, как в аду. Лезвие вошло глубоко - он почувствовал, что задета кость." 
        "Но у него не было времени оценить ущерб. Он вырвал клинок, подавив крик боли, и отбросил прочь." 
        ro "Это...хм ... ничего."
        ro "Не стой вот так, вперед, быстрее!"
        $ delaneEscapeWound = True
        
else:
    show wild orc neutral at edgeright with moveinright
    "Наконец то один из орков оказался достаточно умен, чтобы соединить точки. С двумя топорами в руках он прыгнул на Роуэна словно из ниоткуда."
    "Не успев увернуться, Роуэн схватил нападавшего за запястья, с легкостью остановив атаку. Орк был меньше, чем большинство воинов его вида."
    show bg26 with sshake
    "Орк ударил Роуэна головой в лицо."
    if check_stat(10, 'strength')[0]:
        ro "Nnn-!"
        "Несмотря на боль, Роуэн не упал и бесцеремонно ткнул орка коленом прямо в пах."
        "Его противник оказался куда менее стойким, чем он сам. Он сгорбился, давая Роуэну достаточно времени, чтобы вырвать топор из руки орка и вонзить его в череп."
        hide wild orc with dissolve
        ele "Роуэн, ты..."
        ro "Я в порядке, вперед, быстрее! "

    else:
        "Атака была недостаточно сильной, чтобы вырубить его, но она дезориентировала его. Он почувствовал, что его хватка ослабла..."
        ele "Берегись!"
        "...и орк вырвал свою руку."
        show bg26 with sshake
        show bg26 with redflash
        "Топор ударил его в плечо, легко пробив броню и глубоко войдя в тело."
        ro "Нннн...! Ты....! "
        "Боль помогла ему прийти в себя. Он ударил орка локтем в лицо, возвращая ему должок." 
        "Орк отшатнулся, давая Роуэну достаточно времени, чтобы выхватить меч и пронзить противника насквозь."
        ele "Роуэн, у тебя кровь идет!"
        "Рана горела, как в аду. Лезвие вошло глубоко - он почувствовал, что задета кость." 
        "Но у него не было времени оценить ущерб. Он вырвал его, подавив крик боли, и отбросил прочь." 
        ro "Это...хм....ничего."
        ro "Не стой вот так, вперед, быстрее!"
        $ delaneEscapeWound = True


"Их схватка длилась несколько секунд,но этого было достаточно, чтобы орки посмотрели в их сторону. У них было мало времени."
"Сделав пару быстрых прыжков, Роуэн уже был у входа в конюшню. Двумя быстрыми ударами он расправился со все еще сбитыми с толку охранниками, и Делейн догнала его." 

ro "Ты умеешь ездить верхом?"

ele "Немного.... "

show eleanor rags neutral at edgeleft with dissolve

ro "Да!"

"Этого недостаточно. Тогда им понадобится одна лошадь."
"Он быстро оценил то, что было доступно им. Их выбор был довольно ограничен, но это не имело значения."
"Он нашел самого большого и сильного жеребца, на которого он мог положиться, и подвел их к нему." 

ro "Мы возьмем вот этого. Я поеду верхом."
ro "Нам нужно только немного отъехать от лагеря, а потом мы пойдем пешком и скроемся в горах."

"Он перерезал путы Делейн и приказал ей сесть позади него."

ro "Держись крепче."

scene bg26 with fade
show rowan necklace angry behind bg26

"Они выскочили из конюшни, растоптав двух орков, которым не повезло: а) родиться в том же время, что и Роуэн, и Б) встать у него на пути."

ro "Держи голову низко! И не смотря ни на что, не отпускай!"

"Они миновали сторожевые вышки и по прямой направились к лесу. Это был Роуэн Блэкуэлл. Пока у него есть время, он сможет замести их следы.."
"Одинокая стрела ударила в землю рядом с ними. Другая прошла над его ухом." 
"Следующая в лошадь." 

ro "Что за... успокойся, мальчик, успокойся!"

if delaneEscapeWound == True:
    "Он попытался обуздать лошадь, но в тот момент, когда он натянул ремни, его левая рука взорвалась от боли."
    "Лошадь встала на дыбы, сделав их легкой мишенью для орков." 
    jump desperateEscapeFail
    
else:
    if check_skill(15, 'survival')[0]:
        "С титанической силой он натянул поводья, не давая жеребцу встать на дыбы."
        jump desperateEscapeSuccess
        
    else:
        "Он попытался обуздать лошадь, но зверь не слушал его. Лошадь встала на дыбы, сделав их легкой мишенью для орков."
        jump desperateEscapeFail
    
label desperateEscapeSuccess:

ro "Головокружительно!"

"Стрелы продолжали сыпаться на них дождем,но мало кто из орков практиковался в стрельбе из лука." 
"Ни один из них не попал в свою цель, и вскоре Роуэн и Делейн оказались вне их досягаемости."
"…"

jump escapeSex

label desperateEscapeFail:

show bg26 with sshake
show bg26 with sshake
show eleanor rags concerned behind bg26

ele "Ннгх! НН!"

ro "Делейн?"

ele "Я в порядке!"
ele "Бежим!"

"Ее голос дрогнул. Роуэн опасался самого худшего." 
"Стрелы продолжали сыпаться на них дождем, не оставляя герою времени ни о чем другом беспокоиться."
"С титаническим усилием ему удалось взять лошадь под контроль и направить ее в сторону леса." 
"Все, что ему было нужно, - это время. Тогда ... он бы убедился, что все в порядке." 
"Хватка Делейн ослабла."
"…" 

jump delaneDeath


label escapeSex:

scene bg31 with fade

show wild orc neutral at center with moveinleft
show orc soldier neutral at edgeleft with moveinleft

wo "Следы уходять в сторону."

"Три орка-следопыта оглядели широкую полосу скалистых холмов, которые находились почти в пяти милях от лагеря орков. Наверняка следопыты пошли и в других направлениях, пытаясь выяснить, куда делся их пропавший пленник."
"И, возможно, это сработало бы, если бы они не пытались найти одного из лучших следопытов и разведчиков того времени."

os "Много пещер. Много скрытых дыр."

wo "Может быть. Но, много пещер, чтобы проверить. Много мест, чтобы еще поискать."

"Орки мрачно согласились двигаться дальше. Слишком мало, что они могли сделать здесь."

hide wild orc with moveoutright
hide orc soldier with moveoutright

"Пара глаз, наблюдающие из выбранной пещеры, смотрели за их уходом. Они в  безопасности."

show eleanor rags happy at edgeleft with dissolve
show rowan necklace concerned at midleft with dissolve

ele "Они ушли?"

ro "Пока. Они двинулись дальше, но возможность, что они вернутся с подмогой реальна."

if desperateEscape == True:
    "Тот факт, что они послали отряды, чтобы выследить их, не вызывал особого беспокойства."
    "Он планировал это заранее, и дополнительное время, полученное от того, что он не поднял тревогу, позволило ему более обдуманно сбежать с меньшим количеством следов."
    "Шансы на то, что их обнаружат, были весьма малы."

    if delaneEscapePerimeter == 2:
        "Особенно с фальшивыми следами Матроны. Это было чудом, что им удалось забраться так далеко."
        
    else:
        pass
else:
    pass
    
"Делейн прислонилась к стене пещеры. Они разбили небольшой лагерь глубже, где любой свет, который они могли бы создать, не был бы замечен снаружи."

ro "Мы должны оставаться здесь в течение следующих нескольких часов. По крайней мере, до сумерек. Передвигаться в дневное время слишком опасно."

"Она молча кивнула на предложение Роуэна и последовала за ним в глубь пещеры, где их ждал костер." 
"Они обнялись, чтобы согреться в темном кишечнике пещеры."

ele "Полагаю... я теперь свободна, не так ли?"

ro "Полагаю, да. Каково это?"

"Делэйн сделал паузу, чтобы подумать об этом. Ночная усталость все еще давила на нее."

ele "Было бы намного лучше, если бы я не была заперта в пещере. Из одной тюрьмы в другую."

show rowan necklace happy at midleft with dissolve

ro "Вы никогда не будете счастливы."

"Делэйн засмеялась."

ele "Я полагаю, различие в том, что из бывшей я могла сбежать только на постели орка."
ele "Из этой я просто должна подождать наступления темноты, не создавая слишком много суеты."

ro "Невыполнимая задача. Я знаю."

"Она ударила его локтем по ребрам."

ele "Тем не менее, я должна поблагодарить тебя за все это. Правда. Ты - это все, о чем рассказывали и многое другое."

show rowan necklace concerned at midleft with dissolve

if tarishBetrayed == True:
    "Роуэн слабо улыбался. Она не могла быть дальше правды. Она никогда не узнает, как близко он был к тому, чтобы отдать ее Тариш." 
    "В конце концов, он поступил правильно. Он доказал, что человек, которого называли Героем Карста, был кем-то больше, чем скромным генералом или рабом демонических сил."
    "Доказано Делейн и самому себе." 
    "Но он не мог избавиться от темных мыслей, которые сейчас мучали его разум."
    "Если бы он был так близок к тому, чтобы выпасть с фургона, как он справиться с трассами, проложенными впереди? Ожерелье все еще висело на шее, как якорь, который тащил его дальше во тьму."
    "Он все еще был прикован цепями к близнецам." 
    "Сможет ли он сделать правильный выбор и в следующий раз?"

else:
    if avatar.corruption < 30:
        "Роуэн слабо улыбнулась. Даже слышать, как кто-то говорит это, было для него чем-то вроде удивления или удара под дых."
        "Он был пленником близнецов уже столько месяцев. А до этого он был изгоем."
        "На этот краткий миг Роуэн снова стал героем шести миров. Не тем человеком, который ничего не сделал, пока мир горел вокруг него."
        "Но потом этот момент прошел."

    else:
        "Роуэн слабо улыбнулся. Мысль о том, что говорила леди Делейн, показалась ему почти абсурдной."
        "Он спас ее, чтобы быть уверенным. Он мог быть героем, как все когда-то называли его. Но она не понимала его. Она не понимала, что он сделал, чтобы добраться до этого момента."
        "Она не понимала, на что он был готов пойти, чтобы продолжить путь слуги."
        "То, что она сказала, прозвучало с благодарностью, но для ушей Роуэна все это звучало как его собственная ложь."

"После этого часы начали тикать. Дров было предостаточно, и Роуэн приготовил еду. Поэтому они никогда не были в темноте или несчастны."
"Но самым утомительным было однообразие. Несколько раз Роуэн украдкой ходил проверять вход, чтобы определить время суток."
"То, что сначала было приятным разговором, превратилось в долгие периоды тишины. Они оба были слишком утомлены и слишком сосредоточены на том, чтобы убежать от орков."
"Но это было не единственное, на чем были сосредоточены их мысли. Или, по крайней мере, не для Леди Делейн." 
"Роуэн удивился, когда примерно через три часа после начала их пребывания в пещере Леди Делейн положила голову ему на плечо."
"Интимность этого контакта была удивительной. Но он не отмахнулся от нее и не стал ее отговаривать. Она через многое прошла."
"Но в тусклом свете костра Роуэн разглядел ее устремленные на него глаза. В них была не просто благодарность."
"Еще некоторое время прошло в молчании. Оно сломалось только тогда, когда Роуэн почувствовал руку на своей ноге. Он повернулся к Элеоноре и увидел ее лицо в опасной близости от своего."

ele "Ты так много для меня сделал. Помог мне сохранить решимость. Ты дал мне надежду на что-то. А теперь ты пошел еще дальше. Ты спас меня."

ele "Если бы не ты, я бы, наверное, умерла в том лагере."

"Ее рука скользнула вверх по его ноге."
"Они обменялись взглядами. Ее намерения не могли бы быть яснее, даже если бы она произнесла их вслух."
"Если бы он хотел этого, она позволила бы ему овладеть собой. Вопрос был только в том, хочет ли он этого."

menu:
    "Отказать ей.":
        $ released_fix_rollback()
        "Прежде чем это продолжилось, Роуэн покачал головой и осторожно убрал руку Леди Элеоноры со своей ноги. Он был здесь, чтобы спасти ее, а не, чтоб трахнуть ее."
        "Тем более он никак не мог заставить себя сделать это."
        "Она дважды моргнула от этого внезапного отказа, но не стала настаивать. Это была логичная ситуация. Она все поймет."
        jump escapeSexConclusion
        
    "Переспать с Делейн.":
        $ released_fix_rollback()
        $ eleanorCaveSex = True
        "Часть мозга Роуэна напомнила ему об их ситуации. Они были одни в лесу, без прикрытия, и если их обнаружат, им грозит серьезная опасность."
        "Но здесь, в пещере, они были в относительной безопасности. Вероятность быть услышанным будет минимальной."
        "У них было время. Почему бы не повеселиться?"
        #noble reward CG#1
        scene cg407 with fade
        show eleanor rags happy behind cg407
        show rowan necklace aroused behind cg407
        pause 3
        "Роуэн не сделал ничего, чтобы остановить движение ее руки, даже когда она ползла все выше и выше. При первом же прикосновении ее пальцев к внешней ткани, покрывающей его промежность, он резко выдохнул."
        ele "Я просто хочу поблагодарить тебя за все, что ты для меня сделал. Я хочу…"
        "Она медленно развязала его пояс и протянула правую руку к его члену. Он застонал от ощущения ее нежной кожи на своем члене."
        ele "Я хочу дать тебе какой-то знак моей признательности…"
        "Позади них тени на стене пещеры отражали их движение. Тень Делейн становилась все ближе к нему, пока они не слились в одну."
        "Но когда она положила руки на его член, и он откинулся назад, тени снова разделились."
        ro "Ах...."
        "Он почувствовал трение ее руки, когда она начала дрочить ему. В мгновение ока член перешел от полустоячего состояния к напряженному."
        "В глубине души Роуэн лениво осознал, что он не первый мужчина, которому она дрочила."
        "Но любопытство к сексуальным походам молодой леди отодвинулись на задний план, уступив место грубым ощущениям."
        "Движение руки. Она наклонилась ближе, с нетерпением ожидая его реакции. Движение руки. Его глаза слегка затрепетали. Движение руки."
        ro "Подожди."
        "Рука Делейн замерла на месте."
        ro "Не рукой."
        "Леди Делейн тихо сглотнула. Сексуальные отношения с аристократкой считались запретными для простолюдина. И просить о сексе."
        "И все же ответом Делейн был кивок. Для того, чтобы начать секс с простолюдином, который был так же близок ей, как и наряд с ее головы."
        "Очевидно, классовые различия мало что значили для двух возбужденных травмированных людей, прячущихся в пещере."
        ele "Хорошо. Не моей рукой. Но тебе придется лечь на спину, хорошо?"
        "Роуэн уже откинулся назад, но по просьбе леди Элеоноры принял позу на спине, его член все еще стоял и теперь указывал вверх."
        "Можно было предположить, что дама умела ездить верхом."
        #noble reward CG#2
        scene cg341 with fade
        show eleanor naked aroused behind cg341
        show rowan necklace aroused behind cg341
        pause 3
        "Леди Элинор встала. Ее тело блестело в свете костра от пота. Затем с некоторым опасением она медленно опустилась вниз."
        "Он почувствовал что-то влажное и горячее на самом кончике своего члена, и когда она опустилась, это чувство поползло вниз вместе с ней."
        "Его руки впились в ее бедра. Он увидел, как выражение ее лица сменилось судорожным вздохом. Она  была обрамлена светом костра."
        "Теперь тень на стене показывала единственную фигуру, хотя и с широким основанием."
        ele "Т...трахай."
        "Ее бедра качались и дергались. Сначала медленно, но ей не потребовалось много времени, чтобы найти свой ритм."
        "Это движение хотя и не практиковалось, имело изящество того, кто делал его раньше. Вскоре она уже прыгала вверх и вниз на его члене в постоянном ритме."
        "Она громко вскрикнула, откинув голову назад."
        ro "Мы должны...молчать…"
        "Она покраснела."
        ele "Правильно. Извини."
        #noble reward CG#2 var 2
        show cg342 with dissolve
        pause 0.1
        show cg343 with dissolve
        pause 0.1
        show cg344 with dissolve
        pause 0.1
        show cg345 with dissolve
        pause 3        
        "Разговор сбил ее с ритма, но ей потребовалось всего несколько секунд, чтобы восстановить его. Катаясь и подпрыгивая, она бросалась на его член."
        "Только на этот раз одной рукой она прикрыла рот, чтобы не быть слишком громкой."
        "Ногти Роуэна впились в ее тело. Каждый поворот ее бедер приносил с собой все больше удовольствия. Еще больше трений. Еще одно движение его члена внутри нее."
        "Ему тоже пришлось заглушать свои звуки."
        "Ярко освещенный мерцанием костра. Прерываемые лишь редкими стонами и ворчанием, они сжимали свои губы."
        "Движение их тел было отчаянным и усталым. Секс скрывающихся и испуганных."
        ele "Я собираюсь…"
        ro "...Шшшшш"
        show cg346 with dissolve
        pause 0.1
        show cg347 with sshake
        pause 0.1
        show cg347 with sshake
        pause 0.1
        show cg348 with dissolve
        pause 3
        "Леди Делейн прижала обе руки ко рту, чтобы заглушить долгий стон, который она собиралась издать. У нее начались судороги, и она упала на него с последним всплеском энергии."
        "Взрыв, которого, в свою очередь, было достаточно, чтобы отправить его через край вслед за ней."
        "Он задохнулся и почувствовал, как его семя извергается. Мгновение спустя он упал на землю, и она повалилась на него."
        "Их дыхание поднималось и опускалось почти одновременно, и ее дыхание касалось его шеи."
        ele "Я ждала, когда ты это сделаешь, с тех пор, как ты впервые пришел."
        ro  "Так рано сдалась?"
        "Делейн слабо усмехнулся."
        ele "Простишь ли ты за это, рыцарь в сияющих доспехах?"
        ro "Миледи, я не рыцарь."
        ele "Это хорошо. Если бы это было так, ты бы знал лучше, чем рискуешь спя с кем-нибудь выше себя."
        "Они прижались к друг другу. Роуэн потрудился пошевелиться только тогда,когда увидел, что огонь почти потух."
        
 
label escapeSexConclusion:

scene black with fade
show rowan necklace neutral behind black

"Через некоторое время Роуэн снова проверил вход в пещеру. Сгустились сумерки, и видимость стала низкой. Пришло время двигаться."

ro "Двигайся быстро, веди себя тихо. Не разговарива без крайней необходимости. Если мы будем идти ровным шагом, то к ночи доберемся до крепости."

scene bg31 with fade

"Следующие часы тянулись мучительно медленно, так как каждая тень за каждым деревом и каждый хруст листьев сигнализировали об опастности."
"Но, по правде говоря, это было довольно скучно. Медленные осторожные движения, напряженные нервы и отсутствие встреч."
"Когда рассвело, Роуэн заметил вдали небольшую крепость. Их цель."
"Это была простая крепость, но она должна была помочь Делейн воссоединиться со своей семьей в целости и сохранности."

show rowan necklace happy at midleft with dissolve
show eleanor rags happy at edgeleft with dissolve

"Он остановил ее на обочине дороги."

ro "Здесь наши пути расходятся. Мне нужно вернуться, чтобы мое отсутствие не заметили. Но, а тебе нужно идти."
ro "Если возможно, то до самого Растедейля. В столице должны знать, что положение в провинциях крайне тяжелое."

"Она кивнула, но он уловил вспышку грусти в ее глазах."

ele "Я сделаю так, как ты скажете. Ни у кого нет больше опыта общения с неистовыми орками, чем у меня."

"Роуэн сомневался, что это правда."

ele "И перспектива воссоединения с моей семьей-это то, от чего я не могу отказаться ни на минуту."
ele "А что насчет тебя? С тобой все будет в порядке?"

ro "Тебе не нужно беспокоиться обо мне. Я выживу. Я уже многое пережил. Я думаю, что скоро снова окажусь в Растедейле. Тогда мы сможем поговорить."

"Делейн кивнула и удивила его долгим объятием."
"Когда его отпустили, Роуэн повернулся и без слов попрощался с ней. В голове у него уже крутились другие мысли."

show rowan necklace concerned at midleft with dissolve

"Легкая часть была закончена. Без Делейн его попытки объеденить различные группировки орков оказались под серьезной угрозой."
"Вернувшись в Бладмин, Андрас и Изера почти наверняка разозлятся из-за сложившейся ситуации, даже если не поймут, какую важную роль сыграл Роуэн в этой неудаче."
"Тем не менее, он шел назад с более высокой головой, чем раньше."
"Он был немного больше похож на человека, который впервые появился в Бладмине, так обманчиво недавно."

$ delane_status = "free"
$ change_base_stat('c', -10)   
$ change_base_stat('g', -10) 

return

label delaneDeath:

scene bg31 with fade

show wild orc neutral at center with moveinleft
show orc soldier neutral at edgeleft with moveinleft

wo "Следы уходять в сторону."

"Три орка-следопыта оглядели широкую полосу скалистых холмов, которые находились почти в пяти милях от лагеря орков."
"Наверняка следопыты пошли и в других направлениях, пытаясь выяснить, куда делся их пропавший пленник."
"И, возможно, это сработало бы, если бы они не пытались найти одного из лучших следопытов и разведчиков того времени."

os "Много пещер. Много скрытых дыр."

wo "Может быть. Но, много пещер, чтобы проверить. Много мест, чтобы еще поискать."

"Орки мрачно согласились двигаться дальше. Слишком мало, что они могли сделать здесь."

hide wild orc with moveoutright
hide orc soldier with moveoutright

"Пара глаз, наблюдающих из пещеры, наблюдали за их уходом. Это означало безопасность. Это дарило надежду."

"А последнее, в частности, было в дефиците." 

show rowan necklace concerned at midleft with dissolve

ro "Они ушли."

"Хриплый, отдаленный звук откуда-то из глубины пещеры был для него единственным ответом. Делейн не могла подойти и посмотреть, как уходят орки." 
"У нее едва хватало сил оставаться в сознании."  
"... Он потерпел неудачу. Он обещал доставить ее в безопасное место, но потерпел неудачу. Во время их побега две стрелы поразили ее в спину. Одну он смог удалить." 
"Другая добралась до ее легкого."

if check_skill(10, 'survival')[0]:
    "Он проклял свои познания в медицине. Он знал, что удаление стрелы, скорее всего, приведет к коллапсу легкого, и это быстро убьет ее." 
    "Ему нужно было место, где она могла бы отдохнуть, и квалифицированный медик, который мог бы помочь ему стабилизировать ее состояние." 
    "Но ни на то, ни на другое у него не было под рукой."  
    
else:
    "Он знал кое-что о первой помощи, но такая рана ... была выше его сил. Ей нужен был опытный медик." 
    "Но его ни где не было видно." 

"Тяжело сглотнув, он прошел дальше в пещеру, где разбил лагерь. Он велел Делейн лечь на бок, поближе к огню."

ro "Постарайся не говорить. Ты сделаешь только хуже."

"И снова хриплый звук был его единственным ответом, но на этот раз он увидел ее пристальный взгляд и почти расслышал резкую реплику. Все месяцы плена не смогли сломить ее духа."
"И смерть тоже." 
"…"
"Он не мог ей сказать. У него не хватило духу." 
"Но она чувствовала ее приближение. Она не покинет эту пещеру живой." 

if tarishBetrayed == True:
    "И это была его вина. Если бы он не встал на сторону Тариш, если бы не колебался в своих убеждениях – возможно, все сложилось бы иначе."
    $ change_base_stat('g', +5) 
    
else:
    "И это была его вина. Если бы только он был лучше. Быстрее. Сильнее." 
    "Если бы только он был героем, за которого все его принимали. "

"Он опустился на колени рядом с ней. У них было много еды, но она отказалась есть что-либо. Она просто смотрела в никуда, иногда морщась." 
"Он не мог оставить ее в таком состоянии. Он потянулся к одному из бюрдуков с водой и поднес его к ее губам."  

ro "Выпей. Вы не должны быть обезвоженной. "

show eleanor rags concerned behind bg31

if ulcro_path > 1:
    "Она медленно покачала головой. На большее у нее не было сил."
    ele "Надо было… "
    "Ее лицо исказилось от боли, но она отказалась подчиниться ей. "
    ele "Выбрать... У… "
    ele "Улькро… "
    "Ее затуманенные глаза обвиняюще уставились на него. Роуэн сунул ей в рот бюрдук с водой, заставляя выпить хотя бы немного."
    $ change_base_stat('g', +3)
    
else:
    "Она не выказала никаких признаков того, что заметила воду в руке Роуэна. Ее губы шевелились, но не для того, чтобы пить."
    ele "В розыске… "
    "Ее лицо исказилось от боли, но она отказалась подчиниться ей."
    ele "заваруха…"
    ele "столько…"
    "Ее затуманенные глаза нашли его, ища хоть какое-то утешение. Роуэн попытался улыбнуться, но тут же почувствовал, что улыбка его дрогнула." 
    "Ее собственный рот слегка дернулся. Похоже, она оценила эту попытку." 
    "Роуэн осторожно сунул ей в рот бюрдук с водой, заставляя выпить хотя бы немного." 

ro "Ты уйдешь отсюда живой, Миледи. "

show rowan necklace happy at midleft with dissolve

ro "Ты вернешься к своей семье."
ro "Ты еще решишь, что хочешь сделать со своей жизнью."
ro "Я не могу позволить тебе сдаться здесь."
ro "Это было бы неподобающе для Леди Элеоноры из дома Делейн."

"Его слова, сказанные с таким воодушевлением, теперь звучали пустыми даже для него. Но он должен был попытаться поднять ее дух." 
"Он видел ее глаза в воде. Она закрыла их и попыталась отвернуться. Безуспешно."

hide rowan with dissolve

"Роуэн отвернулся, решив оставить ее наедине."
"Она была слишком горда, чтобы плакать.." 
"Поэтому он притворился, что не слышит ее."

scene black with fade

"..."

scene bg3 with fade

show rowan necklace neutral at midleft with dissolve

"Когда солнце взошло, Делейн была мертва."

show rowan necklace neutral at center with moveinleft

"Он потушил огонь, затем достал носовой платок и бюрдук с водой."
"Он аккуратно умыл лицо Делейн. У неё всё ещё был вчерашний макияж. Он ей уже не нужен." 
"Как и тряпки, которые она носила для побега, но у него не было под рукой ничего лучше."  
"Ей бы не хотелось, чтобы ее так хоронили." 

ro "..."

"Ему нужна была могила для нее. Только у него не было лопаты." 
"Каменный холм должен был бы подойти." 

hide rowan with moveoutleft

"Орки ушли, гоняясь за другими зацепками. Горный склон был пуст."

scene black with fade

"В течение следующих нескольких часов он бродил по пустым пещерам в поисках камней, как больших, так и малых."
"Наконец, он собрал достаточно, чтобы сложить скромную кучу."

scene bg3 with fade

show rowan necklace concerned at center with dissolve

"После некоторых размышлений он вытащил ее тело наружу, на окраину близлежащего леса."
"По утрам Делейн любила прогуливаться по саду. Ей бы понравилось быть похороненной под открытым небом, а не в пещере." 
"…"
"Камень за камнем, он собирал насыпь. Один безжизненный камень за другим, ложились на безжизненное тело Делейн."  

ro "..."

"Через час курган был готов. Из импровизированной могилы была видна только голова Делейн." 
"И последнее, что осталось." 
"Роуэн прочистил горло." 

ro "Делейн..."

"Его голос сорвался. Он снова прочистил горло."

ro "Делейн... "

"Нет, не так. Он снова прочистил горло." 

show rowan necklace neutral at center with dissolve

ro "Леди Элеонора из дома Делейн была... "

menu:
    "Замечательной женщиной.":
        $ released_fix_rollback()
        ro "Женщина с невероятным характером. Сильной. Восприимчивой. Умной."
        ro "Она проявила несравненную стойкость перед лицом невзгод, не желая предавать свои идеалы."
        
    "Дворянкой, достойной своего титула.":
        $ released_fix_rollback()
        ro "Она была так молода и скрытна."
        ro "Но в отличие от многих в ее положении, она не смотрела на других свысока. Она понимала всю ответственность, связанную с ее происхождением, и делала все возможное, чтобы соответствовать этому."
        
    "Той, кто заслуживала лучшего.":
        $ released_fix_rollback()
        ro "Чудесной женщиной, ушедшая от нас слишком рано."
        ro "С ее умом и силой она могла быть гордостью дома Делейн, но сейчас..."

ro "Мир без нее - стал меньшее."
ro "Все, кто ее знал, восхищались бы ее... "

menu:
    "...обширными знания.":
        $ released_fix_rollback()
        ro "Она любила книги. Она любила читать."
        show rowan necklace happy at center with dissolve
        ro "Однажды она сказала, что готова убить за книгу, и я не сомневаюсь, что она бы исполнила это, если бы осталась в плену дольше."
        show rowan necklace neutral at center with dissolve
        
    "... острым языком.":
        $ released_fix_rollback()
        ro "Если бы Улькро достаточно хорошо разбирался в человеческой этикете, ее бесцеремонные выпады быстро бы обезглавили ее."
        ro "Я представляю, как она жаловалась бы на то, что низкое качество топора палача оскверняет женщину ее положения."
        
    "...невероятная храбрость.":
        $ released_fix_rollback()
        ro "Несмотря на нависшую угрозу, я видел страх в ее глазах только, когда она думала о своих родителях и их местонахождении."
        show rowan necklace happy at center with dissolve
        ro "Ну, и может быть еще, когда мы шли через лагерь, но это понятно."
        show rowan necklace neutral at center with dissolve
        
ro "..."
ro "Я никогда не забуду, как..."

menu:
    "... она приказала мне спасти ее.":
        $ released_fix_rollback()
        show rowan necklace happy at center with dissolve
        ro "'Пришел ты сюда спасать меня или нет, теперь это твоя миссия!'"
        ro "Ха-ха ... кая наглость..."
        
    "...ее лицо светилось каждый раз, когда она видела меня.":
        $ released_fix_rollback()
        ro "Всю неделю ты только и делала, что ждала меня. Ты пыталась быть беспечной, но твоя улыбка мгновенно выдала тебя."
        ro "…. Мне жаль, что я не мог проводить с тобой больше времени."
        
    "...она напомнила мне, что значит быть героем.":
        $ released_fix_rollback()
        ro "Я знал, что последствия борьбы с Близнецами будут ужасными, но… "
        ro "Я просто не мог оставить тебя там."
        ro "Никто не останется там."
        
show rowan necklace concerned at center with dissolve

ro "…"

menu:
    "<Помолитесь Солансии>":
        $ released_fix_rollback()
        ro "Пусть твоя душа обретет мир с богиней, Делейн."
        
    "<Не молится>":
       $ released_fix_rollback()
       ro "Пусть твоя душа обретет покой в загробной жизни, Делейн."
       
ro "..."

"Он не знал, что еще добавить." 
"Он взял последние камни и положил их на лицо Делейн, полностью закрыв ее."  
"Все было кончено." 
"Еще один союзник ушел." 
"..."
"После того как они убили Карнаса, он думал, что эти времена остались позади."   
"Что он никогда больше не будет отвечать за свои промахи, смертью кого-то важного для него."  
"Сейчас…" 
"Старейшина деревни мертв…" 
"И Делейн тоже." 
"Сколько их еще?" 

ro "..."

menu:
    "Он никогда не должен забывать, за что сражается." if serveChoice != 4:
        $ released_fix_rollback()
        "Это было только начало. Пока он служит Близнецам, ему придется идти на все новые и новые риски, чтобы защитить то, что ему дороже всего."  
        "На этот раз ему это не удалось." 
        "Больше этого не произойдет." 
        $ change_base_stat('c', -5)
        jump delaneDeathConc

    "Дальше будет только хуже. Он должен закалить свое сердце.":
        $ released_fix_rollback()
        "Это было только начало. Скорее всего, ему придется принести в жертву больше, гораздо больше, пока он служит Близнецам."
        "Он не может отчаиваться из-за каждой потерянной жизни."
        ro "(Я не позволю этому сломить меня)."
        $ change_base_stat('c', +5)
        $ change_base_stat('g', -5)
        jump delaneDeathConc
        
    "… Это уже слишком …" if avatar.guilt > 70:
        $ released_fix_rollback()
        ro "..."
        "Связанный магией, под постоянной угрозой смерти и пыток..." 
        "Его жена разделяет его страдания, ее единственное преступление - вышла замуж за человека, которого она любит." 
        "Приносить боль и агонию, куда бы он ни прибыл..." 
        "Он должен был стать героем…"
        "Но не смог спасти ни одной живой души." 
        ro "..."
        "Он перевел взгляд на нож, лежавший рядом. Его рука взяла его почти сама по себе." 
        "Какой во всем этом был смысл? Он только усугублял ситуацию." 
        "Может быть…" 
        "Может быть, было бы лучше просто…"
        "Покончить с этим?" 
        menu:
            "Это к лучшему.":
                $ released_fix_rollback()
                "Он нацелил клинок себе в сердце." 
                "Это будет быстро. Он не промахнется." 
                "Герой легенд никогда не промахивался." 
                ro "(Мне очень жаль всех.)"
                ro "(Это к лучшему.)"
                ro "(Удачи, Алексия.)"
                ro "(Я люблю тебя.)"
                show bg3 with sshake
                show bg3 with redflash
                scene black with fade
                "..."
                $ renpy.full_restart()
                
            "…. Нет.":
                $ released_fix_rollback() 
                "Дрожащей рукой он убрал нож." 
                "Он не мог."
                "Несмотря на все…"
                "Это еще не конец."
                $ change_base_stat('c', +5)
                $ change_base_stat('g', -5)
                jump delaneDeathConc

label delaneDeathConc:

"Он впоследний раз взглянул на могилу Делейн." 
"Если повезет, орки не найдут курган, и ее тело останется нетронутым." 
"Это было все, на что он мог надеяться." 
"…"

scene bg31 with fade

"Остаток недели он провел в скитаниях по северной Розарии, избегая поселений и людей. Ему нужно было проветрить голову. По крайней мере, немного." 
"Довольно скоро амулет позвал."
"Близнецы ждали." 

$ delane_status = "dead"
return

