init python:

    #Orcish families and the neutrals
    event('orcish_families_and_neutrals', triggers='orciad_explore', conditions=("week >= 4",), run_count=1, group='orciad_camp', priority=pr_map_res)
    #Batri's men going on a raid
    #Requires that player have visited Batri's tent once, but they don't need to have accessed it.
    event('batri_s_men_going_on_raid', triggers='orciad_explore', conditions=("week >= 4", "rowan_met_batri != 'first visit'"), group='orciad_camp', priority=pr_map_res)
    #Find noblewoman's prison (Side quest storyline)
    #Requires that the player have spoken to either Ulcro or Batri.  Priority event that always happens first when exploring until the player succeeds.
    event('find_noblewoman_s_prison', triggers='orciad_explore', conditions=("week >= 4", 'found_delane_tent == False', "rowan_met_batri=='met' or rowan_met_ulcro=='met'", "last_delane_visit_attempt < 1"),
        group='orciad_camp', priority=pr_story)
    #Orcish courtship in action
    event('orcish_courtship_in_action', triggers='orciad_explore', conditions=("week >= 4",), run_count=1, group='orciad_camp', priority=pr_map_res)


label orcish_families_and_neutrals:
#Orcish families and the neutrals
#"Explore The Camp" event

$ change_mp(-5)
$ prevent_tile_exploration()

scene bg26 with fade

#If (Orciad quest is still incomplete)
if orciad_state == 1:
    "В основном районе лагеря, рядом с лагерем Тариш, поселились большинство женщин вместе и детьми."

#If (Orciad quest is complete)
elif orciad_state == 2:
    "Для проживания матерей и их детей вблизи центра лагеря была отведена отдельная территория."
    "Из своего опыта войны и пребывания в Бладмине Роуэн был хорошо знаком с тем, что женщины-орки были так же способны к бою, как и мужчины."
    "Хотя в войсках хаоса их обычно было не так много, как мужчин."
    "Здесь он мог понять, почему, так было, воспитание детей все еще было за ними."
    "Все матери собрались вместе, чтобы помочь друг другу растить своих детей в общине. Иногда заходил мужчина, чтобы все проверить, но ответственность за это в первую очередь ложилась на молодых матерей."
    "Он обнаружил, что среди местных родителей были и беременные орки, и кормящие новорожденных, и те, кто курировал малышей, участвовавших в крупных драках."
    "Была даже одна мать, которая учила дочь махать оружием."
    "Орки жили, чтобы сражаться, даже с детства. Когда-то один из них был достаточно взрослым, чтобы сражаться топором, клюшкой или мечом, они оставили своих матерей и вступили в ряды воинов."
    "Единственная причина, по которой они покидали эту должность, заключалась в том, что они были ранены, больны или вынуждены воспитывать ребенка."
    "Потом возвращался обратно к боям, как только смог. Иначе поступить считалось трусостью и означало бы, что тебя либо убьют, либо изгонят из племени."
    "Эта повседневная жизнь была настолько разительно противоположна тому, как жил Роуэн, где драка была лишь необходимостью."
    "Даже несмотря на это, он мог видеть гордость матерей за своих детей, так же как гордились им его родители."

#If (orciad sidequest is incomplete)
if orciad_state == 1:
    "Роуэн оставил позади матерей и исследовал эту часть лагеря. Сразу же ему показалось, что здесь явно выделяется наличие большинства женщин по отношению к мужчинам."
    "Сначала он думал, что, может быть, это были сторонники Тариш, но они явно все еще были частью лагеря Улькро."
    "После нескольких вопросов и бесед с несколькими орками выяснилось, что эта группа была в основном нейтральной в ссоре между двумя лидерами орков."
    "Особенно женщинам нечего было сказать об этой ссоре из-за захваченной дамы с розовой кожей."

#rejoin
jump campMenu

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label batri_s_men_going_on_raid:
#Batri's men going on a raid
#"Explore The Camp" event
#Requires that player have visited Batri's tent once, but they don't need to have accessed it.

#Rowan meets a group of Batri's soldiers and can choose to join them on a raid.  Doing so triggers a skill check and Rowan gains guilt.
#If successful, Batri's power in the camp increases, Rowan gets some personal money, and the player can access his tent if they can't already.

$ change_mp(-5)
$ prevent_tile_exploration()

scene bg26 with fade

#Variant if Orc-iad is not complete
if orciad_state == 1:
    "Роуэн наткнулся на группу орков Батри, в лагере, где собирались воины для рейда. Объектами нападения, естественно, были близлежащие населенные пункты, фермы и караваны, если они смогли их найти."
    "Похоже, что королевству было наплевать на нападения, поэтому им не нужно было беспокоиться о репрессиях в их отсутствие."
    "Как человек, Роуэн не собирался идти на это"

    #if Rowan has no access to Batri's tent
    if rowan_met_batri != 'met':
        "Однако он мог к присоединиться, чтобы добиться встречи с самим Батри."

    #if Rowan has access to Batri's tent
    else:
        "Присоединение к ним даст ему шанс укрепить власть Батри в лагере."

    #rejoin
    "Он даже получит свою долю добычи. Если его совесть сможет справиться с нападением на соотечественников."

#If orciad is complete, it is a generic group of raiders instead.  The player can still get some personal money at the cost of guilt by going on a raid.
else:
    "Роуэн наткнулся на отряд орков, ищущих воинов, чтобы отправиться в набег. Это была рейд по сельской местности Розарии."
    "Учитывая его близость к вождю, Роуэна, естественно, пригласили принять участие в рейде. Если он присоединится, ему обещали изрядную долю добычи, а потом устроят пышный праздник."
    "Если его совесть сможет справиться с нападением на соотечественников."

#rejoin
menu:
    "Присоеденится к набегу орков":
        $ released_fix_rollback()
        $ rowan_joined_batri_s_raiders = True
        jump orcRaid

    "Не сегодня.":
        $ released_fix_rollback()
        #no effect
        jump campMenu

################################

label orcRaid:

#countryside bg
scene black with fade

#roll a d20 and add the players luck modifer
$ event_tmp['raid_luck_roll'] = roll_stat('luck')[0]

if event_tmp['raid_luck_roll'] <= 7:
#low luck (less than 7) # less or equal
    #Attacking a weak farm.
    #No combat roll needed, always succeeds.

    "Добыча на этом рейде была невелика."
    "В этом рейде было недостаточно орков, чтобы захватить большие цели, поэтому они прошлись вокруг лагеря, захватывая небольшие фермы и усадьбы, большинство из которых уже были заброшены или разграбленны."
    "И все же в тот день к ногам Роуэна было обращено не одно отчаянное прошение о пощаде, когда он помогал усмирить то немногое сопротивление, которое крестьяне и фермеры могли оказать им."
    "Хотя мало что было достигнуто, они не вернулись в лагерь с пустыми руками."
    #Roll for loot and Batri strength. Inform the player how much Rowan's share was. Value should be fairly small.
    $ temp = dice(10) + 5
    $ change_personal_gold(temp)
    "Rowan's share: [temp] gold"
    $ batri_power += dice(10) + 5
    $ delane_gifts += 3
    #Rowan gains some experience, guilt, and a small amount of infamy.
    $ add_exp(15)
    $ change_base_stat('g', 2)
    $ change_base_stat('f', 2)
    jump campMenu

elif event_tmp['raid_luck_roll'] <= 14:
#medium luck (8-14)
    #Attacking a small hamlet

    "Орки вели Роуэна к одной из деревушек в этом регионе. Это была довольно легкая цель, но она могла оказать большее сопротивление, чем случайные фермы и усадьбы."
    "Склонность героя в подобных ситуациях всегда заключалась в том, чтобы взять на себя ответственность, хотя орки просто не были заинтересованы в том, чтобы слушать его или посылать разведку вперед."
    "Они просто обрушивались на крестьянина в яростной боевой атаке."

    #Roll combat test, DC8.
    if check_combat(8)[0]:
    #Pass
        "Несмотря на усилия местных жителей собрать ополчение, чтобы дать отпор набегу, дела у них шли очень плохо. В течение нескольких минут орки окружили людей и собрали все ценности, которые смогли найти."
        "Затем вспыхнуло пламя, и в одном из домов начался пожар.  Торжествующе ревя, орки бежали со своей добычей. Используя горящие здания как прикрытие, они легко вернулись в лагерь с несколькими плениками."
        #Roll for loot and Batri strength.  Inform the player how much Rowan's share was.  Value should be middling.
        $ temp = dice(10) + 15
        $ change_personal_gold(temp)
        "Доля Роуэна: [temp] золота"
        $ batri_power += dice(10) + 10
        $ delane_gifts += 5
        #Rowan gains some experience, guilt, and a small amount of infamy.
        $ add_exp(20)
        $ change_base_stat('g', 3)
        $ change_base_stat('f', 2)
        jump campMenu

    #Failure
    else:
        "В то время как орки были в состоянии опрокинуть селян с помощью численности, они понесли гораздо больше потерь, чем должны были."
        "Среди них был и Роуэн, которому не повезло получить стрелу сразу после начала сражения."
        "Его отступление, чтобы залечить раны, не очень понравилось оркам, которые отказались поделиться с ним своей добычей, когда вернулись в лагерь."
        #End event, Rowan gets an injury.
        $ add_effect(MultiEffect('Wound', 'neg', (('strength', -1),), 2))
        #Rowan gains some experience, guilt, and a small amount of infamy.
        $ add_exp(15)
        $ change_base_stat('g', 2)
        $ change_base_stat('f', 2)
        jump campMenu

elif event_tmp['raid_luck_roll'] <= 20:
#high luck (15-20)
    #Attacking a trade caravan

    "Вскоре после того, как они покинули лагерь, налетчикам посчастливилось наткнуться на следы каравана, который недавно проходил мимо, вероятно, путешествуя между Протеей и Растедейлем."
    "Роуэн быстро определил направление, в котором он двигался, и повел их к добыче."
    "Это оказались торговцы, одна из лучших мишеней, по которым они могли бы стрелять, но и одни из самых опасных. Это будет трудная схватка, которую орки так жаждали начать."

    #Roll combat test, DC14.
    $ event_tmp['raid_combat_roll'] = check_combat(14)
    #Pass
    if event_tmp['raid_combat_roll'][0]:
        "Хотя стражники были готовы к нападению, особенно со стороны бандитов или орков, они не были готовы к тому, что Роуэн навлек на них."
        "Он быстро определил, их командиров,и поразил их несколькими стрелами подряд. Затем он присоединился к рейдерам, которые напали на дезорганизованных и деморализованных наемников."
        "Его спутники, казалось, не понимали, насколько он помог им выиграть эту битву."
        "Но налетчики действительно думали, что он заработал хорошую долю, когда они вернулись в лагерь с безделушками и оружием, которые они награбили у торговцев."
        #Roll for loot and Batri strength.  Inform the player how much Rowan's share was.  Value should be high.  Rowan gets a little extra experience for this result.
        $ delane_gifts += 7
        $ temp = dice(10) + 25
        $ change_personal_gold(temp)
        "Доля Роуэна: [temp] золота"
        $ batri_power += dice(10) + 15
        #Rowan gains some experience, guilt, and a small amount of infamy.
        $ add_exp(25)
        $ change_base_stat('g', 3)
        $ change_base_stat('f', 2)
        jump campMenu

    #Partial pass (10+)
    elif event_tmp['raid_combat_roll'][1] >= 10:
        "Стражники были готовы к внезапным атакам, как от бандитов так от орков. Так что борьба была довольно ожесточенной после того, как она началась."
        "Почти все, включая Роуэна, получили несколько царапин во время рукопашной схватки."
        "В конечном счете победу в битве одержало упрямство орка. Люди в конце концов решили, что получили слишком много повреждений, и отступили, бросив свои фургоны."
        "В то время как они почти были разбиты, эта атака считалась успешной, поэтому Роуэн считал ее чем-то вроде пирровой победы. И все же он заслужил свою долю."
        #Roll for loot and Batri strength.  Inform the player how much Rowan's share was.  Value should be high on loot, middling on Batri strength.  Rowan is injured.
        $ delane_gifts += 5
        $ temp = dice(10) + 25
        $ change_personal_gold(temp)
        "Доля Роуэна: [temp] золота"
        $ batri_power += dice(10) + 10
        $ add_effect(MultiEffect('Wound', 'neg', (('strength', -1),), 2))
        #Rowan gains some experience, guilt, and a small amount of infamy.
        $ add_exp(20)
        $ change_base_stat('g', 3)
        $ change_base_stat('f', 2)
        jump campMenu

    #Failure
    else:
        "Стражники были готовы к внезапным неорганизованным атакам, как от бандитов так орков."
        "И как ни старался Роуэн, он ничего не мог поделать с толпой, к которой примкнул, и их свирепость не шла ни в какое сравнение с дисциплиной наемника."
        "Атака за атакой отбрасывались назад, и потери становились все больше и больше."
        "Наконец, герой смог заставить орков отступить, прежде чем все силы были уничтожены, но он сам получил много повреждений в этом процессе."
        "Это был не первый неудачный рейд, вернувшийся в лагерь, хотя мало у кого были такие потери. Одни называли их трусами, другие хвалили за шрамы."
        "Что касается репутации, то Роуэн полагал, что она в основном не упала."
        "Однако его тело было довольно изношено."
        #Rowan is seriously injured.
        $ add_effect(MultiEffect('Wound', 'neg', (('strength', -2),), 3))
        #Rowan gains some experience, guilt, and a small amount of infamy.
        $ add_exp(15)
        $ change_base_stat('g', 3)
        $ change_base_stat('f', 2)
        jump campMenu

#luck critical (21+)
else:
    #Attacking a weakly defended noble retinue

    "К их великой глупости, один из представителей розарианской знати решил путешествовать по дорогам в этот день с минимальным эскортом."
    "Когда Роуэн сообщил им, кого именно они выслеживают, орки очень разволновались и задали темп, который утомил Роуэна."
    "Тем не менее, они быстро догнали небольшую свиту и быстро победили скудные экскорт."
    "К большому удовольствию орка, аристократ отказался сдаваться и пошел сражаться."
    "Когда они разделили добычу, Роуэн вскоре понял цель этой поездки. По-видимому, он должен был доставить подароки кому-то из знати, а возможно, и нескольким."
    "Здесь было довольно много редких вещей, которые он мог выбрать для своей доли."
    #Player is presented a list of items and chooses one.  Jewelry and a noblewoman dress are potential options, which can each be gifted to Ulcro once to then give to Delane.  They can also be sold to merchants for a profit. (TODO)
    # TODO: jewerly and dress
    $ delane_gifts += 10
    $ temp = dice(10) + 35
    $ change_personal_gold(temp)
    #Roll for Batri strength.  Value should be high.
    $ batri_power += dice(10) + 20
    #Rowan gains some experience, guilt, and a small amount of infamy.
    $ add_exp(25)
    $ change_base_stat('g', 3)
    $ change_base_stat('f', 2)
    jump campMenu


#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label find_noblewoman_s_prison:
#Find noblewoman's prison (Side quest storyline)
#"Explore The Camp" event
#Requires that the player have spoken to either Ulcro or Batri.  Priority event that always happens first when exploring until the player succeeds.

if met_with_delane == False:

    $ change_mp(-5)
    $ prevent_tile_exploration()
    # using this var for attempt to find tent also
    $ last_delane_visit_attempt = 1

    scene bg26 with fade

    "Пока Роуэн исследовал лагерь орков, он высматривал место, где плененная аристократка могла находиться в плену."

    #Spot check (DC15) to locate noblewoman
    if check_skill(15, 'spot')[0]:
        #Success, trigger first time visit to Delane.
        "В уединенной части лагеря, где находился Улькро, странным образом сосредоточились солдаты. Это было почти напротив того места, где стояла Тариш, по другую сторону ограждений."
        "Чувствуя, что именно это он и ищет, Роуэн принялся тайно наблюдать за патрулями и передвижениями в этой части лагеря."
        "К счастью для него, хотя эти орки были хорошо дисциплинированы, они не были самыми острыми наблюдателями."
        "Это не заняло много времени, чтобы исчесзнуть с их поля зрения и терпеливо наблюдать из тени."
        "Через несколько часов он сменил укрытие и снова стал наблюдать. Затем снова примерно через час после этого."
        "Герой решил, что он уже близко, когда нашел большую палатку, которая находилась под постоянным наблюдением с трех разных сторожевых постов."
        "Его подозрения подтвердились, когда он заметил, что палатка на самом деле была зданием, замаскированным под палатку."
        "Орки никогда бы не взяли на себя труд сделать что-то настолько сложное, если бы это не было таким особенным, как эта аристократка для Улькро."
        "Теперь Роуэн мог попытаться прокрасться внутрь, чтобы увидеть ее своими глазами."
        #Player can now sneak to visit Delane on the camp menu, but they will have to wait a week between visits.  It is listed as visiting a noblewoman.  Add note6 to journal.
        $ journal.add_quest_note('orciad', 'note6')
        $ found_delane_tent = True
        jump campMenu

    #Failure, trigger a different random camp event.
    else:
        $ choose_and_insert_next_event('orciad_explore')
        return
        
else:
    $ choose_and_insert_next_event('orciad_explore')
    return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label orcish_courtship_in_action:
#Orcish courtship in action
#"Explore the Camp" event

scene bg26 with fade

"Эта часть лагеря была особенно шумной сегодня, с большим количеством гортанных криков и ударов в грудь."
"Подойдя ближе, Роуэн обнаружил, что большой участок земли был расчищен для соревнований."
"Орки копали ямы вручную, поднимали различные тяжелые предметы и балансировали ими на пластинах на конце лопат."
"Другие состязания, кажется, закончились не так давно, с обломками, разбросанными вокруг от того, что происходило раньше."
"Понаблюдав несколько минут, мужчина наконец - то начал понимать, что происходит."
"Это был своего рода ритуал ухаживания, состязания были начаты женщинами-орками, которые хотели иметь ребенка."
"Каждая из них устроила для мужчин свое собственное состязание, сначала показав, что они хорошо делают, а затем предоставив претендентам, право доказать, что они достойны ее."
"Это было не просто соревнование в том, чтобы быть лучшим, но и в том, чтобы быть выносливым. Ни один орк не выбывал до тех пор, пока он не был сильно превзойден."
"Каждый из них держался до тех пор, пока другие не уходили, и они оставались одни."
"По крайней мере две пары, которые были выбраны на предыдущих конкурсах, также смотрели шоу, плюс женщина, которая была привязана к столбу и в ее лицо иногда плевали другие женщины-орки."
"Роуэн спросил, почему ее публично унизили. После нескольких вопросов он понял, что она пыталась начать соревнование, в котором не была сильна."
"А это недостойной орков, желающих дать ей ребенка. Все орки должны были быть могущественными, а не только мужчины."
"В свободное от комментариев время герой задавался вопросом, действительно ли эта женщина хотела ребенка, а не развлечься."
"Это заставило его посмотреть все под иным углом и предположить, что если все, что она хотела сделать, это повеселиться, она могла была пойти и найти каких-нибудь пленных, чтобы трахнуться."
"После этого он вернулся к просмотру конкурсов. Кажется, что они только начинали, новые соперники все еще присоединялись к подвигам силы, скорости и ловкости."

menu:
    "Попробовать свои силы, копая ямы голыми руками.":
        $ released_fix_rollback()
        show orc soldier neutral at midright with dissolve
        show wild orc neutral at edgeright with dissolve
        show rowan necklace neutral at midleft with moveinleft
        "Роуэн встал среди конкурсантов.  Тут же один из них положил руку ему на плечо."
        os "Что ты делаешь, хьюми?  Это орк спорт для тех, кто делает детей."
        ro "Да, я здесь, чтобы соревноваться."
        os "А? Что заставляет тебя думать, что это нормально?"
        ro "Что, боишься, что я могу тебя победить?"
        "Это привлекло внимание орков. Он отошел от него и начал следующий раунд без единого слова. Никто больше не пытался остановить Роуэна, когда он участвовал в соревновании."
        "Это не остановило толпу насмешек над ним, когда настала его очередь."

        #strength check (DC18)
        if check_stat(18, 'strength')[0]:
            $ released_fix_rollback()
            #success
            "Выкапывание ям голыми руками было утомительным и очень трудным занятием, но Роуэн не позволил себе замедлиться."
            "Снова и снова, он был в состоянии соперничать или, по крайней мере, приблизиться к своим конкурентам."
            "Внезапные аплодисменты отвлекли его внимание на мгновение, когда он увидел, что объявили победителя. Среди его группы остались только двое из десяти конкурентов."
            "Он был удовлетворен тем, что бросивший ему вызов был среди тех, кто сдался."
            "Вскоре после этого другие соревнования так же закончились."
            "Роуэн понял, что, возможно, это была ошибка, хотя он, безусловно, набрал мускулатуру после присоединения к близнецам, сможет ли он победить орков в тестировании силы?"
            hide wild orc with moveoutright
            "Рытье и копание шло в ногу, длительное время. Большую часть времени Роуэн не замечал ни чего вокруг, даже когда другой орк выбыл из игры, он неуклонно продолжал копать."
            "Его руки онемели, руки горят, но он никогда не переставал хватать еще горсть грязи."
            hide orc soldier with moveoutright
            "Наконец, когда было почти темно, мощные руки схватили его сзади и подняли с земли. Он повернулся и оказался лицом к лицу с женщиной-орком, которая начала этот конкурс."
            #show female orc
            "Она провела своими мозолистыми руками по его рукам. Со своей стороны, мужчина нашел, глядя на ее мощное, но все еще женственное тело, облегчение."
            "Конец копания ям был замечательным.  Их глаза встретились, белые шары без зрачков изучали карие глаза мужчины."
            femorc "Ты очень сильный для хьюми. Это должно быть весело."
            jump courtshipVictory

        #fail
        else:
            $ released_fix_rollback()
            "Роуэн был достаточно хорош для первых нескольких раундов, однако вскоре стало очевидно, что он, вероятно, совершил ошибку, бросив вызов оркам на подвиги силы."
            "Он уже начал уставать, но те, кто его окружал, выглядели полными сил."
            "Понимая, что он никогда не сможет справиться с ними, он решил попытаться, по крайней мере, продолжать копать достаточно долго, чтобы сделать хоть еще одного орка."
            "К его большому удовлетворению и удовольствию наблюдателей, орк, бросивший ему вызов, оказался первым, кто отказался от участия в состязании."
            "Роуэн вскоре остановился, но теперь уже никто не издевался над ним."
            jump courtshipFail

    "Доказать, что вы лучший бегун, чем орки, в беге.":
        $ released_fix_rollback()
        show orc soldier neutral at midright with dissolve
        show wild orc neutral at edgeright with dissolve
        show rowan necklace neutral at midleft with moveinleft
        "Роуэн встал среди конкурсантов.  Тут же один из них положил руку ему на плечо."
        os "Что ты делаешь, хьюми?  Это орк спорт для тех, кто делает детей."
        ro "Да, я здесь, чтобы соревноваться."
        os "? Что заставляет тебя думать, что это нормально?"
        ro "Что, боишься, что я могу тебя победить?"
        "Это привлекло внимание орков. Он отошел от него и начал следующий раунд без единого слова. Никто больше не пытался остановить Роуэна, от участия в соревновании."

        #reflexes check (DC18)
        if check_stat(18, 'reflexes')[0]:
            $ released_fix_rollback()
            #success
            "Скорость была специальностью Роуэна. В бою он всегда старался быть на шаг впереди противника, на расстоянии вытянутой руки, на расстоянии вдоха и выдоха."
            "Вот почему он избегал металлических доспехов во время войны даже после того, как они стали доступны, придерживаясь кожи, которую ему изначально выдали."
            "Скорость в бою отличается от скорости бега, но это было такое же соревнование на выносливость, как и на скорость."
            "Роуэн делал круги снова и снова, удивляя толпу и других участников соревнований."
            hide wild orc with moveoutright
            "Вскоре те, кто наблюдал за ним, сменили свои насмешки на приветствия, когда он закончил еще один круг, оставив одного орка все еще пытавшегося одолеть его почти в пыли."
            "Он попытался доказать фол, приводя аргумент, что человек не должен быть в соревновании."
            "Однако теперь зрители были против него и требовали, чтобы он ушел после такого плохого забега."
            "Он выглядел так, будто собирался сказать что-то еще, но тут сзади на него набросилась женщина, которая начала соревнование."
            hide orc soldier with moveoutright
            #show female orc
            "Сбив его с ног и потоптав упавшее тело, женщина-орк повернулась к Роуэну."
            femorc "Я говорю, Ты победитель!"
            "Зрители в последний раз одобрительно закричали, празднуя победу человека, как будто это была их собственная победа."
            jump courtshipVictory

        #fail
        else:
            $ released_fix_rollback()
            "Хотя гонки против орков были, безусловно, одним из самых легких соревнований, к которым мог бы присоединиться Роуэн, разница в росте между человеком и людьми-орками вскоре оказалась проблемой."
            "Их шаги были намного длиннее, чем у Роуэна, поэтому ему пришлось приложить гораздо больше усилий, чтобы передвигать ноги, чтобы соответствовать своим конкурентам."
            "Темп, который был установлен, и огромное количество раз, когда тот же самый пробег устраивался, скоро начали сказываться на его выносливости."
            "К счастью, многие другие орки обнаружили, что это состязание было выше их сил, и уход Роуэна был примерно после того, как половина уже ушла."
            "Получив несколько одобрительных кивков от толпы, он присоединился к ним, довольный  тем, что выбил орка, который первый остановил его."
            jump courtshipFail

    "Соревноваться в праве спаривания с женщиной-орком - это не то, что вы хотите.":
        $ released_fix_rollback()
        jump courtshipFail


label courtshipVictory:

"Женщина-орк обняла мужчину, который был чуть меньше ростом, и повела его прочь от площадки для соревнований."

scene bg30 with fade

show rowan necklace neutral at midleft with dissolve
#show female orc

"Она подвела его к палатке, которая казалась пустой, и с любопытством посмотрела на Роуэна."

femorc "Ну и что? Ты собираешься трахнуть меня?"

"Обычно он был скорее любовником, чем ебарем, но Роуэн полагал, что орки в постели так же свирепы, как и на поле боя."
"Так что он собирался быть намного более агрессивным, чем обычно."

#kissing and fondling/stripping CG.
scene cg321 with fade
show rowan necklace neutral behind cg321
#show female orc behind cg321
pause 3

"Решив действовать инстинктивно, он шагнул вперед и крепко поцеловал женщину.  Поначалу она казалась смущенной, но вскоре ответила ему и заставила свой язык войти в его рот в ответ."
"Этот жест был оценен по достоинству, так как раздвигать ее клыки было больно."

"Через мгновение она оттолкнула Роуэна и улыбнулась."

femorc "У хьюми забавный способ трахаться."

ro "Будет лучше, если мы немного поиграем."

femorc "Покажи мне."

"Они снова поцеловались, просовывая языки друг другу в рот и исследуя разницу в анатомии. Герой по существу игнорировал раздражение, вызванное этими клыками. Они не были острыми, просто неуклюжими."
"Вместо этого он сосредоточился на других частях ее тела, например, на ягодицах.  Почувствовав это, орк фыркнул от удовольствия, но не прервал поцелуй."
"Это только подтолкнуло ее притянуть его ближе, чтобы она могла сделать то же самое."
"Роуэн положил руку ей на зад, чтобы скользнуть вверх по спине, на ее скудную одежду, чтобы начать снимать ее."
"Это заставило его партнера отступить от него. Поначалу опасаясь, что он сделал что-то не так, но орк вместо этого помог ему снять с нее доспехи."
"После этого она попыталась заняться снаряжением Роуэна, хотя ее усилия были в основном неуклюжими, и она начала разочаровываться."
"Вместо того чтобы позволить ей сорвать их силой, мужчина расстегнул свои доспехи, а затем стянул с себя одежду.  Как только она увидела 'Дика', то набросилась на Роуэна и повалила его на землю."

#Show 69 CG.
scene cg189 with fade
show rowan necklace naked behind cg189
#show female orc behind cg189
pause 3

"С некоторым усилием, наконец-то закончив снимать штаны, Роуэн сумел забраться наверх и снова взять инициативу в свои руки."
"Во время потасовки они оказались лицом к лицу с половыми органами друг друга в позе 69."

femorc "Глупый хьюми, ты неправильно понял!"

ro "Я бы так не сказал..."

"Он открыл рот и провел языком по зеленой киске перед собой. Следуя его примеру, орк провела языком по его яйцам и вверх по твердому стволу."

femorc "Это другое дело, но весело."

"Мужчина покусывал ее нижние губы, двигал бедрами, чтобы протолкнуть свой член мимо клыков. Не было никакого сопротивления, когда он вошел в ее горло."
"На самом деле она очень хотела проглотить его длину и сделала глубокий минет."
"Покачивая бедрами вверх и вниз несколько раз и наслаждаясь этим ощущением безмерно, Роуэн также прикладывал еще усилия, чтобы дразнить и играть, кусая вагину орка."
"Ей очень нравилось, когда он был сильным, поэтому он становился все жестче и грубее."

#Show fucking missionary CG.
scene cg110 with fade
pause 3

"Решив, что прелюдии достаточно, человек высвободился и развернул ее, чтобы снова оказаться лицом к лицу с женщиной-орком."
"Инстинктивно он встал и двумя быстрыми движениями протиснулся в ее проход. В конце концов, она сказала, что хочет, чтобы ее трахнули."
"Ее влагалище было очень скользким, а также более глубоким и широким, чем у человека. Были и другие различия, и Роуэн немного отвлекся, чтобы сосредоточиться на них."
"Он входил и выходил из нее так быстро, как только мог, а она двигала бедрами навстречу ему с каждым толчком. Громкий мясистый шлепок сопровождал каждое из этих движений, наряду с ворчанием удовольствия от них обоих."
"Это было то, к чему привыкли орки; грубый жесткий животный трах.  Это не помешало мужчине протянуть руку и 'оценить' ее более мягкие достоинства."
"Как и прежде, как только он начал ласкать ее, орк сделал то же самое с ним. Особенно ей нравилось тянуть его вниз все быстрее и сильнее."

#Show cuming CG.
scene cg110 with sshake
scene cg110 with sshake
scene cg111 with flash
show rowan necklace naked behind cg111
#show female orc behind black

"Роуэн даже не заметил, что приближается к своему пику, поэтому немного удивился, когда кончил внутрь орка."
"Судя по звукам, которые она издавала, и ощущению пульсации от ее движения, от женщины вряд ли можно было ожидать жалоб."

femorc "Хе-хе, хью был хорош. Сомневаюсь, что хьюми даст мне ребенка, но пусть у тебя будет свой путь со мной, чтобы компенсировать это."

ro "Ха, мне определенно понравилось, что ты была подо мной весь вечер. Как с прелюдией лучше?"

femorc "Отлично. Я, вероятно, начну еще один конкурс, если я не получу ребенка. Не возражаю, если ты попробуешь еще раз."

ro "Интригующе."

#rowan gains a small amount of corruption
$ change_base_stat('c', 2)
jump campMenu


label courtshipFail:

"Роуэн наблюдал за участниками состязания, с интересом наблюдая за тем, как орки справляются с каждой из задач."
"Один за другим конкурсанты уходили, когда раунды заканчивались, и они понимали, что их превзошли."
"Бегуны были первыми, кто выявил победителя, который быстро убежал с женщиной, чтобы потребовать свой приз."
"После этого закончили балансиры.  В отличие от бегунов, они уходили скорее разочарованными, чем усталыми."
"Неудивительно, что состязание сил все еще продолжалось, когда Роуэн решил идти дальше через три часа."
"Когда дело доходило до чего-то столь обыденного, как рытье ямы, внутри орков начинали работать электростанции."
"Уходя, он заметил, что женщина, которая была связана, ушла в какой-то момент во время состязания."
"Вероятно, кто-то освободил ее, так что наказание было не таким уж долгим."
jump campMenu

############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################
