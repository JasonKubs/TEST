init python:

    #Spy on the orcs
    #Requirement - orciad main story not complete.
    event('spy_on_the_orcs', triggers='orciad_explore', conditions=("week >= 4", "orciad_state <=1"), run_count=1, group='orciad_camp', priority=pr_map_res)
    #The Woodcarver
    event('the_woodcarver', triggers='orciad_explore', conditions=("week >= 4",), run_count=1, group='orciad_camp', priority=pr_map_res)
    #The (un)lucky slave.
    #Requires seeing the “Orc families” event.
    event('the_unlucky_slave', triggers='orciad_explore', conditions=("week >= 4",), depends=('orcish_families_and_neutrals',), run_count=1, group='orciad_camp', priority=pr_map_res)
    #Meeting the priest of Kharos
    #Requirement: Third visit to the camp (for reason no other than for the initial description to make sense)
    event('meeting_the_priest_of_kharos', triggers='orciad_explore', conditions=("week >= 4", "orciad_explore >= 3", 'not orciad_priest_visited'), group='orciad_camp', priority=pr_map_res)
    # small events for "meeting_the_priest_of_kharos"
    event('orciad_returning_to_camp_climb', triggers='map_res_105', active=False, run_count=1, group='orciad_camp', priority=pr_story)
    event('orciad_returning_to_camp_sneak', triggers='map_res_105', active=False, run_count=1, group='orciad_camp', priority=pr_story)
    event('orciad_returning_to_camp_listen', triggers='map_res_105', active=False, run_count=1, group='orciad_camp', priority=pr_story)
    event('orciad_returning_to_camp_dodge', triggers='map_res_105', active=False, run_count=1, group='orciad_camp', priority=pr_story)


label spy_on_the_orcs:
#Spy on the orcs
#Requirement - orciad main story not complete.

$ change_mp(-5)
$ prevent_tile_exploration()
#init
$ event_tmp['orcSpyTarget'] = None
$ event_tmp['reroll_random'] = False
$ event_tmp['spot_check_passed'] = False

scene bg26 with fade
show rowan necklace neutral behind bg26

#if first visit
if orciad_explore == 0:
    "В какой-то степени, разделение лагеря было захватывающим."
    "Роуэн не в первый раз был свидетелем разногласий между высшими командными инстанциями, но это был первый случай, когда он видел физический раскол лагеря."
    "Он находил бы это забавным, если бы не тот факт, что в будущем орки должны были стать их союзниками, и раскол оставил лагерь настолько открытым для вражеских атак, что было больно смотреть."
    "При отсутствии единого руководства вся их оборона была абсолютно дезорганизована."
    "Маршруты патрулирования не соответствовали логике, и охранники были в основном заняты бросанием презрительных взглядов в сторону орков-оппонентов."
    "Если кто-то захочет проникнуть в лагерь и устроить диверсию, ему не составит труда прокрасться туда-сюда."
    "… Именно это, Роуэн и собирался сделать."
    "Темные облака покрывали небо весь день, и когда наступила ночь, ни луна, ни звезды не были видны. Идеальная ночь для того, чтобы нанести для саботажа."
    "У него были некоторые варианты, доступные ему... Единственный вопрос был - против кого или кому помочь?"
else:
#else
    "Темные облака покрывали небо в течение дня, и когда наступила ночь, ни луна, ни звезды не были видны."
    "Еще раз повторюсь, что ночь была идеальной для того, чтобы заняться саботажем. В лагере еще многое предстоит сделать."
    "Единственный вопрос был - против кого  или кому помочь?"

#rejoin

menu:
    "Саботаж Улькро.":
        $ released_fix_rollback()
        $ event_tmp['orcSpyTarget'] = 'Ulcro'
        jump orcSpyRNG

    "Саботаж Батри":
        $ released_fix_rollback()
        $ event_tmp['orcSpyTarget'] = 'Batri'
        jump orcSpyRNG

    #requires player to have chosen to betray the slaves in the event orc slaves and EITHER of the following:
    #Corruption lower than 50 / Guilt higher than 49
    "Помочь рабам." if orciad_betray_the_slaves and (avatar.corruption < 50 or avatar.guilt > 49):
        $ released_fix_rollback()
        jump orcSpySlaves

################################################################################
label orcSpyRNG:

#choose one of the following at random:

if dice(2) == 1:
    #1
    jump anOfficerDrunk
else:
    #2
    jump unhappyOrcs

################################################################################
label anOfficerDrunk:

"Роуэн нашел, хорошую возможность."

if event_tmp['orcSpyTarget'] == 'Ulcro':
    "Сидя у костра с горсткой прихвостней, Роуэн заметил, знакомого ему орка, сторонника Улькро."

else:
    "Сидя у костра с горсткой приспешников, Роуэн заметил, чзнакомого ему орка, сторонника Батри."

"Он довольно сильно пил из бочки того, что Роуэн считал украденным пивом, и, судя по веселой атмосфере, он и его друг пьянствовали довольно долгое время."
"И не похоже, что они собирались останавливаться в ближайшее время. Вот тут и появилась возможность."
"Он может уьитб его. Пьяный орк легкая мишень, и не было никаких сомнений, что рано или поздно орк отойдет, чтобы отлить или что-то в этом роде."
"Зарезать его в темноте было бы легко.... Но только, Роуэн презирал такую тактику...."
"Имеет ли он право быть благородным, и даже если да, то заслужили ли это орки?"

menu:
    "Убить капитана орков.":
        $ released_fix_rollback()
        "… Нет, это не так. Роуэн сделал свой выбор давным-давно, когда присоединился к Близнецам."
        "Хладнокровное убийство офицера-орка даже близко не было так ужастно , что он делал уних на службе."
        "Поэтому он терпеливо ждал, прячась в тени, для возможности нанесения удара."
        "Час спустя он наконец-то получил врзмржность. Пивная бочка была опустошена, и после нескольких долгих минут ворчания орки окончательно решили на этом остановиться."
        "Они направились в свои палатки, включая капитана."
        "Роуэн следил за ним, крадась за ним."

        if event_tmp['orcSpyTarget'] == 'Ulcro':
            "К сожалению, оказалось, что нахождение орка в таком уязвимом состоянии исчерпало его запасы удачи. Пьяный капитан поставил свою палатку совсем рядом с палаткой Ультро..."
            "А охранники здесь были гораздо более бдительны…"

        else:
            "К сожалению, оказалось, что нахождение орка в таком уязвимом состоянии исчерпало его запасы удачи. Пьяный капитан поставил свою палатку совсем рядом с палаткой Батри..."
            "А охранники здесь были гораздо более бдительны…"

        #spot check, DC12
        if check_skill(12, 'spot')[0]:
            #pass
            $ event_tmp['spot_check_passed'] = True
            "… Но, как оказалось, он был недостаточно внимателен. Понаблюдав за окрестностями некоторое время, Роуэн смог обнаружить брешь в их маршрутах патрулирования."
            "Он подождал еще немного – чтобы убедиться, что капитан заснул в своей палатке, – затем поспешно прокрался до палатки. Он разрезал ножом стенку палатки и вошел внутрь."
            jump orcSpyTent
        else:
            #fail
            "… Но это были не первые стражники, которых Роуэну приходилось дурачить в своей жизни."
            "Он подождал немного, чтобы убедиться, что капитан заснул в своей палатке, затем, выбрав подходящий момент, осторожно прокрался до палатки."

        #move silently check, DC12
        if check_skill(12, 'move_silently')[0]:
            #pass
            "Как он и планировал, его никто не видел. Он разрезал ножом стенку палатки и вошел внутрь."
            jump orcSpyTent
        else:
            #fail
            orc1 "А? Что это было?"
            "Роуэн остановился, замерз. Не осмеливаясь даже дышать, он медленно повернулся в сторону голоса."
            "Орк пялился прямо на него, щуря глаза. Он не мог его увидеть - по крайней мере, пока нет. Не с такого расстояния."
            orc2 "Мы ошиблись?"

            $ orcName = 'Orc 1'

            orc1 "Я что-то слышал. Я думаю."
            "Орк обратился к своему спутнику, и Роуэн мгновенно извлек выгоду из этого, скрывшись с места преступления. Слишком близко."
            jump campMenu


    "Найти что-нибудь еще." if not event_tmp['reroll_random']:
        $ released_fix_rollback()
        if event_tmp['orcSpyTarget'] == 'Ulcro':
            "Ночь еще только начиналась, и Роуэн не собирался превращаться в наемного убийцу. Были и другие способы саботирования Улькро."
        else:
            "Ночь еще только начиналась, и Роуэн не собирался превращаться в наемного убийцу. Были и другие способы саботирования Батри."
        #reroll random event, only allow this option once
        $ event_tmp['reroll_random'] = True
        jump orcSpyRNG

################################################################################
label orcSpyTent:

"Капитан орков лежал на своей койке и громко храпел. Хорошо."
"Теперь оставалось только выбрать правильный инструмент для дела."

#if strength is 15 or more
if avatar.strength >= 15:
    "Его взгляд упал на ведро с чистой водой в углу. Идея."
    "Нахрапом, Роуэн перекатил орка на его кровати, на живот. Совершенно пьяный, орк лишь раздраженно хмыкал."
    "Он сел на спину и перенес свой вес, чтобы должным образом обездвижить орка. Затем он обнял орка за шею..."
    "...И начал душить его."
    "Орк проснулся почти сразу, но было уже слишком поздно что-либо предпринимать."
    "Роуэн держал его в Железной хватке, и как бы орк ни сопротивлялся, он не смог освободиться и не смог позвать на помощь."
    "… Через пару минут все было кончено. Орк безвольно лежал под ним."
    "Роуэн встал и отнес его тело к ведру. Он положил его перед ведром, лицом в воду."
    "Пью и засыпаю, пытаясь напится свежей воды. Какая трагическая судьба."
    "Было ли это правдоподобно? Нисколько. Может ли простое вскрытие обнаружить отсутствие воды в легких орка? Да."
    "Но орки ни когда не утруждали себя исследованием мертвых тел, хотя они и значение слова “вскрытие” нврядли знают."
    jump orcSpyLeave

#else
else:
    "Роуэн внимательно осмотрел палатку. Вскоре он нашел свое орудие убийства-грубый кинжал с зазубренным лезвием."
    "Он схватил его рукой и без малейшего колебания перерезал горло орку."

if avatar.corruption < 50:
#if corruption is less than 50
    "Роуэн смотрел, стиснув зубы, как орк открывает глаза."
    "Издав булькающий звук, он попытался закрыть дыру в горле, но это было бесполезно – прежде чем он сделал что-либо еще, Роуэн прикончил его, вонзив кинжал прямо в череп."
    "Жестокая смерть, но Роуэн не мог найти в себе силы пожалеть орка."
    "В это время он пытался посочувствовать своим будущим подчиненным, лидеры набегов орков обычно были худшими в своем роде. Мир не будет скучать по нему."
    "Другие орки, вероятно, зададутся вопросом, что именно тут произошло, но не было никакого риска, что они когда-нибудь найти настоящего убийцу."
    jump orcSpyLeave

#else
else:
    "Он равнодушно наблюдал, как орк открыл глаза. Издав булькающий звук, существо под героем попыталось остановить кровотечение, но это было бесполезно."
    "Прежде чем орк успел сделать что-то еще, Роуэн прикончил его, вонзив кинжал прямо в череп. Он не мог допустить, чтобы тот поднял шум."
    "Другие орки, без сомнения, зададутся вопросом, что именно тут произошло. И не то чтобы Роуэн это волновало. Но ни один из них не смог бы найти настоящего убийцу"
    jump orcSpyLeave

################################################################################
label orcSpyLeave:

#if earlier spot check was passed
if event_tmp['spot_check_passed']:
    "Повернувшись, Роуэн заглянул в отверстие палатки. Наверное, лучше было не задерживаться."
    "К счастью для него, он уже знал слепые зоны охранников. Дождавшись подходящего момента, он быстро вышел из палатки – и исчез в ночи."
    jump campMenu

#else
else:
    "Не желая рисковать быть обнаруженным в этот момент, Роуэн связался с Изерой, чтобы демоница телепортировала его обратно в замок, чтобы он мог продолжить свое путешествие от северного портала."
    "Небольшое неудобство, но лучше перестраховаться, чем потом жалеть."
    #move Rowan to northern portal
    # TODO: moving avatar out of camp to given map location
    # change Rowans location to hex(id 86)
    $ world.cur_map.pos_id = 86
    return

################################################################################
label unhappyOrcs:

"Госпожа удача, улыбнулась ему этой ночью. Пробираясь по лагерю, он заметил двух орков, стоявших в стороне от своих собратьев и перешептывавшихся между собой."

#listen check, DC12
if check_skill(12, 'listen')[0]:
    #pass
    "Роуэн подкрался к ним и прислушился к их разговору."
    jump unhappyOrcsPassTests
else:
#fail
    if check_skill(12, 'move_silently')[0]:
    #move silently check, DC12
        #pass
        "Хотя они были слишком далеко, Роуэну потребовалось всего несколько секунд, чтобы подкрасться к ним и подслушать их разговор."
        jump unhappyOrcsPassTests
    else:
        #fail
        $ orcName = 'Orc 1'
        "Однако они были слишком далеко, чтобы расслышать их как следует."
        "Когда Роуэн попытался подкрасться к ним, он, должно быть, издал какой-то звук, потому что два орка внезапно замолчали."
        orc1 "Что это?"
        orc2 "Не знаю. Но лучше поговорить завтра."
        orc1 "Да"
        "Стиснув зубы, Роуэн отступил, прежде чем орки смогли его найти. Все усилия потрачены в пустую."
        jump campMenu

label unhappyOrcsPassTests:
#rejoin (after passing on either test)
"Вскоре тема их разговора стала очевидной. Оба орка были не слишком довольны тем, как лидеры рейдов произвели недавнее разделение добычи."

$ orcName = 'Попандопуло'
orc1 "…А та бледная, с большими сиськами?"

orc2 "Черные волосы, черное платье?"

orc1 "Да."

if event_tmp['orcSpyTarget'] == 'Ulcro':
    orc2 "Гротт взял ее."

else:
    orc2 "Тротт взял ее."

orc1 "Задница."

if event_tmp['orcSpyTarget'] == 'Ulcro':
    orc2 "Да. Гротт всегда берет большие сиськи. Мы получаем плоские. Коровье дерьмо."

else:
    orc2 "Да. Тротт всегда берет большие сиськи. Мы получаем плоские. Коровье дерьмо."

orc1 "Ба! Кого мы должны разбить, чтобы получить большой сиско раба?"

"… Роуэн закрыл глаза и постарался не вздохнуть. Его будущие солдаты. Прекрасно."
"Неважно. Если кто-то готов умереть за тебя, ты не имеешь права смотреть на него свысока. Аристократы совершили эту ошибку, и Роуэн не собирался делать то же самое."

ro "Если вы недовольны своими лидерами рейдов, то я считаю, что мы можем помочь друг другу."

"Роуэн вышел из тени, и оба орка удивленно посмотрели на него. Он опустил капюшон, открывая свое лицо двум оркам. Один из них, казалось, понял, с кем они разговаривают."

orc2 "Рука демона. Чего ты хочешь?"

"Несмотря на свою сдержанность, этот конкретный орк, казалось, питал к нему хоть какое-то уважение."
"Должно быть, он был одним из немногих, кто видел, как он вошел с Андрасом, и решил, что лучше не раздражать эмиссара своего будущего босса."
"Хорошо. Это все облегчало."
"Будет не так уж трудно заставить двух орков сменить место. Но кому они должны были себя посвятить?"

if event_tmp['orcSpyTarget'] == 'Ulcro':
    menu:
        "Батри.":
            $ released_fix_rollback()
            jump orcSpyBatri

        "Тариш":
            $ released_fix_rollback()
            jump orcSpyTarish

else:
    menu:
        "Улькро.":
            $ released_fix_rollback()
            jump orcSpyUlcro

        "Тариш.":
            $ released_fix_rollback()
            jump orcSpyTarish

################################################################################
label orcSpyUlcro:

ro "Демону надоело, что Батри притворяется кем-то большим, чем просто самоуверенным мускулистым болваном."
ro "Вождь Улькро знает, как правильно руководить, и не допустит такой несправедливости. Ты сражаешься не на той стороне."

orc2 "И ты говоришь, что вождь Улькро будет относиться к нам справедливо?"

ro "Как только вождь Улькро поклонится Бладмину, Андрас позаботится о том, чтобы все орки получили по заслугам."

"… Может, он перестарался? Возможно, немного. Но он был эмиссаром будущего повелителя демонов. Быть смутно зловещим было ожидаемо от него."

#decrease support for batri by 1
$ batri_power -= 1
jump orcSpyEnd

################################################################################
label orcSpyBatri:

ro "Демонам не нужен вождь, который предпочитает прятаться в своей палатке, чем встретиться со своим противником в открытом поле."
ro "Ваши лидеры рейдов никогда бы не украли у вас, если бы у вас был сильный лидер, поддерживающий порядок."

orc2 "И ты говоришь, что Батри все исправит?"

ro "Вождь Батри не потерпит, чтобы орки-налетчики воровали у других. Демон ожидает от него дисциплины."
ro "И он ее получит."

"Не слишком ли велика была завуалированная угроза? Скорее всего, нет. Орки уважали силу и принимали дисциплину только в том случае, если ее им навязывали."

#increase support for batri by 1
$ batri_power += 3
jump orcSpyEnd

################################################################################
label orcSpyTarish:

ro "Демон устал от этого конфликта. Если Улькро слишком боится встретиться с Батри, а Батри слишком слаб, чтобы взять то, что, по его мнению, принадлежит ему..."
ro "... то ни один из них не заслуживает того, чтобы служить избранным Хароса."

orc2 "Тогда кто...?"

ro "Нам все равно. И тебе тоже. Просто молитесь, чтобы вы не были на стороне ни того, ни другого, когда придет время суда."

"… Может, он перестарался? О, совершенно определенно."
"Но ему нужно, напугать, как можно больше орков, чтоб они ушли от Батри и Улькро. Это облегчит приход Тариш во власть."

#increase support for tanish by 1
$ tarish_power += 1
jump orcSpyEnd

################################################################################
label orcSpyEnd:

"Оба орка посмотрели друг на друга. Они не выглядели слишком убежденными, но это не имело значения. Они не интересовались политикой."
"Они просто хотели получить свою долю добычи, и они оба знали, что этого не произойдет с их нынешними лидерами рейдов."
"Через несколько мгновений один из орков, с которым разговаривал Роуэн, неуверенно кивнул."

orc2 "Рука демона говорит за повелителя демонов. Будем доверять его словам."

orc1 "Я чувствую, что мы на грани грандиозного шухера."

ro "Хорошо."

show rowan necklace happy behind bg26

ro "Твой выбор будет вознагражден."

orc2 "Хотим только честных трофеев. Ничего больше."

orc1 "… И раба с большими титьками."

orc2 "И раба с большими титьками."

"Роуэн подавил желание вздохнуть. Он надеялся, что у этих двоих будет достаточно здравого смысла, чтобы держать рот на замке об его  участии в этом деле."
"Пока все, что ему нужно было сделать, это подождать."
"Когда придет время Батри и Улькро сражаться, его маленький трюк, вероятно, принесет свои плоды, даже если сам он не заметит результатов."
"По отдельности, такие маленькие орки, как эти двое, имели маленькое значение..."
"Но достаточно для выполнения дела, и его победа была гарантирована."

jump campMenu

################################################################################
label orcSpySlaves:

"Как бы Роуэн ни хотел, он не мог забыть, как предал пленников. Они доверились ему... И он отвернулся от них."
"Да, он не мог рисковать, отвратить от себя лагерь орков."
"Он должен был это сделать."
"Он должен был пожертвовать ими."
"Для общего блага."
"…На сколько еще компромиссов он будет вынужден пойти, служа Близнецам?"

#if Rowan was convinced by Jezera in the intro
if introserve == 3:
    "Он хотел сделать мир лучше. Место, где люди наконец-то освободятся от угнетения. И что же он сделал вместо этого? Продал пленников-людей, как продал собственную душу."

#else
else:
    "Он не мог двинуться против них, пока не мог. Но было ли это действительно оправданием, чтобы просто слепо выполнять их приказы?"
    "Неужели теперь он проведет всю свою жизнь, совершая одно злодеяние за другим, снова и снова повторяя себе: 'у меня не было выбора'?"

#rejoin
"Роуэн тяжело сглотнул. Часть его знала, что он не мог позволить этому съесть его. Но другая часть лелеяла это. Лелеяла тот факт, что он все еще способен заботиться."
"Предатель он или нет, но все равно герой."
"Он ничего не мог поделать с рабами, которых продали. Этот корабль уже отплыл. Но с тех пор орки наверняка поймали новых."
"Он намеревался сделать хоть что-то, чтобы облегчить их ношу, пусть даже самую малость."
"Приближаясь к пещерам рабов, Роуэн лишь отчасти обрадовался, увидев того же орка на страже, что и раньше."
"Он надеялся, что никогда больше не увидит его лица... но ... теперь это могло быть ему на руку."
"Сбросив капюшон, Роуэн вышел из тени и пересек оставшееся расстояние по открытой местности. Вскоре, орк заметил его, и расплылся в широкой улыбке."

orc1 "Хьюми! Рад тебя видеть! Снова проверяешь рабов? Ха-ха! Мы будем следить за ними, не беспокойся!"

"Роуэн хотел в ярости стиснуть зубы, но вместо этого вежливо улыбнулся стражнику-орку."

ro "Хорошо. Подумал, что стоит взглянуть. Убедитесь, что они ведут себя хорошо."

orc1 "Бвахаха! Конечно, заходи!"

"Роуэн прошел мимо орка, не сказав больше ни слова."
"Как бы то ни было, доклад о готовящемся побеге сделало Роуэна достаточно надежным человеком, чтобы позволить ему свободно входить в пещеры рабов."
"…"
"Внутри, в темноте, он увидел испуганные глаза, обернувшиеся, чтобы увидеть лицо их нового мучителя. Никто не узнал его, и тот факт, что он был человеком, не ослабил их подозрений."
"Дошли ли до их ушей слова о его предательстве?"
"Знали ли они, что некоторые люди сотрудничают с орками? Или они просто знали, что больше никому не могут доверять?"
"Роуэн отбросил эти мысли в сторону, он смотрел на рабов с холодным безразличием. Он знал, что орк все еще наблюдает за ним."
"Он ходил от клетки к клетке, разглядывая рабов. Старые, молодые ... несколько женщин."
"Остальных, без сомнения, увезли на ночные 'развлечения'."
"Он услышал, как орк хмыкнул и обернулся. Идеально."
"Роуэн подошел к металлической решетке. Сгорбленный пожилой человек сидел перед ними, наблюдая за его движениями мертвыми глазами."
"Чтобы сломить человека, не потребовалось много времени."

#if Rowan was convinced by not wanting to go crazy in the intro
if introserve == 1:
    "Неужели Роуэн окажется таким же, если окажется в подземелье близницов?"
    "Сойдет ли он с ума, как тот пленник в клетках Бладмина, или просто ... превратится в ничто, как этот человек?"
    "Он предал свои идеалы, чтобы избежать судьбы, которую теперь навязывал другим. Как низко он пал…"
#gain small amount of guilt and corruption
$ change_base_stat('g', 2)
$ change_base_stat('c', 2)

"Роуэн присел перед ним на корточки. Мужчина ничего не сделал, только продолжал смотреть. Роуэн приложил палец к губам, приказывая ему молчать."
"Он медленно сунул руку под плащ и достал оттуда хорошо спрятанный большой сверток. Он частично развернул его, открыв мужчине его содержимое."
"Еда. В основном сушеная говядина. И немного трав, для ран."
"Это было не так уж и много, Роуэн это знал. Это не хватит и на день."
"Но это было нечто другое. Луч надежды. Это было самое малое, что он мог сделать."
"Глаза мужчины расширились, блестя от жадности и лихорадочного голода. Его рука рванулась вперед, чтобы схватить пакет – но была остановлена рукой Роуэна."
"Герой держал его руку железной хваткой, выражение его лица было мрачным. Сначала мужчина попытался отдернуть руку, но в его слабом теле уже не осталось сил."
"… Раб медленно успокоился. Он слегка кивнул Роуэну, недовольный тем, что от него ожидают, но понимая, что выбора у него нет."
"Роуэн отпустил его руку, и старик осторожно схватил сверток."
"Прижав его к груди, он поспешил к другим рабам, чтобы поделиться тем, что ему дали."
"Роуэн встал и принял свое лучшее беспечное выражение лица. Он вернулся к орокам и вступил в непринужденную беседу с ними о будущих набегах ..."
"... и о том, что присоединение к близнецам даст оркам больше возможностей для них."
"Как только Роуэн убедился, что рабы спрятали провизию, он попрощался с орками и покинул пещеры."
"Это был небольшой жест. Но, возможно, это поможет вновь зажечь тот маленький огонек надежды, который Роуэн погасил в прежних рабах…"
# lose some personal gold and guilt (partially offsets the penalties for betraying the slaves earlier)
$ change_personal_gold(-20)
$ change_base_stat('g', -2)
jump campMenu


#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label the_woodcarver:
#The Woodcarver
# init
$ event_tmp['spot_success'] = False

$ change_mp(-5)
$ prevent_tile_exploration()

scene bg26 with fade

"Орки никогда не отличались дисциплинированностью, что Роуэн неоднократно использовал во время войны."
"И хотя большинство вождей обычно понимали важность поддержания охраны периметра вокруг лагеря..."
"...заставить регулярных пехотинцев следить и придерживаться установленных маршрутов патрулирования никогда не было легкой задачей."

#feud is unresolved
if orciad_state <= 1:
    "Его будущие союзники ни чем, не отличались от остальных. С каждым шагом он находил все больше и больше брешей в их обороне."
    "Когда их руководство разделилось, проникнуть внутрь, чтобы отравить их запасы еды или поджечь их палатки, было бы детской игрой."
    "Он мог только надеяться, что ситуация улучшится, как только восстановится субординация."

#else
else:
    "Несмотря на это, оказалось, что орки не собирались оставлять себя незащищенными."
    "Несколько сторожевых башен были построены по краям лагеря, и хотя они были расположены слишком далеко друг от друга, чтобы должным образом выполнять свою функцию..."
    "... тем не менее все они были укомплектованы людьми, а острые глаза орков подозрительно наблюдали за ним, когда он проходил под ними."
    "Он также встретил несколько патрулей. Один из них даже расспрашивал его, справедливо находя подозрительным то, что он шныряет вокруг."
    "Используя свое положение посланника Андраса и близкого доверенного лица вождя, он быстро сумел заставить их оставить его в покое..."
    "... но, судя по выражению их лиц, они, несомненно, сообщат своим начальникам об этом инциденте."
    "В целом лагерь был защищен лучше, чем он ожидал."
    "Были несколько 'слепых пятен', и кому-то вроде Роуэна не составило бы особого труда проникнуть туда, но это было то, над чем они могли бы работать, как только орки присоеденятся к Бладмину."
    "При некотором усилии их можно было бы привести под человеческие стандарты."

#rejoin
scene bg31 with fade
show rowan necklace neutral behind bg31
show wild orc neutral behind bg31

"Когда Роуэн шел по краю лагеря, что-то привлекло его внимание. Одинокий орк сидел на поваленном дереве, подальше от своих соотечественников."
"Судя по количеству шрамов на его спине, он был одним из тех орков, которые когда-то служили в войсках вторжения у Карнаса."
"Может быть, даже с самого начала войны. Ветераны орков были редкой находкой, так что, возможно, стоит обменяться с ним парой слов."

menu:
    "Подойти к нему.":
        $ released_fix_rollback()
        "Движимый любопытством, Роуэн подошел к старому воину. Он быстро заметил, что его первоначальное предположение о том, что орк наблюдает за дальним лесом, было неверным."
        "Орк не смотрел вдаль, вместо этого его глаза были сосредоточены на чем-то в его руках."
        "Это была почти готовая деревянная фигурка. Быстрыми движениями охотничьего ножа в правой руке орк безжалостно работал им, не обращая особого внимания на Роуэна."

        #spot check: dc10
        if check_skill(10, 'spot')[0]:
        #success
            $ event_tmp['spot_success'] = True
            "Не обращал он внимания и на двух орков, прятавшихся в высокой траве неподалеку. Юные орки наблюдали за мастером с видимым волнением, перешептываясь между собой."

        #rejoin
        "Зная, что лучше не прерывать человека, столь поглощенного своей работой, Роуэн остановился рядом и продолжал наблюдать, не говоря ни слова."
        "Статуэтка изображала дикого волка, крадущегося по лесу."
        "Оскалив зубы, зверь рычал на какого-то невидимого противника. С того места, где он стоял, Роуэн не мог разглядеть всех мелких деталей, но быстро заметил..."
        "...что, хотя резьба была довольно грубой, это, несомненно, была хорошая работа."
        "В этом было какое-то первобытное очарование. Волк выглядел почти как живой. Может быть, это было что-то, из прошлого мастера? Роуэн не мог сказать наверняка."
        "Тем временем орк продолжал резать твердое дерево, добивая последнюю лапу волка."
        "Нож, слишком большой для такой тонкой работы, тем не менее, действовал с большим изяществом, разрезая дерево именно так, как того желал его владелец."
        "Орк не только обладал большим опытом, но, как и предполагал Роуэн, должен был обладать немалым талантом, чтобы создать такую реалистичную копию."
        "Это было странно, для орка, который обычно считался не более чем простым животным, быть способным создать что-то настолько очаровательное."
        "Поскольку работа орка подходила к концу, Роуэн решил остаться и посмотреть, что мастер-любитель собирается с ней делать."
        "Через пятнадцать минут фигурка была закончена. Орк опустил нож и несколько раз повертел деревянного волка в пальцах."
        "Роуэн не могла понять выражение его лица. Доволен ли он результатом? Но его лицо, ни капли, не изменилось – выражение скучающего безразличия."
        "Не говоря ни слова, орк выбросил статуэтку через плечо."

        #if previous spot check succeeded
        if event_tmp['spot_success']:
            "В одно мгновение двое орков прыгнули вперед, пытаясь схватить деревянного волка."
            "Брыкаясь и крича, они начали драться из-за него, не желая расставаться с тем, что, по-видимому, считалось ценным трофеем."

        #else
        else:
            "Откуда ни возьмись, двое молодых орков прыгнули вперед, пытаясь схватить деревянного волка."
            "Брыкаясь и крича, они начали драться из-за него, не желая расставаться с тем, что, по-видимому, считалось ценным трофеем."

        #rejoin
        "Тем временем орк взял еще один кусок дерева и начал свою работу заново, не потрудившись посмотреть ни бой, ни молчаливую аудиенцию."
        "Видя, что они ничего не добьются, пока он не заговорит, Роуэн решил наконец обратиться к орку."
        ro "Не был удовлетворен результатом?"
        "Нож остановился на полпути,и его владелец поднял голову. Как бы то ни было, он не казался слишком раздраженным тем, что его прервали."
        "Нахмурив брови, он ответил низким монотонным голосом::"
        wo "Бесполезный."
        "... и больше он ничего не добавил. Роуэн оглянулся через плечо на дерущихся детей."
        ro "Дети могут не согласиться."
        wo "Дети тупые. Бесполезный."
        "Последовало долгое молчание. Несмотря на резкие слова в адрес своего собственного творения, орк, кажется, не был согласен с этим."
        ro "Тогда зачем это делать?"
        "Орк пожал плечами."
        wo "От скуки. Но ничего не стоит."
        "В глазах Роуэна деревянная игрушка была далеко не бесполезной."
        "Хотя это и не самое замысловатое произведение искусства, тем не менее это была красивая безделушка, которую, вероятно, можно было бы продать за хорошую цену на любом базаре."
        "Он сказал ему это. И снова орк пожал плечами в ответ."
        wo "Розовые стремные. Бесполезный. Все лесные животные ничего не стоят."
        "Последовало еще одно долгое молчание, нарушаемое только звуками продолжающейся борьбы двух детей. Роуэн услышал победный вопль и заметил, что один из них наконец одержал победу над другим."
        "Высоко подняв фигуру к небу, он побежал обратно в лагерь, радостно смеясь. Его спутник хромал за ним, бормоча проклятия."
        "Резчик по дереву даже не взглянул в их сторону. Но Роуэн заметил, что выражение его лица стало отстраненным, и его большой палец погладил кусок дерева, который он все еще держал почти нежно."
        "Мгновение прошло, и орк теперь выглядел таким же скучающим и хмурым, как и раньше."
        "… Понимали ли орки вообще понятие искусства? Для них деревянный зверек мог оказаться действительно бесполезным. Он мог служить только как приз для детей, чтобы бороться за него."
        "Но если орк хотел создать что-то 'ценное', то он мог бы сделать резьбу орочьего воина. Роуэн знал, что некоторые орки считают, что они приносят удачу, и готовы заплатить за них."
        "И все же он решил делать деревянных животных. Почему? Это специально для детей?"

        #if the player has scene the orc families scene
        if ev_happened('orcish_families_and_neutrals'):
            "Мужчины обычно не участвовали в их воспитании, и неужели это единственный способ сделать что-то хорошее для них, не будучи высмеянным за это?"

        #rejoin
        "Или ему просто нравилось делать что-то красивое, несмотря на то, что это не имело реальной ценности?"
        "Роуэн не мог сказать наверняка, а его странный спутник вряд ли прольет свет на этот вопрос, поскольку уже начал вырезать следующую статуэтку."
        "…"
        "Может быть, сам орк не совсем понимал, почему он продолжает делать статуэтки.  Все орки были воспитаны как воины. Наслаждаться чем-то, что не было связано с насилием... это неправильно для орка."
        "Но это не помешало ему делать это. Роуэн не смог сдержать легкой улыбки."
        ro "Прошу прощения, что отнял у тебя время."
        "Орк слегка кивнул, его нож вонзился в дерево."
        "Он больше ничего не добавил, и Роуэн понял, что лучше не приставать к нему."
        "Он попрощался с резчиком по дереву и возобновил работу по проверке оборонительного периметра, его настроение немного улучшилось после странной встречи."
        #lower guilt by small amount
        $ change_base_stat('g', -2)
        $ woodCarverSaw = True
        jump campMenu

    "Идти дальше.":
        $ released_fix_rollback()
        "Роуэн подумал, не заговорить ли с орком, но сомневался, что старый воин сумеет хоть что-то рассказать."
        "Опыт у орков заключался в изнасилованиях и грабежах – и Роуэн не собирался обращаться за советом в этой области."
        "Он оставил орка в покое и продолжил осмотр."
        jump campMenu


#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label the_unlucky_slave:
#The (un)lucky slave.
#Requires seeing the “Orc families” event.

$ change_mp(-5)
$ prevent_tile_exploration()

scene bg26 with fade
show rowan necklace neutral behind bg26

#if feud is unresolved
if orciad_state <=1:
    "Прочесывая лагерь в поисках новых проявлений внутреннего разделения, которые он мог бы использовать в своих интересах, Роуэн наткнулся на необычное зрелище."
#else
else:
    "Осматривая лагерь своих союзников, Роуэн наткнулся на необычное зрелище."

#rejoin
"Проходя мимо секции, где жили матери орков, он заметил бледную фигуру, сидящую рядом с одной из палаток."
"Простая, человеческая женщина, в рабском ошейнике, но одетая - в одежду орков."
"Сгорбившись с иглой в руке, она чинила порванные брюки. Даже на расстоянии он видел, что для рабыни эта женщина была гораздо здоровее, чем ожидал Роуэн."
"Сгорая от любопытства, он подошел к ней. Он снял капюшон, надеясь, что этот простой жест поможет вызвать в женщине хоть какую-то доброжелательность."
"Но это, похоже, привело к обратному результату. Когда девушка повернула голову на звук его шагов и увидела его лицо, в ее глазах появился страх. Она резко встала..."
"... демонстрируя округлость ее живота...."
"... она развернулась, пытаясь бежать. Она, казалось, забыла о своем положении, потому что остановилась на полпути при виде орков, которые, естественно, были рядом."
"Охваченная ужасом, она снова повернулась к Роуэну, а затем неуклюже попыталась отойти от героя, как будто это сделает ее поведение менее подозрительным."

menu:
    "Окликнуть ее.":
        $ released_fix_rollback()
        ro "Подожди! Я имею в виду, ты ..."
        "Женщина начала качать головой, не желая слушать. Пряча лицо в руках, она отвернулась от героя."
        orcm "Что это за суматоха?!"

    "Оставатся на месте.":
        $ released_fix_rollback()
        "Роуэн остановился. Может женщине запрещено общаться с другими людьми? Если так, у нее будут неприятности, если он попытается поговорить с ней."
        "Вместо этого он поднял руки и сделал шаг назад."
        orcm "Ха! Боишься беременной женщины, хьюми?"

$ matronName = "Матрона"

"Из соседней палатки вышла старшая орочья Матрона. Шрамы украшали ее гибкое тело, но годы отняли многое у того, кто, как полагал Роуэн, когда-то был уважаемым воином."
"Волосы у нее были седые и тонкие. Морщины украшали ее лицо, и один ее глаз был выбелен, слепой к миру."
"Но другой был здоров и бодр, и как только он узнал Роуэна, то стал холодным от ненависти."

#if feud is unresolved
if orciad_state <= 1:
    orcm "Воробей. Неужели ты устал сеять ложь среди нашего рода? Проверяешь, сколько из наших юношей достаточно большие, чтобы носить топор?"
    orcm "Боюсь, тебе придется довольно долго ждать этого."

#else
else:
    orcm "Воробей. Пришел осмотреть легионы своего господина? Смотришь, сколько наших юношей достаточно большие, чтобы носить топор?"
    orcm "Боюсь, они еще не совсем готовы присоединиться к вашим легионам рока."

#rejoin
"Она оскалила зубы, злобно улыбаясь."

orcm "Я думаю, что девушка может присоединиться, если вам это нужно. Не очень хороший боец, но я верю, что человеческих женщин можно убить, как орка."

"Роуэн не находил слов, ошеломленный внезапной враждебностью."
"Но, не имея привычки отступать, он быстро восстановил самообладание и встретил пристальный взгляд женщины своим холодным, безразличным выражением."

ro "Я просто проходил мимо. Никто не собирается посылать детей или беременных женщин на войну."

orcm "Так ли это? О, благослови тогда предков! Разве твой хозяин самый добрый из всех повелителей демонов?"
orcm "Пожалуйста, позови меня в следующий раз, когда он придет, чтобы я могла сосать его член и целовать его задницу в знак благодарности! О, будь благословен демон-босс!"

"Она насмешливо поклонилась, ее голос сочился ядом. Позади нее человеческая женщина продолжала наблюдать за ними, прячась за палаткой."

#if feud is unresolved
if orciad_state <= 1:
    "Роуэн нахмурился. Немногие орки доживали до сорока, а этой женщине было далеко за пятьдесят."
    "Она, вероятно, пользовалась уважением среди других орков, и хотя ему больше не нужно было искать у них поддержки, то, что она так холодно говорила об Андрасе, не предвещало ничего хорошего."

#else
else:
    "Роуэн нахмурился. Немногие орки доживали до сорока, а этой женщине было далеко за пятьдесят."
    "Вероятно, она пользовалась уважением среди других орков, и слышать, как она так холодно отзывается об Андрасе, было по меньшей мере тревожно."

menu:
    "Запугать ее, чтобы она подчинилась.":
        $ released_fix_rollback()
        $ orcMatronState = 1
        #if might is right
        if society_type == 'might':
            ro "Мастер Андрас скоро приведет твой род к победе над всеми людьми в Розарии. И он не любит инакомыслящих. Ты поступишь мудро, проявив к нему уважение, которого он заслуживает."
        #else
        else:
            ro "Император Андрас скоро приведет твой род к победе над всеми людьми в Розарии. И он не любит инакомыслящих. Ты поступишь мудро, проявив к нему уважение, которого он заслуживает."
        #rejoin
        orcm "Ха! Что такое? Раб угрожает свободному орку?! Что дальше, падет ли с неба дождь тухлой рыбы? Появится ли сама Солансия перед нами, чтобы лизать мою пизду? Ха-ха!"
        "Ее фальшивая улыбка исчезла, сменившись яростным хмурым взглядом."
        orcm "У меня нет терпения выслушивать твои ядовитые слова, Воробей. Уходи и больше не приставай ко мне. Молись, чтобы я умерла в рабстве у твоего господина."
        "В воздух запахло жаренным."
        "Роуэн опасался, что столкновение с орочьей Матроной неизбежно, однако после некоторых размышлений решил отступить."
        "Если женщина начинала причинять неприятности, то забота о ней ложилась на вождя."
        "Ему придется спросить о беременной девушке в другой раз. Бросив на Матрону последний угрожающий взгляд, он отступил. У него было еще, чем занятся."
        jump campMenu

    "Подойти к этому дипломатично.":
        $ released_fix_rollback()
        "Губы Роуэна сжались. Она была не первым орком, который пытался его обидеть. Но он чувствовал, что дело не только в неприязни к демонам и людям. В данный момент конфронтация ни к чему не приведет."
        "Вместо этого он решил, что будет лучше сначала попытаться выудить какую-нибудь информацию."
        ro "Раньше ты называла меня 'Воробей'. Почему?"
        orcm "Разве ты не он, раб? Вестник войны? Несущий смерть?"
        "Ее лицо исказилось от ярости."
        orcm "Мы слышали о твоем хозяине, несущем войну, о повелителе красных демонов. Не избранный Харасом, да?"
        orcm "Если бы это было так, он убил бы и Улькро, и Батри, двух дураков, и заставил бы нас всех встать на колени. Да... Он слишком слаб для этого, не так ли?"
        orcm "И, как все слабые дураки, недостаток власти он восполняет честолюбием. Держу пари, он мечтает стать следующим лордом демонов."
        orcm "И для этого ему нужны мы, не так ли? Ему нужны безмозглые рабы, чтобы проливать кровь за его идиотский крестовый поход.  "
        #if feud is unresolved
        if orciad_state <= 1:
            orcm "И вот он посылает тебя, своего верного раба, чтобы склонить нас на свою сторону. Чтобы положить конец этой нелепой вражде и заставить нас идти вместе с ним."
            orcm "Прошло всего десять лет с тех пор, как ваши герои убили предыдущего повелителя демонов, а вы уже принесли нам новую войну."
            orcm "И с собой несете ошейник. Тот, который, мы должны надеть на себя добровольно."
        #else
        else:
            orcm "Поэтому он послал тебя, своего верного раба, выбрать марионетку, чтобы возглавить его армию."
            orcm "Раб принес нам ошейник, и мы с радостью должны надеть его на себя."
            orcm "Прошло всего десять лет с тех пор, как ваши герои убили последнего повелителя демонов, и мы уже должны продавать себя в рабство еще раз."
        #rejoin
        "Горечь вошла в ее голос, и на мгновение маска гнева упала, открывая боль, которая была под ней."
        "Без сомнения, она сражалась под началом Карнаса. Что она потеряла в его служении, что заставило ее так сильно презирать демонов?"
        "Настолько, что ей удалось побороть у орков естественное стремление к войне?"
        orcm "Век за веком избранный Харосом приходит к нам и требует, чтобы мы пролили кровь во имя его. И как глупцы мы повинуемся, только чтобы быть убитыми твоим видом."
        orcm "Как долго это будет продолжаться, Воробей? Сколько еще сыновей, дочерей и любимых должно быть принесено в жертву, чтобы разжечь амбиции демонов, подобных твоему хозяину?"
        "A dry laugh escaped her lips."
        orcm "Думаю, для тебя это не имеет особого значения, не так ли? Нет, я не верю, что он делает. Ты уже предал свой собственный род, я сомневаюсь, что ты позаботишься о моем."

        #if corruption is greater than 49
        if avatar.corruption > 49:
            $ orcMatronState = 2
            ro "..."
            "И она была права, он этого не делал."
            "Рабы они Хароса или нет, он не мог забыть бесчисленные зверства орков, совершенные во время последней войны. Изнасилование, убийство, рабство. Даже если они были сделаны по приказу Карнаса…"
            "… Тогда бесчисленные преступления, которые орки совершают, не были необходимы для прибытия другого повелителя демонов."
            "Демоническое наследие или нет, орки все еще были ответственны за свою собственную судьбу."
            ro "И тебе тоже нет дела до людей, так что давай отбросим притворство."
        #if feud is unresolved
            if orciad_state <= 1:
                ro "Хочешь ты того или нет, но как только вражда вождей закончится, твои братья будут сражаться за нас."
                ro "Но уверяю вас, я не позволю вам, безмозглым животным, мешать мне."
        #else
            else:
                ro "Хочешь ты того или нет, твои братья будут сражаться за нас. Но уверяю вас, я не позволю вам, безмозглым животным, мешать мне."
            #rejoin
            ro "Близнецы гораздо хитрее, чем был Карнас. На этот раз все будет по-другому."
            orcm "Еще яд. Ты думаешь...."
            ro "У меня нет времени слушать унылую историю какой-то дряхлой старухи. У тебя есть два варианта, ведьма. Встать в строй и помочь нам завоевать шесть королевств. Или бежать."
            ro "Мне все равно, что ты выберешь."
            "… Ее рука потянулась к кинжалу на боку."
            orcm "Здесь есть третий выбор, Воробей…"
            "Роуэн слегка улыбнулся."
            ro "Ты можешь свободно попробывать. Но даже если ты добьешся успеха, это ничего не изменит."
            "Они смотрели друг на друга, холодные глаза Роуэна смотрели в яростные глаза орка. Ни один из них не сдвинулся ни на дюйм."
            "Наконец Матрона смягчилась. Ее плечи опустились, и сила, казалось, покинули ее."
            orcm "Нет, не будет. Другой раб займет твое место. "
            orcm "… Делай свое дело, Воробей."

        if avatar.corruption < 49 or introserve == 3:
            $ orcMatronState = 3
            ro "…"
            "Роуэну было трудно сочувствовать оркам. Рабы Хароса или нет, они совершии бесчисленные зверства, как во время войны, так и вне ее – по собственной воле."
            "Они постоянно жаждали крови, и несколько странных личностей с совестью не изменять этот факт."
            "Но ... нравилось ему это или нет, теперь они были его подчиненными. Он не собирался просто так бросатся ими без веской причины."
            ro "… Это правда, что до сих пор ваши хозяева не были особенно... внимательны к жизни орка. Но мы не намерены повторять их ошибки."
            ro "Близнецы гораздо хитрее, чем был Карнас. Причина, по которой Андрас не убил Улькро с Батри, заключалась не в том, что он не мог этого сделать."
            ro "Это было потому, что мы считаем, что организованная боевая банда орков является гораздо лучшим союзником, чем какая-то толпа без лидера."
            orcm "Пустые слова, Воробей."
            ro "Возможно.  Но даже если бы мы оставим вас в покое ... ты действительно веришь, что не будет другой войны?"
            "Орчиха Матрона долго смотрела на него."
            "Наконец, она отвернулась."
            orcm "Нет, я не знаю."
        if avatar.corruption < 49 and introserve != 3:
            $ orcMatronState = 3
            "Роуэн почувствовал, как у него перехватило горло. “Предатель человечества”. “Вестник войны”."
            "Он мог тайно планировать тысячу заговоров, чтобы вызвать гибель Близнецов, но пока он им был. И хотя он не мог точно посочувствовать оркам…"
            "Они были убийцами и насильниками, хотя он не знал, в какой мере их гнусная натура проистекала из их происхождения…"
            "Слова Матроны ранили его так, как он и не ожидал."
            "Сколько страданий он принесет окружающим, прежде чем представится возможность нанести удар в спину его проклятым хозяевам? Сколько еще таких речей ему придется выслушать?"
            "Обычно он старался не думать об этом. Но время от времени они всплывали в голове, затем что бы через несколько мгновений утонуть снова ."
            "То же самое произошло и сейчас – но на секунду его обычно непроницаемое выражение лица, должно быть, исчезло, потому что он увидел, как глаза орка расширились от удивления."
            orcm "..."
            ro "..."
            "Роуэн тяжело сглотнул."
            ro "На этот раз все будет по-другому. Крови прольется не больше, чем необходимо."
            orcm "… Да, это было бы здорово."
            "Печальная улыбка украсила ее губы."
            orcm "Но, боюсь, ты не в том положении, чтобы давать такие гарантии, раб."
            "Для нее он по-прежнему оставался всего лишь посыльным. Бессильная марионетка Андраса, возможно, она думала бы иначе, если бы знала, кто он на самом деле."
            "… Но он уже раскрыл слишком много, чем должен был. Лучше пока помолчать."

        #rejoin
        $ emmaMet = True
        "Последовало неловкое молчание. Гнев орка утих, сменившись меланхолией."
        "Теперь, когда Матрона была должным образом ... подавлена, он мог спросить ее о беременной женщине."
        "Невзрачная девушка все еще наблюдала за ними издали, слишком далеко, чтобы расслышать их тихую, хотя и оживленную 'беседу'."
        "Роуэн кивнул в ее сторону, побуждая орка обернуться."
        ro "Эта девушка. Она выглядит... ухоженной."
        orcm "… Она. Эмма! Иди сюда!"
        "Девушка робко подошла к ним. Она опустила голову, отказываясь смотреть Роуэну в глаза."
        "Его первоначальная оценка оказалась верной. Он не видел никаких следов побоев,а ее щеки имели здоровый оттенок."
        "Хотя рабство явно сказалось на девушке, это был не тот уровень физического насилия, которого он ожидал от орков."
        "Этому было только одно объяснение."
        ro "Она носит в себе полу-орка."
        "Он заявил больше, чем спросил. Матрона обнажила зубы в невеселой улыбке."
        orcm "Маленькой Эмме так нравилось набивать свою пизду, что она постоянно просила трахать ее каждую ночь. Кто знал, что она забеременеет?"
        "Бедняжка покраснела от стыда, но ничего не сказала."
        orcm "Что касается того, что она несет ... Мы не знаем. Может быть, орк. Может быть, человек. Ребенок покажет."
        "Роуэн нахмурился. Это... так не работает. Полуорки были редкостью,но они существовали."
        "Почти исключительно зачатые через насилие, среди людей они были париями. Очевидно, орки не рассматривали их таким образом."

        #if corruption is greater than 49
        if avatar.corruption > 49:
            $ emmaIgnored = True
            "Удовлетворив свое любопытство, Роуэн попрощался с Матроной. У него были гораздо более важные дела, чем заниматься каждым человеческим рабом, на которого он натыкался."
            jump campMenu

        #else
        else:
            ro "Я вижу. Вы не возражаете, если я поговорю с Эммой наедине?"
            "Матрона пожала плечами в ответ."

        #if feud is unresolved
        if orciad_state <= 1:
            orcm "Делай, что хочешь, Воробей."

        #else
        else:
            orcm "Теперь это твой лагерь, Воробей."

        #rejoin
        "Орк оставила их. Эмма не казалась слишком счастливой по этому поводу-во всяком случае, выражение ее лица стало еще более болезненным, чем раньше."
        "Роуэн подвел ее к скамье, на которой она сидела раньше, и сел рядом."
        "Он обдумывал, как помочь девушке успокоиться, пока давление, наконец, не оказалось слишком сильным для нее, и она внезапно взорвалась...."
        emm "Я не хотела этого делать! Я не ... "
        "Ее голос сорвался, и она снова закрыла лицо руками."
        emm "Я не... я не хотела, но ... они так старались, что я думала, что будет ... что будет легче просто ... …"
        ro "..."
        "Рябину поместил руки на плечи, и нежно обнял ее."
        ro "Все нормально. Ты сделала то, что должна была сделать. Никто тебя не осуждает."
        emm "Я думала, что так будет проще! Я просто подумала, что так будет проще…"
        "Она начала плакать в его объятиях, а Роуэн шептал ей успокаивающие слова. Он ничего не мог поделать с ее нынешним положением, не сейчас."
        "Но ... даже если все, что он мог предложить-это сиюминутная доброта, все равно это было лучше, чем ничего."
        "Возможно, позже он сможет помочь ей каким-то другим способом. Но не сейчас."
        #lower guilt by small amount
        $ change_base_stat('g', -2)
        jump campMenu


#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label meeting_the_priest_of_kharos:
#Meeting the priest of Kharos
#Requirement: Third visit to the camp (for reason no other than for the initial description to make sense)

$ change_mp(-5)
$ prevent_tile_exploration()

scene bg26 with fade

#first time intro
if orciad_priest_visit_count > 0:
    jump .after_intro

"Во время своего последнего визита в лагерь Роуэн увидел довольно любопытные сборища орков, несущих различное снаряжение, отходящих от поселения."
"Вооружившись, они явно не собирались в набег – слишком многие из них были ранены."
"По крайней мере, один из них хромал, а у другого не хватало глаза. Один орк прятал правую руку, и тогда Роуэн заподозрил, что он пытается скрыть какую-то серьезную травму."
"Сегодня он снова заметил то же самое сборище. Хотя он больше не видел одноглазого орка, хромой все еще был там, как и тот, кто пытался спрятать свою руку."
"Они готовились к очередному походу."
"Не желая снова упускать такую возможность, Роуэн подошел к собравшимся и спросил, какова их  цель."

lorc "Священник учит. Мы идем."

"… Это был единственный ответ, который ему удалось из них вытянуть. В отличие от большинства орков, которых он встречал до сих пор, эта группа не была особенно разговорчивой."
"Они даже не потрудились назвать его 'демоническим хуесосом', что было ново."
"Он лениво размышлял, кто же этот 'священник'. Насколько он знал, орки обычно называли своих духовных лидеров 'шаманами'. 'Священник' мог означать только одно…"
"Слуга Хароса."
"Хотя он видел признаки поклонения Харосу вокруг лагеря, орки не казались ему особенно религиозными, и он еще не встречал ни одного жреца Многоликого Бога."
"Такая возможность представлялась не часто."
"Он спросил орков, может ли он присоединиться к ним, и хотя никто из них не выглядел слишком довольным такой перспективой, ни один из них не высказал своего несогласия вслух."
"Это было достаточно хорошо для него, и вскоре он шел на некотором расстоянии позади орков – чтобы не раздражать своих новых товарищей своим присутствием."
"По крайней мере, не больше, чем необходимо."

scene bg3 with fade
show rowan necklace neutral behind bg3

"… Жрец орков оказался совсем не таким, как ожидал Роуэн."
"У Роуэна были свои истории с жрецами Хароса еще во время войны, но это были демоны, и они обычно соответствовали стереотипам священнослужителя темного Бога."
"Но жрец орков, с другой стороны, выглядел таким... мягким в сравнении с ними."
"Жрец ждал группу орков на небольшой поляне в лесу. Сидя на Большом Камне, он приветствовал их широкой улыбкой."
"Он не казался особенно сильным или более пугающим, чем любой другой орк, которого Роуэн встречал до сих пор."
"Если бы не символ Хароса, вытатуированный на его обнаженной груди, Роуэн легко мог бы принять его за любого другого орка."
"Священник был не один. Рядом с ним стояли четверо орков-трое мужчин и одна женщина. Его помощники, предположил Роуэн."
"Все еще молча и все еще улыбаясь, священник указал на луг вокруг них. Группа орков разделилась без единого слова, каждый направился на свое место."
"Между тем, священник подозвал к себе одного из орков, чтобы тот вышел вперед."
"Роуэн продолжал наблюдать за всем этим издали, не прячась и не пытаясь пока показаться священнику."
"Но его приход не остался незамеченным. Глаза священника встретились с его собственными, и Роуэн мог поклясться, что его улыбка стала шире."
"Но он ничего не сказал о присутствии героя. Вместо этого он обратился к вышедшему орку:"

orcp "Говори свой позор."

"Его голос был тихим и ровным. Он говорил медленно, словно смакуя каждое слово."

lorc "Варф медленный. Варф позволил рабу убежать."

orcp "Великий позор. Иди с Согом."

"Один из послушников шагнул вперед и повел орка прочь. Процесс повторился несколько раз, и послушники быстро вернулись, чтобы забрать следующего орка."
"Роуэну не потребовалось много времени, чтобы понять цель этого собрания."
"Он увидел, как хромой орк установил несколько мишеней на деревьях у края поляны."
"С довольно примитивным луком в руках (скольких орков Роуэн когда-либо видел с луком?) он упражнялся в стрельбе из лука."
"Орк, который продолжал прятать свою руку – теперь Роуэн видел, что у него не было трех пальцев, кроме двух, – пытался отбиться от другого орка с мечом в левой руке."
"Другой, далеко от центральной группы, пытался увернуться от ударов с повязкой на глазах."
"Обучение. Они все тренировались. На самом деле тренировка, а не просто ... рейд."
"Роуэн оставил свое удивление при себе. Несмотря на заботу о солдатах-орках на службе Близнецов, он действительно мало что понимал в их культуре."
"Через несколько мгновений последнему орку удалось найти учителя, и жрец перевел взгляд на Роуэна. Как и в прошлый раз, он сделал знак мужчине выйти вперед."
"Его улыбка все еще не исчезла, даже на мгновение."

orcp "Человек. Говори свой позор."

#Check: Intelligence = DC: 10
if check_stat(10, 'intelligence')[0]:
#Success:
    ro "Невежество."
    "Орк удивленно наклонил голову, затем одобрительно кивнул."
    orcp "Да, невежество."

#Failure.
else:
    ro "… Простите?"
    orcp "Значит, невежество."

    orcp "Твой учитель плохо поработал, обучая тебя, человек."

    ro "Возможно."

    orcp "Я избавлю тебя от твоего позора, человек. Если ты хочешь избавиться от него."

    ro "Так же, как ты делаешь это для них?"

    "Роуэн указал на ближайших орков. Священник кивнул."

    orcp "Да. Они слабы."
    orcp "А быть слабым-грех в глазах Хароса."
    orcp "… Но оставаться слабым - это гораздо большее оскорбление."

    "Священник больше ничего не добавил и вместо этого принялся наблюдать за Роуэном."
    "Его взгляд был острым, даже пронзительным. Священник говорил мало, но видел много."
    "И улыбка на его губах так и не исчезла с глаз."

jump orcPriestMenu

################################################################################
label .after_intro:
#after first visit intro

scene bg3 with fade
show rowan necklace neutral behind bg3

"Проходя по краю лагеря, Роуэн понял, что приближается к тому месту, где орк-жрец проводил свои уроки. Должен ли он навестить его на этой неделе?"

menu:
    "Да.":
        $ released_fix_rollback()
        "Роуэн направился к знакомой поляне. Орки уже были там, выполняя свои обычные тренировочные режимы. Орк-жрец наблюдал за ними, улыбаясь от уха до уха, как всегда."
        "Увидев, что Роуэн приближается к ним, он опустил голову, что обычно было бы почтительным приветствием, если бы не насмешливая улыбка на его губах."
        "Он весело поздоровался с героем и спросил, не может ли он сегодня чем-нибудь помочь."
        jump orcPriestMenu

    "Нет.":
        $ released_fix_rollback()
        "Сейчас было не время для этого. У него были дела поважнее."
        #don't trigger the event again this turn
        $ orciad_priest_visited = True
        jump campMenu

################################################################################
label orcPriestMenu:

$ orciad_priest_visit_count += 1

menu:
    "Спросить о Харосе и орках":
        $ released_fix_rollback()
        ro "Я думаю, что это первый раз, когда я стал свидетелем ... того, что делается во имя Хароса, в вашем лагере."
        orcp "Ритуалы... бессмысленны. Хорошы для шаманов."
        orcp "Путь Хароса - это путь действия. Путь крови. Не из историй и песен."
        "Роуэн ждал, что орк продолжит, но тот лишь склонил голову набок, забавляясь."
        orcp "Если вы хотите узнать о Харосе, вы не познаете этого словами. Слова слабы. Харос должен быть изучен телом. С болью и кровью."
        "… И это было все, что он мог сказать по этому поводу. Возможно, было бы лучше посоветоваться на эту тему с Клионой или Близнецами."
        jump orcPriestMenu

    #if feud is not resolved
    "Спросить о вражде орков." if orciad_state <= 1:
        $ released_fix_rollback()
        "Роуэн оглядел поляну. Он не был полностью уверен в лояльности большинства собравшихся орков,но он был совершенно уверен, что воины обоих вождей присутствовали."
        ro "Я вижу, вы пользуетесь уважением как последователей Улькро, так и последователей Батри."
        orcp "Все орки знают Хароса по крови. Все следуют за ним, независимо от начальника."
        ro "… Неужели это не имеет значения?"
        "Священник ответил не сразу. Он напряженно наблюдал за Роуэном и только через некоторое время тихо ответил:"
        orcp "… Более сильный выйдет победителем."
        "...Его глаза слегка сузились..."
        orcp "… Если только кто-то не вмешается."
        orcp "Тщательно подбирай слуг для своего хозяина, человек."
        "Похоже, священник был нейтрален... до некоторой степени."
        "Если жрец придерживался принципа Хароса “сила дает право” – а он, очевидно, так и делал, - то Тариш, скорее всего, была для него неприемлема."
        "Хотя, возможно, его можно будет склонить к Батри или Улькро ... как только Роуэн сумеет завоевать его уважение."
        jump orcPriestMenu

    "Спросить о тренировках.":
        $ released_fix_rollback()
        "Как ни противно было Роуэну признавать это, его навыки ... атрофировались."
        "Несколько месяцев избиений и голода во время его заточения в замке Бладмин сказались на его теле. Он был гораздо слабее, чем во время войны или даже до заключения."
        "За последние несколько месяцев у него почти не было времени отдохнуть и восстановить силы."
        "… Но с точки зрения близнецов, что бы он ни делал в лагере орков, это достаточно важно, чтобы он мог оставаться там по нескольку дней. Он мог бы использовать это в своих интересах…"
        "Он не был уверен, насколько надежным учителем окажется жрец Хароса, но прошли месяцы, и это была его первая возможность получить наставника."
        "Он сомневался, что еще будет случай в ближайшее время."
        ro "Это обучение только для орков?"
        "Орк наклонил голову, затем приветственно развел руками."
        orcp "Все, кто стыдится своей слабости могут присоединиться. Даже ты, человек."
        "Роуэн на мгновение задумалась, распространяется ли это и на рабов."
        "Скорее всего, нет."
        "Независимо от того, какое обучение он намеревался попросить, оно, вероятно, займет несколько дней."
        "Чтобы извлечь из этого пользу, если он будет отозван в замок в течение этого времени, он должен будет вернуться к своей тренировке сразу же после возвращения в лагерь орков."
        "Он должен был тщательно обдумать, подходящее ли сейчас время для этого."
        #unlock training options
        $ orciad_priest_training_unlocked = True
        jump orcPriestMenu

    #All options below require said skill to have below 5 points in them. #5 is a joke option. Rowan can take any 3 of #1-#4 options, but they are all one time only.
    #Each training session takes 7/8 (?) movement points.
    #If the player runs out of Movement points during it, he gets a short message telling him he will return to finish it at the start of his next visit.

    #unlocks after discussing training
    "Тренировать восхождение." if orciad_priest_training_unlocked and orciad_priest_complete_count < 3 and avatar.skill('climb') < 5 and (not orciad_priest_complete_climb):
        $ released_fix_rollback()
        #if first time any training is chosen
        if orciad_priest_first_training:
            "Обращаясь к священнику, Роуэн объяснил суть своей проблемы."
            "Хотя он не вдавался в подробности того, почему его навыки больше не были на высоте, он признал, что давно не практиковал их."
            "Священник внимательно слушал, И хотя Роуэн видел, что он хочет узнать больше, герой старался отвечать как можно более туманно."
            "Наконец орк сдался и перешел к сути просьбы."
        else:
        #if after first time
            "И снова Роуэн объяснил природу своих проблем. И снова он постарался быть как можно более неопределенным."
            "Но на этот раз священник не стал настаивать на дополнительной информации. Скорее, в его глазах был намек на веселье, когда Роуэн говорил."
            "Все, что он хотел узнать о герое, он уже сделал в их первую встречу. Так что теперь он просто слушал, и как только Роуэн закончил, обратился к проблеме."

        $ orciad_priest_first_training = False
        #rejoin
        orcp "Только рабы и дураки не отрывают глаз от Земли. Хорошо, что ты этого стыдишься."
        "Он повернулся в сторону и кивнул одному из послушников. Орк шагнул вперед и поманил Роуэна за собой. Он будет его инструктором сегодня."
        "Они вдвоем направились к краю поляны, где кончался лес. Некоторое время они шли вдоль границы, и орк осматривал каждое дерево на их пути."
        "Они внезапно остановились рядом с широким деревом с множеством ветвей."
        orca "Так, хьюми. Поднимись на этот."
        ro "Что? Вот так просто?"
        "Орк недовольно хмыкнул."
        orca "Хумы знают, что делают. Хуми просто нужно сделать."
        "… Правда, главной его проблемой было отсутствие формы, а не отсутствие знаний. Действие действительно может быть лучшим решением."
        "Роуэн начал подниматься-но в тот момент, когда его вторая нога оторвалась от Земли, орк за его спиной ударил его по руке веткой, заставив потерять хватку."
        "Роуэн упал, ударившись о траву. Он поднял голову и бросил на орка злобный взгляд."
        orca "Урок первый. Учись падать."
        ro "(Как это просветленно.)"
        "Так начался утомительный режим тренировок Роуэна с послушником-орком."
        "После первоначального 'урока' орк позволил ему взобраться на дерево в своем собственном темпе."
        "Как только он смог сделать это на приличной скорости, орк начал вводить препятствия – бить его сломанной веткой или бросать в него камни, чтобы нарушить его концентрацию."
        "Удивительно, но орк действительно дал ему несколько реальных советов о том, как использовать свои ноги и плечи, как сбалансировать центр своего тела и как планировать свой маршрут к вершине."
        "Несмотря на упрощенный язык и более или менее откровенную враждебность в его тоне, послушник действительно знал, свое дело, и приложил реальные усилия, чтобы улучшить навыки человека."
        "Через несколько дней, когда он убедился, что Роуэн может справиться с собой относительно легко, он приказал ему подняться еще раз, но на этот раз только правой рукой."
        "Это оказалось непростой задачей, но, опять же, благодаря подсказкам послушника, Роуэн смог подняться довольно высоко уже на следующий день."

        #if rowan has enough MP left to complete
        if avatar.mp >= 7:
            "Наконец, после недельной практики, его обучение было официально закончено."
            "Некоторые из его альпинистских навыков вернулись – не на тот уровень, на котором они были до заключения, но все равно это был прогресс."
            "Он поблагодарил священника и его помощника и вернулся в лагерь орков. Были у него и другие обязанности."
            #gain 2 points in climb
            $ avatar.skills['climb'] += 2
            $ change_mp(-7)
            $ orciad_priest_complete_climb = True
            $ orciad_priest_complete_count += 1
            jump campMenu
        else:
        #Does not have enough movement points remaining.
            "К сожалению, в середине его обучения Близнецы вызвали его обратно, чтобы он занялся делами замка."
            "Он должен будет закончить свое обучение во время следующего визита в лагерь орков."
            #see "returning to camp - climbing" text further down
            $ avatar.mp = 0
            $ activate_event('orciad_returning_to_camp_climb')
            return

     #unlocks after discussing training
    "Тренировка движения без шума." if orciad_priest_training_unlocked and orciad_priest_complete_count < 3 and avatar.skill('move_silently') < 5 and avatar.skill('spot') < 5 and (not orciad_priest_complete_sneak):
        $ released_fix_rollback()
        #if first time any training is chosen
        if orciad_priest_first_training:
            "Обращаясь к священнику, Роуэн объяснил суть своей проблемы."
            "Хотя он не вдавался в подробности того, почему его навыки больше не были на высоте, он признал, что давно не практиковал их."
            "Священник внимательно слушал, И хотя Роуэн видел, что он хочет узнать больше, герой старался отвечать как можно более туманно."
            "Наконец орк сдался и перешел к сути просьбы."
        else:
        #if after first time
            "И снова Роуэн объяснил природу своих проблем. И снова он постарался быть как можно более неопределенным."
            "Но на этот раз священник не стал настаивать на дополнительной информации. Скорее, в его глазах был намек на веселье, когда Роуэн говорил."
            "Все, что он хотел узнать о герое, он уже сделал в их первую встречу. Так что теперь он просто слушал, и как только Роуэн закончил, обратился к проблеме."

        #rejoin
        orcp "Нет ничего постыдного в том, чтобы ударить врага в спину. Враг, достаточно слабый, чтобы быть застигнутым врасплох, не заслуживает честной битвы."
        "Он повернулся в сторону и кивнул одному из послушников. Орк шагнул вперед и поманил Роуэна за собой."
        "Они направились в сторону поляны - к орку с завязанными глазами, которого Роуэн видел раньше."
        "Вместо того чтобы тренироваться с одним из послушников, ему теперь помогала коренастая женщина-орк, которая сердито посмотрела на героя, но молчала."
        orca "Изменение планов. Теперь ты тренируешься с хьюми."
        "Орк снял повязку с глаз. Увидев Роуэн, он агрессивно оскалил зубы, но, как и женщина-орк, ничего не сказал. Слово священника здесь было законом."
        "Вместе они вошли в лес неподалеку и продолжали идти, пока лес не стал совсем густым. Затем они остановились, и послушник объяснил им упражнение."
        "Цель Роуэна была проста-он должен был подкрасться к орку, который останется с завязанными глазами."
        "Женщина-орк будет наблюдать за этим упражнением-определять угол подхода Роуэна, засколько времени, он добрался до него, и когда он начинаеть красться."
        "Близлежащие ветви немного усложнили ему жизнь, но кроме того, Роуэн не был знаком и ни с чем другим. Ему потребовалось некоторое время, чтобы вспомнить правильные действия."
        "Перемещение без звука заключалось не только в осторожных шагах - он также должен был заранее спланировать свой маршрут, чтобы избежать каких-либо препятствий, которые трудно проскользнуть."
        "Его оппонент был достаточно внимательным и смог извлечь выгоду из большинства ошибок Роуэна."
        "Когда наступил вечер, и хоть ни кто из них не признался в этом вслух, они оба извлекли большую пользу из упражнения."
        "На следующий день они тренировались на поляне."
        "Женщина-орк собрала корзину, полную сухих веток, и разбросала их по округе, чтобы Роуэну стало немного сложнее, так как звуки других орков, занимавшихся рядом, помогли замаскировать его подход."
        "Она продолжала накапливать для него гандикапы, требуя, чтобы он хотя бы один раз обошел орка по кругу, или давая ему деспотично малое время, чтобы добраться до него."

        #if rowan has enough MP left to complete
        if avatar.mp >= 7:
            "Они меняли два режима подготовки в течение нескольких дней, прежде чем Роуэн решил, что он не сможет посвятить больше времени учениям."
            "Они могут продолжать наращивать темпы выполнения трудных задач, но другие вопросы требовали его внимания."
            "Он поблагодарил двух орков и покинул обучение, решив, что смог вернуть некоторые из своих утраченных способностей."
            #Gain Move silently +1, Spot +1
            $ avatar.skills['move_silently'] += 1
            $ avatar.skills['spot'] += 1
            $ change_mp(-7)
            $ orciad_priest_complete_sneak = True
            $ orciad_priest_complete_count += 1
            jump campMenu
        else:
        #Does not have enough movement points remaining.
            "К сожалению, в середине его обучения Близнецы вызвали его обратно, чтобы он занялся делами замка."
            "Он должен будет закончить свое обучение во время следующего визита в лагерь орков."
            #see "returning to camp - sneaking" text further down
            $ avatar.mp = 0
            $ activate_event('orciad_returning_to_camp_sneak')
            return

    #unlocks after discussing training
    "Тренировка слуха." if orciad_priest_training_unlocked and orciad_priest_complete_count < 3 and avatar.skill('listen') < 5 and (not orciad_priest_complete_listen):
        $ released_fix_rollback()
        #if first time any training is chosen
        if orciad_priest_first_training:
            "Обращаясь к священнику, Роуэн объяснил суть своей проблемы."
            "Хотя он не вдавался в подробности того, почему его навыки больше не были на высоте, он признал, что давно не практиковал их."
            "Священник внимательно слушал, И хотя Роуэн видел, что он хочет узнать больше, герой старался отвечать как можно более туманно."
            "Наконец орк сдался и перешел к сути просьбы."
        else:
        #if after first time
            "И снова Роуэн объяснил природу своих проблем. И снова он постарался быть как можно более неопределенным."
            "Но на этот раз священник не стал настаивать на дополнительной информации. Скорее, в его глазах был намек на веселье, когда Роуэн говорил."
            "Все, что он хотел узнать о герое, он уже сделал в их первую встречу. Так что теперь он просто слушал, и как только Роуэн закончил, обратился к проблеме."

        #rejoin
        orcp "Глаза могут обмануть тебя. Мудрое решение - попытаться услышать."
        "К его огромному удивлению, священник не обратился ни к одному из своих последователей. Вместо этого он спрыгнул со скалы, прямо перед Роуном."
        orcp "Я избавлю тебя от твоего стыда."
        "Его улыбка стала шире. Один из послушников вышел вперед и протянул Роуэну повязку на глаза, в то время как другой дал два посоха, по одному для каждого из них."
        orcp "Пойдем, человек."
        "Выражение лица Роуэна стало напряженным. Ему не нравилось, к чему все идет, но сейчас он не мог отступить. Как бы то ни было, он верил, что ему ничего не угрожает..."
        "... но священник явно что-то задумал для него."
        "Они отошли в сторону, чтобы дать себе немного места. Многие из окружающих орков прекратили свои тренировочные режимы и повернулись, чтобы понаблюдать за зрелищем."
        orcp "Надень повязку на глаза. Закрыть глаза. Затем..."
        "Священник приготовил свой посох."
        orcp "...терпи."
        "Дальнейших объяснений не требовалось. Что касается подготовки, то она была довольно стандартной. Даже граничишей с клише."
        "Но это не означало, что Роуэн может быть спокоен. Не с жрецом Хароса в качестве его противника."
        "Роуэн надел повязку на глаза и приготовил оружие. Пока все смотрели, на поляне стало тихо. Единственными звуками, долетавшими до него, были звуки окружающего леса."
        "Он услышал, как посох просвистел в воздухе, и его левая рука отозвалась болью. Он не успел среагировать. Он вообще не слышал, как орк подошел к нему.."
        "Еще один свист, и на этот раз удар пришелся по его правой руке. Еще один удар – но не настолько болезненный,чтобы помешать ему держать свое оружие."
        "… Конечно. Священник не хотел калечить его. Он просто хотел заставить его страдать."
        "Роуэн стиснул зубы и насторожился."
        "Не то чтобы это принесло ему пользу. В течение следующих пяти минут, удар за ударом, посох орка обрушивался на него."
        "Он мог слышать его – но недостаточно быстро, чтобы отреагировать должным образом."
        "Его движения становились вялыми. Он получил слишком много повреждений, чтобы выдержать безжалостную атаки священника."
        "Роуэн уже собирался сдаться, когда услышал, как слева от него раздался удар посоха – и на этот раз ему удалось блокировать удар."
        "Но не потому, что он открыл в себе какой-то скрытый талант и нераскрытую силу. Священник просто замедлил свой удар, чтобы Роуэн мог теперь поспевать за ними. Он играл с ним."
        "Он подтолкнул его к краю, и когда каждый удар был для него мучением, он дал Роуэну шанс среагировать."

        #if corruption is greater than 49
        if avatar.corruption > 49:
            "Герой блокировал еще один удар. Если священник думает, что Роуэн, один из шести героев, убивших Карнаса, позволит себя так унизить, то его ждет разочарование."
            "Упражнение длилось около часа. Роуэн падал от усталости, но холодная ярость продолжала толкать его вперед. Ярость и грубая, неразбавленная ненависть. Это притупило боль и обострило его чувства."
            "И это дало ему терпение ждать подходящей возможности, чтобы проявить себя."
            "Наконец он услышал его-низкий свист с левой стороны. Вертикальный удар, направленный в туловище."
            "На этот раз он не блокировал его. Вместо этого он поднял руку, и когда посох ударил его, поймал его под подмышкой."
            "Отчаянно пытаясь нанести ответный удар, он махнул своим собственным посохом, прямо там, где, по его мнению, находилась голова священника."
            "Он почувствовал, как его посох достиг цели, но крика боли не было слышно."
            "Ни один из них не пошевелился. Он медленно отпустил посох орка и снял повязку с глаз."
            "Священник заблокировал оружие левой рукой. Его ухмылка, которую Роуэн теперь презирал всем сердцем, не сходила с его лица."
            orcp "Хорошо. Урок окончен."
        else:
        #else
            "Герой блокировал еще один удар. Если бы священник думал, что он сломает его этим, он был бы очень разочарован."
            "Раб он близнецов или нет, но Роуэн был одним из шести героев, убивших Карнаса. Ему было и хуже."
            "Упражнение длится около часа. В конце концов, Роуэн толкал себя вперед только из чистого упрямства."
            "Хуже всего было то, что он старался быть настороже и в то же время не обращать внимания на боль. Если он хочет победить, то должен постоянно быть настороже."
            "… Хотя технически это не было соревнованием. Это была тренировка. 'Победа'' здесь не была целью."
            "И все же гордость запрещала ему просто пассивно терпеть побои."
            "Стиснув зубы, он ждал подходящего момента, чтобы нанести ответный удар."
            "Наконец, он услышал его - низкий свист с правой стороны. Вертикальный удар, направленный в туловище."
            "Вместо того чтобы блокировать удар, Роуэн взмахнул посохом и нанес удар вниз, туда, где, по его мнению, находилась рука орка."
            "Он почувствовал, как его посох достиг цели, но жрец не застонал от боли. Неважно. Вложив всю свою силу в удар, он  опустил свой посох ."
            "…Вражеский удар так и не прошел. Роуэн медленно снял повязку с глаз. Как он и подозревал, ему удалось попасть в посох орка."
            "После удара, он отбил посох орка в землю, и прижал его своим посохом."
            "Священник не пытался освободить свой посох. Вместо этого он не сводил глаз с Роуэна, и на его губах плясала обычная раздражающая усмешка."
            orcp "Хорошо. Урок окончен."

        #rejoin
        "После этого священник больше никогда не тренировался с ним. Все, что он хотел узнать о Роуэне, он узнал из этого упражнения."
        "Роуэн повторил это упражнение дважды, один раз с одним из послушников, а затем с одним из орков из лагеря."
        "У него было подозрение, что это было только для того, чтобы удостоверится, что то, что он испытал со священником - обнаружение входящего удара, с завязанными глазами не случайно."
        "Для этого, нужно было иметь хорошие рефлексы и немалый боевой опыт."
        "Неудивительно, что в конце второго дня один из послушников сказал ему, что отныне они будут тренироваться в лагере орков."
        "Шумное поселение гораздо лучше подходило для тренировки слуха, чем тихая поляна."
        "Там он просидел несколько часов с закрытыми глазами, пытаясь определить многие элементы лагерной жизни по одному их звуку."
        "Борба, спор, секс ... распознавать все это было непростой задачей."

        #if rowan has enough MP left to complete
        if avatar.mp >= 7:
            "Однако после нескольких дней практики он начал привыкать к этому. Это пригодится в будущем."
            #gain 2 Listening
            $ avatar.skills['listen'] += 2
            $ change_mp(-7)
            $ orciad_priest_complete_listen = True
            $ orciad_priest_complete_count += 1
            jump campMenu
        else:
        #Does not have enough movement points remaining.
            "К сожалению, в середине его обучения Близнецы вызвали его обратно, чтобы он занялся делами замка."
            "Он должен будет закончить свое обучение во время следующего визита в лагерь орков."
            #see "returning to camp - hearing" text further down
            $ avatar.mp = 0
            $ activate_event('orciad_returning_to_camp_listen')
            return

    #unlocks after discussing training
    "Тренировка уклонения."  if orciad_priest_training_unlocked and orciad_priest_complete_count < 3 and avatar.skill('dodge') < 5 and (not orciad_priest_complete_dodge):
        $ released_fix_rollback()
        #if first time any training is chosen
        if orciad_priest_first_training:
            "Обращаясь к священнику, Роуэн объяснил суть своей проблемы."
            "Хотя он не вдавался в подробности того, почему его навыки больше не были на высоте, он признал, что давно не практиковал их."
            "Священник внимательно слушал, И хотя Роуэн видел, что он хочет узнать больше, герой старался отвечать как можно более туманно."
            "Наконец орк сдался и перешел к сути просьбы."
        else:
        #if after first time
            "И снова Роуэн объяснил природу своих проблем. И снова он постарался быть как можно более неопределенным."
            "Но на этот раз священник не стал настаивать на дополнительной информации. Скорее, в его глазах был намек на веселье, когда Роуэн говорил."
            "Все, что он хотел узнать о герое, он уже сделал в их первую встречу. Так что теперь он просто слушал, и как только Роуэн закончил, обратился к проблеме."

        #rejoin
        orcp "Крепкая броня-это хорошо. Лучше не нуждаться в броне."
        "Он повернулся в сторону и кивнул прислужнику, стоявшей рядом с ним. Женщина-орк шагнула вперед и поманила Роуэна за собой."
        "Вместе они подошли к одному из орков, который тренировался сам по себе в центре поляны – невысокому и громоздкому воину, который, как догадался Роуэн, только недавно достиг совершеннолетия."
        "Он выполнял традиционный спринт вперед-назад, бегая между двумя скалами."
        "Сразу стало понятно, почему послушник хотел соединить их вместе. Будучи ниже Роуэна ростом, орк не имел ни малейшего шанса одолеть большинство своих собратьев."
        "В то же время, его широкая структура тела делала его медленнее, чем большинство. Это были естественные препятствия для бойца, особенно для орка, который предпочитал грубую физическую силу."
        "Орк-послушник объяснил ситуацию коротышке-орку. Отныне он будет тренироваться вместе с Роуэном."
        "Эти двое будут драться, причем орку будет приказано сосредоточиться на попытке ударить его, а Роуэну – уклониться от его ударов."
        "Женщина-орк наблюдала за ними, по крайней мере, первоначально – с 4 послушниками, разделенными примерно на 20 орков, они обычно не посвящали все свое время одному человеку."
        "Судя по выражению лица его напарника, молодой орк не был особенно доволен такой договоренностью."
        "Это было уже достаточно позорно, что он нуждался в тренировке, тренироваться с человеком было просто добавлением к оскорблению."
        "Но орк не осмелился высказаться против этой идеи. Только не рядом с послушником."
        "Поэтому они начали спарринг..."
        "...И очень скоро стало ясно, насколько сильно орк уступает ему."
        "Ослабленный тюремным заключением или нет, Роуэн все еще был тренированным бойцом. Без форы он мог легко танцевать вокруг гораздо более медленного орка."
        "Он почти жалел своего противника, который, несмотря на несколько неудач, принял решительное выражение лица и продолжал пытаться одержать верх над Роуэном."
        orca "Неправильно. Все не так."
        "Послушница ударила орка своим посохом, остановив их битву на полпути."
        orca "Нестоящий. Нужно больше практики."
        "Она начала объяснять, какие упражнения они оба будут выполнять в предстоящий день."
        "Роуэн был знаком с большинством из них - точечные тренировки, челночные пробежки, прыжки с места ... послушница, возможно, не была знакома с надлежащей терминологией, но она знала свое дело."
        "Рутина, которую она приготовила для них, несомненно, будет утомительной, и ее придется разбавить регулярными упражнениями, чтобы они не перенапрягали ноги..."
        "... но в целом Роуэн находил это вполне разумным."
        "К сожалению, это заставляло его и орка делать многочисленные перерывы в течение дня. И перерывы им приходилось проводить в компании друг друга."
        "Хотя Роуэн и не собирался дружить с кем-либо из орков лагеря, тяжелая атмосфера и, откровенно говоря, скука от сидения в тишине оказались слишком тяжелыми для героя."
        "Он начал болтать с орком. Совсем чуть-чуть."
        "Орк был не совсем дружелюбен, но ему, должно быть, тоже было скучно, потому что Роуэну не потребовалось много времени, чтобы заставить его открыться."
        "Его звали Улган, и он оказался довольно порядочным человеком. Он не очень-то охотно делился своей историей, но Роуэн без труда сумел собрать ее воедино из расплывчатых ответов орка."
        "Как он и ожидал, с самых ранних лет Улган не имел большого успеха в борьбе со своими физически превосходящими сверстниками."
        "В обществе орков это обычно означало остракизм – быть низведенным до черной работы, более подходящей для раба, чем для гордого воина-орка."
        "Он должен был научиться сражаться головой, а не телом – но даже это не помогло ему продвинуться далеко."
        "Роуэн не могл не сочувствовать орку. Он выказывал все признаки отличного военного офицера-холодную голову, здравый смысл и железную решимость."
        "Но для орков такие качества не имели никакой ценности, если их нельзя было подкрепить боевым мастерством. Столько талантов потрачено впустую…"
        "Роуэн с горечью вспоминал, как его самого возвели в генералы. Если бы не ужасная ситуация на фронте, дворяне никогда бы не стали слушать такого простолюдина, как он."
        "Ирония в том, что оба стороны страдали от схожих проблем, несмотря на столь радикальные различия."

        #if rowan has enough MP left to complete
        if avatar.mp >= 7:
            "Через неделю их обучение подошло к концу, и Роуэну пришлось попрощаться со своим новообретенным спутником."
            "Он искренне пожелал ему удачи, надеясь, что Улган однажды получит заслуженное признание."
            #note Rowan has met Ulgan
            #gain 2 Dodge
            $ rowan_has_met_ulgan = True
            $ avatar.skills['dodge'] += 2
            $ change_mp(-7)
            $ orciad_priest_complete_dodge = True
            $ orciad_priest_complete_count += 1
            jump campMenu
        else:
        #Does not have enough movement points remaining.
            "К сожалению, одна из его бесед с Улганом была прервана Изерой, требующей его внимания в замке. Ему придется закончить их обучение позже."
            #note Rowan has met Ulgan
            $ rowan_has_met_ulgan = True
            $ avatar.mp = 0
            $ activate_event('orciad_returning_to_camp_dodge')
            #see "returning to camp - dodging" text further down
            return

    #unlocks after discussing training
    "Тренировка освобождение от пут." if orciad_priest_training_unlocked:
        $ released_fix_rollback()
        "Роуэн на миг подумал, стоит ли позволить священнику безумного бога связать его, чтобы он мог по практиковаться в освобождении от пут."
        ro "…"
        ro "(Нет!)"
        jump orcPriestMenu

    #unlocks after discussing training
    "Покинуть поляну.":
        $ released_fix_rollback()
        "После некоторого раздумья Роуэн решил, что сейчас не самое подходящее время, чтобы беспокоиться о жреце орков и его уроках обучения."
        "Возможно, позже."
        jump campMenu


################################################################################
label orciad_returning_to_camp_climb:
### returning to camp - climbing
#This text should appear when Rowan arrives at the camp the following week after not having enough MP to complete training, afterwards, return to usual orc camp option screen

"Прежде чем перейти к своим обычным делам с орками, Роуэн направился на поляну, где обычно жил жрец Хароса. Он должен был закончить свои предыдущие уроки скалолазания."
"Потребовалось некоторое время, чтобы вернуться к темпу событий, но достаточно скоро Роуэн вернулся в установленный режим. Через несколько дней его обучение было завершено."
"Некоторые из его навыков вернулись - не на тот уровень, на котором они были до заключения, но это был прогресс. Он поблагодарил священника и его помощника и вернулся в лагерь орков."
#gain 2 climbing
$ avatar.skills['climb'] += 2
$ change_mp(-7)
$ orciad_priest_complete_climb = True
$ orciad_priest_complete_count += 1
jump campMenu


################################################################################
label orciad_returning_to_camp_sneak:
### returning to camp - sneak
#This text should appear when Rowan arrives at the camp the following week after not having enough MP to complete training, afterwards, return to usual orc camp option screen

"Прежде чем перейти к своим обычным делам с орками, Роуэн направился на поляну, чтобы завершить свое тайное обучение с двумя орками."
"Некоторое время спустя Роуэн с радостью отметил, что он получил более или менее все, что мог от этого упражнения."
"Он мог бы продолжать увеличивать трудности, если бы захотел, но он уже потерял слишком много времени на поляне. Другие дела требовали его внимания."
"Он поблагодарил двух орков и ушел, довольный тем, что ему удалось вернуть хотя бы часть утраченных способностей."
#gain Move silently +1, Spot +1
$ avatar.skills['spot'] += 1
$ avatar.skills['move_silently'] += 1
$ change_mp(-7)
$ orciad_priest_complete_sneak = True
$ orciad_priest_complete_count += 1
jump campMenu


################################################################################
label orciad_returning_to_camp_listen:
### returning to camp - hearing
#This text should appear when Rowan arrives at the camp the following week after not having enough MP to complete training, afterwards, return to usual orc camp option screen
"Прежде чем приступить к своим обычным лагерным делам, Роуэн нанес визит орочьему жрецу."
"Он сообщил послушнику, что останется в лагере орков на некоторое время и хотел бы возобновить тренировки."
"Следующие несколько дней они продолжали свои обычные упражнения, пытаясь распознать множество звуков, которые издавал лагерь орков."
"Со временем Роуэн настолько овладел этим искусством, что орк-послушник счел его обучение законченным."
"И снова к нему вернулась часть его прежней доблести."
#gain 2 Listening
$ avatar.skills['listen'] += 2
$ change_mp(-7)
$ orciad_priest_complete_listen = True
$ orciad_priest_complete_count += 1
jump campMenu


################################################################################
label orciad_returning_to_camp_dodge:
### returning to camp - dodging
#This text should appear when Rowan arrives at the camp the following week after not having enough MP to complete training, afterwards, return to usual orc camp option screen

"Вернувшись в лагерь, Роуэн направился на поляну, где жрец Хароса давал ему уроки."
"Не было никакого смысла оставлять его обучение незаконченным, и ему не терпелось проверить прогресс, достигнутый Улганом за время его отсутствия."
"Его новообретенный спутник вежливо поздоровался с ним, и, обменявшись любезностями, они вернулись к своему обычному распорядку."
"В конце концов, Роуэн добился значительного прогресса в своих навыках уклонения и сумел встретить кого-то, кто мог бы однажды стать ценным союзником."
"Он искренне пожелал Улгану удачи, надеясь, что в один прекрасный день он получит заслуженное признание."
#gain 2 Dodge
$ avatar.skills['dodge'] += 2
$ change_mp(-7)
$ orciad_priest_complete_dodge = True
$ orciad_priest_complete_count += 1
jump campMenu
