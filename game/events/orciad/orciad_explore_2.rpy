init python:

    #Humans are not welcome
    event('humans_are_not_welcome', triggers='orciad_explore', conditions=("week >= 4",), run_count=1, group='orciad_camp', priority=pr_map_res)
    #Lost Knowledge
    #repeatable
    event('lost_knowledge', triggers='orciad_explore', conditions=("week >= 4",), group='orciad_camp', priority=pr_map_res)
    #Camp Slaves
    event('camp_slaves', triggers='orciad_explore', conditions=("week >= 4",), run_count=1, group='orciad_camp', priority=pr_map_res)
    #Youth of the Nation
    #Requires the “Orcish Families and Neutrals” event
    event('youth_of_the_nation', triggers='orciad_explore', conditions=("week >= 4",), depends=('orcish_families_and_neutrals',), run_count=1, group='orciad_camp', priority=pr_map_res)
    #The Woods Outside of Camp
    event('the_woods_outside_of_camp', triggers='orciad_explore', conditions=("week >= 4",), run_count=1, group='orciad_camp', priority=pr_map_res)


label humans_are_not_welcome:
#Humans are not welcome
#"Explore the Camp" event

$ change_mp(-5)
$ prevent_tile_exploration()

scene bg26 with fade
show rowan necklace neutral at midleft with dissolve
show orc soldier neutral at midright with dissolve
#female orc

os "Хьюми, тебе здесь не место."

"Учитывая, что это был лагерь орков, можно было предположить, что рано или поздно кто-нибудь остановит Роуэна. С защита демонов или без нее."

ro "Я здесь по делу мастера демонов и имею разрешение от..."

femorc "Заткнись, слабый розовый!"

"Она замахнулась рукой, чтобы ударить Роуэна по лицу, но он рефлекторно отскочил назад, чтобы избежать удара."

"Во всяком случае, это только разозлило орков еще больше. Еще двое подошли к нему сзади, отрезая все шансы на спасение."
"Очевидно, что все это было запланировано."

ro "Чтож тогда дуэль и уладим все, как орки."

os "Ты не орк. Ты не имеешь права драться на дуэли."

"Остальные проворчали в знак согласия. Это выглядело не очень хорошо."
"Он сомневался, что эти хулиганы хотели убить его или даже серьезно ранить, они, казалось, использовали его как козла отпущения для чего-то еще, возможно, неудачного рейда или разочарования из-за текущих проблем в лагере."
"Роуэн обдумал свои варианты, если дела пойдут худо, он всегда сможет вернуться в замок. Однако это означало, что он зря потратит время, если у него были дела здесь на этой неделе. Он также может получить ранения."

#(If no exploration time left)
if avatar.mp < 1:
    "С другой стороны, Роуэн вряд ли сможет сделать в лагере что-то, прежде чем ему придется вернуться в замок.  Лишние несколько часов, которые он сможет провести в лагере, не стоили того, чтобы его избили."
    "Герой положил руки на ожерелье и тихо произнес:"

    ro "Изера, верни меня в замок."

    hide rowan with dissolve

    "А потом он исчез."
    return

#else
else:
    menu:
        "Вернится к порталу.":
            $ released_fix_rollback()
            "Не стоило рисковать. Лучше неудобство, чем позволить группе орков избить его."
            "Герой положил руки на ожерелье и тихо произнес:"
            ro "Изера, верни меня в замок."
            hide rowan with dissolve
            "А потом он исчез."
            #Rowan is placed on the map at the portal nearest the orc camp.
            # TODO: move to the portal
            return

        "Ввязатся в драку.":
            $ released_fix_rollback()
            "Его работа здесь была слишком важна, чтобы сбежать сейчас. Роуэну пришлось принять вызов и надеяться, что орки оставять его в работоспособном состоянии после этого, или он смог бы сбежать позже."
            "Скрипя зубами, Роуэн остался на месте, когда орк ударил его, приняв на себя всю силу удара. На лице оркской женщины образовалась ухмылка, а затем она ударила его в живот."
            femorc "Будешь должен. Сказал, что  хочешь дуэль, а получишь свидание."
            "Она приняла на себя основную тяжесть повреждений от удара, из-за его брони. Однако несколько  орков схватили его за руки и начали срывать броню с его тела."
            os "Теперь мы увидим, хороша ли розовая кожа против зеленой."
            "Еще один удар, на этот раз выбивший воздух из легких Роуэна, и он согнулся пополам от боли."
            #rowan hurt
            ro "Гха. Черт."
            "Как он ни старался, ему не удавалось сохранить совершенно невозмутимое выражение лица, и орки вокруг него, очевидно, это заметили. Потом один из них пнул его ногой."
            femorc "Видишь, хьюми слабак. Хьюми нет места в лагере могучих орков."
            "Огромным усилием воли он заставил себя встать и снова повернуться лицом к своим оппонентам."
            "Это вызвало у него раздражение, но он постарался сохранить нейтральное выражение лица, стараясь быть как можно более скучным."
            os "Эй, плачь! Покажи нам свои слезы хьюми!"
            "Он ударил Роуэна кулаком в лицо, отчего у того потекла кровь. Было чертовски больно, но в подземельях Андраса этот человек пережил и худшее. Он не плакал."
            #Rowan bloodied

            #vitality test (DC12)
            $ event_tmp['vitality_test'] = check_stat(12, 'vitality')
            $ released_fix_rollback()
            if event_tmp['vitality_test'][0]:
                #success
                "Они били на него еще несколько минут, однако те, кто наблюдал за этим, вскоре заскучали из-за отсутствия слез."
                "По мере того как аудитория уменьшалась, те, кто изначально подошел к нему, быстро потеряли интерес."
                hide orc soldier neutral with moveoutright
                #hide female orc with moveoutright
                "В конце концов, Роуэн был весь в синяках и слегка окровавлен, но гордился собой."
                #gain 25 xp and one injury
                $ add_exp(25)
                $ add_effect(MultiEffect('Wound', 'neg', (('strength', -1),), 2))
                jump campMenu
            elif event_tmp['vitality_test'][1] >= 9:
                #partial success (9 or more)
                "Он старался, как мог, но в конце концов Роуэн больше не мог больше оставаться на ногах и упал на землю от боли."
                "Так он лежал несколько минут, снося насмешки над ним и редкие удары ногой."
                "В конце концов орки решили пойти и поесть или выпить что-нибудь в честь того, что они 'разобрались' с ним."
                hide orc soldier neutral with moveoutright
                #hide female orc with moveoutright
                "Человек остался, пока не смог подоняться и очистить свое тело от крови и грязи. Даже когда он, наконец, сделал это и покинул место нападения, никто не подошел и не заговорил с ним."
                #rowan gains 10xp and 2 injuries
                $ add_exp(10)
                $ add_effect(MultiEffect('Wound', 'neg', (('strength', -1), ('vitality', -1)), 3))
                jump campMenu

            #fail
            else:
                "Удары продолжали приходить и приходить.  Где-то вдоль линии соприкосновения он упал на землю, но это не помешало оркам продолжать бить его снова и снова."
                scene black with fade
                "Звезды летали над его головой, когда он почувствовал костный хруст под ботинком женщины-орка, которая ударила его первым."
                "Нет, это было слишком.  Роуэну пришлось бежать. Он отчаянно пытался отползти к восторгу своих мучителей, прежде чем вспомнил про амулет."
                "Он вернулся в замок, сотрудники замка подняли его в свою комнату, чтобы он оправиться от мучений. На этой неделе разведка подошла к концу."
                #rowan gains 2 injuries, end week
                $ add_effect(MultiEffect('Wound', 'neg', (('strength', -1), ('vitality', -1)), 3))
                return

############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label lost_knowledge:
#Lost Knowledge
#"Explore the Camp" event
#repeatable

$ change_mp(-5)
$ prevent_tile_exploration()

scene bg26 with fade

"Роуэн отправился осматривать палатку с добычей. Это была самая большая и самая величественная палатка в лагере, охраняемая десятком орков."
"Когда он подошел к входу, охранники спорили о том, следует ли убить его сразу, или позволить подойти, а затем убить."
"Начальник охраны лагеря мародеров, однако, был готов впустить Роуэна, если он ничего не тронет."
"Внутри палатки огромные стопки золота, драгоценных камней и товаров всех видов были собраны, казалось бы, случайными кучами."
"Для нормального человека это было бы достаточно богатства, чтобы прожить всю жизнь. Но Роуэн мог сказать, что несмотря на все великолепие перед ним, этого было недостаточно."
"Орки - это не собиратели урожая или ремесленники, а налётчки. Их повседневное выживание зависит от того, сколько они захватят, чтобы обменять на жизненные нужды."
"Склад такого размера мог бы прокормить племя не более чем несколько месяцев."
"Роуэн заметил вход в маленькую палатку сзади. Здесь был разбросан огромный ассортимент хлама."
"Безделушки и в основном бесполезные предметы, что были собраны во время рейда, а затем выброшены из-за низкой стоимости стоимости."
"Здесь охране уделяли гораздо меньше внимания."
"Роуэн прошелся между штабелями, изучая то, что они нашли. Орки не нуждались ни в столовых приборах, ни в инструментах для изготовления кирпича, ни в сувенирах."
"Однако в одной из стопок он нашел книгу с гниющими страницами."
"С первого взгляда он усомнился, что в библиотеке Бладмина есть ее копии. Книга может быть полезна для исследовательских целей."
"Роуэн протянул руку, чтобы взять книгу. Однако один из охранников наблюдал за ними. Он подошел к нему, нахмурив брови."

#diplomacy check (dc15)
if check_skill(15, 'diplomacy')[0]:
    #success
    "Роуэн обезоружил охранника, объяснив, что книга ничего не стоит. Охранник был вынужден согласиться, учитывая, что это была мусорная палатка."
    "Он по-прежнему не хотел, чтобы Роуэн прикасался к чему-либо еще, заставив его поклясться, что он возьмет только одну книгу. "
    "Однако этого было достаточно. Знания не считались очень ценными в лагере орков, но в руках его хозяев они могли оказаться ценным оружием."
    #gain 2 research points
    $ castle.rp += 2
    jump campMenu

#fail
else:
    "Роуэн попытался объяснить, насколько бессмысленна эта книга для орков, но все, что ему удалось сделать, - это оскорбить стражника."
    "Капитану стражи пришлось вмешаться, чтобы предотвратить нападение на Роуэна."
    "Однако в этот момент все шансы забрать книгу с собой были упущены. Роуэну пришлось оставить книгу и пообещать не возвращаться до тех пор, пока охранник не сменится."
    jump campMenu

############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label camp_slaves:
#Camp Slaves
#"Explore the Camp" event

scene bg26 with fade

$ change_mp(-5)
$ prevent_tile_exploration()

"Это племя не помещало своих рабов ни в какие шатры. Вместо этого пленных людей отвели в несколько пещер у подножия холма на северной стороне лагеря. Там их охраняли днем и ночью."
"Орки обоих полов приходили и уходили из пещер, свободно используя пленников для собственного удовольствия."
"Когда Роуэн исследовал эту местность, он вынужден был быть очень осторожен. С его происхождением любой из орков мог бы напасть на него. А убийство члена племени не было эффективным способом ведения дипломатии."
"Звуки криков разносились по всем пещерам. Одни от удовольствия, другие от боли. Это было невыносимо."
"Голос окликнул его из-за толстых стальных прутьев. Избитый мужчина средних лет окликнул его, желая что-то сказать."
"Роуэн предпринял большую осторожность, чтобы не быть замеченными охранниками. Пленник был частью группы рабов, которые планировали побег."
"Они строили туннель в течение последнего месяца, чтобы сбежать. Прошлой ночью их охранник насмехаясь над ними, сказал, что они будут проданы утром."
"Орки часто продавали пленников работорговцам и другим темным существам."
"Теперь им нужно было воспользоваться своим туннелем."
"Но он еще не был до конца готов. Туннель заканчивался перед стенами лагеря всего в тридцати футах."
"Поэтому им как-то требовалось пройти через ворота. И им нужна была помощь Роуэна."

menu:
    "Сказать охранникам.":
        $ released_fix_rollback()
        "Роуэн пообещал ему помочь. Но Роуэн здесь не для того, чтобы спасать пленников. Он был здесь, чтобы заключить союз с племенем орков."
        "Час спустя Роуэн подошел к главному стражнику и объяснил план рабов."
        "Сначала на эту информацию не обратили внимания, но когда охранник обыскал камеру, он обнаружил огромный туннель, который тянулся почти до самого лагеря."
        "В ту ночь пленников, пытавшихся бежать, выпороли. Чтобы вознаградить Роуэна за службу, племя предало золото в казну Бладмина."
        #Gain treasury gold and guilt
        $ change_treasury(15)
        $ change_base_stat('g', 2)
        $ orciad_betray_the_slaves = True
        jump campMenu

    "Помочь рабам.":
        $ released_fix_rollback()
        "Роуэн обещал человеку, что поможет. Он велел ему дождаться сигнала от него и бежать позже той же ночью. Роуэн обещал справился с воротами."
        "Некоторые из рабов подошли к решеткам клетки, протянув руку и потянувшись к нему."
        "Это было рискованно, но он не мог оставить этих людей на произвол судьбы. Он уже и так, продал много своей души."
        "В ту ночь он прошел мимо решетки и махнул рукой. Затем он медленно направился к северным воротам. Большинство охранников было на стенах лагеря. Орки не ожидали нападения изнутри."
        "Только один орк стоял возле колеса, открывавшего ворота. Хотя он был большим. Если он слишком долго будет расправляться с охранником, это может насторожить остальных."
        "Роуэн обнажил клинок и тихо подкрался к нему."
        #reflexes check (DC12)
        if check_stat(12, 'reflexes')[0]:
            #Success
            "Роуэн подкрался ближе к орку, оставаясь в тени. Орк сидел в кресле, стараясь не заснуть, с чувствами, притупленными сонливостью."
            "Роуэн сомневался, что он увидит его приближение."
            "И все же ему приходилось продумывать каждый шаг, чтобы не издать ни единого шального звука. Все ближе, ближе, ближе."
            "Роуэн приготовил свой клинок и приготовился к убийству. Для этого потребовался один быстрый удар по горлу. Орк даже не понял, что его ударило."
            "Он молча откинулся назад, издавая булькающие вздохи."
            "В тени Роуэн увидел приближающиеся фигуры. Он не расслаблялся, пока они не подошли достаточно близко, чтобы подтвердить что это они."
            "Пленники сделали это. Роуэн тихонько приоткрыл ворота, чтобы пропустить людей."
            "Утром орки нашли мертвого стражника возле ворот. Исследование помещений рабов выявило туннель для побега. Никто никогда не подозревал, что именно Роуэн освободил их."
            "Одним грузом меньше для его совести. "
            #lose guilt
            $ change_base_stat('g', -2)
            jump campMenu

        #fail
        else:
            "Роуэн подкрался ближе к орку, оставаясь в тени. Орк сидел в кресле, стараясь не заснуть, с чувствами, притупленными сонливостью."
            "Роуэн сомневался, что он увидит его приближение."
            "И все же ему приходилось продумывать каждый шаг, чтобы не издать ни единого шального звука. Все ближе, ближе, ближе."
            "Роуэн вытащил свой клинок и приготовился к убийству. В это было особенно неподходящее время, угараздило его наступить на шальную ветку."
            "Стражник проснулся, схватив свой клинок. Прикрытие Роуэна было потеряно."
            "Роуэн рванулся вперед, нанося удар охраннику. Пленники прибудут в любую секунду. Если они справятся быстро, у них еще есть шанс на успех."
            "Орк-стражник сделал несколько отчаянных замахов, но все мимо."
            "Один из них открыл свою защиту достаточно, чтобы Роуэн смог сократить дистанцию и прикончить его."
            "На крепостных валах раздавались крики и беготня. Охранники поняли, что что-то не так. Он был рад, что в этот момент появились пленники."
            "Роуэн открыл ворота, позволив им бежать из лагеря."
            "Тем временем Роуэн сам совершил побег. Стражники устили его всего на несколько мгновений."
            "К счастью, никого из пленников так и не нашли. Однако первые несколько дней ходили упорные слухи, что Роуэн принимал участие в побеге."
            "Он был единственным свободным человеком в лагере."
            "Чтобы не вызвать еще большего переполоха, Роуэн решил, что до конца недели ему лучше не показыватся в лагере."
            #lose guilt. Rowan can’t go the camp for a week.
            $ change_base_stat('g', -2)
            # TODO: forbid access
            return

############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label youth_of_the_nation:
#Youth of the Nation
#"Explore the Camp" event
#Requires the “Orcish Families and Neutrals” event

$ change_mp(-5)
$ prevent_tile_exploration()

scene bg3 with fade
show rowan necklace neutral behind bg3

"Роуэн вернулся  к лагерю кормящих женщин и детей. Он провел часть утра, разговаривая с некоторыми из них. Возвращаясь в лагерь, он заметил одного молодого орка у ручья."
"Этот юноша был необычайно тощ для своего возраста. Удивительно, но в руках у него была книга. Роуэн никогда раньше не видел, чтобы молодой орк читал."
"Роуэн подошел к юноше-орку. Его звали Эйдуд"

ro "Где ты это нашел?"

eid "У раба. Мне захотелось посмотреть картинки."

"Эйдуд указал на книгу. Похоже, это была иллюстрированная рукопись. На правой стороне каждой страницы была акварельная картинка, а на левой - плотный текст."

eid "Что это?"

ro "Это ветряная мельница. Это то, что люди используют, чтобы раздавить зерно, чтобы мы могли его съесть."

"Эйдуд перевернул страницу. На ней был изображен рыцарь верхом на коне, атакующий отряд вооруженных людей. Молодой орк уставился на картинку."

eid "Кто это металлический человек?"

ro "Это рыцарь. Это люди, которые носят металлические доспехи в бою."

eid "Броня? Зачем?"

ro "Он блокирует удары оружия, поэтому его труднее убить. Рыцари поклялись защищать свой народ, поэтому им нужно более сильное оружие и броня."

"Эйдуд, казалось, был смущен этим. В оркском обществе существовали клятвы, но ничего более сложного или запутанного, чем феодальные отношения."

ro "Крестьяне, племя рыцаря, дают рыцарю пищу и деньги. Рыцарь воспитывает сильных сыновей и покупает мощные доспехи. Так рыцари защищают племя."
ro "Рыцари - одни из самых могущественных воинов среди людей."

eid "Рыцари сильные?"

ro "Очень."

"Это, казалось, дошло до Эйдуда. Юноша-орк в благоговейном страхе открыл рот и провел пальцем по изображению рыцаря на коне."
"Конечно, Роуэн и раньше знал многих рыцарей, и лишь немногие из них соответствовали героическому идеалу, запечатленному в рукописи."
"И все же идея была захватывающей. Может ли орк научиться рыцарству?"

eid "Может ли Эйдуд быть рыцарем?"

ro "Я никогда раньше не встречал рыцаря-орка. Это было бы странное зрелище."

eid "Тогда Эйдуд будет первым рыцарем-орком. Рыцари могущественные. Орки могущественны. Орочий рыцарь Самый могущественный!"
eid "Орочий рыцарь Эйдуд принесет славу племени. Сожгет много деревень. Заставит врагов кричать от ужаса. Сильный."

"Эйдуд схватил страницу с рыцарем, грубо вырвав ее из книги. Он встал, позволив остальной книге упасть с его колен."
"Хоть раньше он проявлял к ней столько благовения, по сравнению с тем, как к ней отнесся бы обычный орк."
"Роуэн вздохнул.  Он поднял с земли рукопись. Даже поврежденная, она все равно может чего-то стоить."
"Позже Роуэн прошел мимо боевой ямы, где тренировались орки на пороге зрелости. Сегодня был громкий переполох. Один из бойцов делал что-то совершенно необычное."
"Роуэн подошел посмотреть. Он обнаружил, что Эйдуд сражается с одним из молодых орков."
"Только Эйдуд был одет в очень примитивную броню, уродливо сделанную из деревянной коры. И все же это имело значение."
"Эйдуд одолел своего противника, пропустив несколько ударов дубинкой, используя броню. Никто из зрителей не знал, как реагировать."
"Орки ценили победу, но находили мало славы в том, чтобы быть закованными в броню. А Роуэн? Роуэн не знал, гордиться ему или бояться."

#gain 10 personal gold
$ change_personal_gold(10)
jump campMenu

############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label the_woods_outside_of_camp:
#The Woods Outside of Camp
#"Explore the Camp" event

$ change_mp(-5)
$ prevent_tile_exploration()

scene bg3 with fade

"На этот раз Роуэн осмотрел лагерь. Он нашел небольшой лес сразу за воротами. Зеленое пятно спокойствия в пределах слышимости пиров и сражений орков."
"Он выбрал этот лес, чтобы посидеть и отдохнуть."
"У подножия дерева он заметил несколько старых костей. К этому времени они превратились в осколки, память о каком-то давно умершем воине-орке."
"При ближайшем рассмотрении самого дерева обнаружились сильно выцветшие пиктограммы."
"В прошлом они были вырезаны на дереве так неглубоко, что стали шепотом в лесу. Легко не заметить неподготовленным глазом."
"На пикограммах был изображен орк необычного размера и силы, что было видно по тому, как он возвышался над остальными, совершая подвиги силы."
"Убивая врагов, забирая женщин, собирая сокровища для своей шкатулки с добычей."
"На последней он был весь в крови, окруженный деревьями. Без сомнения, большая часть его была ложной."
"Это был умирающий орк, рассказывающий свою собственную историю. Но, глаза Роуэна внимательно смотрели на финальную пиктограмму."
"Символ скрытого кэша."
"Роуэн вскопал землю рядом с костями. Его любопытство было вознаграждено. Под костями лежал сгнивший ящик, а внутри-остатки сокровищ орка."
"Это была не та большая сумма, о которой рассказывали пиктограммы, но то, что не было заржавлено, можно было еще использовать."
"Настоящим испытанием дня стало сокрытие этой суммы сокровищ от любопытных глаз орков-хозяев."

#gain 25 personal gold
$ change_personal_gold(25)
jump campMenu
