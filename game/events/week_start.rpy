init python:

    event('castle_access_week4', triggers="week_start", conditions=('week == 4',), run_count=1, priority=pr_system)
    event('castle_alexia_job_week23', triggers="week_start", conditions=('week == 23',), run_count=1, priority=pr_system)
    event('week4_introduction', triggers="week_start", conditions=('week == 4',), run_count=1, priority=pr_story)


label castle_access_week4:
    # this enables access to castle on week 4, enable researching and building
    $ systems.castle = True
    $ systems.research = True
    $ systems.building = True
    return


label castle_alexia_job_week23:
    $ systems.npc_jobs = True
    # assign starting job class to Alexia
    $ all_actors['alexia'].job_state = ActorJobState()
    $ castle.npc_with_jobs.append('alexia')
    $ set_job_class('alexia', 'pure_housewife')
    return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label week4_introduction:

#### Fix later

$ alexiaOffer = False

####### start of week 4 #######

#sunrise CG

scene black with fade

show jezera happy behind black

"Роуэн проснулся по привычке рядом с женой. Это не их дом в Артдейле, но ведь это могло быть и так."

je "Доброе утро, мой герой."

"Нет, это не так."
"Изера отдыхала в соседнем кресле, наблюдая за ними с улыбкой на лице."

scene bg9 with fade
show alexia necklace naked behind bg9
show rowan necklace naked behind bg9
show jezera happy at edgeright with dissolve

ro "Как давно ты здесь?"

je "Всего несколько минут, просто подумала, что позволю тебе поспать подольше и насладиться видом."

al "Хм... что такое Роуэн?"

je "И вам доброе утро, юнная леди."

show alexia necklace naked shocked behind bg9

al "Ах! Что ты здесь делаешь?"

je "Оставляю завтрак и спрашиваю, не хочешь ли ты, чтобы твои вещи принесли сюда, Алексия."

"- Она указала на накрытые тарелки, стоявшие на столе позади нее."

show alexia necklace naked behind bg9

al "О! Э..."

"Алексия посмотрела на мужа, не зная, что ответить."

menu:
    "Быть вместе.":
        $ released_fix_rollback()
        $ alexiaSeparateRoom = False
        "Роуэн ответил на ее взгляд, улыбнулся и кивнул."
        "Алексия просияла в ответ и повернулась к синекожей демонице."
        al "Да, я думаю, я буду спать здесь."
        "Изера кивнула в ответ и встала."
        je "Хорошо, я сейчас же пошлю кого-нибудь позаботиться об этом. Я также приготовлю ванну для вас."
        je "Роуэн, ты свободен до обеда."


    "Жить в разных комнатах":
        $ released_fix_rollback()
        $ alexiaSeparateRoom = True
        ro "Я хотел бы некоторую свободу, может быть, ты сохранишь свою старую комнату?"
        al "На самом деле я думала о том же. Нам не придется беспокоиться если... произойдет что-то неизбежное."
        al "Хотя это облегчение, что мне не пришлось предлагать это самой."
        "Алексия слегка улыбнулась и повернулась к синекожей демонице."
        al "Спасибо, но я думаю, что вы можете оставить мои вещи в моей комнате."
        "Изера кивнула в ответ и встала."
        je "Очень хорошо. Тогда я распоряжусь, чтобы тебе приготовили ванну. Алексия можешь вернуться в свою комнату, когда пожелаешь."
        je "Роуэн, ты свободен до обеда."

"Она направилась к двери, но остановилась, едва коснувшись ручки."

je "О, и я так рада, что вы двое не пытались сбежать прошлой ночью. Я поспорила с братом, что ты не сделаешь такой глупости."
je "Он будет так разочарован, узнав, что проиграл."

hide jezera with moveoutright

scene black with fade
scene bg9 with fade
show rowan intro necklace happy at midleft with dissolve
show alexia necklace happy at midright with dissolve

"Полчаса спустя Алексия и Роуэн ели вместе и обсуждали демонических близнецов, чьим капризам они теперь подчинялись."

if alexiaOffer == True:
    al "... и это все, что я знаю об Андрасе..."
    show alexia necklace concerned at midright with dissolve
    "Алексия сгорела от стыда, пытаясь выдавить из себя эти слова."
    "Муж, почувствовав, как неловко ей от этих воспоминаний, взял ее за руку и перевел разговор на другую тему."
    ro "Спокойно, любовь моя. Скажи мне, что тебе удалось узнать о Изере."
    show alexia necklace happy at midright with dissolve

else:
    al "... и это все, что я знаю об Андрасе. Как я уже сказала, я не уверена точно, что реально или нет, так как он, притворялся большую часть наших встреч."
    ro "Хорошо, а что насчет сестры?"

al "Боюсь, я мало что о ней знаю."
al "После твоего ухода она какое-то время лгала о своей жизни в Уварте, а потом раскрыла свою истинную сущность и сбила меня с ног магическим заклинанием."
al "Иногда она приходила в гости, но почти не разговаривала."
al "Оба они, кажется, очень хорошие лжецы и обладают мощной магией. Дорогой, пожалуйста, будь осторожен с ними."
"Роуэн улыбнулся и кивнул, и они провели остаток утра, обсуждая более приятные темы для разговора, пока не пришло время встречи с Джезерой."

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################

scene bg14 with fade
show rowan necklace neutral at midleft with dissolve
show jezera neutral at midright with dissolve

"Вскоре после обеда..."

ro "Я вижу, мы больше не под охраной."

je "Да, такое было условие моего выиграша в пари. Теперь вы оба можете свободно передвигаться по замку."
je "Мой герой, пожалуйста, иди за мной."

"Синекожая демоница пошла по коридору, Роуэн на мгновение заколебалась, а затем последовала за ней."

ro "Это дорога не к камере портала."

je "Нет, сегодня я даю тебе новый набор обязанностей."

ro "И что именно это будет?"

je "Управление и исследования."
je "Хотя у нас здесь и есть могучая крепость, она ужасно недоукомплектована, ведь большая часть инфраструктуры была разрушена в последней войне."
je "Вот почему я выбрала тебя. Ты не самый сильный, и не самый умный из героев, убивших моего отца."
je "Тем не менее, ты хитер и имеешь навыки, необходимые для того, чтобы превратить этот замок в резиденцию империи."

ro "Почему ты так думаешь?"

"Изера вдруг остановилась."

show jezera happy at midright with dissolve

"Через мгновение повернулась и улыбнулась Роуэну."

je "Ты герой осады Карста. После того, как лорд умер, а другие дворяне бежали, ты  принял командование и продержался в осаде, четыре месяца"
je "Пока подкрепление из Протеи не прибыло, чтобы ее снять."
je "Ты тот, кто организовал тайные поставки продовольствия в замок по ночам."
je "Ты тот, кто сумел отразить два штурма, несмотря на низкие шансы победы."

ro "Три штурма."
ro "Большинство не помнит второго, когда я ночью уничтожил осадные башни и они вынужденны были отказатся от штурма."
ro "Кроме того, во время первого они не успели собрать осадные башни и были обречены на провал"

je "Истории имеют тенденцию пропускать много деталей и украшать некоторые вещи."

ro "Люди всегда предпочитают легенду реальности. Когда я думаю о Карсте, я вижу лица людей, которые умерли."

je "Тем не менее, это остается впечатляющим резюме. Такой позор, что простолюдин в Розарии не может стать генералом, не говоря уже об чине офицера, ."
je "Как и твоя группа героев, мы с братом не позволим такой вещи, как рождение, мешать использованию чьи-их то навыков."

ro "Думаю, ты не понимаешь, почему я решил вернуться в Артдейл."

je "Что ж, сейчас ты сделал свой выбор работать на нас, и я не позволю твоим талантам пропасть даром."

"Она продолжила идти, ведя Роуэна в тронный зал."

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################

scene bg6 with fade
show rowan necklace neutral at midedgeleft with dissolve
show jezera neutral at trueedgeleft with dissolve

je "Вот почему я назначаю тебя ответственным за замок."

ro "Что?"

je "Мой брат и я будем принимать решения, что мы хотим сделать, а как мы это совершим это уже твоя работа."
je "О, и не беспокойся о повседневных операциях, мы все еще хотим, чтобы ты был нашими глазами и ушами в мире."
je "Твоя задача будет выяснить общую картину."
je "Это включает в себя выбор того, как тратить наши средства и куда мы должны направить наши исследования."
je "Полагаю, теперь ты  наш управляющий."

show clamin neutral pipe at midmidright with moveinright
show skordred neutral at skorright with moveinright

"Изера остановилась перед странной парой: темным гномом и женщиной-гоблином."
"Дварф выглядел по-королевски в своей изысканной позолоченной одежде, но его внешность была очень типичной для его расы."
"Роуэн сразу заметил, что смуглый гном не любит его, его напряженная поза и презрительный взгляд были очень заметны."
"Женщина-гоблин с другой стороны улыбалась."
"У нее был вид торговки, и от нее исходило привычное ощущение, когда она пытается казаться твоим  другом и у нее есть идеальный предмет, который решит все проблемы."

je "Позвольте представить вам Скордреда и Кла-Мину."
je "Вы двое, это ваш новый босс, Роуэн."

"Гном Скордред сплюнул на пол, когда Изера произнесла имя Роуэна."
"С другой стороны, гоблин Кла-Мин низко поклонилась. Изера проигнорировала их действия."

je "Скордред, мастер-строитель темных дварфов, и возглавлял команду, которая восстановила замок для Карнаса."
je "Он согласился вернуться на свой прежний пост, чтобы служить моему брату и мне, и будет курировать строительные проекты, на которые ты выделишь средства."

sk "Слушай, герой, я не хочу говорить с тобой больше, чем нужно.  Даже если ты сейчас служишь новым хозяевам, ты часть группы, которая убила старого мастера."
sk "Я не простил тебе этого тяжкого греха."

hide skordred neutral with moveoutright

je "Он не особенно дружелюбный, но, как и наша мать, сильно тянется к темноте. Я нисколько не сомневаюсь в его преданности."

"Не в силах больше сдерживаться, женщина-гоблин прыгнула вперед и начала энергично трясти руку Роуэна."

show clamin happy at midmidright with dissolve

cla "Это честь, наконец, встретиться с великим мастером-интриганом Роуэном! Великий гоблин в человеческой плоти!"

"Через мгновение Роуэн с некоторой тревогой понял, что зеленая женщина действительно заискивает перед ним!"

cla "Было так много историй о великих делах, которые ты сделал во время войны, о саботаже и обмане! Никакой магии, никакой силы, только ловкость и хитрость."
cla "Для меня будет честью работать под твоим началом... или под моим, если это твои предпочтения."
cla "Честно говоря, я предпочитаю быть сверху, но у меня есть много девочек в семье, которые любят принимать его всеми способами и..."

je "Достаточно. Дай ему перевести дух."

"К большому облегчению Роуэна, открыто флиртующая гоблинша отступила на шаг и снова поклонилась ему."

je "Кла-мин глава торгового каравана гоблинов. Она согласилась предоставить нам все необходимое в обмен на доступ к моему порталу."
je "Если,тебе нужно обновить оборудование или приобрести снаряжение для приключений, она поможет."

"Мелкая гордо встала и подмигнула герою. Затем она заколебалась, показалось,что она была поражена своим внезапным озарением."

cla "У нас в семье так же много мальчиков, если ты не любишь женщин. Я уверена, мы сможем договориться..."

show jezera displeased at trueedgeleft with dissolve

je "Мин!"

cla "Извини."

"В смятении покачав головой, Изера повела Роуэна прочь от торговца к лестнице, ведущей в подземную часть замка."

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################

scene bg8 with fade
show rowan necklace neutral at midleft with dissolve
show jezera neutral at midright with dissolve

ro "Так это было... интересно."

je "Я предполагала, что Скордред отнесется к тебе холодно, но чрезмерное обожание Мин было совершенно неожиданным."
je "Ты когда-нибудь слышал, чтобы гоблины боготворили тебя?"

ro "До меня доходили слухи об этом, но я не обращал на них внимания."

scene bg11 with fade
show rowan necklace neutral at midleft with dissolve
show jezera neutral at edgeleft with dissolve
show andras displeased at midright

"Вдвоем они нашли Андраса в казарменном комплексе, наблюдающего за тренировками солдат орков."
"Заметив приближающуюся пару, красный демон заметно помрачнел."

an "Ах, сестра и служанка. Значит, время для осмотра?"

je "Дорогой брат, неужели ты такой жалкий неудачник, что даже не умеешь вести себя вежливо?"

"Андрас только зарычал в ответ, затем отмахнулся от синекожей женщины и жестом велел Роуэну остаться."
"Изера подождала, не скажет ли брат что-нибудь еще, потом пожала плечами и оставила их."

hide jezera with moveoutleft

an "Это наш нынешний военный объект. У Скордреда не было достаточно ресурсов, чтобы построить что-то большее, чем это."

scene black with fade

"Далее он подробно разъяснил их нынешнюю способность поддерживать силы и свои усилия по вербовке."
"После этого Роуэн совершил экскурсию по окрестностям и немного поговорил с некоторыми солдатами."
"В результате  он получил хорошее представление о нынешнем военном потенциале замка."
"И все, что он узнал, говорило о том, что положение вещей не идеально."
"При максимальной мощности они могли занять лишь несколько деревень, и то не смогли бы их удержать при хорошей контратаке."
"Кроме того, на то, чтобы заполнить казармы, у Андраса уйдут месяцы."
"Для завоевания одного из шести королевств, не говоря уже обо всех, требовалось много улучшений в оборудовании и большое количество рекрутов."

scene bg6 with fade

show rowan necklace neutral at midleft with dissolve
show jezera neutral at midright with dissolve
show andras displeased at edgeright with dissolve

je "Теперь, когда ты знаешь, каковы наши возможности, пришло время узнать, чего мы с братом ожидаем от тебя в ближайшие месяцы."

"Все трое вернулись в тронный зал, на троне сидела Изера, рядом с ней стоял Андрас."

je "Во-первых, мы хотим, чтобы ты собрал силы, достаточно мощные, чтобы захватить крепость Рейв в Розарии, рядом с моим порталом."
je "Это положит конец правлению барона в этом районе и создаст плацдарм для дальнейших завоеваний."

show jezera happy at midright with dissolve

je "Если вы сможете каким-то образом склонить Герцога на нашу сторону или взять крепость под контроль с помощью уловок, это нас тоже удовлетворит."

"Демонессе явно нравилась идея действия из тени. Ее брат фыркнул, очевидно, он предпочел бы более прямой путь альтернативе, которую предложила его сестра."

show jezera neutral at midright with dissolve

je "Кроме того, доведите наш военный потенциал до уровня настоящей армии и увеличьте нашу казну по крайней мере до 500 золотых крон."
je "Выполните эти задачи за шесть месяцев."

show jezera happy at midright with dissolve

je "Преуспей, и ты получишь много наград."

show andras angry at edgeright with dissolve

an "Потерпи неудачу, и Алексия сильно пострадает, ."

je "Удачи, мой герой."


####new content #####

show andras displeased at edgeright with dissolve

je "Простая формальность, казначейство в твоем распоряжении для строительных проектов. Скордред расскажет тебе о том, какие проекты доступны."
je "Мы также начнем выплачивать тебе еженедельную зарплату за твою работу из казны."

an "Я бы не советовал тратить деньги легкомысленно. На них ты так же будешь покупать свое личное оборудование."

ro "Значит, я должен работать с тем, что у меня есть? Думаю, Кли-Мин придется смириться с тем, что она не увидит во мне клиента еще несколько недель."

je "Ну, если у нее есть что-то, что поможет тебе в службе, возможно, я смогу направить часть наших строительных средств на покупку оборудования"
je "А пока ты не принял решения, я оставлю их себе."

ro "Я подумаю об этом."

an "Поговори с нашими слугами и возвращайся, когда примешь решение."

scene bg20 with fade
show rowan necklace neutral at midleft with dissolve

"Роуэн постучал в дверь мастерской Скордреда."

play sound "music/SFX/door knock.ogg"
pause 1

show skordred neutral at skorright with moveinright

"Мгновение спустя появился гном с горящими глазами."

ro "Позвольте мне увидеть текущие варианты строительства."

"Очевидно, гном ожидал этого и вручил Роуэну планшет с тремя диаграммами и кратким описанием каждой."
"Вероятно, они готовились для близнецов, но теперь их смотрел Роуэн. Строителю это не понравилось."

hide skordred with moveoutright

ro "(У меня есть выбор: кузница, темное святилище или таверна.)"
ro "(Кузница значительно увеличит мощь наших солдат.)"
"(Темное святилище даст нам другой тип солдат и улучшит нашу способность проводить исследования, помогая в долгосрочной перспективе.)"
ro "(Таверна увеличила бы доходы нашей казны.)"

"Все еще держась за планшет, Роуэн покинул мастерскую и направился к фургону, припаркованному у главных ворот замка."

scene bg19 with fade
show rowan necklace neutral at midleft with dissolve
show clamin neutral at midright with dissolve

"Женщина-гоблин была гораздо дружелюбнее, стремясь показать, все товары какие у нее есть."
"Ее энтузиазм несколько поутих, когда она узнала, что у Роуэна нет денег, чтобы много купить, но она продолжала делать это из своего странного обожания."
"У Кла-Мин был небольшой выбор оружия; ничего особенного, но всегда полезного для любого в приключении."

ro "(Что ж, пришло время принять решение.  Должен ли я начать один из строительных проектов или отказаться от части казны на стипендию, чтобы купить оборудование?)"

scene black with fade

#### give player two choices - "Purchase something from Cla-Min" or "Discuss construction with Skordred"
#### for the Cla-Min option, use the current shop we made for castle earlier with Cla-Min as the shopkeeper
#### for the Skordred option, give the player the choice of building the forge, the dark sanctum, or the tavern (must choose one before game continues)
menu:
    "Купите что-нибудь у Кла-Мин":
        call screen shop_screen(castle_shop_trader)
        $ released_fix_rollback()
        scene bg6 with fade
        show rowan necklace neutral at midleft with dissolve
        show jezera neutral at midright with dissolve
        show andras displeased at edgeright with dissolve
        #If Rowan purchased equipment
        # TODO: detect if equipment was purchased actually
        je "Вот твоя стипендия, 100 золотых.  Я сохраню 50 из того, что мы потратили бы на здание, ты все равно не смог бы использовать его в течение нескольких недель."
        an "Такой позор, что вы, люди, должны полагаться на металл и инструменты, чтобы сделать что-то."
        an "Очень хорошо, но знай, что тебе лучше заняться строительством быстрее, если ты надеешся успеть выполнить нашу задачу."
        $ change_personal_gold(100)

### end of new content ###

    "Обсудите строительство со Скордредом":
        menu:
            'Кузница':
                $ castle.scheduled_upgrades.append('forge')
                $ change_treasury(-all_buildings['forge']['cost'])
                $ released_fix_rollback()
                scene bg6 with fade
                show rowan necklace neutral at midleft with dissolve
                show jezera neutral at midright with dissolve
                show andras displeased at edgeright with dissolve
                #If Rowan built the forge
                an "Отлично. Наличие надлежащего снаряжения должно сделать моих орков на самом деле несколько сильнее в бою."
                an "У меня уже есть кое-кто на примете в качестве кузнеца. Я приведу его к тому времени, когда Скордред закончит строить кузницу."

            'Темное святилище':
                $ castle.scheduled_upgrades.append('sanctum')
                $ change_treasury(-all_buildings['sanctum']['cost'])
                $ released_fix_rollback()
                scene bg6 with fade
                show rowan necklace neutral at midleft with dissolve
                show jezera neutral at midright with dissolve
                show andras displeased at edgeright with dissolve
                #If Rowan built the dark sanctum
                je "Я рад, что мы наконец-то соберем здесь магов, они смогут помочь Клионе. Хм, мне нужно найти кого-то, кто будет держать суккубов и инкубов в узде."

            'Таверна':
                $ castle.scheduled_upgrades.append('tavern')
                $ change_treasury(-all_buildings['tavern']['cost'])
                $ released_fix_rollback()
                scene bg6 with fade
                show rowan necklace neutral at midleft with dissolve
                show jezera neutral at midright with dissolve
                show andras displeased at edgeright with dissolve
                #If Rowan built the tavern
                je "Чем раньше мы увеличим наш доход, тем быстрее мы сможем построить все остальное."
                je" Я надеюсь, что ты хорошо подумал, успеешь ли, когда дело дойдет до строительства нашей армии."


#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################

je "Окончательное решение куда направить исследования нашего библиотекаря ты должен будешь принять сегодня."
je "Я думаю, что она готова начать в любое время, поэтому, пожалуйста, навести ее прямо сейчас."

show bg12 with fade

"Роуэн вошел в большую библиотеку замка Бладмин."

show rowan necklace neutral at midleft with dissolve

ro "Я помню, какой восторг охватил Лоралин, когда она впервые вошла сюда."

"Он скользил по проходам в поисках библиотекаря."
"В армии Карнаса было несколько колдунов, которые работали здесь во время войны, но Роуэн не помнил, чтобы кого-нибудь называли 'библиотекарем'."

"Кем бы она ни была, она не принадлежала к старой гвардии, как Скордред."

show cliohna neutral at cliohnaright with dissolve

"Затем герой нашел ее, стоящую на кафедре в одной из ниш. Она выглядела очень юной, то ли в подростковом возрасте, то ли, едва вышедшей из него."
"Длинные светлые волосы струились по ее спине, и она носила очень открытую одежду, которая едва прикрывала ее тело."
"Женщина, казалось, была поглощена чтением довольно большого тома. Стопка книг такого же размера лежала рядом с магическим посохом, прислоненным к ним."

hide rowan with dissolve
show rowan necklace neutral at midleft with moveinleft

ro "Так ты библиотекарь?"

"Пока Роуэн говорил, она осторожно закрыла книгу и повернулась к нему лицом. Герой мгновенно понял, что ошибся насчет ее возраста; глаза выдавали это."
" Это были не невинные глаза девушки, а старые, полные цинизма."

cl "Клиона, а ты, должно быть, Роуэн. Я подготовила список тем, на которых я могу сосредоточить свое исследование."
cl "Пожалуйста, выберите тот, который, по твоему мнению, нужно начать."

"Она взяла с кафедры лист пергамента и протянула его Роуэну. Он принял его и быстро пробежался по темам."

ro "Хорошо, я могу понять, почему нам нужно работать над дипломатией или контактной сети для исследований, но зачем  методы исследования и история войны?"
ro "Я, вероятно, знаю о войне больше, чем кто-либо другой, и имею большой опыт разведки."

cl "Что ты знаешь о том, как изменился мир после войны?"
cl "Ты работал с графическими курсами для армий или прошел подготовку по использованию картографического и геодезического оборудования?"

ro "Э..."

cl "Я могла бы поподробнее объяснить, почему каждый из этих предметов будет очень полезен близнецам и твоей службе им."

ro "Нет, я думаю, я понял."

cl "Хорошо, тогда я буду кратка. Любое из них отлично подходит для начала, главное, - это то, что бы ты не сосредотачивал свои усилия  в одной категории."
cl "В конце концов, наличие отличных солдат и знание тактики не будут полезны, если у вас нет дипломатических навыков для вербовки новых армий."
cl "Опыт в обучении и разведении монстров не поможет, если у тебя нет умения находить их гнезда."
cl "А теперь, Лорд Роуэн, дайте мне знать, когда сделаете свой выбор."

#force Rowan to choose a research subject
call screen researches_screen(True)

cl "Очень хорошо. Я начну свою работу и представлю отчет, когда закончу."

"Очевидно, на этом разговор закончился. Клиона повернулась и выбрала из стопки другой том. Взмахом руки она выплыла из кучи, а старая книга заняла свое прежнее место."

"Роуэн просто покачал головой и попрощался, прежде чем уйти."
"Эта женщина действительно казалась человеком, но он был уверен, что она намного старше, чем кажется."
"Сила, которую он чувствовал, была похожа на магию Лоралин, которая сама была одной из самых искусных эльфийских магов."

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################

scene bg6 with fade
show rowan necklace neutral at midleft with dissolve
show jezera neutral at midright with dissolve
show andras displeased at edgeright with dissolve

"Он вернулся в тронный зал."

an "Похоже, на этом твои обязанности заканчились."

je "Есть вопросы о замке?"

ro "Интересно, где ты нашла Клиону? Я могу сказать, что она могущественная чародейка, к тому же древняя, но я никогда не слышал о ней."
ro "Даже большинство эльфов живут всего пару столетий, но она кажется еще старше."

show jezera happy at midright with dissolve

"Синекожая демоница только улыбнулась."

je "Я не знаю, откуда Клиона.  Она пришла к нам сама. Дело в том, что она получает полный доступ к библиотеке в обмен на наши исследования."
je "На этом наши отношения заканчиваются."

show jezera neutral at midright with dissolve

je "Во всяком случае, уже поздно, и тебе пора спать. В конце концов, мы посылаем тебя на разведку утром."

# add codex entries
$ codex_add('cla_min_starting')
$ codex_add('skordred_starting')
$ codex_add('cliohna_starting')
$ glossary_add('treasury')
$ glossary_add('morale_start')
$ glossary_add('military_capacity_start')
#start turn
return
#~ jump gameend
