init python:

    #requires 3 abbeys to have been visited - TODO
    event('the_faithful_caretaker', triggers='map_res_19', group='map_res_abbey', run_count=1, priority=pr_map_res)
    event('abbey_cliohna', triggers='map_res_19', group='map_res_abbey', run_count=1, priority=pr_map_res)
    event('abbey_first', triggers='map_res_19', conditions=('week > 3',), group='map_res_abbey', run_count=1, priority=pr_story)

label the_faithful_caretaker:

#abbey BG
scene bg46 with fade

$ garin_in_rastedel = True

"Еще один день, и на горизонте появилось еще одно аббатство."
"Но на этот раз Роуэн не сразу связался с Клио. Что-то не давало ему сделать это…"
"Доверяя своей интуиции, герой медленно приближался к ней."
"Вокруг было тихо. Сад аббатства, обычно гордость многих храмов, теперь оставлен без присмотра."
"Тяжелые двери во внутренние помещения храма, днем всегда открытые, чтобы приветствовать приходящих верующих,теперь были закрыты."
"Место заброшено? Если так, то это произойти недавно, так как храм все еще был в хорошем состоянии... Нападение на него тоже было маловероятным."
"Тайна не разрешится сама по себе, и с его стороны было бы глупо посылать своих орков вслепую."
"Приняв решение, Роуэн толкнул ворота и вошел в здание."
"Как и ожидалось, большой восьмиугольный зал был пуст, вокруг не было ни души."
"Многие религиозные атрибуты – картины, чаши, священные символы-также отсутствовали."
"Но опять же, никаких признаков сражения. Аббатство было эвакуировано, но совсем."
"Роуэн коснулась поверхности ближайшей скульптуры. Она была чистой - ни пылинки."
"Кто-то по-прежнему присматривал за аббатством, пусть даже только за его интерьером."

if all_actors["kharos"].favors > all_actors["solancia"].favors:
    "Решив докопаться до сути вещей, Роуэн пересек зал, проходя мимо алтаря и направляясь к одной из боковых стен."
    
else:
    "Решив докопаться до сути, Роуэн пересек холл. Подойдя к алтарю, он опустился на колени и произнес пару слов быстрой молитвы."
    "Затем он направился к одному из боковых выходов."
    
"Он проходил одну комнату за другой. Все они были пусты, лишены чего-либо ценного, но все еще чистые. Кем бы ни был смотритель, он не расслаблялся."
"Войдя в кабинет аббата, Роуэн увидел смотрителя аббатства."

show rowan necklace neutral at midleft with dissolve

"Смотритель оказался живым стариком, почти совсем лысым, с несколькими седыми прядями на голове."
"Напевая старую песню, он вытирал пыль с наполовину пустой книжной полки."
"Офис также был лишен чего-либо ценного. Хотя, возможно, для кого-то это имело какое-то значение."

ro "Простите?"

care "Ох!"

"Мужчина ахнул от удивления и повернулся лицом к нежданному гостю."

care "Здравствуйте, молодой человек! Присаживайтесь."
care "Примите мои искренние извинения, но, боюсь, сегодня не будет проповеди, как и  в ближайшем будущем."

"Мужчина грустно улыбнулся."

show rowan necklace happy at midleft with dissolve

care "Но я могу сделать тебе чаю, если хочешь?"

ro "Нет, спасибо. Но вы можете рассказать мне, что здесь произошло? Где все?"

show rowan necklace neutral at midleft with dissolve

care "Они ушли."

"Роуэн наблюдал, как смотритель вернулся к своим обязанностям, серьезным голосом объясняя ситуацию."

care "В последнее время дела идут не слишком хорошо. Паломники говорили, орки грабят сельскую местность"
care "Люди перестали чувствовать себя в безопасности. Некоторые пытались найти здесь защиту, но…"
care "… Преподобный Гарин сказал, что аббатства тоже небезопасны. Одно сгорело дотла."
care "Другое перестало отвечать на письма, и когда он послал нескольких послушников проверить его"
care "они даже не решились зайти туда, сказали, что с ним что-то не так."
care "Поэтому он собрал свои вещи и ушел."

"Рука мужчины замерла, как будто он решался, продолжать или нет."

care "В этом-то все и дело."

if avatar.corruption > 74:
    show rowan necklace angry at midleft with dissolve
    "Роуэн нахмурился. Отлично. В аббатстве не было ни рабов, ни ценных книг, ни богатств. У него был только один дряхлый старик."
    "Не то чтобы от него совсем не было пользы, просто Роуэн сомневалася, что Изера питает слабость к старикам."
    "… хотя возможно."
    show rowan necklace neutral at midleft with dissolve
    ro "Я вижу. Тогда я вас оставлю."
    care "Ты уверен, что не хочешь..."
    show rowan necklace angry at midleft with dissolve
    ro "Нет."
    "Роуэн прервал разговор и вышел из кабинета, не сказав больше ни слова. Он быстро связался с Клио, сообщив ей, что у него есть еще одно аббатство, но в этом нет ничего ценного."
    "Аббатство все еще было полезно для его магических фокусирующих свойств, но разграбленная библиотека будет ударом по исследованиям Клионы."
    "Еще одно неприятное осложнение."
    $ change_research_bonus(1)
    return
    
else:
    pass
    
show rowan necklace angry at midleft with dissolve

"Роуэн нахмурился. Это было непохоже на священиков. Сразу покинуть свое аббатство. Их долг - заботиться о нем, невзирая на опасность."
"Думаю, он не мог винить их за это, учитывая, что он был буквально рядом, чтобы захватить их."
"Но то, что он сбежал, в то время как простой крестьянин остался присматривать за домом, было... неприятно."

show rowan necklace happy at midleft with dissolve

ro "Как тебя зовут, добрый человек?"

$ caretakerName = "Луис"

care "Луис. Луис из Fernsworth. Мать назвала меня в честь великого Лорда Луиса Дилейна."
care "Он был замечательным человеком. Много лет назад он проходил мимо нашей фермы по пути домой..."

show rowan necklace neutral at midleft with dissolve

ro "Луис"
ro "Думаю, тебе тоже стоит уйти."
ro "Ваш аббат был прав. Здесь небезопасно."

care "..."

"Смотритель Луис, не ответил. Он на мгновение прервал работу, чтобы получше рассмотреть Роуэна."
"Выражение его лица было мрачным, и Роуэну показалось, что в его глазах мелькнул гнев, но он исчез так быстро, что Роуэну показалось что это только его воображение."
"Мужчина тяжело вздохнул и отложил тряпку."
"Обойдя вокруг стола, он сел в кресло, обычно предназначенное для гостей, и откинулся на спинку с усталым стоном."
"Через мгновение он слегка улыбнулся и, наконец, заговорил."

care "Не могли бы вы назвать свое имя, молодой человек?"

show rowan necklace happy at midleft with dissolve

ro "Роуэн"

care "Ха, как герой Карста! Хорошее имя, молодой человек!"

show rowan necklace neutral at midleft with dissolve

ro "… Угу."

care "Ну, Роуэн ... я 40 лет присматривал за  этим аббатством. Уборка полов, стирка штор, мытье посуды."
care "Я был его хранителем до появления Карнаса, и я был его хранителем, когда он вел свою войну."
care "И теперь, когда он ушел, я остаюсь его хранителем."

"Мужчина доброжелательно улыбнулся."

care "Это воля Святой богини поместила меня сюда, когда я был молод, это ее воля, что я остаюсь здесь."
care "И по ее воле я буду делать это до последнего вздоха."
care "И я сделаю это с улыбкой, зная, что служил ей верой и правдой."
care "Так что, пожалуйста, не обращай на меня внимания, Роуэн. Я доволен своей судьбой. Умереть, заботясь об одном из храмов Солансии..."
care "Я не могу представить себе большей чести."

menu:
    "Он напрасно растрачивает свою жизнь. Убеди его уйти.":
        $ released_fix_rollback()
        ro "Луис, я понимаю, что это место важно для тебя, но без аббата и без паломников... это больше не храм Солансии."
        "Роуэн указал на стены вокруг них."
        ro "Это просто ... здание, как и любое другое."
        ro "За это не стоит умирать."
        ro "Я не верю, что Солансия хотела бы, чтобы ты вот так прожил свою жизнь."
        ro "Поэтому, пожалуйста, переосмыслите свое решение."
        care "..."
        "Луис не ответил. Не сразу."
        "Но затем Роуэн увидел, как его лицо окаменело, а в глазах снова вспыхнул гнев."
        care "… Думаю, вам понравится преподобный Гарин, молодой человек. Он также сказал что-то в этом роде."
        care "Сказал, что он был ... 'обременен славной целью'. И что он не умрет, защищая кого-то.…"
        show rowan necklace shock at midleft with dissolve
        care "“Обшарпанное старое аббатство посреди гребаной пустоты.”"
        show rowan necklace neutral at midleft with dissolve
        "Его лицо исказилось от ненависти, и он продолжил голосом, полным такой желчи, что Роуэн не мог поверить,"
        "что это тот же самый добродушный старик, которого он встретил всего несколько минут назад."
        care "Ну, Роуэн, я скажу тебе то, чего не смог сказать ему, потому что, похоже, ты забыл, чего от тебя ждут."
        care "Не нам решать, когда и где умирать."
        care "Богиня решает наш жребий в жизни, и наш долг-исполнить его до конца, какой бы тяжелой или трудной ни казалась наша судьба."
        care "Только когда тщеславные маленькие дети, такие как ты и Гарайн, начинают думать, что они слишком важны,"
        care "чтобы умереть, и уходят, играть героя, мир катится в дерьмо."
        care "Так что перестань притворяться, что знаешь, чего может хотеть Святая богиня, и перестань пытаться 'спасти' меня,"
        care "как будто это твоя чертова работа или что-то в этом роде."
        care "Потому что это не так. А теперь убирайся с моих глаз."
        ro "..."
        ro "Если это твой выбор."
        hide rowan with dissolve
        "Некоторое время они смотрели друг на друга, пока Роуэн не отвернулся и не направился к выходу."
        "У него не было времени спорить со стариком о теологии. Все равно убедить его не удастся."
        "Он связался с Клио и сообщил ей, что нашел новое аббатство, но без персонала и с большей частью забранных книг."
        "К счастью, оно все еще будет полезено, из-за своего расположения."
        "Он ничего не сказал о смотрителе. Если этот человек хочет отдать свою судьбу в руки Солансии..."
        "Так тому и быть."
        $ change_research_bonus(1)
        $ change_base_stat('g', -2)
        return
        
    "Уважайте его выбор.":
        $ released_fix_rollback()
        ro "… Если это то, что вы считаете лучшим."
        "Пожилой мужчина выдохнул с легким сердцем."
        care "Ты хороший парень, Роуэн."
        "Он отвел взгляд в сторону, и его голос стал отдаленным."
        care "Гораздо лучше, чем  Гарайн..."
        "Он снова посмотрел на Роуэна и выпрямился."
        care "Так вот, молодой человек, я не думаю, что вы об этом упоминали. Почему вы здесь...?"
                
        menu:
            "Хотел где-нибудь поразмыслить." if avatar.guilt > 30:
                $ released_fix_rollback()
                ro "Я надеялся, что проповедь пойдет мне на пользу."
                care "Тяжелая неделя?"
                "Роуэн не удержалась и фыркнул. Немного преуменьшено, надо сказать."
                ro "Мягко говоря."
                "Старик задумчиво почесал подбородок."
                care "Боюсь, здесь я ничем не могу вам помочь... мне не следовало бы повторять эти слова, даже если бы я слышал их тысячу раз, но ..."
                care "Моя дочь всегда говорила, что садоводство помогает ей восстановить душевное спокойствие."
                care "Может, останешься ненадолго и поможешь мне?"
                care "Это может тебе помочь."
                "Амулет на шее Роуэна тяжело давил. У него действительно не было времени играть в садовника."
                " Особенно с кем-то, кто скоро станет слугой учеников Клионы."
                "… Но, может быть, это именно то, что ему нужно?"
                
                menu:
                    "Соглашсится.":
                        $ released_fix_rollback()
                        ro "..."
                        show rowan necklace happy at midleft with dissolve
                        ro "Может, ненадолго."
                        care "Ха! Молодец!"
                        care "Хорошо, помоги этим старым костям подняться. У нас много дел..."
                        hide rowan with dissolve
                        "Следующие дни были днями успокаивающей лени."
                        "Он не должен был этого делать. Роуэн знал это. Он должен бегать по Розарии, сжигая деревни и развращая аристократов, или что там Близнецы требовали от него."
                        "Но в жизни есть нечто большее, чем сжигание мира, и он знал, что в интересах близнецов забыть об этом."
                        "..."
                        "Через несколько дней, когда сад был уже готов, Роуэн попрощался со стариком."
                        "Он сказал ему, что надеется, что с ним все будет в порядке."
                        "Он уже сказал Клионе, что Аббатство пустует, но все еще является местом силы, поэтому ее ученики должны прибыть туда через несколько дней."
                        "Им было приказано оставить этого человека сторожем, как только он достаточно успокоится."
                        "Вероятно, не та судьба, которую запланировала для него Солансия.…"
                        "Но опять же, кто может знать?"
                        $ change_research_bonus(1)
                        $ change_base_stat('g', -5)
                        $ change_base_stat('c', -5)
                        $ change_mp(-4)
                        #add abbey flowers (TODO)
                        return
                        
                    "Отказаться.":
                        $ released_fix_rollback()
                        ro "…"
                        ro "Я прошу прощения, Лоис. Но мне придется отказаться."
                        ro "Есть вещи, о которых мне нужно позаботиться."
                        "Луис вздохнул, обреченно."
                        care "Ах, вы, молодые люди…"
                        care "Береги себя парень. Приятно было снова увидеть хорошего человека."
                        
            "Просто проходил мимо.":
                $ released_fix_rollback()
                ro "Я был поблизости. Вот и все."
                care "Ах."
                care "Ну ... тогда я тебя не задержу?"
                    
hide rowan with dissolve

"Достигнув своей цели, Роуэн попрощался со старшим."
"Он сказал ему, что надеется, что все будет хорошо, и что однажды придет новый Аббат, и аббатство снова будет процветать."
"Конечно, как только он вышел из ворот храма, он связался с Клио, чтобы сообщить ей о местоположении аббатства."
"Аббатство не могло быть разграблено, но, как и другие, оно было помещено в магическую связь, так что это все еще была ценная находка."
"Ее ученики прибудут через несколько дней. По указанию Роуэна они должны были оставить смотрителя в покое, как только его согласие будет обеспечено магией."
"Вероятно, не та судьба, которую запланировала для него Солансия.…"
"Но опять же, кто может знать?"
$ change_research_bonus(1)
return

##########################################################################################################################################################################

label abbey_cliohna:

#abbey BG
scene black with fade


show rowan necklace neutral at midleft with dissolve
show orc soldier neutral at midright with dissolve

ro "Осторожно, это реликвия."

os "Да, маста."

hide orc soldier with moveoutright
show rowan necklace concerned at midleft with dissolve

"Орки поспешили прочь, унося древнюю картину, с гораздо меньшей осторожностью, чем требовал предмет в их руках. Роуэн неодобрительно покачал головой."
"Как будто преступлений против человечества было недостаточно,"
"теперь ему придется добавить культурное разграбление к своему удручающему резюме героя."

show rowan necklace concerned at center with moveinleft

"Культурный грабеж огромного значения, так как это было не обычное аббатство."
"С деревянными низкими стенами и редким лесом вокруг, он не имел великолепия, но он имел в богатую историю."
"Он снова огляделся, осматривая окрестности. Одино из старейших аббатств Розалии, созданные вскоре после 'Глупости'. Незаметно спрятаное…"

show rowan necklace shock at center with dissolve

quest "Орки закончили Роуэн?"

show rowan necklace neutral at center with dissolve

"Резкий голос вернул его к реальности. Он повернулся к человеку, который навел его на это место."

show rowan necklace neutral at midleft with moveoutleft
show cliohna neutral at cliohnaright with dissolve

ro "Ты в плохом настроении, Клиона."

cl "Я хочу скорее закончить здесь, чтобы возобновить свои исследования. Тебя это удивляет?"

ro "Нет, конечно, нет. Но тебе незачем было идти с нами."

cl "Я не могла рисковать, оставляя это дело кому-то другому, Роуэн."
cl "Это древнее место, и я ожидала, что у него будет защита и артефакты, соответствующие его истории."

ro "И...?"

"Разочарованный вздох был ему ответом. Ничего."

ro "Мы почти закончили. Как только орки унесут статую, я оставлю тебя, чтобы ты провела завершила захват."

cl "Мы не будем этого делать."
cl "Я не собираюсь захватывать аббатство. Оно не был построено на луче, поэтому оно для меня бесполезено."

ro "Тогда, я полагаю, мы можем пойти....?"

show rowan necklace neutral at edgeleft with moveoutleft

"Он сделал несколько шагов к выходу. Клиона не двигалась, все еще глядя на аббатство, на то место, где стоял алтарь Солансии."

show black with redflash

"Она подняла руку и щелкнула пальцами. Маленькое пламя вырвалось из ее руки и ярко вспыхнуло между пальцами."

show cliohna happy at cliohnaright with dissolve

cl "Конечно. Я пойду следом, Роуэн."

menu:
    "Остановите ее, сейчас же!":
        $ released_fix_rollback()
        $ change_base_stat('c', -2)
        show rowan necklace neutral at center with moveinleft
        show cliohna neutral at cliohnaright with dissolve
        "В доли секунды он был рядом с ней, держа ее за запястья. Клиона даже не вздрогнула. Она медленно перевела взгляд на Роуэна, и в его глазах отразилось легкое раздражение."
        cl "Ты что-то хочешь сказать, Роуэн?"
        ro "Что ты делаешь, Клиона? Мы получили то, что хотели. Зачем сжигать это место?"
        cl "А почему бы и нет? Поправь меня, если я ошибаюсь, но разве Близнецы не стремятся ниспровергнуть Розарию и заявить на нее свои права?"
        cl "Нам это место не нужно, и, уничтожив его, мы подадим ясный сигнал, что Церковь Солансии больше не контролирует этот регион."
        ro "Мы уже сделали это, разграбив его. На данный момент сжигать его - это просто... мстительно."
        show rowan necklace concerned at center with dissolve
        ro "Что происходит Клиона?"
        "Колдунья стряхнула его хватку, сердитый хмурый взгляд на мгновение пробежал по ее лицу."
        show rowan necklace neutral at center with dissolve
        cl "Это место-не что иное, как ненужный памятник пустой культуре невежества и подавления."
        cl "Я делаю одолжение будущим поколениям, удаляя все ее следы  из этого мира."
        cl "И я предполагаю, что Солансия не является союзником близнецов, пытающихся разрушить ее морально пустой порядок."
        cl "Так зачем защищать ее храм, Роуэн?"
        "Ее холодный взгляд пронзил его череп. Нечасто Клиона из-за чего-нибудь горячилась. Будет ли он стоять у нее на пути?"
        menu:
            "Солансия все еще имеет место в этом мире. Аббатство должно остаться стоять.":
                $ released_fix_rollback()
                $ change_base_stat('c', -2)
                $ all_actors["solancia"].favors += 1
                $ all_actors['cliohna'].relation -= 3
                show rowan necklace angry at center with dissolve
                ro "Я не знаю, какая у вас вражда с Солансией, но я не позволю тебе сжигать ее аббатства без причины."
                "Ее тонкие брови слегка сузились, как у матери, которая собирается отчитать непослушного ребенка."
                show cliohna neutral at edgeleft with moveoutleft
                "Только для того, чтобы она развернулась и направилась к выходу."
                cl "Очень хорошо, но знай: со временем ты поймешь, что миру будет лучше, если Солансия исчезнет, так же, как это аббатство."
                scene black with fade
                show cliohna neutral behind black
                cl "Забытая временем, она станет добычей тех, кто ищет большего."
                return
                
            "Здесь нет смысла противостоять Клионе. Пусть сожжет его дотла.":
                $ released_fix_rollback()
                $ change_base_stat('c', 2)
                $ all_actors["kharos"].favors += 1
                $ all_actors['cliohna'].relation += 3
                "Они стояли друг на против друга, не желая отступать."
                show rowan necklace neutral at midleft with moveoutleft
                show cliohna happy at cliohnaright with dissolve
                "В конце концов, именно Роуэн отступил, вскинув руки в знак поражения."
                ro "Делай, что хочешь"
                cl "Спасибо, Роуэн. Поверь мне: ничего ценного не будет потеряно."
                hide rowan with moveoutleft
                show cliohna happy at center with moveinright
                "Волна жара достигла его спины, когда он покинул храм, красный свет осветил окрестности. 800 лет истории…"
                scene black with fade
                "Исчезло в одно мгновение."
                return
                
    "Пусть сожжет его дотла.":
        $ released_fix_rollback()
        $ change_base_stat('c', 2)
        $ all_actors["kharos"].favors += 1
        $ all_actors['cliohna'].relation += 3
        ro "Не задерживайся слишком долго."
        hide rowan with moveoutleft
        show cliohna happy at center with moveinright
        "Волна жара достигла его спины, когда он покинул храм, красный свет осветил окрестности. 800 лет истории…"
        scene black with fade
        "Исчезло в одно мгновение."
        return
                
#############################################################################################################################################################

label abbey_first:

#abbey bg
scene black with fade
show rowan necklace neutral at center with dissolve

"Перед ним вырисовывались ворота аббатства."
"Согласно инструкциям, он связался с замком Бладмин, увидев здание на горизонте."
"Он разведал местность, ожидая прибытия сил порабощения, так что неприятные сюрпризы их не ждали."

hide rowan
show orc soldier neutral at midleft with dissolve
show wild orc neutral at edgeleft with dissolve
show rowan necklace neutral at center with dissolve

"К его легкому раздражению, орки не пытались скрыть своего приближения, и священики забаррикадировались внутри аббатства, готовые дать отпор захватчикам."
"Впрочем, это ничего не изменит, подумал Роуэн."

show cliohna neutral at cliohnaright with dissolve

ro "Начнем, Клиона?"

cl "Хм? Ах да, через минуту, Роуэн. Сначала я хотела хорошенько его рассмотреть."

"Он не ожидал, что библиотекарь замка присоединится к ним, но колдунья сделала это, в сопровождении двух своих учеников."
"И теперь она продолжала смотреть на храм, как будто видела его впервые."

cl "Хм... я ожидала большего..."

"Ее голос был тихим, и по ее тону казалось, что она... злится на себя? За ожидание большего?"
"Роуэн не мог понять, что сейчас происходит в голове Клионы, а колдунья вряд ли могла его просветить."

cl "Полагаю, мы можем начать прямо сейчас."

hide wild orc with moveoutleft
show orc soldier neutral at edgeleft with moveoutleft
show rowan necklace neutral at midleft with moveoutleft
show cliohna neutral at center with moveinleft

"Он поднял руку, чтобы отдать приказ оркам, но Клиона остановила его. Колдунья развела руками."

show black with flash

"На них прыгнула дуга света. Роуэн увидел, как студенты заткнули уши."

if check_stat(8, 'reflexes')[0]:
    show black with sshake
    "В последнюю секунду ему удалось прикрыть свои уши, когда грохочущий треск пронзил воздух и массивные ворота аббатства разлетелись вдребезги."
    "Вокруг посыпались осколки, свидетельствующие о силе Клионы."
    hide cliohna with moveoutright
    show rowan necklace shock at midleft with dissolve
    "Не теряя ни секунды, колдунья направилась к аббатству."
    "Роуэн увидел, как ошеломленный аббат поднялся на ноги, услышал, как он что то пробормотал, прежде чем он был оглушен еще одним заклинанием Клионы."
    show rowan necklace concerned at midleft with dissolve
    show cliohna neutral behind black
    cl "На колени, и молчи."
    hide cliohna
    show rowan necklace concerned at center with moveinleft
    "Она взмахнула рукой, и мужчина упал на колени, схватившись за горло. Остальные послушники вскоре последовали его примеру."
    "Роуэн не мог поверить своим глазам. Вся сцена длилась в общей сложности десять секунд. Насколько могущественна была колдунья?"
    show rowan necklace neutral at center with dissolve
    "Он обдумает это позже. Покачав головой, он повернулся, чтобы отдать приказ ошеломленным оркам."
    "Используя военный язык ударов по голове и указательных пальцев, он приказал охранять периметр, а затем снова приблизился к Клионе."
    
else:
    #Add Wound: Light penalties to listening/perception, for a couple of weeks (perforated eardrums) (TODO)
    show black with sshake
    "Громовой треск пронзил воздух,и массивные ворота аббатства разлетелись вдребезги."
    "Осколки посыпались вокруг них, когда Роуэн выругался вслух – или, по крайней мере, был уверен, что выругался, в ушах звенело."
    hide cliohna with moveoutright
    show rowan necklace shock at midleft with dissolve
    "Тем временем Клиона вошла в аббатство, не обращая внимания на хаос."
    "Роуэн увидел, как ошеломленный аббат поднялся на ноги, но Клиона просто взмахнула рукой, и человек снова упал на землю, хватаясь за горло."
    show rowan necklace concerned at center with moveinleft
    "Через десять секунд аббатство было их. Насколько могущественна была колдунья?"
    show rowan necklace neutral at center with dissolve
    "Он обдумает это позже. Он заставил своих орков встать и, используя военный язык-ударяя по голове и указывая пальцем, - закрепил периметр."
    "Когда звон в ушах утих, он снова подошел к Клионе."
    
show rowan necklace neutral at midleft with moveoutleft
show cliohna neutral at cliohnaright with dissolve

"Она допрашивала молодого послушника. Бедняга опустился перед ней на колени, запрокинул голову и, не мигая, посмотрел ей в глаза."

cl "Почему вы служите Солансии?"
    
aco "Она... Защищает Нас... …"

cl "Если бы вы могли выбирать между изучением магии или служением этой вашей богине, что бы вы выбрали…"

aco "Мы... Служим ... Солансии ... Она ... Защищает ... Нас. …"

"Колдунья раздраженно нахмурила брови. Она отбросила мальчика в сторону и подошла к следующему."

ro "Что ты делаешь?"

cl "Ищу студентов. Люди, которые ценят стремление к знаниям превыше представлений о 'стабильности'."
cl "Насколько я понимаю, Церковь Солансии часто набирает свое духовенство из числа магически одаренных."
cl "И я надеюсь, что по крайней мере некоторые из них все еще сохраняют интерес к самой магии,"
cl "а не ктому, как лучше направить ее для Божественной энергии Солансии."

"Роуэн увидела, как один из ее учеников направил орков к лежащему без сознания человеку, и зеленые солдаты потащили его в одну из боковых комнат."
"Другой опустился на колени перед настоятелем храма, держа перед глазами светящийся кристалл."

ro "А что с ним?"

cl "Слишком поздно для таких, как он. Ему промоют мозги вместе с другими неудачниками, и он будет продолжать присматривать за аббатством, а моя ученица станет его правой рукой."
cl "Согласно моим исследованиям, монастыри Розалии часто строились на магических лучах, что делает их хорошим исследовательскими аванпостами."

show rowan necklace shock at midleft with dissolve

cl "И если ситуация в регионе ухудшится из-за твоих действий, я надеюсь, что они привлекут беженцев, а это обеспечат нас стабильным снабжением испытуемых."

show rowan necklace concerned at midleft with dissolve

ro "(Надеюсь, до этого не дойдет.)"

show rowan necklace neutral at midleft with dissolve

os "Эй, босс, здесь нет золота!"

"Колдунья выгнула бровь. Роуэн пожал плечами."

ro "Я не ожидал, что будет. Подарки от верующих обычно отправляются в столицу."

hide orc soldier with moveoutleft
show cliohna happy at cliohnaright with dissolve

cl "Конечно. Было бы неразумно ожидать, что богиня будет поддерживать себя только верой."

show cliohna neutral at cliohnaright with dissolve

cl "Теперь, если у тебя нет больше вопросов, твое присутствие здесь больше не требуется."
cl "Ты можешь оставить орков со мной, я отправлю их обратно в замок Бладмин, как только закончу с этим местом."

ro "Они даже не были нужны."

cl "Они будут нужныв следующий раз, я не собираюсь сопровождать вас всегда."
cl "Важно время от времени выходить в поле, но у меня нет намерения завести привычку посещать храмы Солансии. Они мало что могут предложить."

"Аббат наконец встал, его ошеломленное выражение сменилось суровым хмурым взглядом, вероятно, таким же, как у него самого."
"Роуэн увидел, как он медленно кивнул, когда ученица Клионы спросила его о библиотеке аббатства, и указал на заднюю часть здания."

ro "Ты, конечно, в полной мере используешь то, что у них есть."

hide cliohna with moveoutright

"Но Клиона уже не слушала, она была на пути к библиотеке."

show rowan necklace concerned at midleft with dissolve

"С печальным вздохом Роуэн бросил последний взгляд на аббатство и направился к выходу."
"Ему не нравилось то, что Клиона делала со жрецами... но он не был уверен, что может позволить себе помещать библиотекарю замка."

hide rowan with moveoutleft
scene black with fade

if serveChoice != 4:
    "Судя по предыдущему показу, она не испытывала недостатка в силе."
    "Если однажды он взбунтуется против близнецов ... ему может понадобиться ее поддержка, не важно какой ценой."
    
else:
    "Судя по предыдущему показу, она не испытывала недостатка в силе, и ее исследование имело решающее значение для их усилий."
    "Если ему удастся свергнуть орден Солансии, он будет нуждаться в ее поддержке, независимо от цены."
    
return


