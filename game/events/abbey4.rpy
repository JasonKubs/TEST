init python:

    #TODO - Nasim not TF'd
    event('nasim_abbey_event', triggers='map_res_19', conditions=('nasimAvailable == True',), group='map_res_abbey', run_count=1, priority=pr_map_res)
    
label nasim_abbey_event:

scene bg46 with fade
show rowan necklace concerned at center with dissolve

"Он тихо вздохнул,затем бросил последний взгляд на последнее приобретение близнецов."
"Аббатство было одним из старейших в розарии, построенным более половины тысячелетия назад. Высокие и внушительные, его стены были украшены картинами, которые пересказывали былую славу нации."
"Впечатляющая каменная статуя, более 15 футов высотой, стояла в центре главного зала, прямо за алтарем Солансии."
"На нем был изображен святой рыцарь, догадался Роуэн, и в данный момент его окружала группа учеников Клионы, которые ссорились друг с другом. Один голос особенно возвышался над остальными."

show rowan necklace shock at center with dissolve

quest "Нет, нет, идиоты! Отсутствие поверхностных рунных Привязок автоматически исключает третью блокировку защиты Силина!"
quest "Так что перестаньте тратить время на то, чтобы определить, какой именно защитой была оснащена статуя, и продолжайте тестировать точки блокировки!"

show rowan necklace happy at center with dissolve

"… Очень знакомый голос."

show rowan necklace happy at midleft with moveoutleft

#nasim cloak angry
show nasim angry at midright with dissolve

ro "Как непохоже на тебя выходить в поле, Насим."
ro "Не заметил тебя под капюшеном. Стыдно за компанию?"

"Волшебник бросил на него усталый взгляд, который почти сказал “А тебе разве нет?”."

#nasim cloak neutral
show nasim neutral at midright with dissolve

nas "Просто нужно быть осторожным. Маловероятно конечно быть замеченным агентами Солансии, но я бы предпочел, чтобы мое лицо не обогащало местный список “разыскиваемых”."

ro "Ты напрасно беспокоишься. Я еще не обнаружил ни одного шпиона или разведчика в этом регионе."

nas "Я верю..."

show rowan necklace angry at midleft with dissolve

ro "Насим, у меня пятнадцатилетний опыт работы следопытом. Даже не пытайтесь сказать  'не быть замеченным - это их работа'."

#nasim cloak angry
show nasim angry at midright with dissolve

nas "… Конечно, Лорд Блэквэлл. Мои извинения."

"Его раздраженный тон не ускользнул от Роуэна."
"Как всегда, самоуверенный волшебник вел себя так, что у всех возникло искушение ударить его по лицу, и он едва не спровоцировал эту реакцию."

#nasim cloak neutral
show nasim neutral at midleft with moveinleft
show rowan necklace neutral at midright with moveoutright

"Вместо того чтобы тратить время на то, чтобы переубедить мужчину, Роуэн предпочел приблизиться к внушительной статуе."
"Раньше он не обращал на это особого внимания, но теперь…"

ro "Так что же в нем такого особенного?"

nas "Как бы это объяснить ... вы случайно не знакомы с историей 'чуда Демарета'?"

if castle.researches['history_of_rosaria'].completed:
    ro "Молодой каменщик, который молился Солансии о защите, когда на его деревню напали гоблины?"
    nas "И богиня ответила, дав жизнь камню. И это прямо здесь должно быть конечным продуктом."
    
else:
    ro "Думаю, что нет."
    nas "К сожалению, я не могу утверждать, что слишком хорошо знаком с ним так же. Мне говорили, что Солансия дала жизнь камню."
    nas "Детали немного туманны. Важно то, что эта вещь прямо здесь, по словам госпожи Клионы, является конечным продуктом титульного “чуда”."
    
"Назим постучал по статуе, чтобы подчеркнуть свои слова. Роуэн заметил, что для памятника пятисотлетней давности каменный рыцарь не несет никаких следов течения времени."

ro "Магия сохранения?"

nas "Или магии консервации. Ничего не могу сказать, да и не очень важно для нас. Хотя я бы не исключил магического сохранения, учитывая количество защитных заклинаний на этой штуке."

ro "Я слышал, как ты жаловался на них."

nas "Нет ключа от третьего замка. Мы можем только применить в основном грубую силу."
nas "На данный момент это просто вопрос медленного и тщательного демонтажа магического барьера, который окружает статую, который препятствуют ее перемещению."
nas "Если мы сохраним то, что содержит следы божественной магии, мы, скорее всего, раскроем любую тайну, которую статуя пытается скрыть."

ro "Звучит довольно трудоемко."

nas "Если честно, это займет несколько недель."

show rowan necklace angry at midright with dissolve

"Роуэн обматерился сквозь зубы. Это было... Не очень хорошо, мягко говоря. Они слишком полагались на исследования Клионы, чтобы задержать половину его команды в этом ее проекте."

ro "Ты пытался отговорить Клиону от этого?"

nas "Роуэн, Клиона наслаждается оппозицией только тогда, когда она бросает ей вызов. Препятствие для нее, чтобы преодолеть."

nas "Мы все подчиняемся ее приказам, как из уважения к ее положению, так и из признания ее интеллекта и способностей."
nas "Хотя, признаюсь, это первый раз, когда она потребовала моего – нашего - безраздельного внимания к чему-то столь... казалось бы, несущественному в общей схеме вещей."
nas "Но опять же, мы не в том положении, чтобы отказываться."
nas "… Не то чтобы кто-то из нас даже помышлял о том, чтобы пренебречь приказами госпожи Клионы. Верно?"

"По комнате пронесся ропот удрученного одобрения. Никто из них не хотел быть здесь, но ни один не признался бы в этом более открыто, чем Насим только что."
"Они останутся и расколют эту статую…"
"Если только кто-то, кто выше Клионы, как, скажем, Роуэн, не скажет им этого."

show rowan necklace happy at midright with dissolve

"Он снова взглянул на каменного рыцаря. Святая статуя, созданная для защиты невинных ... была ли она настоящей?"
"Он, конечно, ничего не сделал, чтобы защитить это место, но в то же время, количество заклинаний на нем намекало, что он обладал некоторой силой внутри."
"Всели ли условия для его активации были выполнены? Или это был не живой голем, а что-то совсем другое?"

if avatar.corruption < 31 and serveChoice != 4:
    "Волна спокойствия накрыла его, когда он посмотрел на вверх. Если статуя была настоящей... тогда, возможно, они должны оставить ее здесь."
    "Дремлющий защитник справедливости ... ждущий момента, чтобы нанести удар."

else:
    show rowan necklace concerned at midright with dissolve
    "Холодная дрожь пробежала по его спине. Дремлющий поборник справедливости... если бы он был настоящим, Роуэн предпочел бы, чтобы он не просыпался, чтобы нанести удар по врагам Солансии."
    "Тем более что он был совершенно уверен, что теперь должен причислить себя к их числу."

menu:
    "Сказать Назиму, чтобы он оставил статую в покое.":
        $ released_fix_rollback()
        show rowan necklace neutral at midright with dissolve
        ro "Несмотря на Клиону, мы здесь не для того, чтобы гоняться за давно забытыми мифами."
        #nasim cloak happy
        show nasim happy at midleft with dissolve
        ro "Скажите ей, что она может исследовать историю “чуда Демарета” в свое свободное время. А вы, остальные, заканчивайте и возвращайтесь в замок. У вас есть реальная работа."
        #nasim cloak neutral
        show nasim neutral at midleft with dissolve
        nas "… Могу я вас процитировать?"
        ro "… Да. Да, можешь."
        nas "И если кто-нибудь будет спрашивать, мы все яростно выступали против этой идеи."
        ro "Конечно же, ты это делал."
        hide nasim with moveoutright
        show rowan necklace neutral at center with moveinleft
        "Роуэн покачал головой, затем приказал оркам выйти."
        "Он был уверен, что скоро получит очень громкую жалобу от Клионы, поэтому хотел сделать как можно больше, прежде чем предстоящий спор полностью поглотит его."
        $ change_relation('cliohna', -10)
        $ nasimAttitude += 1
        $ change_favor('solancia', 1)
        $ demeretStatue = "InSolansia"
        return
        
    "Просто оставь Назима наедине с этой проблемой.":
        $ released_fix_rollback()
        show rowan necklace neutral at midright with dissolve
        ro "Это, безусловно, сложная проблема, с которой вы столкнулись здесь, Назим."
        show rowan necklace neutral at center with moveinright
        "Он повернулся к волшебнику и положил руку ему на плечо."
        ro "Удачи."
        hide rowan with moveoutleft
        nas "...?"
        nas "Лорд Роуэн?"
        nas "Ах ты бесхребетный сукин сын..."
        $ nasimAttitude -= 1
        #-5 research for 4 weeks. TODO
        $ demeretStatue = "Taken"
        return
        
    "Направьте больше рабочей силы на то, чтобы взломать статую.":
        $ released_fix_rollback()
        show rowan necklace concerned at midright with dissolve
        ro "Вы же подтвердили, что статуя-артефакт какой-то силы, скорее всего Солансии. Не могу же я оставить статую здесь, особенно если однажды она начнет топтать наших орков."
        #nasim cloak happy
        show nasim happy at midleft with dissolve
        nas "У нас их действительно много…"
        #nasim cloak shocked
        ro "А теперь вы говорите, как Андрас."
        #nasim cloak angry
        show nasim angry at midleft with dissolve
        nas "… Сурово, но справедливо."
        ro "Я скажу Клионе, что она вольна посылать больше людей, она даже может прийти сама, если ей так хочется расследовать эту легенду."
        ro "Когда она зацикливается на чем-то, ее ничто не остановит, так что нам надо покончить с этим как можно быстрее."
        #nasim cloak neutral
        show nasim neutral at midleft with dissolve
        nas "Это делает все возможное из плохой ситуации. Примите мою благодарность, Лорд Роуэн."
        ro "Не упоминайте об этом. Просто убедитесь, что Клиона не злоупотребляет."
        "Он мог бы сказать 'прощай'' всем своим исследованиям на этой неделе... и, возможно, на следующей тоже."
        #-10 research for 2 weeks. TODO
        $ nasimAttitude += 1
        $ change_relation('cliohna', 10)
        $ demeretStatue = "Taken"
        return
