# random map events that happen on plains
init python:

    # Famine Looms
    event('famine_looms', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # The New Goblin Prince
    # should happen only if rumor about new goblin prince is not known yet
    event('new_goblin_prince', triggers='map_expl', conditions=('not new_goblin_prince_rumor', 'world.cur_tile_type == "plains"'), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Goblin Scouts
    event('goblin_scouts', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Is Rowan A Thief?
    event('is_rowan_a_thief', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Travelling Monk
    event('travelling_monk', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Please Fetch Medicine
    event('please_fetch_medicine', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Orcs in the North
    event('orcs_in_the_north', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Ancient Chest
    event('ancient_chest', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Thief in the Night
    event('thief_in_the_night', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)


label famine_looms:
# Famine Looms

scene bg31 with fade

"Во время своих путешествий Роуэн остановился в одном из небольших фермерских сообществ, чтобы поболтать с местными жителями и услышать последние новости."
"Он узнал, что недавно произошел пожар в одном из крупнейших зернохранилищ Артдейла."
"В этом были обвинены гоблины, но фермеры были в основном обеспокоены действиями барона впоследствии."
"Похоже, он так отчаянно стремился пополнить запасы продовольствия в крепости,что заставлял фермеров продавать ему даже свои скудные запасы."
"В сочетании с плохим урожаем прошлого года, существует очень реальный риск голода среди крестьянства сейчас."
"Борьба за поддержание достаточного количества продовольствия в столице и замках оставила многие из небольших общин с опасно низкими запасами."
$ add_exp(5)
#end event
return

##############################################################################
##############################################################################


label new_goblin_prince:
# The New Goblin Prince

scene bg31 with fade

"В этом уголке Розарии распространился слух, что гоблины Блэкхолтского леса потеряли страх."
"За последние несколько месяцев они осмелели в своих набегах и начали партизанскую войну с бароном."
"Как и все слухи, эти истории запутаны и противоречивы.  Некоторые предполагали, что армия спустилась с восточных гор. Другие предполагали, что разрозненные кланы объединились."
"Кто-то даже утверждал, что видел, как гоблины создавали себе двойников с помощью нечистой магии."
"Единственное, в чем они сходятся, так это в том, что эти рейдеры снова и снова скандируют имя после своих побед: 'Ту-Рув'."
"Точно, кто или что такое Ту-Рув, оставалась еще одной темой диких спекуляций."
$ add_exp(5)
$ new_goblin_prince_rumor = True
#end event
return

##############################################################################
##############################################################################


label goblin_scouts:
# Goblin Scouts

scene bg31 with fade

"В этот день путь Роуэна пересекся с небольшая группой гоблинов.  Они, были очень удивлены, встретив друг друга, но герой быстро оправился и приготовился к битве."
"Он сразу понял, что это либо разведка, либо налетчики из Блэкхолта."
"Вместо того чтобы приготовиться к схватке с этим человеком, группа маленьких зеленых людей окликнула его и предложила уйти, если он даст им немного денег."
"Если нет, то вполне возможно, что его жизнь будет в опастности в течение следующих нескольких дней."

# Choice
menu:
    # TODO
    # Req: Must have completed goblin quest (not done yet)
    #menu response - "Tell them you're allied with Tue-Row."
    "Сказать им, что ты в союзе с Ту-Рув." if False:
        $ released_fix_rollback()
        "Узнав, кто такой Роуэн и о союзе, заключенном с Ту-Рув, гоблины быстро извинились за причиненные неприятности и заверили его, что никакой взятки не требуется."
        "Перед самым отъездом один из гоблинов даже попросил у героя автограф."
        $ add_exp(10)
        #end event
        return

    #menu response - "Pay their bribe."
    "Заплатить им.":
        $ released_fix_rollback()
        "Решив, что пока лучше избежать неприятностей, Роуэн отдал гоблинам их деньги. Надеюсь, в ближайшее время ему не понадобятся деньги из казны замка."
        # lose money from treasury
        $ change_treasury(-10)
        $ add_exp(10)
        return


    #men response - "Let them do their worst."
    "Послать их.":
        $ released_fix_rollback()
        "Гоблины ушли после этого. Поначалу Роуэн думал, что это были пустые угрозы, но в течение следующих нескольких дней его постоянно преследовали и беспокоили далекие фигуры."
        "Это заставляло его путать следы, иногда возвращаться по своим следам назад и тратить значительное количество времени на поиски безопасных мест для ночлега."
        #lose movement points
        $ avatar.mp -= 2
        $ add_exp(10)
        return

##############################################################################
##############################################################################


label is_rowan_a_thief:
# Is Rowan A Thief?

scene bg31 with fade

"Торговцы - встретить их на равнинах Розарии обычное дело.  Там почти всегда живут люди, поэтому всегда есть спрос на их товары."
"Один из них, с которым Роуэн столкнулся сегодня, специализировался на различных ароматизаторах, специях и травах."
"Ничего такого, что могло бы быть нужным, но Роуэн заметил, что он держит свои деньги в ненадежном месте, по крайней мере для кого-то с навыками Роуэна."
"Хотя кража у нее шла вразрез с идеалами Роуэна, возможность получить немного золота, чтобы помочь ему выполнить квоты близнецов, пришла ему в голову."
"Тем более что купец, судя по всему, был довольно богат и вел свой бизнес успешно."


# Choice
menu:
    #(Req: Rowan must be somewhat pure)
    #menu response - "Don't try to steal any money."
    "Не пытататься украсть деньги." if avatar.corruption < 3:
        $ released_fix_rollback()
        "Оставаясь верным своей натуре, Роуэн даже не попытался украсть деньги торговца."
        $ add_exp(5)
        #end event
        return

    #menu response - "Try to steal a little money."
    "Попробывать украсть немного денег.":
        # Roll a low difficulty sleight of hand check.
        $ released_fix_rollback()
        if check_skill(5, 'sleight_hand')[0]:
            #Success:
            "Не обращая внимания на торговца, Роуэн быстро вытащил из шкатулки несколько монет. И затем пошел своей дорогой."
            "Он взял достаточно небольшую сумму, чтобы торговец никогда не понял, что кто-то его ограбил."
            # Gain a small amount of treasury, modified by a luck roll.  Gain a little bit of guilt and corruption.
            $ change_treasury(10 + dice(avatar.luck))
            $ change_base_stat('c', 1)
            $ change_base_stat('g', 1)
            $ add_exp(10)
            return
        else:
            #Failure
            "Роуэн совершил ошибку, пытаясь открыть шкатулку с деньгами, что, к сожалению, привлекло взгляд торговца."
            "Не желая больше испытывать судьбу, Роуэн продолжил свой путь."
            # Gain a small amount of guilt and infamy.
            $ change_base_stat('g', 1)
            $ change_base_stat('f', 1)
            $ add_exp(5)
            return

    #menu response - "Try to steal a lot of money."
    "Попробывать украсть много денег.":
        # Roll a moderately difficult sleight of hand check.
        $ released_fix_rollback()
        if check_skill(10, 'sleight_hand')[0]:
            #Success:
            "Как только он возникла возможность, Роуэн быстро открыл денежный ящик и взял большую часть монет, хранящихся внутри."
            "Он почувствовал смесь стыда и возбуждения, когда оставил торговца позади."
            "Он взял достаточно много, чтобы торговец, достаточно быстро сообразит, что что-то случилось."
            "Возможно, пойдут слухи, но не пойман не вор."
            # Gain a larger amount of treasury, modified by a luck roll.  Gain a little bit of guilt and corruption.  Gain a small amount of infamy.
            $ change_treasury(20 + dice(avatar.luck))
            $ change_base_stat('c', 2)
            $ change_base_stat('g', 2)
            $ change_base_stat('f', 1)
            $ add_exp(10)
            return
        else:
            # Failure
            "Роуэн приступил к краже. К сожалению, она закончились преждевременно, и он был пойман торговцем с поличным, что заставило его сбежать."
            "В руке у него осталось немного денег, и если его поймают как мелкого воришку, это будет большой проблемой для его репутации."
            "Надеюсь, не слишком много людей поверят рассказу торговца."
            # Gain a small amount of treasury, modified by a luck roll.  Gain a little bit of guilt and corruption.  Gain a small amount of infamy.
            $ change_treasury(10 + dice(avatar.luck))
            $ change_base_stat('c', 2)
            $ change_base_stat('g', 2)
            $ change_base_stat('f', 1)
            $ add_exp(5)
            return

    #(Req: Rowan must be somewhat corrupt)
    #menu response - "Try to steal all of the merchant's money."
    "Попробывать украсть все деньги торговца." if avatar.corruption > 5:
        # Roll a difficult sleight of hand check.
        $ released_fix_rollback()
        if check_skill(15, 'sleight_hand')[0]:
            #Success:
            "Отбросив любую попытку скрыть свои действия в дальнейшем, Роуэн воспользовался первым же шансом, который у него был, чтобы взять шкатулку с деньгами и пределал ей ноги."
            "У него будет достаточно времени, чтобы открыть ее позже."
            "Не было никаких сомнений, что купец узнает, кто это сделал, когда увидит, что шкатулка исчезла, и Роуэн знал, что это произойдет заранее."
            "Вопрос был в том, сколько людей поверят нищему торговцу пряностями."
            # Gain the largest (though still small) amount of treasury, modified by a luck roll.  Gain a little bit of guilt and some corruption.  Gain a small amount of infamy.
            $ change_treasury(30 + dice(avatar.luck*3))
            $ change_base_stat('c', 2)
            $ change_base_stat('g', 2)
            $ change_base_stat('f', 1)
            $ add_exp(10)
            return
        else:
            #Failure
            "Дела пошли плохо, и торговец ловит Роуэна, пытающегося вытащить денежную шкатулку из его тележки."
            "Купец направил арбалет на героя, заставив Роуэна отомстить и заколоть их ножом."
            "Затем он удрал с шкатулкой денег, чтобы открыть ее позже, оставив лежащую на земле истекающую кровью фигуру позади себя."
            # Gain largest (though still small) amount of treasury, modified by a luck roll.  Gain a lot of guilt and corruption.  No witnesses, no infamy.
            $ change_treasury(30 + dice(avatar.luck*3))
            $ change_base_stat('c', 3)
            $ change_base_stat('g', 3)
            $ add_exp(5)
            return

##############################################################################
##############################################################################


label travelling_monk:
# Travelling Monk

scene bg31 with fade

"В этот день пути монаха Соленсии пересеклись с Роуэном. Старик, казалось, почувствовал тень на душе Роуэна и предложил ему помолиться вместе, чтобы успокоить ум и сердце."
"Он утверждал, что всегда хорошо размышлять о своем месте в мире и о том, как они вписываются в план богини."
"Потратив на это какое-то время, Роуэн, конечно, многое бы сделал для своей совести, но время, проведенное таким образом, - это время, впустую."

#Choice
menu:
    #menu response -  "Spend only a short time praying."
    "Провести в молитве только короткое время.":
        $ released_fix_rollback()
        "В конце концов, Роуэн решил просто провести символический набор молитв, прежде чем попрощаться с монахом."
        $ add_exp(5)
        #end event
        return

    #menu response - "Spend a long time in deep prayer, meditation, and be purified."
    "Провести много времени в глубокой молитве, медитации и очиститься.":
        $ released_fix_rollback()
        "Роуэн позволил монаху провести себя через семь молитв, за которыми последовали три стадии саморефлексии."
        "Затем он попросил, чтобы его очистили и омыли от греха. Часы шли и шли, он даже захватил следующий день."
        "Тем не менее, к концу Роуэн почувствовал себя чище и более самим собой, чем когда-либо за долгое время."
        # Lose movement points, lose corruption.
        $ change_mp(-2)
        $ change_base_stat('c', -2)
        $ add_exp(10)
        return

##############################################################################
##############################################################################


label please_fetch_medicine:
# Please Fetch Medicine

scene bg31 with fade

"В одной из маленьких деревушек, Роуэн наткнулся на селян сражавщихся с гигантским пауком."
"Он быстро присоеденился к ним и помог им отбиться от существа."
"Однако, оказалось, что зверь отравил главу общины, и тот стал готовиться к худшему. Противоядие можно было купить в гильдии убийц монстров, но ближайшее было далеко от них."
"Вдобавок ко всему, это было недешево. Крестьяне пришли к выводу, что Роуэн-единственный, кто может успеть вовремя, чтобы спасти вождя, и предложили дать ему деньги, необходимые для покупки противоядия."
# Choice
menu:
    #(Req: Rowan must be somewhat pure.)
    #menu response - "Go get the antidote."
    "Идти и принеси противоядие." if avatar.corruption < 4:
        $ released_fix_rollback()
        "Роуэн не мог оставить этих людей в трудную минуту, поэтому он взял деньги и отправился в долгое путешествие туда и обратно."
        "Он успел вернуться как раз вовремя, чтобы спасти жизнь старейшины."
        "Крестьяне пришли в восторг, устроили небольшую вечеринку в честь героя и пообещали петь ему дифирамбы долгие годы."
        # Lose movement points, lose infamy.
        $ change_mp(-2)
        $ change_base_stat('f', -2)
        $ add_exp(10)
        return

    #menu response - "Refuse to get the antidote."
    "Отказаться.":
        $ released_fix_rollback()
        "Роуэн просто не могл найти время, чтобы помочь фермерам."
        "Сначала они не могли поверить, что этот герой покидает их, но в конце концов смирились с потерей своего старейшины."
        # Gain guilt.
        $ change_base_stat('g', 2)
        $ add_exp(10)
        return

    #(Req: Rowan must be slightly corrupt)
    #menu response - "Take the money for yourself."
    "Взять деньги себе." if avatar.corruption >= 4:
        $ released_fix_rollback()
        "Роуэн согласился достать противоядие для фермеров, но после того, как покинул деревню, он вернулся к своей работе."
        "Деньги, которые они ему дали, к концу недели уже лежали в казне Бладмина."
        # Gain treasury, infamy, guilt, and corruption.
        $ change_treasury(10)
        $ change_base_stat('f', 2)
        $ change_base_stat('g', 3)
        $ change_base_stat('c', 1)
        $ add_exp(10)
        return

##############################################################################
##############################################################################


label orcs_in_the_north:
# Orcs in the North

scene bg31 with fade

"С севера распространился слух о том, что отряд орков пересек Розарианскую долину, преследуемый Протейскими войсками."
"Эта группа начала совершать набеги на разбросанные поселения к северу от Растедейля."
"Вполне вероятно, что это были одни из остатков рассеянных армий Карнаса."
"Многие люди беспокоились, что им прийдется переезжать в другие часть королевства, если они появятся возле их селений."
"Но орки оставались по прежнему на севере."  
"Некоторые были недовольны бароном за его бездействие против этой угрозы. По словам местного дворянина, это связано с более насущными проблемами бандитов и гоблинов."
"Но если орки направятся дальше на юг, то они почувствуют ярость розарийских армий. Если же они останутся на севере, Протейские силы в конечном счете догонят и разберутся с ними."  
"Однако, поскольку протейские войска охотятся на многих отставшихся в этом районе последователей Карнаса, это может занять несколько месяцев."
$ add_exp(5)
return

##############################################################################
##############################################################################


label ancient_chest:
# Ancient Chest

scene bg31 with fade

"Фортуна улыбнулась Роуэну в этот день, когда он споткнулся о древний сундук, наполовину зарытый в землю."  
"Он часто задавался вопросом, почему ему иногда везло найти сокровища, обычные люди, никогда не рассказывали истории о старых сундуках или сокровищах, лежащих вокруг."  
"Если подумать, то только другие герои были единственными, кто когда-либо упоминал, что с ними происходило нечто подобное."
"Возможно, именно так Солансия благословляла своих чемпионов. (Или автор игры) Может быть, это было способом помочь ему."  
"Роуэн мог только гадать. Единственным ответом, который он получит в этот день, будет то, что находится внутри сундука."
"(Солансия это Автор, Автор это Бог... теперь и у переводчика много вопросов...)"

#Roll luck and get a random simple piece of equipment based on result.
if check_stat(15, 'luck')[0]:
    $ get_rnd_item(200, 300)
    $ add_exp(10)
else:
    $ get_rnd_item(100, 250)
    $ add_exp(10)
return

##############################################################################
##############################################################################


label thief_in_the_night:
# Thief in the Night

scene bg31 with fade

#Make a spot check
if check_skill(10, 'spot')[0]:
    #Success
    "Пока Роуэн отдыхал ночью, его сон были прерван звуком того, что кто-то рядом роется в его сумке!"
    "Сработали инстинкты, усвоенные много лет назад, и он бросился к незваному гостю так бесшумно, как только мог."
    "Вор испустил крик ужаса, когда Роуэн напал на него."
    "Маленький человечек, который, как теперь понял Роуэн, был гоблином, отчаянно пытался вырваться, но вскоре был усмирен и связан."
    "Роуэн снова разжег костер и обыскал гоблина, освободив его от того, что, вероятно, было украденными вещами."
    "Герой несколько минут допрашивал гоблина, чтобы узнать, не может ли он извлечь из него что-нибудь полезное, но потом понял, что зеленый человек, скорее всего, одиночка."
    "Утром Роуэн отпустил несостоявшегося вора."
    # Gain a random piece of low value equipment.
    $ get_rnd_item(0, 100)
    $ add_exp(10)
else:
    #Failure
    "Пока Роуэн отдыхал ночью, его сон был нарушен звуком бегущих ног. Он внимательно прислушался, нет ли еще какого-нибудь движения, но ничего не обнаружил."
    "Осмотрев лагерь, Роуэн проверил свои вещи и понял, что его ограбили! Он проклинал себя за то, что позволил этому случиться, и надеялся, что ему не понадобится то, что он потерял."
    "К счастью, они не взяли ничего важного."
    #Lose a piece of non-unique equipment, or if the player cannot lose a piece of equipment, a small amount of gold.
    if not lose_rnd_item(0, 100):
        $ change_treasury(-10)
    $ add_exp(5)
return
