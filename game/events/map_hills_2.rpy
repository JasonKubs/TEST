# random map events that happen on hills
init python:

    #Iron Seam
    event('iron_seam', triggers='map_expl', conditions=('world.cur_tile_type == "hills"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #Landslide
    event('landslide', triggers='map_expl', conditions=('world.cur_tile_type == "hills"',), run_count=1, group='map_expl', priority=pr_map_rnd)


label iron_seam:
#Iron Seam
#No requirements

scene bg43 with fade

"Далекий склон утеса, освещенный солнечным светом, привлек внимание Роуэна, когда он пересекал гористую местность. Он выбрал путь вдоль долины, чтобы взглянуть поближе."
"Там, наверху, на этой отвесной скале определенно была-широкая полоса серого, мягко поблескивающего камня. Железо?"
"В этой труднопроходимой и отдаленной местности наверняка можно было найти нетронутые жилы."
"Однако узнать это было нелегко и небезопасно. Роуэн задумался, стоит ли это усилий."

menu:
    "Подняться на скалу.":
        $ released_fix_rollback()
        #climb check dc12
        $ event_tmp['climb_roll'] = roll_skill('climb')[0]

        if event_tmp['climb_roll'] < 8:
        #less than 8
            "Роуэн отложил свое снаряжение в сторону и положил руки на камни, пристально глядя на дразнящий блеск наверху."
            "Он вскарабкался футов на пятнадцать вверх по отвесному склону скалы, прежде чем сапог провалился."
            "Он наполовину упал, наполовину соскользнул вниз, тяжело приземлившись на ребра с ужасным треском. Он с трудом поднялся на ноги, чертыхаясь."
            "У него не было ни малейшего шанса на повторную попытку, рана колола его бок - что бы там не было, путешественник, более опытный в лазании по скалам, чем он смог бы узнать."
            #gain 10 xp and 1 wound
            $ add_exp(10)
            $ take_damage(1)
            return
        elif event_tmp['climb_roll'] < 12:
        #8-11
            "Роуэн отложил свое снаряжение в сторону и положил руки на камни, пристально глядя на дразнящий блеск наверху."
            "Подъем был трудным, скала зазубренной, и его сердце почти выскочило из груди, когда он внезапно потерял хватку и упал на несколько футов, ударившись лодыжкой и разбив нос о камень."
            "После того, как он провел несколько минут, делая несколько долгих, глубоких вдохов, он смог продолжить."
            #gain 1 wound and continue to reward step
            $ take_damage(1)
        else:
        #pass
            "Роуэн отложил свое снаряжение в сторону и положил руки на камень, пристально глядя на дразнящий блеск наверху."
            "Используя весь свой опыт, он взобрался по отвесной скале, как ящерица по выгоревшей на солнце стене."
            "Дальше уклон стал менее резким, и он смог встать на выступ скалы рядом с большой полосой обесцвеченного камня, внимательно его изучая."

        #reward step
        $ tmp = dice(5)
        if tmp <= 2:
            $ event_tmp['reward'] = 'Big'
        elif tmp <= 4:
            $ event_tmp['reward'] = 'Average'
        else:
            $ event_tmp['reward'] = 'No reward'

        if event_tmp['reward'] == 'Big':
        #40% chance - Big score
            "Его настроение поднялось, когда он приставил к камню нож и услышал предательский 'звон'. Железо!"
            "И, судя по всему, в концентрированном количестве; весь этот горный склон вполне мог быть рудой хорошего качества."
            "Единственной проблемой было добраться до него. Он мог бы вызвать команду, чтобы построить несколько платформ, пандусов и шкивов, но на это потребуется время."

            menu:
                "Использовать орков.":
                    $ released_fix_rollback()
                    "Роуэн вызвал угрюмую стаю орков и заставил их заняться строительством деревянных лесов и веревочных подъемников, которые позволили бы им добраться до богатого железного пласта и спустить руду вниз."
                    "Это заняло утомительное количество времени, но в конце концов рабочие создали приличную импровизированную систему, и зазвучали удары кирок. Усталый, но довольный, Роуэн снова отправился в путь."
                    #lose 2 move points and 5 morale, gain 20 xp and a medium sized mine
                    $ change_mp(-2)
                    $ change_morale(-5)
                    $ add_exp(20)
                    # add iron/week as for "important mine" #7
                    $ castle.mines += 1
                    $ castle.iron_per_week += mines_defs[7][2]
                    return

                "Использовать людей.":
                    $ released_fix_rollback()
                    "Роуэн спустился в долину, нашел деревню, где были люди, желающие наняться на работу."
                    "Он заставил их построить деревянные леса и канатные подъемники, которые позволили бы им добраться до богатого железного пласта и спустить руду вниз."
                    "Это заняло утомительное количество времени, но в конце концов рабочие создали приличную импровизированную систему, и зазвучали удары кирок. Усталый, но довольный, Роуэн снова отправился в путь."
                    #lose 2 move points and 30 treasury, gain 20 xp and a medium sized mine
                    $ change_mp(-2)
                    $ change_treasury(-30)
                    $ add_exp(20)
                    # add iron/week as for "important mine" #7
                    $ castle.mines += 1
                    $ castle.iron_per_week += mines_defs[7][2]
                    return

                "Не создовать шахту.":
                    $ released_fix_rollback()
                    "Роуэн решил, что в местности, куда легче добраться, есть железные рудники, и ему не хотелось тратить много времени и сил на их обустройство."
                    "Он неохотно спустился с утеса, собрал свои пожитки и снова отправился в путь."
                    #gain 20xp
                    $ add_exp(20)
                    return
        elif event_tmp['reward'] == 'Average':
        #40 chance - Average Score
            "Его настроение поднялось, когда он приставил к камню нож и услышал предательский 'звон'. Железо!"
            "Его было не так много, как он надеялся, но все же здесь было приличное количество, вполне достаточное, чтобы оправдать создание шахты."
            "Единственная проблема - добраться до него. Он мог бы вызвать команду, чтобы построить несколько платформ, пандусов и шкивов, но на это потребуется время."

            menu:
                "Использовать орков.":
                    $ released_fix_rollback()
                    "Роуэн вызвал угрюмую стаю орков и заставил их заняться строительством деревянных лесов и веревочных подъемников, которые позволили бы им добраться до богатого железного пласта и спустить руду вниз."
                    "Это заняло утомительное количество времени, но в конце концов рабочие создали приличную импровизированную систему, и зазвучали удары кирок. Усталый, но довольный, Роуэн снова отправился в путь."
                    #lose 2 move points and 5 morale, gain 20 xp and a small sized mine
                    $ change_mp(-2)
                    $ change_morale(-5)
                    $ add_exp(20)
                    # add iron/week as for "small mine" #6
                    $ castle.mines += 1
                    $ castle.iron_per_week += mines_defs[6][2]
                    return

                "Использовать людей.":
                    $ released_fix_rollback()
                    "Роуэн спустился в долину, нашел деревню, где были люди, желающие наняться на работу."
                    "Он заставил их построить деревянные леса и канатные подъемники, которые позволили бы им добраться до богатого железного пласта и спустить руду вниз."
                    "Это заняло утомительное количество времени, но в конце концов рабочие создали приличную импровизированную систему, и зазвучали удары кирок. Усталый, но довольный, Роуэн снова отправился в путь."
                    #lose 2 move points and 30 treasury, gain 20 xp and a small sized mine
                    $ change_mp(-2)
                    $ change_treasury(-30)
                    $ add_exp(20)
                    # add iron/week as for "small mine" #6
                    $ castle.mines += 1
                    $ castle.iron_per_week += mines_defs[6][2]
                    return

                "Не создовать шахту.":
                    $ released_fix_rollback()
                    "Роуэн решил, что в местности, куда легче добраться, есть железные рудники, и ему не хотелось тратить много времени и сил на их обустройство."
                    "Он неохотно спустился с утеса, собрал свои пожитки и снова отправился в путь."
                    #gain 20xp
                    $ add_exp(20)
                    return
        else:
        #20 chance - Fuck you iron
            "Еще до того, как он приставил к камню нож, Роуэн понял, что это бесполезно. Лезвие легко прошло сквозь блеск в скалу."
            "Здесь была железная руда, но это был просто некачественный блеск материала над бесполезным силикатом."
            "Роуэн спустился вниз по склону утеса, забрал свое снаряжение и снова отправился в путь, пытаясь избавиться от ощущения, что его обманули, показав огромную фигу."
            #lose 2 move points, gain 20xp
            $ change_mp(-2)
            $ add_exp(20)
            return


    "Не делать этого.":
        $ released_fix_rollback()
        "Роуэн решил, что в местности, куда легче добраться, есть железные рудники, а ему не хотелось ломать ногу, исследуя то, что может оказаться бесполезным."
        "Он закрепил свой рюкзак и снова отправился в путь."
        return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label landslide:
#Landslide
#No Requirements

scene bg43 with fade

"Звон падающей гальки. Удаленный, сердитый гул. Предупреждающие признаки оползня звучат несущественно, как чья-то чужая проблема."
"Пока, внезапно, она не происходит."
"Роуэн дернул головой вверх, чтобы увидеть, как падает занавес со склона горы, и пыльная нагрузка валунов движется вниз к нему."

#spot check dc12
$ event_tmp['spot check'] = roll_skill('spot')

if event_tmp['spot check'][0] >= 12 or event_tmp['spot check'][1] == 20:
#pass
    "Однако высококвалифицированное подсознание Роуэна заметило опасность, даже когда остальная часть его разума блуждала."
    "Сразу же он бросился назад, едва не раздавленый одним из больших валунов, которые упали, закрывая все перед ним в облаке пыли и мусора."
    "Потребовалось некоторое время, чтобы остановить кашель и успокоить бьющееся сердце, но Роуэн остался невредим в остальном."
    "Благодаря удачу и присмотревшись ближе к местности, он выбрал новый путь в обход."
    #gain 20xp
    $ add_exp(20)
    return
else:
#fail
    #escape artist check dc12
    $ event_tmp['escape artist check'] = roll_skill('escape')
    if event_tmp['escape artist check'][0] >= 12 or event_tmp['escape artist check'][1] == 20:
    #pass
        "Роуэн бросил свое проворное тело вперед, бросившись под выступ скалы, чтобы избежать большего веса камней."
        "Они поднимали облака ослепительной пыли, с грохотом падая перед ним, уничтожая тропинку, на которой он только что стоял."
        "Он услышал, как над ним застонал камень, и быстро перекатился на бок, грудь его вздымалась, когда он увидел, как выступ затряся под тяжестью последнего валуна, который с глухим стуком упал вниз."
        "Ему потребовалось некоторое время, чтобы перестать кашлять и успокоить бьющееся сердце, но в остальном Роуэн был невредим."
        "Благодоря свою удачу и внимательно следя за местностью над головой, он пошел дальше."
        #gain 20xp
        $ add_exp(20)
        return
    elif (8 <= event_tmp['escape artist check'][0] <= 11):
    #8-11
        "Роуэн бросил свое проворное тело вперед, бросившись под выступ скалы, чтобы избежать большего веса камней."
        "Они поднимали облака ослепительной пыли, с грохотом падая перед ним, уничтожая тропинку, на которой он только что стоял."
        "Он услышал, как над ним застонал камень, и быстро перекатился на бок, грудь его вздымалась, когда он увидел, как выступ затряся под тяжестью последнего валуна, который с глухим стуком упал вниз."
        "Схватившись за руку и закашлявшись, Роуэн сел и подождал, пока сердце перестанет биться, пытаясь вырваться из груди. Боль сжала его плечо, и он обнаружил, что едва может пошевелить рукой."
        "Но, учитывая опустошение вокруг него, все могло быть намного хуже. Благодаря свою удачу и внимательно следя за местностью над головой, он пошел дальше."
        #gain 10xp and 1 wound
        $ add_exp(10)
        $ take_damage(1)
        return
    else:
    #fail
        "Роуэн застыл на один роковой миг, и его окутали падающие обломки. Камень размером с кирпич ударил его сбоку по голове, оглушив и отправив в аут."
        "Это оказалось удачей - большие валуны с глухим стуком покатились по его распростертому телу, увлекаемые выступом скалы."
        "Еще один кусок размером с дыню врезался ему в спину, достаточно сильно, чтобы сломать ребро."
        "После того как оползень сошел на нет и по крайней мере часть окружающего мира вновь обрела четкость очертаний, покрытая пылью Роуэн поднялся на ноги."
        "Он застонал и закашлялся, стараясь не чувствовать тошноты."
        "Боль пронзила его, он почувствовал, что едва может двигаться, и все же он знал, что должен это сделать. Схватившись за бок, он заковылял вперед."
        #gain 5 xp and 2 wounds
        $ add_exp(5)
        $ take_damage(2)
        return
