init python:

    #Corrupted Abbey - Dazzanath
    #Requirement: Dark sanctum
    event('corrupted_abbey_dazzanath', triggers='map_res_19', conditions=('castle.buildings["sanctum"].lvl > 0', 'castle.buildings["brothel"].lvl > 0',),
        group='map_res_abbey', run_count=1, priority=pr_map_res)
    #dazzanath arrives event
    #//Quick message after 4 weeks when Dazzanath arrives at the castle. Occurs at end of week, in addition to any other events.
    event('dazzanath_arrives', triggers="week_end", active=False, group='ruler_event', run_count=1, priority=pr_ruler_high)


label corrupted_abbey_dazzanath:
#Corrupted Abbey - Dazzanath
#Requirement: Dark sanctum

scene black with fade
show rowan necklace neutral behind black

#if corruption is higher than 49
if avatar.corruption > 49:
    "Обычно Роуэн никогда не проверял отдельные аббатства, прежде чем приказать своим оркам начать атаку."
    "Это было неэффективно, разведывать каждое место, которое он неизбежно прикажет сжечь дотла."
else:
#else
    "Обычно Роуэн никогда не проверял отдельные аббатства, прежде чем приказать своим оркам войти."
    "Так было проще. Он не хотел видеть лица людей, которые скоро умрут или будут порабощены."

#rejoin
"Но на этот раз все было по-другому, поскольку аббатство перед ним лежало в области, на которую ему сказали обратить особое внимание."

show xzaratl neutral at center with dissolve

xz "Если ты захочешь, то сможешь найти там что-нибудь... интересное."

hide xzaratl with dissolve

"Было бы слишком, чтобы люди, с которыми он работает, перестали быть такими загадочными?"
"Наверное, да."

#abbey bg
scene black with fade
show rowan necklace neutral behind black

"Как бы то ни было, о чем бы ни говорила К'заратл, Роуэн знал, что было бы неразумно просто игнорировать его слова."
"Вот почему на этот раз, проведя разведку в аббатстве, он не сообщил в замок, а вместо этого отправился навестить духовенство."
"До сих пор он не находил в этом месте ничего странного. Само здание ничем не отличалось от других храмов Солансии."
"Это было небольшое восьмиугольное строение с большими окнами."
"Роуэн подошел к главным воротам и дважды постучал. Через некоторое время двое послушников открыли ее и спросили о характере его визита."
"Он представился, но не упоминул о своем прошлом, и объяснил, что просто проходил мимо."
"Он сказал, что хочет засвидетельствовать свое почтение Солансии и немного отдохнуть, прежде чем продолжить путешествие."
"Простое прикрытие, которое избавит его от не нужных вопросов."
"Но послушники не очень-то стремились впустить его внутрь, это было видно по выражению их лиц."
"Они подозрительно поглядывали на него и обменивались взглядами, как будто ожидали, что другой придумает предлог, чтобы отослать посетителя."
"Но они ничего не нашли и с некоторой неохотой провели его внутрь, в большой восьмиугольный зал, который был центром аббатства."
"Роуэн взглянул на статую Солансии и опустился перед ней на колени, чтобы вознести короткую молитву."
"Но как только он собрался начать, приятный голос окликнул его."

aga "Роуэн Блэкуэлл?"
aga "Это ты?"

"Из одной из боковых комнат в холл вошла невысокая полная монахиня. Она выглядела не старше сорока, если судить по маленьким морщинкам вокруг рта."
"Она подошла к нему с доброй улыбкой на губах, с блеском в глазах и почтительно склонила голову."

#if guilt higher than 49
if avatar.guilt > 49:
    "Роуэн ответил на приветствие пустой улыбкой."
else:
#else
    "Роуэн ответил на приветствие вежливой улыбкой."

#rejoin
ro "Собственной персоной. Мы знакомы?"

$ agathaName = "Преподобная Агата"

aga "Преподобная Агата, настоятельница. Не совсем."
aga "Я видела тебя однажды. Давным-давно. После битвы при Сили Филдс."

"Ее улыбка стала меланхоличной, и болезненная тень пробежала по ее лицу."

aga "Я была одной из медсестер, которые заботились о раненых. Вы посетили лазарет, чтобы проведать старого друга."

"Роуэн невольно поморщился. Он помнил битву и то, что произошло после нее."
"Сэр Рейнальд, один из немногих дворян, открыто поддержавших назначение Роуэна генералом, получил тяжелую рану на Сили-Филдс."
"Вскоре после этого он умер, в своих последних словах умоляя Роуэна создать мир, где его дочери могли бы жить счастливо."

#if corruption is higher than 49
if avatar.corruption > 49:
    "Роуэн уже давно не вспоминал о старом рыцаре и был не доволен тем, что монахиня напомнила ему об обещаниях, которые он дал в тот день."
    "Но он не мог сказать, как далеко отклонился от пути, по которому поклялся следовать."
else:
#else
    "Время от времени Роуэн все еще думал о старом рыцаре."
    "Веселый парень, он всегда хвастался, что его младший ребенок-жемчужина королевства."
    "Если бы он знал, как далеко отклонился Роуэн от пути, по которому он когда-то поклялся идти, то, без сомнения, ударил бы его по лицу, пытаясь привести его в чувства."
    "Это была неприятная мысль, но Роуэн не мог не улыбнуться при воспоминании о своем старом компаньоне. Королевства многое потеряли, когда Рейнальд покинул их."

#rejoin

ro "Сэр Рейнальд. Он был хорошим человеком."

aga "Так оно и было. Помню, в последние минуты жизни он говорил о своих дочерях. Сказал, что надеется, что однажды они найдут такого благородного мужа, как ты."

ro "Надеюсь, так и случилось."

aga "...."

"Монахиня тепло улыбнулась. Двое послушников все еще были с ними, стоя позади Роуэна."

aga "Но хватит воспоминаний о прошлом. Роуэн Блэкуэлл, не могу выразить, как я счастлива видеть вас здесь. Солансия наконец ответила на наши молитвы."
aga "Есть кое-что, в чем вы должны нам помочь."

aco "Преподобная мать, вы не должны!"

"Один из послушников неожиданно повысил голос."

aco "Этот человек не является частью Кодекса! Он не может узнать..."

aga '"Этот человек" - один из шести героев, которые убили последнего демона. Он больше подходит для этой задачи, чем кто-либо из нас!'

"Голос монахини прорезал воздух, заставив обоих послушников замолчать."
"Роуэн оглянулся. Хотя мужчин явно отчитывали, они, казалось, не были убеждены окончательно в этой идеи."

aga "Прошу прощения. Они хотят как лучше. Очень много тьмы, и трудно найти доверие."
aga "Но я знаю, что твой приезд-знак богини. Я уверена в этом."

"Монахиня повернулась к двум своим помощникам."

aga "Оставте нас."

"Мужчины поколебались, потом неохотно повиновались. Агата снова улыбнулась Роуэну."

aga "Будет проще просто показать вам. Пойти со мной."

"Ровена подняла бровь и последовала за аббатисой. Думаю, он наткнулся на то, о чем говорил Кзаратл."
"Они направились к маленькой двери в задней части храма, войдя в жилые помещения соланского духовенства."
"Судя по размерам, в аббатстве жило около дюжины верующих, включая аббатису."
"Агата подвела его к винтовой лестнице, которую охраняла молодая монахиня."
"Хотя осторожность была, пожалуй, лишней, поскольку женщина не заметила их приближения."
"Она смотрела вдаль с ошеломленным выражением лица и вернулась к реальности, только когда преподобная мать коснулась ее плеча."

aga "Отдохни немного, сестра. Я пока понаблюдаю."

"Монахиня покраснела, пробормотала извинения и быстро ушла."
"Агата открыла дверь в то, что Роуэн принял за подвал. Когда они спустились вниз, аббатиса наконец решила пролить свет на эту тайну."

aga "Месяц назад он пришел к нам во сне. Демон самый мерзкий. Он шептал слова, которые лучше не произносить, и пытался ввести нас в заблуждение обещаниями удовольствия."
aga "Сначала, мы его прогнали."
aga "Но потом, ко мне пришло видение. Сама богиня предстала передо мной, рассказывая о демоне, который мучил нас."
aga "Она напомнила мне о долге, который мы все взяли на себя и который даровал нам святой поиск."
aga "Видите ли, прогоняя демона, мы только приговариваем других быть его добычей."
aga "Вместо того, чтобы защищать людей, мы решили защитить себя."
aga "Так что  ... мы устроили ловушку..."

"Ее голос затих. Драматическая пауза раздосадовала героя, но он решил подождать, пока она сама продолжит, и промолчал."
"Мгновение спустя, они подошли к двери. Роуэн краем глаза следил за аббатисой и заметил, что дыхание ее участилось, а щеки покраснели."
"Настоятельница достала тяжелый ключ и открыла погреб."
"В тот же миг обоняние Роуэна было поражено  запахом спермы. Он прикрыл нос рукавом и вгляделся в полумрак."
"Он увидел его в другом конце комнаты. Демон. Он был освещен двумя ближайшими факелами и прикован к стене."
"Услышав звук открывающейся двери, он вскинул голову и яростно уставился на них горящими розовыми глазами."

aga "Его зовут Даззанат. Вот уже больше трех недель он прикован здесь цепями, и мы держим его истощенным, чтобы он не мог освободиться от своих уз."

"Они подошли к инкубу. Несмотря на то, что он был заперт под землей почти месяц, он не казался таким уж слабым для Роуэна."
"Его розовые глаза пронзили Ровену насквозь, и цепи тревожно зазвенели, когда его сильные руки угрожали разорвать их одной грубой силой."
"- но пока безуспешно."
"Кроме того, демон был окружен кругом рун. Хотя Роуэну не хватало опыта, чтобы опознать их, он считал, что они истощают его силы, по крайней мере частично."
"Наконец, Роуэн не могла не заметить, что нижняя половина инкуба была выставлена на всеобщее обозрение."
"Его член покачивался перед их глазами, выглядя болезненно возбужденным."
"По какой-то причине Роуэн почувствовал, что его глаза сами собой устремились на него, и ему пришлось напрячься, чтобы отвести их."
"Теперь не было сомнений, что именно об этом говорил К'заратл."
"И хотя Роуэн уже достаточно хорошо представлял себе, что происходит, у него оставалось еще несколько вопросов к Аббатисе."

$ event_tmp['ask_name'] = False
$ event_tmp['ask_rune_circle'] = False
$ event_tmp['ask_naked'] = False
$ event_tmp['ask_drained'] = False
$ event_tmp['Dazzanath cum dump'] = False
label AgathaMenu:

menu:
    "Спросил, откуда она знает его имя.":
        $ released_fix_rollback()
        aga "Один из послушников сказал на прошлой неделе. Они регулярно проверяют его, и некоторые из них используют возможность узнать о нем больше."
        ro "Вы сами с ним не разговариваете?"
        "Агата покачала головой."
        aga "Я была слишком занята изучением древних текстов в поисках способов изгнать его."
        aga "Боюсь, это оставило мне мало времени для других дел, поэтому мне пришлось передать наблюдение другим членам Кодекса."
        $ event_tmp['ask_name'] = True
        jump AgathaMenu

    "Спросите о рунном круге.":
        $ released_fix_rollback()
        "Агата широко улыбнулась в ответ и гордо выпрямилась."
        aga "Это подарок самой Солансии! Пока мы готовили ловушку, она послала мне еще одно видение, показывая, какие руны сделают его бессильным."
        'Роуэн еще раз взглянул на инкуба. Демон зарычал на него в ответ.'
        ro "Испытывали ли вы подобные видения в прошлом?"
        "Монахиня поколебалась, потом покачала головой."
        ro "(Ничего подозрительного.)"
        $ event_tmp['ask_rune_circle'] = True
        jump AgathaMenu

    "Спросите, почему он голый.":
       $ released_fix_rollback()
       aga "О, это просто. Мы не могли позволить ему спрятать отмычки в одежде."
       ro "Вы нашли что-нибудь?"
       aga "Нет."
       ro "Так почему же он до сих пор голый?"
       "Аббатиса нахмурилась, не зная, что ответить."
       aga "ГМ... Кажется, кто-то из послушников сказал, что не хочет подходить к нему без крайней необходимости?"
       "Она покраснела, стыдясь себя, и отвернулась."
       aga "Честно говоря, я не думала об этом до сих пор."
       ro "(Право...)"
       $ event_tmp['ask_naked'] = True
       jump AgathaMenu

    "Спросите, про запах.":
        $ released_fix_rollback()
        aga "О, это просто! Мы сливаем его сперму."
        ro "…"
        ro "Вы сливаете его сперму."
        aga "Да. Это главный источник энергии инкубов. Пока его яички пусты, он бессилен убежать."
        ro "…"
        "Он посмотрел ей прямо в глаза. Она наклонила голову, явно не понимая его явного недоверия."
        ro "(О Богиня.)"
        $ event_tmp['ask_drained'] = True
        jump AgathaMenu

    #only available after other four choices have been chosen
    "Спроси, почему они его не убили." if event_tmp['ask_name'] and event_tmp['ask_rune_circle'] and event_tmp['ask_naked'] and event_tmp['ask_drained']:
        $ released_fix_rollback()
        aga "О, мы не могли! Мы не можем рисковать его преобразованием во внешней темноте!"
        aga "Такого могущественного инкуба нельзя убить простым мечом, его сущность просто покинет это царство и со временем создаст себе новое тело!"
        aga "Поэтому мы пока держим его здесь. И вот тут-то вы и вступаете."
        aga "Если конечно вы согласитесь помочь нам."
        "Аббатиса с надеждой посмотрела на него, а Роуэн постарался подавить вздох. Его глаза блуждали по рунам, окружавшим инкуба."
        "Он сомневался, что они действительно что-то делали, кроме как выглядели таинственно."
        "Так же, как он сомневался, что удержание его “истощенным” на самом деле ослабляло инкуба."
        "Вся эта история разлетелась по швам в тот момент, когда кто-то задумался об этом"
        "и Роуэн не сомневался, что инкубу пришлось некоторое время поработать с жрецами, чтобы они так легко отбросили элементарную логику."
        "Демоны ни когда не возвращались после того, как были убиты. Как и люди, они все умирают, когда их убивают."
        "Даже Карнас, несмотря на всю свою силу, никогда не мог воскреснуть из мертвых."
        "Пришло время решить, что делать со всей этой ситуацией. Роуэн снова посмотрел на инкуба, его горящие глаза смотрели прямо на него.-"
        "..."
        "Э..."
        "…"
        "О чем это он? .. "
        aga "Вы хорошо себя чувствуете, Роуэн?"
        "Чувствуя легкое головокружение, Роуэн несколько раз быстро моргнул, пытаясь собраться с мыслями. Кем он был..."
        ro "И... Да, я чувствую себя хорошо, просто..."
        "Это было на него не похоже-вдруг так себя вести. Что-то было-"
        "Он услышал, как Агата громко ахнула."
        aga "О нет! Смотри, Роуэн!"
        "Она указала на нижние области демона, и Роуэн проследил за ее пальцем."
        "Преякулят сочился из Даззаната болезненно возбужденным членом, наполняя комнату тяжелым ароматом спермы."
        "Демон застонал от неудобства и подался вперед, заставляя белые капли падать на пол перед ними обоими."
        aga "Но мы уже осушили его сегодня! Солансия защити нас, мы не можем позволить ему вернуть свою власть!"
        "Не сводя глаз с органа перед собой, Роуэн знал, что одному из них придется встать на колени перед Даззанатом, чтобы помешать ему сбежать."
        " В конце концов, это был единственный выход. …"

menu:
    "Роуэн опускается на колени.":
        $ released_fix_rollback()
        $ rowanGaySex += 1
        scene black with fade
        show rowan necklace happy behind black
        "Роуэн почувствовал, как у него потекли слюнки. Голова его кружилась от пьянящего запаха члена Даззаната"
        "и он зачарованно наблюдал, как головка члена соблазнительно покачивалась перед ним."
        "Не говоря ни слова, Роуэн опустился на колени перед инкубом."
        "Как и преподобная Агата, сидевшая рядом с ним-либо чтобы посоветовать ему, как правильно дренировать, либо просто чтобы понаблюдать."
        "Роуэн была благодарен ей за это, но в этом не было необходимости. Он был героем и всегда доводил дело до конца. Даже минет."
        "Пока он наслаждался открывшимся перед ним зрелищем, Агата протянула руку и запустила пальцы под член Даззаната."
        "Она зачерпнула вытекшую сперму и поднесла к губам."
        "Она жадно слизнула ее и удовлетворенно вздохнула с выражением полного восторга на лице."
        aga "Ах ... такая вкусная…"
        "Тем временем Роуэн все еще не мог оторвать глаз от налитой кровью головки, ее тяжелый аромат атаковал его чувства."
        "Его разум тяжелел, а тело онемело."
        "Ему становилось все труднее сосредоточиться на чем-либо, кроме растущей потребности внутри него."
        scene cg201 with fade
        pause 3
        "Он медленно наклонился вперед и открыл рот. Бедра инкуба подались вперед, и кончик прошел сквозь его губы."
        "Соленый преякулят покрыл его горло, и он начал исследовать пульсирующий инструмент с благоговением, проводя языком по нему со страстью."
        "Всякий раз, когда он ударяла его по щекам, это оставляло приятное, ошеломляющее ощущение, которое шло прямо в голову."
        "До сих пор, он все еще слышал тихий голос в затылке, который продолжал настаивать, что что-то не так, но он замолчал в тот момент, когда он попробовал член демона."
        "Теперь Роуэн мог сосредоточиться на том, чтобы как следует осушить захваченного инкуба."
        aga "Глубже. Вы должны принять его глубже."
        "Аббатиса подталкивала его со стороны, и Роуэну не нужно было повторять дважды."
        "Он наклонился, взял его наполовину, наслаждаясь его формой и теплом. Мало-помалу он продвинулся вперед, булькая, когда кончик ударил его в горло, а затем дальше, глубоко внутрь."
        aga "Теперь медленно двигай головой... ты должен подвести его к краю, прежде чем мы сможем осушить его."
        "Аббатиса вцепилась в его руку, возбужденно шепча ему на ухо, ее горячее дыхание касалось его шеи."
        "Он слышал голод в ее голосе, и по какой-то причине, он чувствовал радость от того, что он был тем, кто заботился об истощении Даззаната, а не о она."
        "Приподнявшись, он начал медленно скользить губами вверх и вниз по члену демона, его челюсть расслабилась, когда он почувствовал, как все больше преякулята просачивается ему в рот."
        "Быстро он начал набирать темп, не обращая особого внимания ни на что, кроме вкуса члена и его спермы."
        "Мягкая рука коснулась его руки,  и аббатиса подняла его пальцы, направляя их к яичкам инкуба."
        aga "Теперь помассируй их нежно ... мы должны полностью осушить его."
        "Его пальцы коснулись тяжелых шаров, восхищаясь их весом. Источник силы инкуба. Они должны были опустошить их."
        "Он должен был опустошить их."
        aga "Почти приехали! Пей ее! Ты должен это выпить! Это единственный способ запечатать темную силу!"
        "Он услышал ворчание инкуба."
        "Тихий голос в его голове внезапно проснулся снова, крича ему не делать этого."
        "Может быть, пить испорченную, вкусную сперму Даззаната не такая хорошая идея..."

        menu:
            "Пить ее.":
                $ released_fix_rollback()
                scene black with fade
                show rowan necklace aroused behind black
                "Теплая, густая сперма ударила ему в горло. Жадно, Роуэн начал пить все это, теплое, приятное ощущение распространилось через его тело."
                ro "Ах ... Ах…"
                "Ошеломленный, он упал на спину. Туман, который накрыл его разум, начал рассеиваться, заменяясь на жжение внутри его желудка."
                "Он поднял глаза."
                "И увидел, что член инкуба все еще тверд и истекает спермой."
                aga "О нет, он еще не осушен!"
                "Запаниковав, аббатиса быстро встала перед демоном, чтобы исполнить свой долг свалки спермы инкубуса."
                aga "Кончи... Кончи....."
                "Ее возбужденный шепот заполнил комнату, и серьезность ситуации внезапно смыла с Роуэна онемение его мыслей и тела."
                "Он быстро схватил аббатису за руку и больно дернул ее назад."
                "Он увидел, как ее глаза вспыхнули гневом, разъяренные тем, что ей отказали в угощении."
                ro "Преподобная мать, мы не можем оба тратить день на его осушение. Я позабочусь обо всем, пока ты будешь читать священные тексты."
                ro "Может быть, вы найдете то, что поможет нам."
                "Он не мог рисковать тем, что она будет рядом, когда он столкнется с инкубом."
                "Аббатиса пыталась протестовать, пыталась сопротивляться, но когда она повернулась, чтобы посмотреть на инкуба, ее тело внезапно потеряло всю свою энергию."
                "Она кивнула в знак согласия, несколько ошеломленная, и поспешно вышла из комнаты, оставив их наедине."
                "Роуэн встал. Он положил руку на меч и повернулся к инкубу, стараясь не смотреть ему в глаза."
                #gain corruption
                $ change_base_stat('c', 2)
                #set flag: Dazzanath cum dump.
                $ event_tmp['Dazzanath cum dump'] = True
                jump confrontDaz

            "Не пить.":
                $ released_fix_rollback()
                scene black with fade
                show rowan necklace concerned behind black
                ro "(Нет!)"
                "Запаниковав, Роуэн вдруг откинулся назад, выпуская член изо рта."
                "Он сильно пульсировал и выпустил в воздух струю густой спермы, часть которой приземлилась на штаны Роуэна."
                "Аббатиса в ужасе ахнула и мгновенно заняла место Роуэна, послушно выпив всю испорченную сперму."
                "Роуэн мог только смотреть, как некогда чистая монахиня громко сглотнула, задыхаясь от спермы."
                "Через полминуты она отпустила его член, с восторженным выражением на лице, все еще наслаждаясь его вкусом."
                aga "Ах ... Ах.... Это ... это должно сработать."
                "Она счастливо вздохнула. Роуэн бросила быстрый взгляд на член демона."
                "Он все еще был возбужден и все еще истекал спермой."
                aga "О Нет, нет, нет, нет!"
                "В панике, она обернулась и посмотрела на героя."
                aga "Роуэн, мы должны что-то сделать!"
                "Ее перепуганное, запачканное спермой лицо наконец-то вывело героя из ступора."
                "Он заставил себя подняться, чувствуя легкую дрожь в коленях, но туман, окутавший его мысли, быстро рассеялся."
                "Он положил одну руку на меч, а другой потянулся к Аббатисе. Он схватил ее за руку и дернул назад, подальше от члена Даззаната."
                ro "Матушка, вам следует обратиться к священным текстам. Может быть, вы найдете то, что поможет нам."
                "Он не хотел, чтобы она была рядом, когда он столкнется с инкубом."
                "Аббатиса попыталась возразить, но, повернувшись к инкубу, внезапно потеряла решимость и кивнула в знак согласия."
                "Она поспешно вышла из комнаты, оставив их наедине."
                jump confrontDaz

    "Роуэн наблюдает, как Агата опускается на колени.":
        $ released_fix_rollback()
        scene cg212 with fade
        show rowan necklace happy behind cg212
        pause 3
        "Роуэн почувствовал, как у него потекли слюнки."
        "Голова его кружилась от пьянящего запаха члена Даззаната, и он зачарованно наблюдал, как его луковичная головка соблазнительно покачивалась перед ним."
        ro "(… Держись... Роуэн…)"
        "Нахмурив брови, Роуэн не мог отделаться от мысли, что что-то... что-то тут не так."
        'Внутренныий голос продолжал говорить "вырвайся", но он не мог сказать, почему он должен был" вырваться", или из чего именно он должен был вырваться.'
        "Стоя неподвижно, он пытался разобраться в своих мыслях, когда член сукуба дернулся снова."
        "Настоятельница испустил испуганный вздох."
        aga "О нет, становится только хуже!"
        "Она быстро опустилась на колени перед инкубом."
        "Роуэн чувствовал, что должен остановить ее, но не мог понять почему, и в данный момент его тело не слушалось."
        "Он мог только смотреть, как аббатиса открыла рот и медленно обхватила инструмент инкуба губами. Она впустила его медленно, с большим почтением."
        "Но как только ей удалось протолкнуть его полностью, она быстро ускорилась, работая глоткой с большим темпом."
        "Это было ... неправильно, видеть, как женщина веры показывает такую большую практику в сосании члена, но опять же, Роуэн не мог сказать, почему именно."
        aga "Мммм-МММ! .... Мммм!"
        "Инкуб оставался почти неподвижным, слегка покачивая бедрами в такт движениям настоятельницы."
        "Роуэн все еще чувствовал на себе его взгляд, но не мог заставить себя поднять глаза."
        "Смотря на аббатиссу, он чувствовал, что разрывается между отвращением и ревностью, постепенно осознавая, что из этих двух только одно было по-настоящему его..."
        "Внезапно Агата остановилась."
        "Она соскользнула с члена, держа губы сомкнутыми вокруг кончика, и нежно потянулась, чтобы помассировать яйца демона."
        "Даззанат хмыкнул и, выдвинув бедра вперед, вошел прямо ей в рот. Щеки Агаты раздулись, и, громко сглотнув, преподобная мать начала высасывать семя из демона."
        aga "МММ - ах, ах, ах…"
        "Наконец, она отпустила его член с восторженным выражением на лице. Она села, тяжело дыша, не сводя глаз с пульсирующего инструмента перед собой."
        "… Который начал выделять сперму еще раз."
        aga "О нет."
        "В панике, она обернулась и посмотрела на героя."
        aga "Роуэн, мы должны что-то сделать!"
        "Ее перепуганное, запачканное спермой лицо, наконец, вывело Роуэна из ступора."
        "Он положил одну руку на меч, а другой потянулся к Аббатисе. Он схватил ее за руку и дернул обратно."
        ro "Матушка, вам следует обратиться к священным текстам. Может быть, вы найдете то, что поможет нам."
        "Он не хотел, чтобы она была рядом, когда он столкнется с инкубом."
        "Аббатиса попыталась возразить, но, повернувшись к инкубу, внезапно потеряла решимость и кивнула в знак согласия."
        "Она поспешно вышла из комнаты, оставив их наедине."
        jump confrontDaz

    "(Борись с этим, черт возьми!)":
        $ released_fix_rollback()
        scene black with fade
        show rowan necklace neutral behind black
        "Роуэн почувствовал, как у него потекли слюнки. Голова его кружилась от пьянящего запаха члена Даззаната, и он ничего не мог с собой поделать. -"
        ro "(НЕТ!)"
        "Заставив себя действовать, Роуэн ударил себя по лицу, и последовавшая за этим жгучая боль вывела его из оцепенения."
        "Цепи даззаната опасно звякнули. Роуэн поднес руку к мечу и покачал головой, стараясь не смотреть Инкубу в глаза."
        "Ещё нет. Только не в присутствии преподобной Агаты."
        aga "Роуэн? Все в порядке?"
        ro "Да, Преподобная Агата. Не могли бы вы оставить нас наедине?"
        aga "Я не должена..."
        "Инкуб повернулся к Аббатисе. В одно мгновение ее решимость покинула ее, и она послушно кивнула."
        aga "… Если вы чувствуете, что это лучше всего."
        "Аббатиса поспешно вышла из комнаты, оставив их наедине."

label confrontDaz:

scene black with fade
show rowan necklace neutral behind black
show dazzanath neutral behind black

ro "…"
ro "Полагаю, вы говорите, не так ли?"

daz "Mhmmmm, да."

"У инкуба был приятный, почти бархатистый голос."

daz "С Агатой всегда так ... весело. И чем тебе это помешало?"

"Он тихо усмехнулся."
"Цепи, удерживающие его руки, снова загремели и на этот раз отскочили от стены, когда демон снял фальшивое звено, которое должно было быть там с самого начала."

daz 'Так и было... Любопытно. Агата должна была держать свой "священный поиск " в секрете от всех... и все же она привела тебя сюда.'
daz 'Она должна высоко ценить тебя, “Роуэн".'

"Не часто Роуэн встречал демонов, незнакомых с его прошлым. Даззанат был редким исключением. "

daz "B означает, что мы двое, скорее всего, будем драться ... …"

"Инкуб призывно развел руками."

daz "Слушай ... тот факт, что ты до сих пор не ударил меня этой заостренной штукой, заставляет меня думать, что, есть какая-то... общая почва для нас..."
daz "Чего ты хочешь, Роуэн? Это женщины? В аббатстве их немало, вам просто нужно дать мне немного времени, чтобы закончить мою работу. Мужчины? Можно и их."
daz "Это власть? Вы будете удивлены, какие отвары можно приготовить с помощью инкубационных жидкостей..."
daz "Богатства? Сокровища? У меня целая коллекция... Ты можешь выбрать что-нибудь для себя..."

"Инкуб продолжал, проходя через обычную процедуру 'искушения', с которой Роуэн постепенно знакомился."
"Но им действительно нужно было найти общий язык. Роуэн чего-то хотел от инкуба, хотя это было совсем не то, о чем говорил Даззанат."

#if corruption is higher than 49
if avatar.corruption > 49:
    "Его работа над аббатством впечатляла. Если бы не Роуэн, он, вероятно, преуспел бы в преобразовании его полностью, и никто никогда не заметил бы ничего плохого."
    "Его метод проникновения был не обычным, и тот факт, что он смог осуществить его, говорил о большом мастерстве."
    "Близнецы могли бы извлечь выгоду, если бы Даззанат согласился шпионить для них."
else:
#else
    "Инкуб вызывал у него отвращение, этого нельзя было отрицать."
    "Это было не возможно видеть, как слуги Солансии превращаются в простых рабов его члена."
    "Он также не мог отрицать, что Даззанат обладал мастерством, что делало его превосходным шпионом на службе близнецов."

#rejoin
"Его метод контроля, в частности, также будет очень полезен."
"Роуэн привык к тому, что демоны пытаются сломить его волю грубой силой, но воля Даззаната, была не такой прямой, и поэтому гораздо менее эффективна, и была менее агрессивной."
"И в определенном смысле это было гораздо опаснее."

menu:
    "Завербовать его в качестве шпиона.":
        $ released_fix_rollback()
        "Роуэн сделал знак Инкубу остановиться и наконец убрал руку с меча."
        ro "Достаточно. Ничто из этого меня не интересует."
        ro "Но в конце концов мы можем прийти к соглашению.…"
        jump recruitDaz

    "Убить его.":
        $ released_fix_rollback()
        "… Слишком опасно."
        #if Dazzarath cumpdum is true
        if event_tmp['Dazzanath cum dump']:
            "… Но значит ли это, что Роуэн должен убить его?"
            "Герой заколебался, воспоминание о члене Даззаната во рту и сперме в животе, еще свежо в его памяти."
            "Его взгляд скользнул по полностью выпрямленному инструменту инкуба. Демон, продолжая говорить, обратил на это внимание."
            daz "Или ты хочешь ... меня?"
            "… Близнецы найдут его навыки полезными... А с Даззанатом в замке Бладмин Роуэн сможет присматривать за ним, чтобы убедиться, что он не замышляет ничего дурного..."
            "Конечно, это был вариант, который стоит рассмотреть, не так ли?"
            "Прежде чем Роуэн успел хорошенько обдумать эту идею, он понял, что уже согласен."
            ro "В конце концов, мы можем прийти к соглашению.…"
            jump recruitDaz
        else:
        #else
            "Не давая инкубу времени среагировать, Роуэн прыгнул вперед, размахивая мечом."
            "Его разум мгновенно замедлился, когда демон попытался остановить его с помощью магии."
            "Слишком мало, слишком поздно."
            "Преодолевая ментальное доминирование, Роуэн добрался до демона и вонзил меч ему в грудь. Без доспехов, без собственного клинка, без возможности увернуться, судьба Инкуба была решена на месте."
            daz "… Ни за что… "
            "Демон кашлял кровью, отчаянно цепляясь за Роуэна, но силы уже покидали его."
            daz "… Такая глупая смерть…"
            "Роун отодвинул инкуб от него, вытаскивая свой клинок. Демон безжизненно упал на землю."
            "..."
            #abbey
            scene black with fade
            "Потребовалось некоторое время, чтобы объяснить Аббатисе все."
            "Сначала она была в истерике, и на мгновение Роуэн испугался, что вся эта ситуация приведет к насилию, но в конце концов, он смог уговорить послушников."
            "Многие все еще не могли поверить, что демон обманул их, но со временем разрушительное влияние покинет их тела, и они примут правду."
            'На данный момент Роуэн предпочитал не задерживаться, на случай, если кто-то из них решит совершить кровавую месть за смерть "пленника".'
            "Он ускользнул из Аббатства под покровом ночи, зная, что провел в нем гораздо больше времени, чем следовало."
            "По крайней мере сейчас он мог, успокоиться, зная, что его жители будут в безопасности."
            #gain solansia favour
            $ change_favor('solancia', 1)
            #Lose: corruption, guilt, X’zaratl influence (Only a small amount. Dazzanath is more of a passing acquaintance, so she's not to shaken by his loss, and has no proof it was Rowan who slew him. )
            $ change_base_stat('c', -2)
            $ change_base_stat('g', -2)
            $ change_relation('xzaratl', -1)
            return

label recruitDaz:

"В последующие минуты Роуэн кратко объяснил, кто его хозяева и какова их цель."
"Хотя Даззанат оказался ужасно неосведомленным в вопросах демонической политики, он слышал о близнецах и, казалось, был заинтригован идеей работать с Изерой."
"Ее стиль, по крайней мере, интересовал его, потому что цель была достигнута..."

daz "При всем уважении, Роуэн Блэкуэлл, но я нахожу мировое господство... "
daz "О, это ужасное клише! Это забавный способ убить время, не поймите меня неправильно, но я не могу представить, чтобы это было что-то, кроме огромной головной боли."

"Роуэн удержался, чтобы не закатить глаза, и решил промолчать."
"Если отбросить личные взгляды, Даззанат в конце концов согласился присоединиться к ним... У него была только одна маленькая просьба.…"
"Что Ровена позволила ему остаться в аббатстве, чтобы закончить свою работу. Это займет у него не больше месяца."
"Взамен он приведет 'обращенное'' духовенство с собой в замок Бладмин. Конечно, Роуэн найдет применение дюжине голодных рабов."

menu:
    "Соглашаться.":
        $ released_fix_rollback()
        "Аббатство было уже потеряно. Однко из этого что-то еще можно извлечь."
        "Даззанат будет волен делать все, что ему заблагорассудится."
        "Он прикрепил Инкуба к стене и отправился на поиски преподобной Агаты. Он застал ее за изучением текстов, как и велел."
        "Пытаясь избежать ее доверчивых глаз, он сказал ей, что будет искать способ убить Инкуба, и что сейчас они должны истощать и ослаблять его, как и раньше."
        aga "Спасибо, Роуэн! Мы так и сделаем. Теперь наша судьба в ваших руках."
        "Так оно и было. И ее уже было не изменить."
        "Агата настояла, чтобы он остался на ужин, но Роуэн отказалась."
        "Он и так слишком много времени проводил в аббатстве. Он уехал в тот же день."
        #Gain: Guilt. In 4 weeks: Launch event “Dazzanath arrives” (see below)
        $ activate_event('dazzanath_arrives')
        $ set_event_timer('dazzanath_arrives', 'after_abbey', 4)
        $ change_base_stat('g', 2)
        #Lose: -Solansia favour.
        $ change_favor('solancia', -1)
        return

    "Отказываться. Он уезжает с тобой.":
        $ released_fix_rollback()
        "То ли потому, что они нуждались в его услугах сейчас, то ли потому, что Роуэн считал предосудительной идею позволить Даззанату делать с аббатством все, что ему заблагорассудится,"
        "инкубу сообщили, что они немедленно покинут Аббатство."
        "Роуэн нашел Агату изучающие священные тексты, как он просил ее."
        "Он солгал, что знает кого-то, кто мог бы помочь, и они не могли рисковать, ожидая его прибытия в аббатство."
        "Вместо этого он возьмет инкуба и лично сопроводит его до места назначения."
        "Сначала аббатиса воспротивилась этой идее, влияние сукуба заставляло ее искать все более и более странный предлог, чтобы помешать ему покинуть аббатство."
        "Но Роуэн был неумолим. После более чем два часа уговоров, преподобная Агата, наконец, сдалась."
        "В тот же день Инкуба заковали в цепи и отпустили на попечение Роуэна."
        "Вскоре они покинули Аббатство. Агата настояла, чтобы герой остался на ужин, но Роуэн отказался."
        "Все равно он слишком много времени провел в аббатстве."
        #Gain: Dazzanath as a spy (see below). Solansia Favour.
        $ change_favor('solancia', 1)
        $ recruitDaz = True
        python:
            event_tmp['_spy'] = Spy('m')
            event_tmp['_spy'].name = 'Dazzanath'
            event_tmp['_spy'].traits = ['Master', 'Submissive Seducer', 'Methodical']
            event_tmp['_spy'].sprite = 'incubus3'
            castle.spies.append(event_tmp['_spy'].uid)
        return

#Dazzanath as a spy
#Image: Incubus 3
#Traits: Master, Submissive Seducer, Methodical.


label dazzanath_arrives:
#dazzanath arrives event
#//Quick message after 4 weeks when Dazzanath arrives at the castle. Occurs at end of week, in addition to any other events.
"Роуэну сообщили, что Даззанат прибыл в замок, пока его не было, и привел с собой обещанных рабов."
"В тюрьму добавилось духовенство с промытыми мозгами, и инкуб теперь ждал его приказаний."
#Gain: Dazzanath as a spy, +12 slaves.
$ change_prisoners(12)
$ recruitDaz = True
python:
    event_tmp['_spy'] = Spy('m')
    event_tmp['_spy'].name = 'Dazzanath'
    event_tmp['_spy'].traits = ['Master', 'Submissive Seducer', 'Methodical']
    event_tmp['_spy'].sprite = 'incubus3'
    castle.spies.append(event_tmp['_spy'].uid)
return
