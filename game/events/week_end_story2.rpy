# unsorted events for end of week
# story events, goals-related events
init python:
    #### Fail your second mission (end of twenty-second week) ####
    # Always triggers week 22 immediately after exploration finishes if you haven't yet conquered Raeve keep or have fallen short of the required quota.
    # TODO check for real goal2
    # TODO check if Raeve Keep is conquered in the event's body
    event('goal2_fail', triggers='week_end', conditions=('week==22', 'not goal2_completed'), run_count=1, priority=pr_story)
    #### Succeed your second mission (end of twenty-second week) ####
    # Always triggers week 22 immediately after exploration finishes if you've conquered Raeve keep and have met the quotas.
    # TODO check for real goal2
    event('goal2_success', triggers='week_end', conditions=('week==22', 'goal2_completed'), run_count=1, priority=pr_story)
    event('goal_3_warning', triggers='week_end', conditions=('week==52', (('goblinRecruit == False' and 'orciad_state != 2') or 'rastFirstVisit == True')), run_count=1, priority=pr_story)

label goal2_fail:
#### Fail your second mission (end of twenty-second week) ####
# Always triggers week 22 immediately after exploration finishes if you haven't yet conquered Raeve keep or have fallen short of the required quota.

scene black with flash
show bg10 with fade
show rowan necklace neutral at midleft with dissolve
show jezera displeased at midright with dissolve
show andras angry at edgeright with dissolve

je "Оказалось, ты не тот человек, о котором я думала, Роуэн."

# TODO check if Raeve Keep is conquered in the event's body
#If Raeve keep has not been conquered.
if goal2_completed:
    an "Чего ты ожидала, сестра? Все твои планы рушатся, почему этот должен сработать?"
    #jez angry
    je "О, а ты значит не причем? У нас даже не было бы никакой армии, если бы послушались тебя!"
    ro "Послушайте, задачи, которые вы мне поставили, были ..."
else:
    #If Raeve keep was conquered.
    je "Несмотря на то, что ты захватил крепость Рейв, как было запрошено, ты не смог должным образом управлять нашими финансами и расширением, чтобы выполнить требуемую квоту."
    je "Это приведет к недопустимой задержке моих планов."
    show andras happy at edgeright with dissolve
    an "Каких планов? Единственный твой успех, это захват долины Яэль! Ты провалилась хуже, чем Роуэн."
    #jez angry
    je "Не спорь со мной, брат. Помнишь, что мы в этом вместе, не так ли мы договорились?"
    ro "Слушайте, не было разумно предполагать, что после нападения у нас все еще будет ..."
    show andras angry at edgeright with dissolve

#re-merge
an "Молчать!"

show bg10 with flash

"Яркая вспышка энергии окружила Роуэна, и огромная волна боли пронзила его."

show rowan necklace shock at midleft with dissolve

ro "Грррххх!"

an "Вступает в силу немедленно, ты утратил свой пост в Бладмине. Мы должны решить, что будем делать дальше, но без тебя."

je "Я полагаю, что, мы должны найти тебе замену? Возможно, в качестве награды за то, что мы делаем то, что нам нужно?"

#if Alexia slept with Jezera, but not Andras.
if jezNTR1 and not andras_alexia_sex:
    je "Я заберу твою жену. Возможно, я смогу сделать ее более полезной, чем ты. По крайней мере, ее таланты не будут потрачены на то, чтобы быть домохозяйкой."
#else
else:
    an "Я согласен на Алексию, этот вкусная рыжая скоро станет моей личной шлюхой."
    an "Я бы не сказал, что она стоит того года, который мы потратили на тебя, но, по крайней мере, я кое-что из этого получу."

show orc soldier neutral at edgeleft with moveinleft

"Несколько орков-солдат вышли вперёд, когда их демонический мастер дал сигнал."

an "Верните эту неудачника в его камеру. Мы решим, что с ним делать позже."

hide orc soldier with moveoutleft
hide rowan with moveoutleft

scene bg8 with fade

"Игра окончена - Вечный пленник."
jump gameend

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label goal2_success:
#### Succeed your second mission (end of twenty-second week) ####
# Always triggers week 22 immediately after exploration finishes if you've conquered Raeve keep and have met the quotas.

scene black with flash
show bg10 with fade
show rowan necklace neutral at midleft with dissolve
show jezera happy at midright with dissolve
show andras displeased behind bg10

je "Поздравляю. Долина Яэля - наша, и квота была выполнена. Еще раз, ты выполнил то, о чем тебя просили."

an "Кажется, он единственный, на кого мы можем положиться."

hide andras
show andras displeased at edgeright with moveinright

an "Разве это не верно, сестра?"

show jezera displeased at midright with dissolve

je "Почему ты сейчас об этом говоришь, брат?"

an "Потому что наш лучший слуга должен знать, больше."

show jezera neutral at midright with dissolve

"Демонесса глубоко вздохнула и повернулась к Роуэну."

je "Мой герой, в Тундре и Элоаене я потерпела неудачу."
je "Я надеялась заключить там альянс, чтобы помочь нам в совместном нападении на Протею с базы в Розарии, которую ты для нас построил, но не получилось."

show rowan necklace shock at midleft with dissolve

ro "Ты думала взять священный город с тем, что я смог построить за последние несколько месяцев?!"

show jezera displeased at midright with dissolve

je "Не в одиночку! Темные эльфы и людоеды тоже присоединились бы, по крайней мере, если бы выслушали меня......"

show andras angry at edgeright with dissolve
show rowan necklace neutral at midleft with dissolve

ro "Зачем им помогать тебе? Помогите мне Солансия, даже если бы они присоединились, у них не было бы шансов против святых армий с Денарой во главе!"

"В комнате наступило молчание. Роуэн смотрел на своих хозяев, опасаясь, что вышел за пределы дозволеного и вот-вот будет наказан."

show andras happy at edgeright with dissolve

"Потом Андрас улыбнулся."

an "Ну, Изера, похоже, что твой план был обречен с самого начала."

#jez angry
je "Заткнись, это не твой план завербовать героя Карста!"

an "Правда, Роуэн оказался нашим самым ценным активом. Однако теперь мы будем делать все по-моему."
an "Вместо того, чтобы одним ударом вырвать сердце шести царств, мы построим себе армию, которая будет уничтожать их по очереди, пока не останется только Протея."
an "Как думаешь, это более практичный план, Роуэн?"

show rowan necklace shock at midleft with dissolve

ro "Потребуются годы, чтобы построить достаточно сильную армию, чтобы напрямую заняться шестью королевствами!"

an "Но ты признаешь, что это можно сделать."

"Роуэн колебался, кусаясь губы в страхе."

an "Ты видишь сестра? Наш самый способный слуга согласен со мной."

show rowan necklace neutral at midleft with dissolve
show jezera neutral at midright with dissolve

"Изера посмотрела на героя на мгновение, ее глаза пронзили Роуэна, как если бы он мог дать ей все ответы, которые были нужны."

an "У нас с тобой много планов, которые нам нужно будет пересмотреть. Слуга, мы обсудим это и проинформируем тебя позже."

hide andras with moveoutright
hide jezera with moveoutright

"Красный демон подмигнул своей сестре, которая посмотрела на него, прежде чем следовать за ним, оставив Роуэна одного."
"Он потрогал свой амулет и застонал от разочарования."

ro "(Эти двое ... черт их побери.)"

# go to third mission
# TODO maybe some preparation for goal3
#~ return
jump goal3_start


################################################################################


label goal3_start:
#### Third mission ####
# Cla-Min and Skordred argue about titles
# goal3 is given in "new society" event

scene bg14 with fade
show clamin neutral at center with dissolve
show skordred neutral at cliohnaright with dissolve

cla "Эй, Скордри, как думаешь, скоро мы получим титулы за наши услуги? Я хочу сказать своим детям, что они все дворяне."

sk "Глупая девочка, повелители Бладмина всегда шли по пути, указанному нашим повелителем Харосом; закону силы."
sk "Мы не имеем  наследственных титулов, как эти шесть царств."
sk "Каждый должен доказать, что он заслуживает своего положения и потерять его, если другие проявять себя лучше."

show clamin angry at center with dissolve
cla "Чушь собачья! Я заслуживаю душевного спокойствия, зная, что моя работа обеспечила моим детям и их детям место в новом обществе, которое мы строим!"

show skordred angry at cliohnaright with dissolve
sk "Чтобы здесь правяли те, кто родился от правильного человека, потому что какое-то поколение назад его предки водили сюда караваны или кому-то герою просто повезло?"
sk "Нет, этого никогда не будет."

show alexia 2 necklace neutral at edgeleft with moveinleft
al "О чем вы вдвоем спорите?"

cla "Эта серая кожа, говорит мне, что близнецы никому не дадут титулы, даже тем, кто их заслужил!"

sk "Почему ты думаешь, только о титулах? Ты должна гордится честью служить хозяевам!"

al "Эй, эй, успокойтесь сейчас же. Не надо кричать, мы все здесь взрослые люди."

"Алексия выставила руки, чтобы удержать двух маленьких людей от дальнейших действий."

al "(Лучше бы я этого не говорила....)"
al "Почему бы не спросить об этом Андраса и Изеру, а не спорить о том, что они могут сказать? Я видела их в тронном зале всего минуту назад."

show clamin happy at center with dissolve

cla "Отличное предложение, спасибо, Алексия. Ты идешь, Скордри?"

sk "Говорю тебе, мы просто теряем время....."

hide skordred with moveoutright
hide clamin with moveoutright
show alexia 2 necklace concerned at edgeleft with dissolve

al "Фу. (Я рада, что это не закончилось дракой.  Я никогда раньше не видела, чтобы эти двое злились друг на друга.)"

if raeve_keep_rowan_claimed_helayna:
    show rowan necklace neutral at midright with moveinright
    
    ro "Привет, Алексия."
    
    "Его жена не ответила. Думая, что, ему стоит уйти, Роуэн решил продолжить путь по коридору."

    show alexia 2 necklace neutral at edgeleft with dissolve

    al "Дорогой, подожди."
    
    show rowan necklace shock at midright with dissolve
    
    al "Я... Я хотела сказать, что сожалею о том, как я вела себя раньше."
    al "Со всем, что здесь происходит, было неправильно с моей стороны так злиться из-за Хелайны."
    
    show rowan necklace neutral at midright with dissolve
    
    ro "Алексия... Ты действительно уверена в этом?"
    
    al "В эти дни ты находишься в таком стрессе, постоянно оглядываясь через плечо, с этими двумя демонами, мучающими твой разум и тело."
    al "И винить тебя, когда у тебя нет другого способа защитить кого-то, кроме как нарушить клятву, данную мне."

    show alexia 2 necklace concerned at edgeleft with dissolve
    
    al "Я знаю, что ты чувствуешь. Я сама сказала, что мы должны сделать все, что в наших силах, если мы хотим выжить в этом месте. По крайней мере, сейчас я могу снять один груз с твоих плеч."

    show alexia 2 necklace happy at edgeleft with dissolve

    al "Я просто хочу сказать, я прощаю тебя за то, что ты взял Хелайну.  Возможно, что смогу когда-нибудь делить комнату с ней, но я буду ее не замечать"
    
    show rowan necklace happy at midright with dissolve
    
    ro "Я... Спасибо, Алексия."
    
    "Они завернули друг друга в теплые объятия, которые оттолкнули боль."
    "Хотя не было никаких сомнений в том, что Хелайна все еще будет напрягать их, однако это не разрушит узы, которые у Алексии и Роуэна были между собой."
    
    show rowan necklace neutral at midright with dissolve
    
    ro "Жаль, что я только что узнал о других проблемах в замке, которые омрачают наше будущее."

else:

    show alexia 2 necklace neutral at edgeleft with dissolve
    show rowan necklace neutral at midright with moveinright

    al "О, дорогой, рада снова тебя видеть. Как прошла неделя?"

    ro "Хорошо... Хорошо...."

show alexia 2 necklace concerned at edgeleft with dissolve

al "Что то случилось, пока тебя не было?"

ro "Дело не в этом, а в том, что случилось после моего возвращения, и сейчас это меня беспокоит. Я только, что видел трещины в планах близнецов."
ro "Большая часть того, что Изера, планировала, провалилась, теперь  Андрас придумывает планы, как покорять каждое из царств одно за другим."
ro "И походу я буду в центре этих планов."

show rowan necklace happy at midright with dissolve

ro "ОБ этом мне нужно будет беспокоиться позже, эти ублюдки немного поссорились друг с другом, и я сомневаюсь, что они в настроении слушать то, что я хотел бы им сказать."

show alexia 2 necklace shocked at edgeleft with dissolve

al "О, Боже."

show rowan necklace neutral at midright with dissolve

ro "Что?"

al "Я только что отправила Кла-Мину и Скордреда к близнецам за решинием их спора."

ro "О чем они спорили?"

al "Должны ли близнецы давать Кла-Мин дворянское звание за ее службу, Скордред размышлял о том, как правители Бладмина не делают такого рода вещи."
al "Извини, я не слышала всего."

ro "Ну, это звучит как проблема. Я лучше пойду посмотрю, что происходит."

hide rowan with moveoutright

# go to new society: feudalism vs might
#~ return
jump new_society

################################################################################

label new_society:
#### New society: feudalism vs might ####

scene bg6 with fade
#skor angry
show skordred neutral at edgeright with dissolve
show clamin neutral at midright with dissolve
show jezera neutral at midleft with dissolve
show andras happy at edgeleft with dissolve
show rowan necklace neutral behind bg6

an "Скордред, Скордред, ты понимаешь иронию, ты исповедуешь ценность традиции путей Бога Хаоса?"
sk "Я исповедую пути, которые ведут к успеху!  Феодальные пути шести королеств приведут к их полному поражению в последней войне!"

ro "(Я видел достаточно генералов знати, чтобы знать, что это правда. Компетентные среди них, счастье.)"

cla "Но подумайте, насколько лояльными и трудолюбивыми будут ваши люди, если они знают, что у них есть шанс стать новой знатью!"
cla "Как вы думаете, почему Шесть Царств практикуют такую форму феодализма?"

ro "(Потому, что так сказала Солансия. Все знают свое место, все знают мир, все знают счастье.)"

show jezera happy at midleft with dissolve
je "О Роуэн, я вижу тебя."

hide skordred with moveoutright
hide clamin with moveoutright
hide rowan
show rowan necklace neutral at midright with moveinright

je "Не могли бы ты поговорить с нами наедине? Хотелось бы, чтобы ты внест свой вклад в возникшую проблему."

ro "(Твою мать)... конечно."

hide andras with moveoutleft
hide jezera with moveoutleft
hide rowan with moveoutleft

scene bg18 with fade
show andras displeased at edgeleft with dissolve
show jezera neutral at midleft with dissolve
show rowan necklace neutral at midright with dissolve

je "Что ж?"

an "Я думаю, что мы должны принять решение о нашей политике в будущем."
an "Как указал Роуэн, пройдут годы, прежде чем Шесть Царств станут нашими, и мы не можем откладывать это на неопределенное время."

je "На самом деле я пока не спрашивала твоего мнения, брат."

ro "Нет, он прав. Если вы решите его, он только посеет семена сомнения среди ваших последователей."
ro "Я уже сталкивался с этим раньше, и худшее, что ты можешь сделать, это абсолютно ничего."

show andras happy at edgeleft with dissolve

an "Видишь? Сейчас не время для полумер или расплывчатых обещаний. Пришло время объявить себя императорами!"

show rowan necklace shock at midright with dissolve
#jez shock
je "Андрас?! Ты серьезно?"

an "Сестра, ты сама сказала, что мы ничего не должны Богу многих масок. Это наш новый мир, мы можем делать в нем все, что захотим."

je "Что насчет тех, кто тянется к темноте? Для них мы даем способ освободиться от цепей этого мира. Ты хочешь упустить это преимущество?"

ro "Это про солдат. Будут ли они так охотно следовать за демоном, который придерживается феодализма?"

show andras displeased at edgeleft with dissolve
an "Ах, да, орки. Они и так уже идут по пути силы. Я не подумал об этом. Правильно ли Скордред сказал, что они против этого?"

## show jezera neutral at midleft with dissolve
show rowan necklace neutral at midright with dissolve

"Роуэн колебался, понимая всю серьезность ситуации и то, как повлияют его слова на ближайшие месяцы, годы, возможно даже столетия, если близнецы смогут добиться успеха."

ro "(Подожди, я вполне могу решить, какое общество близнецы построят в своем новом мире. Мне нужно очень тщательно подходить к выбору слов.)"

menu:
    "Наследственный феодализм.":
        $ released_fix_rollback()
        jump .heriditaryFuedalism

    "Путь силы.":
        $ released_fix_rollback()
        jump .mightMakesRight

################################################################################

#### Supported hereditary feudalism ####
label .heriditaryFuedalism:

ro "Он был прав. Сила пути Хароса раздавила бы армию Розарии, если бы такие люди, как я, не вступились за власть. Я был не единственным простолюдином, который воскрес, когда знать бежала."
ro "Они ненавидели нас, называли нас 'грязными генералами', но мы изменили ход войны."
ro "Однако царство, управляемое силой, вряд ли можно назвать царством. Ты хочешь мира, процветания, порядка? Тогда вы должны следовать законам, установленным Солансией."
ro "Может быть, сила это и правильно, но ты будешь править трупом, пока Харос будет триумфально смеяться."

"Две полудемона наблюдали за Роуэном в тишине в течение долгого времени, взвешивая его слова. В конце концов, Изера заговорила."

je "Понятно. Ты свободен, Роуэн."

"Человек посмотрел на них, затем кивнул и вышел из комнаты."

scene bg6 with fade

"Почти час спустя близнецы позвали всех своих слуг в тронный зал, чтобы сделать объявление."

#CG of Jezera and Andras standing at the throne.
show andras happy behind bg6
show jezera happy behind bg6

"Они оба стояли перед троном и осматривали своих последователей."
"Роуэн был впервых рядах небольшой толпы с Алексеей рядом, держась за ее руку. Хотя Алексия не знала, почему ее муж так волновался, она знала, что ему нужна ее поддержка прямо сейчас."

an "Последователи, милые слуги, моя сестра и я позвали вас сюда сегодня вечером, чтобы сделать объявление."

je "Да, нам стало известно, что некоторые из вас надеются получить признание за оказанные нам услуги. Действительно, этот вопрос вызвал определенные разногласия среди вас."

an "Поэтому мы собрали всех вас здесь, чтобы изложить нашу волю, чтобы больше не было ложных убеждений. Таким образом, вы будете знать, как наказать тех, кто неверит своими хозяевами."

je "Мы стремимся не только сделать мир нашим, и это наше изначальное право, но и переделать его так, как считаем нужным.... даже если это противоречит воле нашего отца."

"Роуэн с трудом поверил в то, что он слышит, они ведь не могли на самом деле сделать это, не так ли?"

an "Знаю, что сегодня, знаменует собой начало новой эры, начало правления императора Андраса..."

je "и императрице Изере! Те, кто заслужит нашу благосклонность, получат место в нашей иерархии, место, которое перейдет к их детям и детям их детей."

#Crowd talking sound effect

"Среди собравшихся раздался взрыв шума.  Одни были шокированы, другие - в восторге, третьи - в недоумении."

show bg6 with flash
#crash sfx

"Но близнецы громко потребовали тишины, заставив замолчать своих подопечных."

an "Роуэн Блэквелл, шаг вперед."

"Роуэн посмотрел на Алексию и получил обнадеживающее сжатие, прежде чем отпустить ее руку и сделал этот шаг."
"Все глаза следили за каждым его движением, в то время как его сердце грозилось вырватся из груди."

an "На колени."

#Show CG of Rowan kneeling and being knighted.

"Он сделал это."

je "За последние несколько месяцев Ты верно служили нам. Прежде всего, ты достиг наибольшего, вернув Бладмин на мировую арену."
je "Поэтому ты будешь первым, кто получит нашу благосклонность."

"Демоница достала меч и встала перед стоящим на коленях человеком. Она положила кончик лезвия на одно плечо, подняла его над головой и коснулась другого плеча."

je "Я, Императрица Изера из Бладмина, считаю тебя рыцарем и защитником нашей новой империи. Кроме этого, я даю тебе титул лорда и все связанные с этим обязанности и привилегии."
je "Начиная с этого дня, твоя семья всегда будет иметь это право."

an "Восстань, сэр Роуэн, встань как чемпион Бладмина!"

#Title name changes
$ jezera_name = 'Императрица Изера'
$ andras_name = 'Император Андрас'
$ rowan_name = 'Сэр Роуэн'
$ alexia_name = 'Леди Алексия'
$ cliohna_name = 'Примат Клиона'
$ cla_min_name = 'Леди Кла-Мин.'
$ skordred_name = 'Лорд Скордред'

scene bg14 with fade

"Через пятнадцать минут после того, как близнецы выступили с заявлением, и после того, как были вручены еще несколько титулов..."

show skordred neutral at skorright with dissolve
show rowan necklace neutral at midleft with dissolve

sk "Не могу в это поверить!  Как хозяева могут пойти таким путем?"

show jezera happy at edgeleft with moveinleft

je "Потому что мы хозяева, Скордред. Ты сомневаешься в нашей мудрости?"

sk "Нет, конечно нет, я имел в виду, что..."

je "Tы либо принимаешь наш выбор нового пути, который будет выкован Бладмином, либо тебя заменят."

sk "Я, я принимаю."

je "Хорошо."

hide skordred with moveoutright

"Теперь, когда карликовый архитектор ушел, Роуэн задался вопросом, зачем Изера его искала."
"Однако она  обратилась не к нему, а посмотрела мимо него и махнула кому-то стоящему за ним."

je "О, леди Клэ-Мин? Могу я с тобой поговорить?"

"Роуэн начал уходить, только Изера помахала ему рукой, чтобы он остался."

show clamin happy at midright with moveinright

"Очень счастливая гоблинская женщина подскочила к ним обоим."

cla "Да, Изера? Я могу чем-нибудь помочь?"

je "Я вижу, благородство тебе идет. Вообще-то, я подумала, что расскажу тебе маленький секрет. Видишь ли, наш друг...."

"Она указала Роуэна."

je "... помог убедить меня согласиться с тем, что нам нужно немного изменить политику на будущее."
je "Если ты хочешь поблагодарить кого-нибудь за обретенный титул, то Роуэн - твой человек."

hide jezera with moveoutleft

"Каким-то образом, улыбка гоблинши стала еще больше, и она заключила в объятия Роуэна, чтобы поцеловать его. Мужчина оглянулся вокруг в поисках помощи, но обнаружил, что Изера уже ушла."

scene bg9 with fade
show rowan necklace neutral at midright with dissolve
show alexia 2 necklace neutral at midleft with dissolve

"После того, как ему, наконец, удалось сбежать от в какой-то степени очень возбужденого гоблина, Роуэн вернулся в свою комнату, где к нему вскоре присоединилась Алексия."
"Они сидели в тишине несколько минут, обдумывая в покое события, которые только что произошли."

al "Роуэн, тебя это устраивает? Я знаю, что ты сказал в конце войны, что-то изменилось?"

"Поначалу Роуэн не ответил, но его мысли вернулись к тем месяцам, когда Карнас был побежден. Он родился крестьянином, охотником в Артдейле и гражданином Розарии."
"Однако он стал героем и сыграл важную роль в победе."
"Это было проблемой, поскольку он представлял угрозу для установленного порядка. Многие люди уважали его гораздо больше, чем знать, многие солдаты, которых он возглавлял, сделали бы все для него."
"Однако Роуэн вернулся домой. Он решил жениться и прожить жизнь, которой жил бы, если бы не было войны."
"Деанара сказал ему, что он был истинным последователем Солансии за это, но теперь он стал дворянином демонов."
"Как же ему отнестись к такой чести?"

menu:
    "Радоваться наконец-то быть вознагражденным.":
        $ released_fix_rollback()
        ro "Я никогда не был полностью согласен с возвращением в Артдейл после окончания войны. Моей единственной наградой за все, что я делал, был мир."
        ro "В течение многих лет меня называли «грязным генералом», и одни дворяне боялись, что я использую свою силу против них, другие надеялись использовать меня в своих собственных играх."
        ro "Неважно, что я был героем, вакжно было только то, кем я родился."
        ro "Я всегда тайно надеялся, что когда-нибудь стану благородным, но я не хотел, чтобы это произошло так."
        show alexia 2 necklace happy at midleft with dissolve
        "Алексия улыбнулась и положила руку на щеку Роуэну."
        al "Ты никогда мне об этом не говорил. Часть тебя похоронена глубоко в себе."
        "Она долго смотрела на своего мужа."
        al "Если бы ты стал благородным, вернулся бы ко мне?"
        "Вместо того, чтобы ответить, когда-то простой мужчина поцеловал ее."
        #Gain a bit of corruption for this choice.
        $ change_base_stat('c', 4)

    "Смешанные чувства.":
        $ released_fix_rollback()
        ro "Я незнаю.  С одной стороны, должен признать, что часть меня всегда хотела, чтобы меня должным образом вознаграждали за службу в Шести королествах."
        ro "С другой стороны, я знаю, что мир и стабильность обеспечиваются благодаря сохранению статуса."
        show alexia 2 necklace happy at midleft with dissolve
        "Алексия улыбнулась и положила руку на щеку Роуэну."
        al "Мы не боги, мы все совершаем ошибки."
        "Когда-то простой мужчина обнял женщину и положил голову ей на плечи."
        #no effect

    "Хотелось бы, чтобы они никогда не давали мне титул.":
        $ released_fix_rollback()
        ro "Хотел бы я, чтобы они никогда не давали мне этот титул. Я никогда не хотел ничего, кроме той жизни, ради которой я родился."
        ro "Они не могут вернуть меня к ней, после того, как публично объявили меня своим первым благородным членом."
        show alexia 2 necklace happy at midleft with dissolve
        "Алексия улыбнулась и положила руку на щеку Роуэну."
        al "Ты все тот же человек, в которого я влюбилась. Несмотря на то, что наша жизнь не такая, как раньше, твое сердце остается таким же."
        "Когда-то простой мужчина посмотрел на женщину рядом с ним и положил руку ей на плечо, крепко прижав ее к себе, чувствуя ее тепло."
        #Lose a bit of corruption for this choice.
        $ change_base_stat('c', -4)

# The twin's government style is set to feudalism.
$ society_type = 'feudalism'
#Rowan earns a point of favor with Solancia.  Rowan gains a significant relationship boost with Cla-Min.
$ change_relation('cla_min', 20)
$ change_favor('solancia', 1)
jump .thirdMission

################################################################################

#Supported might makes right
label .mightMakesRight:

ro "Благородные, по большому счету, не могли сравниться с войсками Хароса.  Они знали только мир, и это была случайность, когда хоть один из них был дееспособен, как генерал."
ro "Такие люди, как я, простолюдины, вынуждены были стать  великими, мы теми, кто развернул ход войны."
ro "Несмотря на это, дворянство все равно стремилось уничтожить таких компетентных генералов."
ro "Они называли нас 'грязными генералами', распространяли ложь о наших намерениях, подрывали наши усилия."
ro "Это было невероятно глупо, они были настолько втянуты в то, что могло случиться после победы, что чуть не проиграли саму войну."
ro "Мы слабы и это не вопрос. Если ты хочешь победить шесть королевств, ты не должен попасть в ту же ловушку, что и они."

"Две полудемона наблюдали за Роуэном в тишине в течение долгого времени, взвешивая его слова. В конце концов, Изера заговорила."

je "Понятно. Ты свободен, Роуэн."

"Человек посмотрел на них, затем кивнул и вышел из комнаты."

scene bg6 with fade

"Почти час спустя близнецы позвали всех своих слуг в тронный зал, чтобы сделать объявление."

#CG of Jezera and Andras standing at the throne.
show andras happy behind bg6
show jezera happy behind bg6

"Роуэн был впервых рядах небольшой толпы с Алексеей рядом, держась за ее руку. Хотя Алексия не знала, почему ее муж так волновался, она знала, что ему нужна ее поддержка прямо сейчас."

an "Последователи, слуги, моя сестра и я позвали вас сюда сегодня вечером, чтобы сделать объявление."

je "Нам стало известно, что некоторые из вас надеются получить признание за оказанные нам услуги. И этот вопрос вызвал определенные разногласия среди рядовых членов."

an "Поэтому мы собрали всех вас здесь, чтобы изложить нашу волю, чтобы больше не было ложных убеждений. Таким образом, вы будете знать, что ждет,тех кто пойдет за нами."

je "Мы находимся в замке Бладмин, традиционном доме чемпионов Хароса, в том числе и нашего отца Карнаса."

an "Эти лорды всегда шли по одному пути! Мы ничем не отличаемся от них, если вы хотите власти, вы должны доказать, что вы этого заслуживаете, и взять эту власть! Рабы - единственное исключение."

je "Каждый будет иметь свои справедливые шансы, независимо от расы, пола, рождения. Помните, что вы не получите особого отношения только потому, что ваш отец умел указать людям с палками куда идти."

an "Этот замок, нет, этот мир будет нашим, потому что мы можем сделать его своим. Вы все можете получить власть, которую вы можете взять. Это ваше право по рождению. На этом все."

scene bg14 with fade

"Через пятнадцать минут после того, как близнецы сделали свое заявление ...."

show rowan necklace neutral at midleft with dissolve

show clamin sad at midright with dissolve

cla "Вот... дерьмо. Походу, я должна была узнать больше об этом. Я так надеялась, что смогу найти хорошее место для своей семьи здесь."

show jezera happy at edgeleft with moveinleft

je "Не бойся, у тебя есть свое место здесь, и я обещаю, что у твоих детей будет шанс занять любое место. Это то, что вы никогда не имели раньше."

cla "Это правда. Но теперь мне придется заняться их обучением и образованием."

"Она поклонилась Роуэну и Изере."

cla "Добого вечера вам двоим."

hide clamin with moveoutright

je "Секунду, мой герой. Не мог ли бы ты пойти со мной, пожалуйста?"

scene bg6 with fade
show skordred neutral at skorright
show rowan necklace neutral at midleft with moveinleft
show jezera neutral at edgeleft with moveinleft

je "А, Скордред, я хочу с тобой поговорить."

sk "Да, госпожа?"

je "Наше решение сегодня вечером было не столь уверенным, как ты думашь."
je "Мой брат хотел провозгласить себя императором, да и я думала, что в наших интересах присвоить титулы нашим фаворитам."

#skor shock
"Глаза Скордреда широко открылись в шоке."

sk "Что?"

je "Да, именно Роуэн убедил нас в нашей ошибке и в том, что мы должны идти по пути Хароса."

"Глаза карлика выросли еще больше."

je "Это все."

#skor neutral
sk "Похоже, я ошибался насчет тебя. Я ... извини за мое поведение за последние несколько недель."

ro "В чем ты был неправ? Я был непосредственно причастен к убийству твоего бывшего хозяина."

show skordred happy at skorright

sk "Да, но я знаю, чтоты сейчас - настоящий поборник хаоса, заслуживающий моего уважения и преданности."
sk "Я не буду зацикливатся на том, что ты сделал."

"Впервые карлик поклонился в почтении Роуэну, оставив человека ошеломленным этим внезапным изменением в отношении."

scene bg9 with fade
show rowan necklace neutral at midright with dissolve
show alexia 2 necklace neutral at midleft with dissolve

"После разговора со Скордредом Роуэн вернулся в свою комнату, где к нему вскоре присоединилась Алексия."
"Они сидели в тишине несколько минут, обдумывая в покое события, которые только что произошли."

al "Роуэн, что случилось? Я могу сказать, что это объявление беспокоит тебя, но я не знаю почему."
al "Похоже, это то, что ты должен был ожидать от близнецов. Или ты знаешь, чего я не знаю?"

"Роуэн молчал, его мысли крутились вокруг, что могло бы быть и что будет."
"Он вспомнил, какими были завоеванные войсками карнасом земли, жестокость, которую он видел, и что именно он развязал."

ro "Близнецы, походу не собирались идти путем силы. Они были на грани провозглашения себя императорами и превращения  своих фаворитов в феодалов."
ro "Тогда я убедил их в обратном. "

show alexia 2 necklace concerned at midleft with dissolve
al "Что? Почему, во имя Соленсии, ты сделал это?"

menu:
    "Надеялся, что они пойдут другим путем.":
        $ released_fix_rollback()
        ro "Я просто хотел убедиться, чтоб они не выберут феодализм. Я надеялся, что они пойдут по третьему пути, и нарушат закон Хароса, но, что сделано, то сделано."
        ro "Алексия, я видел, как Шесть царств чуть не сгорели дотла из-за привилегированного рождения. Я не мог с чистой совестью поддержать продолжение этих ошибок."
        show alexia 2 necklace happy at midleft with dissolve
        "Алексия улыбнулась и положила руку на щеку Роуэну."
        al "Ты никогда мне об этом не говорил. Часть тебя похоронена глубоко в себе."
        "Она долго смотрела на своего мужа."
        al "Спасибо, что был честен со мной."
        #Lose a bit of corruption for this choice.
        $ change_base_stat('c', -4)

    "Иначе мы не выиграем войну.":
        $ released_fix_rollback()
        ro "Наши жизни связаны с судьбами близнецов. Если они потерпят неудачу, мы умрем."
        ro "Как бы ужасно это ни было, я не смог упустить преимущество, предоставляемое законом Хароса."
        "Алексия улыбнулась и положила руку на щеку Роуэну."
        al "Мы можем делать только то, что можем."
        "Роуэн обнял ее, черпая утешение в тепле ее тела."
        #No effect

    "Часть меня согласна с философией 'Правда на стороне сильного'.":
        $ released_fix_rollback()
        ro "Как бы страшно ни было признать, часть меня на самом деле согласна с философией закона Хароса."
        ro "Почему люди, которые лучше всех подходят для этой работы, не должны получить эту работу? Почему человек должен быть привилегированным из-за своего рождения?"
        "Алексия улыбнулась и положила руку на щеку Роуэну."
        al "Это ради стабильности, чтобы земля могла расти и не была постоянно раздираема войной и борьбой тех, кто стремится погубить свой собственный мир."
        "The hero let out a his own long sigh and pressed his hand into her's, drawing some comfort from the warmth of her touch."
        ro "Я знаю, но было так обидно знать, что у меня нет возможности изменить ситуацию ни для себя, ни для моих детей."
        show alexia 2 necklace happy at midleft with dissolve
        "На мгновение сердце женщины вздрогнуло, но потом она улыбнулась."
        al "Ты никогда мне об этом не говорил. Часть тебя похоронена глубоко в себе."
        "Она долго смотрела на своего мужа."
        al "Спасибо, что был честен со мной."
        #Gain a bit of corruption for this choice.
        $ change_base_stat('c', 4)

# The twin's government style is set to might makes right.
$ society_type = 'might'
# Rowan earns a point of favor with Kharos. Rowan gains a significant relationship boost with Skordred.
$ change_relation('skordred', 20)
$ change_favor('kharos', 1)
$ codex_add('dirt_general_starting')
jump .thirdMission

################################################################################

#Receive your third mission
label .thirdMission:

scene black with fade

"На следующий день."

# TODO
#Once music is being made for the game again, this is the point where the second castle theme should start being used and where it should be played for the first time.

scene bg6 with fade
show andras displeased at edgeright with dissolve
show jezera happy at midright with dissolve
show rowan necklace neutral at midleft with dissolve

je "Доброе утро, мой герой. Вчера было много событий, не так ли?"

ro "Иначе и не скажешь"

an "Вероятно, это не будет последним из таких поворотных моментов как в наших планах, так и в судьбе."
an "Однако то, что произошло, осталось в прошлом. Мы должны смотреть в настоящее и твое будущее."

je "Мы долго обсуждали ситуацию и договорились, что это будет долгая кампания, которая, несомненно, закончится войной."
je "Возможно, мы сможем захватить одно из шести царств без кровопролития, а возможно и два, но другие предпримут меры, как только почуствуют нашу руку."

an "У меня есть сомнения насчет этого, но вопрос спорный."
an "Независимо от всего, нам нужна огромная армия, если мы надеемся бросить вызов любому из царств, которые будут сопротивляться. Особенно легионам Протеи."

"Роуэн с интересом посмотрел на красного демона."

ro "О?"

show andras happy at edgeright with dissolve

an "Клиона рассказала мне о военном вкладе каждого королевства во время последней войны, это было.... позновательно."

ro "(Старый Андрас никогда бы не стал проводить такого рода исследования. Он учится.)"

show andras displeased at edgeright with dissolve

je "Пока мой брат был занят изучением наших врагов, я посмотрела на наши собственные возможности."
je "Как мы и подозревали, наши нынешние перспективы найма не смогут удовлетворить наши потребности."
je "Этих маленьких отрядов будет недостаточно. Нам нужно, чтобы ты нашел настоящую армию, около тысячи солдат или более."
je "Это трудно в Розарии, но гоблины Блэкхолта в последнее время доставляют немало неудобств для дворян."
je "Совокупных сил их племен должно быть достаточно, чтобы удовлетворить первый этап нового плана моего брата."
je "Возможно, тебе удастся добиться успеха там, где мне не удалось и заключить союз с другой расой хаоса."


an "Согласен. Ты должен сделать это к концу года, вместе с большим количеством средств и сил, готовых к дальнейшим завоеваниям."
an "Такие территориальные завоевания тем более важны, чем больше мы берем из одной области, тем слабее она будет, когда придет время ее завоевывать."

je "Если вы случайно найдете другую достаточно большую армию и доведете ее до нашего pfvrf, это также выполнить твою миссию."

"Изера сделала паузу, у нее на кончике языка было что-то другое."

je "Есть еще один вопрос, который я хочу рассмотреть. Герцог Рэйв скоро отправится в Растедель. Он станет ценной еденицей в моих руках столичных союзников."

ro "У тебя есть союзники в столице?"

"Растедель был столицей Розарии. Это имело смысл, что Изера имела некоторое влияние среди самых коррумпированных дворян при дворе."
"Но как глубоко они проникли в благородное общество?"

je "Да. Но я хочу, чтобы ты сам поехал в столицу."
je "Появление великого героя несомненно вызовет ажиотаж, и мне очень хочется посмотреть, как дворянство отреагирует на твое присутствие."

je "Просто посетите Растедель, когда будет возможность. Герцог будет готов, чтобы присоединиться к тебе."

"Роуэн медленно кивнул. Растедель. Это место разрушенных мечтаний."

je "Раз уж это решено...."

if (society_type == 'feudalism'):
    je "Мы зарезервируем часть казначейства для нашего военного корпуса, которая будет использоваться только в случае возникновения чрезвычайных ситуаций."
    je "А также я повышаю твою зарплату, соответствующую новому званию."

else:
    je "Мы зарезервируем часть казначейства для нашего военного корпуса, которая будет использоваться только в случае возникновения чрезвычайных ситуаций."
    je "А также я повышаю твою зарплату, в награду за твою работу."

an "Это все."

jump .jobsIntro

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################

#Alexia wants to help and placeable NPC tutorial

label .jobsIntro:

scene bg14 with fade
show rowan necklace neutral at midleft with dissolve
show alexia 2 necklace neutral behind bg14

al "О, вот ты где Роуэн."

hide alexia
show alexia 2 necklace neutral at midright with moveinright
show rowan necklace happy at midleft with dissolve

ro "Да, Алексия?"

show alexia 2 necklace concerned at midright with dissolve
al "За последние месяцы у меня было ощущения, что я бесполезена. Я познакомилась с персоналом и присматривала за тобой, но я могу сделать больше."

show rowan necklace neutral at midleft with dissolve

al "Поскольку замок так недоукомплектован персоналом, я подумала, что было бы неплохо помочь."

"Она дотронулась до амулета на своей шее, это было знакомо Роуэну."

al "В этот момент наши судьбы связаны с близнецами."

ro "Где именно, ты думаешь помогать?"

al "Я могу помочь персоналу с уборкой и приготовлением пищи, я могла бы помочь Клионе в библиотеке или даже работать в подземелье, если дойдет до этого."
al "Есть много мест, куда можно пойти."
al "Поскольку ты управляешь делами замка, я подумала, что было бы неплохо, если бы ты отправил меня туда, где, по твоему мнению, я нужна и я сделаю все, что смогу."

ro "Ты в этом уверена?"

al "Да Я могу быть домохозяйкой, но это не значит, что я не могу помочь другим. Я готова сделать то, что нужно ..."

al "...Я лишь прошу, чтобы ты не посылал меня к чудовищам внизу, по возможности."

show rowan necklace shock at midleft with dissolve

ro "Alexia...."

scene black with fade

"Теперь вы можете еженедельно назначать Алексии работу, чтобы получать бонусы."
"Она обладает собственными навыками, которые делают ее более пригодной для выполнения некоторых работ."
"На Алексию влияет ее опыт и люди, с которыми она работает, в течение этого времени."
"Демонстрация ее работы будет предоставляться каждую неделю, что может дать вам возможность сделать выбор и иметь представление о том, что происходит с ней."

return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################

#goal 3 warning

label goal_3_warning:

scene bg6 with fade
show rowan necklace neutral at midleft with dissolve
show jezera happy behind bg6

je "Мой герой."

hide jezera
show jezera happy at midright with dissolve

"Роуэн готовился к работе на выезде, когда его посетил нежданный гость."

je "Я хотела сказать тебе кое-что перед твоим отъездом."
je "Я дала тебе несколько целей некоторое время назад, и я не могла не заметить, что они еще не выполнены."

if goblinRecruit == False and orciad_state != 2:
    je "Я попросила привести к альянсу к одну из рас Хаоса. Орков или гоблинов."
    je "Но она еще не достигнута. Это не та ситуация, которую можно отложить."
    if rastFirstVisit == True:
        je "Кроме того, тебе еще предстоит побывать в Растедейле. Бедная столица вашего народа так одинока, без тебя."
    
elif rastFirstVisit == True:
    je "Тебе еще предстоит побывать в Растедейле. Бедная столица вашего народа так одинока, без тебя."
    
#If insufficient money and troops have been collected. (TODO)
# je "Let us not forget the matter of our troop levies and the coppers needed to keep them in the field. It would not due if we failed to be able to put a force of our own in the field. Not due at all."
                                                                                                                                        
je "Часы тикают герой. Не забудь этого."

"Роуэн тихо кивнул."

ro "Я работаю над этим."

je "Тебе лучше сделать это. Я ненавижу разочаровываться."

"Изера ушла прочь, оставив Роуэн думать. Ее тон был недосказанностью, но смысл был ясен. Спеши."

return
    

