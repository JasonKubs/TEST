# random map events that happen on forests
init python:

    # Hunter's Trap
    event('hunter_s_trap', triggers='map_expl', conditions=('world.cur_tile_type == "forest"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Fairy spring
    #Requires Rowan not be very corrupt.
    event('fairy_spring', triggers='map_expl', conditions=('world.cur_tile_type == "forest"', 'avatar.corruption <= 6'), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Drider attack
    event('drider_attack', triggers='map_expl', conditions=('world.cur_tile_type == "forest"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # The Dead Knight
    event('dead_knight', triggers='map_expl', conditions=('world.cur_tile_type == "forest"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #Necromancer's lair (malediction)
    #No requirements.
    event('necromancer_s_lair', triggers='map_expl', conditions=('world.cur_tile_type == "forest"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #Forest's blessing (benediction)
    #No requirements.
    event('forest_s_blessing', triggers='map_expl', conditions=('world.cur_tile_type == "forest"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #The Trapped Dryad
    event('the_trapped_dryad', triggers='map_expl', conditions=('world.cur_tile_type == "forest"',), run_count=1, group='map_expl', priority=pr_map_rnd)


label hunter_s_trap:
# Hunter's Trap

scene forest1
"Даже выживальщики калибра Роуэна иногда оказываются застигнутыми врасплох. То ли из-за минутной беспечности, то ли просто из-за невезения."
"Независимо от причины, Роуэн обнаружил, что у него на ноге висит старый медвежий капкан."
"Ему потребовалось почти десять минут, чтобы освободиться, но он не смог продержаться больше часа из-за боли от очень неприятных порезов, украшавших его голень."
"К счастью, ничего не было сломано, и не было никаких тяжелых повреждений."
"Как только он вернется в замок, он сможет получить магическое исцеление, но в то же время ему придется делать все намного медленнее и возится с повязками."
#lose two movement points, 1d6 + 1 dexterity damage injury until the end of the week.
$ add_effect(Injury('Wounded shin', 'reflexes', -dice(6)-1))
$ avatar.mp -= 2
#gain 10 xp
$ add_exp(10)
#end event
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label fairy_spring:
# Fairy spring
#Requires Rowan not be very corrupt.

scene forest1
"В большинстве из шести миров есть небольшие очаги силы Соленсии, вокруг которых обычно собираются существа, обладающие энергией или силой света."
"Некоторые из этих Нексусов стали сердцами городов, другие в конечном итоге порождают большие рифы в океанах..."
"... а в этом случае образовали центр очаровательной лесной поляны с волшебным источником в центре."
"Роуэн сделал большой глоток чистой воды и наполнил ею свои фляги, прежде чем отправиться в путь.  Он уже почувствовал омолаживающий эффект, проходящий через его тело."
"Изеру не очень-то интересовал его доклад на эту тему, и она велела больше не беспокоить ее по поводу этого."
#remove all injuries from Rowan
$ heal_injuries()
#gain 10xp
$ add_exp(10)
#end event
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label drider_attack:
# Drider attack

# Show forrest background.
scene forest1
$temp = current_weapon()
"Внезапно на Роуэна упало пятно черного, белого и насекомоподобного меха!"
"Он инстинктивно отпрянул назад, выхватывая свой меч [temp] чтобы встретиться с нападавшим лицом к лицу. Врагом был драйдер, получеловек монстр-паук."
"Эти монстры были уроженцами лесов Розарии и одними из самых опасных его обитателей."
"Тот, кто сейчас нападал на Роуэна, самец, очевидно, намеревался превратить его в еду, если представится такая возможность."

# Fight challenge: total failure (should only happen when Rowan is very injured).
if avatar.wounds > avatar.base_mp * .5:
    "Атаки существа были интенсивными и жестокими, заставляя героя отступать снова и снова."
    "При обычных обстоятельствах Роуэн никогда бы так не позволил, но травмы доконали его."
    "После того, как его укусили во второй раз и обнаружив, что его силы почти полностью исчезают, Роуэн принял решение вернуться в замок Бладмин."
    "Он послал сообщение Изере с просьбой немедленно отозвать его."
    "Роуэн исчез, оставив очень расстроенного драйдера позади."
    "Его раны были настолько серьезны, что он был вынужден отдыхать всю оставшуюся неделю, чтобы снова встать на ноги."
    # End scene, add 5 xp return Rowan to previous space, current week of exploration ends, Rowan gains a strength injury.
    $ add_exp(5)
    $ push_to_previous_tile()
    $ add_effect(Injury('Drider bites', 'strength', -4))
    $ renpy.set_return_stack(renpy.get_return_stack()[:1])
    return

$ res_roll = check_combat(15)[1]
$ released_fix_rollback()
if res_roll < 7:
    # Fight challenge: partial failure.
    "Это был не его день, он боролся с существом, по сути, удар за ударом."
    "К сожалению, удары противника смогли отравить его, поэтому Роуэн становился все слабее, но борьба продолжалась."
    "К счастью, ему удалось заставить существо отступить в последней отчаянной атаке. Он никак не мог догнать драйдера в лесу, так что он вернулся назад."
    "К сожалению, яд не мог покинуть тело внезапно, даже если у вас была возможность отдохнуть несколько минут. Это еще одна проблема, добавленная к куче."
    # End scene, add 5 xp Rowan gains a strength injury.
    $ add_exp(5)
    $ add_effect(Injury('Drider poison', 'strength', -2))
    return
elif res_roll >= 15 and (castle.buildings['pit'].free_space >= 1):
    # Fight challenge: total success (req: breeding pit with open space).
    # Rowan ends up capturing the drider and adds it to the breeding pit.
    "Однако борьба быстро обернулась против драйдера.  Этот зверь был большим, сильным и самоуверенным."
    "Он явно считал Роуэна легкой добычей, что позволило герою в короткие сроки обратить против нее его силу."
    "Конечно, всегда существовала вероятность, что дела у Роуэна очень быстро пойдут плохо, если он оступится.  Яд драйдера покалечил бы его, если бы сумел укусить."
    "Хорошо еще, что Роуэн не облажался."
    "Ему удалось заставить врезаться драйдера в дерево, а затем сбросить его с холма, где он приземлился ошеломленный."
    "Почувствовав возможность, Роуэн быстро спустился вниз по склону и использовал веревку, чтобы связать ноги существа, а затем послал сообщение, чтобы его забрали."
    "С полностью взрослым драйдером было бы труднее справиться, но это был приз для племенных ям, который нельзя было пропустить."
    "Как часто ты ловил живого драйдера, сам того не желая?"
    #End scene, gain 1 drider and extra exp.
    $ add_exp(15 + dice(10))
    $ castle.buildings['pit']._driders += 1
else: 
    # Fight challenge: partial success.
    "Борьба была напряженной, с каждым мгновением был шанс на жизнь или смерть."
    "Герой был немного лучше своего противника, хотя он знал, что промах быстро перевернет все из-за яда, которым обладало существо."
    "Наконец он нашел свой шанс,и ему удалось отрубить одну из ног драйдера."
    "Он взвизгнул и несколько мгновений неуверенно метался, дав Роуэну возможность последовать за ним и отрубить одну из его жвал."
    "Поняв, что его одолели, враг героя развернулся и убежал в лес. Роуэн знал, что не сможет поймать драйдера в лесу, даже если у него не будет ноги."
    "Поэтому он просто забрал свой приз и продолжил свой путь."
    "Если вы сможете найти алхимика или охотника на монстров, свежие ядовитые железы драйдера были очень ценны."
    #End scene, add 10 xp, get 25 gold and extra exp.
    $ add_exp(10 + dice(6))
    $ change_personal_gold(25)
    return
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label dead_knight:
    # The Dead Knight
    scene forest1
    $ released_block_rollback()
    "В глубине леса открылся на вид небольшую поляну. Роуэн обнаружил, прислонившись к стволу старого дуба, тело рыцаря, изрешеченное стрелами."
    "Он знал, что это дело рук не одного лучника, а целой шайки бандитов."
    "Против такого количества даже человек в доспехах не имел шансов. Многие стрелы нашли швы между его пластинами, которые плохо сидели."
    "Некоторые даже пробили его насквозь, и Роуэн подумал, что доспех, был дешевым и некачественным."
    'Он не знал, сколько времени пролежало тело, но, судя по резкому запаху разлагающейся плоти, прошло уже несколько дней.'
    'На доспехах виднелись следы зубов и когтей, свидетельствующие о том, что кто-то из крупных лесных падальщиков пытался полакомиться трупом, прежде чем сдаться.'
    'Что же касается убийц рыцаря, то Роуэн видел вокруг так же мало признаков человеческой жизни, как и во время прогулки по лесу большую часть дня.'
    'Снятие доспехов, чтобы обыскать рыцаря, потребует времени, оставляя его незащищенным. Ему придется хорошенько подумать, стоит ли рисковать, разграбив тело.'
    menu:
        'Обыскать рыцаря':
            $ released_fix_rollback()
            jump .dead_knight_search
        'Оставить тело':
            $ released_fix_rollback()
            "Решив, что это слишком опасно, и не желая встретить ту же судьбу, что и рыцарь, Роуэн решил не обыскивать тело."
            "'Пусть кто-нибудь другой рискнет', - подумал он, оставляя его позади, чтобы продолжить свое путешествие."
            $ add_exp(5)
            return

label .dead_knight_search:
        # gain 10xp
        $ add_exp(10)
        $ res_roll = dice(4)
        if res_roll == 1:
            $ released_fix_rollback()
            'Как и подозревал Роуэн, доспехи были сделаны дешево и могли принести больше помех в бою, чем защиты. Меч, однако, был сделан искусным мастером.'
            'Либо мертвый рыцарь потратил на него все свои деньги за счет остального снаряжения, либо это был подарок.'
            "Решив, что будет напрасно оставлять его здесь с трупом, который больше не сможет им пользоваться, Роуэн снял его с тела."
            " По крайней мере, это, принесет ему хороший доход от торговца."
            $ give_item('bastard_sword')
        elif res_roll == 2:
            $ released_fix_rollback()
            "Как и подозревал Роуэн, доспехи были сделаны дешево и могли принести больше помех в бою, чем защиты."
            "Лезвие меча было испорчено, вероятно, вступил в контакт с чем-то более твердым."
            'Шлем, однако, был сделан искусным мастером. Либо мертвый рыцарь потратил на него все свои деньги за счет остального снаряжения, либо это был подарок.'
            "Решив, что будет напрасно оставлять его здесь с трупом, который больше не сможет им пользоваться, Роуэн снял его с тела."
            " По крайней мере, это, принесет ему хороший доход от торговца."
            $ give_item('iron_sallet')
        elif res_roll == 3:
            $ released_fix_rollback()
            "Как и подозревал Роуэн, доспехи были сделаны дешево и могли принести больше помех в бою, чем защиты."
            "Лезвие меча было испорчено, вероятно, вступил в контакт с чем-то более твердым."
            "Перчатки, однако, были редкой находкой. Умело обработанные, пластины сцепились вместе, но были хорошо соединены, чтобы обеспечить гибкость."
            "Тонкие руны, выгравированные на краях пластины, также намекали на то, что перчатки были пропитаны каким-то заклинанием."
            'Поблагодарив сестер за удачу, Роуэн сунул перчатки в рюкзак.'
            $ give_item('gauntlets_of_might')
        elif res_roll == 4:
            $ released_fix_rollback()
            "Как и подозревал Роуэн, доспехи были сделаны дешево и могли принести больше помех в бою, чем защиты."
            "Лезвие меча было испорчено, вероятно, вступил в контакт с чем-то более твердым."
            'Однако, когда он снял сапог, оттуда, где он, должно быть, прятал его в случае ограбления вроде этого, выпал небольшой мешочек с монетами.'
            "Если тот, кто убил его, и обыскивал его, то недостаточно тщательно, что оказалось для Роуэна удачей."
            "Сунув монету в карман, он подумал, что день все-таки выдался неплохим."
            $ change_personal_gold(dice(20, 2) + 10)
        else:
            $ released_fix_rollback()
            "Как и подозревал Роуэн, доспехи были сделаны дешево и могли принести больше помех в бою, чем защиты."
            "Лезвие меча было испорчено, вероятно, вступил в контакт с чем-то более твердым."
            'Сняв с тела все доспехи, Роуэн ничего не мог с этим поделать. Если этот человек и скрывал что-то при себе, то его уже обчистил тот, кто его убил.'
            'Теперь он остался с потраченным впустую днем, довольно непривлекательным трупом перед ним и надеждой, что он сможет избавиться от запаха разложения как можно быстрее.'
            #lose MP
            $ avatar.mp -= 1
        return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label necromancer_s_lair:
#Necromancer's lair (malediction)
#No requirements.

#Show forest background.
scene bg forest1

"Пробираясь сквозь заросли, Роуэн заметил, что что-то не так.  Фоновые звуки становились тише, чем должны были быть, и он почувствовал в воздухе привкус магии."
"Теперь он был настороже, двигаясь как можно тише и незаметнее."
"По мере того как он продвигался дальше, запах гнили начал наполнять лес.  Пение птиц и жужжание полностью исчезли. Это было место смерти и разложения, признаки некромантии."
"Конечно же, вскоре после этого он столкнулся с зомби."
"Она не заметила его, но зато почуствовал хозяин, который пришел вскоре после этого.  Мужчина выглядел намного старше, чем был на самом деле, благодаря тому, что его темное искусство давило на тело."
"Только с помощью магических средств он смог почуствовать Роуэна  и то только благодаря тому, что защитил магией свой кусок леса."

#Stealth test (Move Silently skill): DC 12
if check_skill(12, 'move_silently')[0]:
    #Stealth test: Success.
    "Как ни старался некромант, ни кто-либо из его мертвых слуг, теперь уже четверых, они не смогли найти героя."
    "Роуэн направился к деревьям, бесшумно перескакивая с ветки на ветку и время от времени проносясь над землей, когда была возможность."
    "В конце концов ему удалось выбраться из этой части леса, и в этот момент его охотник прекратил погоню. Кто бы это ни был, он не любил гостей и, похоже, не хотел иметь ничего общего с остальным миром."
    #End scene.  20 xp.
    $ add_exp(20)
    return
else:
    #Stealth test: Failure.
    "Роуэн направился к деревьям, бесшумно перескакивая с ветки на ветку Он подошел к просвету между деревьями, через который должен был проскочить, и подождал, пока некромант и трое его слуг уйдут, прежде чем сделать свой ход."
    "Затем он столкнулся лицом к лицу с четвертым зомби и выругался. Он издал громкий скулящий стон как раз перед тем, как Роуэн смог отделить голову от тела. Слишком поздно, остальные были предупреждены."
    "Кости начали вырываться из земли, отрезая Роуэну путь к отступлению. Он повернулся лицом к своему охотнику и оставшимся трем зомби."

    #Willpower test, DC6 (roll a d20, then deduct one for every ten points of corruption)
    if dice(20) - avatar.corruption >= 6:
        #Willpower test: Success.
        "Темная магия обрушилась на Роуэна, наполняя его тело слабостью, а разум страхом.  Он отогнал эти чувства и бросился в бой. Еще один зомби упал сраженный одним ударом, но остальные стали сильнее, когда некромант понял, что его магия не сработала."
        "Вместо того, чтобы ослабить своего врага, пользователь магии переключил свои усилия на усиление своих приспешников. Они становились сильнее, жестче и, самое главное, быстрее."
        "После нескольких минут боя, который в основном зашел в тупик, Роуэну удалось прорваться мимо нежити и бежать в лес. Некромант прислал ему прощальный подарок, заряд некротической энергии, который сработал по площади героя сзади."
        "Он продолжал бежать через боль, которой, очевидно, была достаточно для его оппонента, так как он не преследовал его.  Кто бы это ни был, они, похоже, не хотели иметь ничего общего с внешним миром."
        #End scene.  Rowan is injured, 10 exp.
        $ add_exp(10)
        $ add_effect(Injury('Necrotic energy', 'strength', -2))
        return
    else:
        #Willpower test: Failure.
        "Невероятная волна первобытного страха наполнила Роуэна, заставляя его бежать. Однако в то же время его тело, казалось, ослабело и двигалось, как во сне."
        "Зомби набросились на него и Роуэн слишком поздно понял, что магические влияния, захватили его."
        "Только после получения нескольких синяков он наконец-то смог освободиться от магии некроманта, только для того, чтобы пользователь смог перейти к более прямому способу атаки."
        "Болт некромантической энергии ударил человека, оставив его почти беспомощным перед натиском нежити."
        "Чувствуя, что любая надежда на выживание в этом противостоянии быстро угасает, Роуэн схватил свой амулет и попросил вернуть его обратно в Бладмин."
        "Вскоре он потерял сознание, но проснулся от фиолетового лица с разными глазами, смотрящее на него с разочарованием. Кажется, его путешествие на неделю было закончено."
        #End scene.  Week instantly ends, tile remains unexplored, no exp.  Rowan is injured.
        $ add_effect(Injury('Necrotic energy', 'strength', -2))
        $ prevent_tile_exploration()
        jump end_map_exploration
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label forest_s_blessing:
#Forest's blessing (benediction)
#No requirements.

#Show forest background.
scene bg forest1

"В воздухе можно было услышать шутливое хихикание. Это звучало и дружелюбно и озорно, что наводило на мысль о том, что дух леса заинтересовался присутствием Роуэна."
"В течение следующих нескольких часов была сделана пара розыгрышей и сыграна игра. Герой шел дальше, зная, что это надо сделать, это событие не будет длиться долго."
"Если бы он этого не сделал, он мог бы получить врага, который ему не нужен."
"Даже с учетом этого он должен признать, что играть с таким чистым духом, как этот, было весело."
"В конце концов, существо показало себя. Пикси оказался маленьким человеком, размером примерно с руку. Они какое-то время держали смотрели друг на друга."

#Test corruption: Low/pure.
if avatar.corruption < 31:
    "Наконец дух улыбнулся и положил руку Роуэну на нос.  Произошла небольшая вспышка света, затем крошечная фигурка бросилась обратно в лес со смехом."
    "После этого бегства последовала радостная улыбка, которая стала более искренней, когда пришло осознание того, что досталось благословением."
    #End scene. 10xp  Rowan is blessed with better stats for three weeks.
    $ add_exp(10)
    $ add_effect(MultiEffect("Forest's blessing", 'pos', (('strength', 1), ('reflexes', 1), ('vitality', 1), ('intelligence', 1), ('luck', 1),), 3))
    return
#Test corruption: Medium.
elif avatar.corruption < 61:
    "Небольшое хмурое лицо духа исказилось. В Роуэне было что-то, что ему не понравилось."
    "Тем не менее, он пошумел вокруг него на мгновение, и небольшое тепло наполнило человека. Потом пикси взлетел на деревья."
    #End scene.  10xp Rowan is blessed with better stats for two weeks.
    $ add_exp(10)
    $ add_effect(MultiEffect("Forest's blessing", 'pos', (('strength', 1), ('reflexes', 1), ('vitality', 1), ('intelligence', 1), ('luck', 1),), 2))
    return
#Test corruption: High.
else:
    "Внезапно дух отшатнулся в ужасе, а затем улетел на максимальной скорости."
    "Он мгновенно растворился в деревьях, оставив позади удивленного Роуана. Раньше фей так на него не реагировали."
    #End scene. 5xp
    $ add_exp(5)
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label the_trapped_dryad:
#The Trapped Dryad

$ released_block_rollback()
scene bg3 with fade
show rowan necklace neutral behind bg3

$ dryadName = "Дриада"

"Для разнообразия, переход Роуэна через лес преподнес ему приятный сюрприз, вместо обычныго кустарника с опасностью."
"На поляне перед ним он увидел видение в зелено-коричневом цвете; пара длинных ног, покрытых корой и листьями, переходивщих в круглую задницу."
"Женщина, очевидно, лесной дух, был согнут пополам, почти горизонтально, и на спине у нее были раскиданы волосы, окрашенные во все оттенки осени."
"Она извивалась и трепеталась, но, как это ни парадоксально, казалось ему, оставалась на месте, как будто держалась там чем-то."
"Когда Роуэн восхищался видом, через поляну раздался гладкий, как шелк, голос."

dry "Алло? Там кто-то есть?"
dry "Я заперта здесь несколько часов назад, пожалуйста..... Пожалуйста, помогите мне..."

menu:
    "Постараться освободить ее.":
        $ released_fix_rollback()
        ro "Подожди, я иду."
        "Роуэн поспешил к женщине в опасности."
        "Когда он достиг дальнего края поляны, он обнаружил две вещи; Первая заключалась в том, что она была ошеломляющей спереди, как и сзади..."
        "... а вторая - в том, что вещь, удерживающая ее на месте, была довольно изощренной ловушкой."
        dry "Большое спасибо. Я не могу двигать руками."
        ro "Позвольте мне взглянуть и посмотреть, что я могу сделать. Кстати, меня зовут Роуэн."
        $ dryadName = "Фиби"
        dry "Фиби."
        ro "Приятно познакомиться Фиби. Мы вытащим тебя отсюда в мгновение ока."
        "Дриада одарила его нервной улыбкой, и герой принялся разглядывать ловушку под ней."
        "В руке она что-то сжимала, но что именно, он не мог разобрать в ее маленьком кулачке."
        "Он мог только предположить, что это была какая-то приманка, потому что прямо под ее руками была металлическая пластина."
        "К нему было прикреплено металлическое кольцо, которое, должно быть, подпрыгнуло, когда она взяла то, что было на нем, поймав ее руки в ловушку."
        ro "Я так понимаю, на этой пластине что-то было?"
        "Она кивнула и раскрыла кулак, чтобы показать маленький, блестящий красный драгоценный камень. Странно было использовать это в качестве приманки, но в данном случае это сработало."
        "Пытаясь найти очевидное решение, он снова надавил на пластину, но это было бесполезно."
        "Ловушка, должно быть, была нагружена пружиной или каким-то другим подобным механизмом, поскольку давление на нее никак не ослабляло кольцо, охватывающее руки."
        "Присмотревшись, он увидел на нижней стороне кольца маленькую замочную скважину."
        "Это имело смысл, поскольку тот, кто установил ловушку, в какой-то момент должен был ее снять. Достав из сапога маленький нож, он попытался вскрыть его."

        # Open Lock DC15
        #pass
        if check_skill(15, 'open_lock')[0]:
            "После нескольких минут возни внутри кольца Роуэн услышал  механизма, и замок открылся. Освободившись, Дриада встала и удивила мужчину крепкими объятиями."
            dry "Спасибо... Большое спасибо! Вы не знаете, каково это, когда один из фей чувствует себя в ловушке…"
            ro "Это не проблема, просто рад, что смог помочь."
            dry "Пожалуйста, возьми это..."
            "Она протянула ему красный камень; от него было мало толку, но он был уверен, что, продав его, сможет выручить за него кругленькую сумму."
            dry "...и я расскажу остальным о вас, чтобы Вы были в большей безопасности от них в будущем."
            "Они немного поболтали, а затем обменялись прощаниями. Роуэн двинулся дальше, а когда оглянулся, дух уже не отличался от остальных деревьев, стоявших позади него."
            #gain 20xp, 50 gold, and favour with the fey
            $ add_exp(20)
            $ change_treasury(50)
            $ change_favor('fey', 1)
            return
        #fail
        else:
            "Роуэн пытался в течение часа, но задача была выше его мастерства. Он крутил и крутил острие ножа, но тот, казалось, так и не нашел защелки, чтобы отпереть замок кольца."
            "В конце концов он понял, что может заниматься этим весь день и ни чего не сделать, и как бы ему не хотелось покидать дриаду, у него были свои задачи."
            "К чести, Фиби прекрасно понимала его, но он все еще чувствовал вину, когда оставил ее, боясь, что она будет беспомощна, когда тот, кто расставил ловушку, вернется и найдет ее в ней."
            #gain 5 xp, lose 1 move point, gain guilt
            $ add_exp(5)
            $ change_mp(-1)
            $ change_base_stat('g', 2)
            return
    "Воспользоваться ею." if avatar.corruption > 30:
        $ released_fix_rollback()
        #should probably require a certain level of corruption (TO DO)
        "Не говоря ни слова, Роуэн подошел сзади к прекрасному созданию, положив руки на ее стройные бока."
        dry "Эй, какого черта ты делаешь?"
        "Не обращая внимания на ее вопрос, он провел руками по ее тугой маленькой попке. Просунув свои ноги меж ее, он использовал свою силу нижней части тела, чтобы заставить их раздвинуться."
        "Она боролась с ним, пытаясь удержать их закрытыми, но ее шаткое положение не давало возможности, и ее легко было одолеть."
        dry "Я серьезно, тебе лучше прекратить это или.... иначе!"
        "Сейчас его не волновали пустые угрозы дриады, поскольку он создал доступ, который хотел."
        "Переместив одну из своих рук вниз ее спины и скользнул одним пальцем в ее удивительно тугую коричневую киску и начал поглаживать ее стены постепено проникая глубже."
        dry "Тебе это с рук не сойдет, человек!"
        "Резкий вдох, сопровождаемый тихим криком, означал, что герой нашел то, что искал. Скользнув еще одним пальцем внутрь, он удвоил свою атаку, пугая ее своей скоростью."
        "Ее сопротивление почти растаяло, сменившись ровным дыханием и тихими стонами. Вскоре пальцы Роуэн стали скользкими от ее липкой жидкости. Шлюха была готова к его члену."
        "Освободив его от брюк, он провел рукой по нему, чтобы покрыть его соками ее киски. Дразня, он терся им о ее отверстие, угрожая войти в нее, но каждый раз резко останавливаясь."
        dry "Ты ... ты ублюдок! Я...."
        scene cg141 with fade
        pause 3
        "Она не закончила фразу, Когда Роуэн скользнул своим членом глубоко внутрь нее, заставив ее издать низкий стон. Не теряя времени, он схватил ее за бедра и начал грубо трахать."
        "Поскольку фей по своей природе являются сексуальными существами, любые чувства, которые Дриада испытывала по поводу своего нынешнего затруднительного положения, были отброшены."
        "Она использовала все возможности, которые могла получить от своего нынешнего положения, чтобы встать удобно для него."
        "Потянувшись вперед, Роуэн грубо схватил и сжал задницу; маленькую, почти мальчишескую, но крепкую и задорную. Удивление заставило дриаду напрячься, сжав его член так, что это было почти невыносимо."
        scene cg141 with sshake
        scene cg141 with sshake
        scene cg142 with flash
        pause 3
        "Он почувствовал, как она вздрогнула, когда подошла, ее крики были такими громкими, что разнеслись по безмолвному лесу. Это ощущение было слишком сильным для него, и он вошел в нее жестко и глубоко."
        "Он вытащил свой член из нее, и выстрелил еще несколько струями спермы в воздух, прежде чем засунуть член обратно в штаны. В то время пока сперма сочилась из ее хорошо использованной пизды."
        dry "Самое меньшее, что ты можешь сейчас сделать, это освободить меня, придурок."
        "Роуэн усмехнулся и ушел, оставив дриаду в ловушке. Вслед ему раздавались проклятия на непонятном языке, но вскоре лес вновь обрел свою обычную тревожную тишину."
        #gain corruption, gain guilt, lose luck for 3 weeks, lose favour with the fey
        $ change_base_stat('c', 2)
        $ change_base_stat('g', 2)
        $ add_effect(MultiEffect("Cursed", 'pos', (('luck', 3),), 3))
        $ change_favor('fey', -1)
        return
    "Покинуть ее.":
        $ released_fix_rollback()
        "Роуэн слышала много историй о феях, и обычно они заканчивались тем, что какого-нибудь беднягу обманывали или очаровывали."
        "Лесной дух мог по-настоящему нуждаться в помощи, но он решил, что лучше всего ошибиться в сторону осторожности, оставив дриаду на кого-то другого."
        return

