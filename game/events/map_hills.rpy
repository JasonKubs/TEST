# random map events that happen on hills
init python:

    # Minor iron vein
    #can only occur in year one
    event('minor_iron_vein', triggers='map_expl', conditions=('world.cur_tile_type == "hills"', 'week <= 48'), run_count=1, group='map_expl', priority=pr_map_rnd)
    # The Shrine on the Hill
    event('shrine_on_hill', triggers='map_expl', conditions=('world.cur_tile_type == "hills"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #Slipped (malediction)
    #no requirements
    event('slipped', triggers='map_expl', conditions=('world.cur_tile_type == "hills"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #Friends in high places (worldbuilding)
    #No requirements.
    event('friends_in_high_places', triggers='map_expl', conditions=('world.cur_tile_type == "hills"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #### Hills Event: Harsh Valley ####
    event('harsh_valley', triggers='map_expl', conditions=('world.cur_tile_type == "hills"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #### Hills Event: The Bridge ####
    event('the_bridge', triggers='map_expl', conditions=('world.cur_tile_type == "hills"',), run_count=1, group='map_expl', priority=pr_map_rnd)


label minor_iron_vein:
# Minor iron vein
#can only occur in year one

scene bg43 with fade

"Во время осмотра этого холмистого района Роуэну посчастливилось наткнуться на открытую железную жилу. Это определенно стоило использовать."
"Он наметил маршрут, чтобы добраться до него, и сообщил подробности Изере, чтобы она могла организовать небольшую горную экспедицию."
"Операция будет настолько незначительной, что не потребуется никаких специальных мер, чтобы получить немного дополнительного железа в запас замка."
"Маленькие блага, подобные этому, могли изменить все, в первые дни восстановления Бладмина."
"Позже было бы маловероятно, что Роуэн будет беспокоиться об этом, поскольку незначительные выгоды просто не стоили бы этого."

#Increase iron income by 1.
$ castle.iron_per_week += 1
$ renpy.notify('Iron per week: +1')
#gain 10 xp
$ avatar.exp += 10
#end event
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label shrine_on_hill:
    # The Shrine on the Hill
    scene bg43 with fade
    $ released_block_rollback()
    # choose which gods the shrine is for
    $ shrine_on_hill_god = renpy.random.choice(('Solansia', 'Balast', 'Nerios', 'Nessa', 'Cessair', 'The Sisters'))
    'Солнце сияло, и небо было ярко-голубым, когда Роуэн начал свой подъем на холм, чуть больше двух часов назад, но сейчас когда он приблизился к вершине, казалось, что вот-вот пойдет дождь.'
    'То, что начиналось как неторопливая прогулка, становилось все более и более труднее, чем выше он поднимался на холм, уклон становился круче, а тропинка уступала место смеси мягкой земли и рыхлого камня.'
    "Он изо всех сил старался удержаться на ногах, так как возможность осыпи делала каждый шаг чуть более предательским, чем предыдущий."
    "И один неверный шаг, без сомнения, заставил бы его скатиться с холма."
    "На вершины, он чуть не поскользнулся, и ему пришлось встать на колени, чтобы удержаться на ногах."
    "В таком положении он заполз на вершину. И обнаружил, что стоит на удивительно плоской вершине холма."
    "Начался дождь, и прохладные капли легкой мороси были очень кстати после такого долгого и трудного подъема."
    "Здесь, наверху, воздух был свежим, и ему потребовалось больше, чем несколько вдохов, чтобы восстановить дыхание."
    'Впереди он увидел небольшое святилище, в котором в настоящее время не было ни одного просителя, что было удивительно, так как он прошел мимо еще нескольких путешественников, спускавшихся с вершины навстречу ему.'
    # TODO: update when researching will be available
    # show the god the shrine for
    if shrine_on_hill_god == 'Solansia ':
        $ released_fix_rollback()
#~         $ unlock_codex('solansia')
        'Роуэн улыбнулся, увидев впереди храм, шесть крыльев большой каменной статуи, стоящей в нем, были видны даже на расстоянии.'
        'Он знал, что это святилище Солансии, богини Света, которая создала мир, как убежище от внешней тьмы и даже сейчас защищает его от злых сил, которые могут причинить вред его обитателям.'
        "По сравнению с экстравагантностью Большого собора в Протее, это святилище было действительно очень скромным, но Роуэн был поражен чувством благоговения, стоя перед богиней."
        "Он был не один, повсюду вокруг статуи люди оставляли подношения."
        'Там было много золотых монет, и тот факт, что в святилище было много очень бедных посетителей, но монеты остались не тронутыми, показывал, насколько почитаема Солансия среди народа шести Королевств.'
        'Те, у кого не было монет, оставляли свои собственные маленькие сувениры: обрывки веревки, вату, пряди волос-все, что они могли дать.'
    elif shrine_on_hill_god == 'Balast':
        $ released_fix_rollback()
#~         $ unlock_codex('balast')
        "Когда Роуэн приблизился к святилищу, он увидел разбросанные по земле на вершине холма клинки разных размеров, кинжалы, мечи, сломанные наконечники копий и многое другое."
        "Все они больше не могли быть использованны, ржавые, сколоты, либо согнутые до такой степени, что остро нуждались в ремонте."
        'Они были оставлены в качестве дани вместе с частями искореженных пластин, кожей и сломанными кольчужными звеньями перед статуей, которую Роуэн узнал.'
        'Стоя на вершине алтаря, сделанного из тяжелой бронзы, он изображал человека в полном облачении, с закрытым лицом, с мечом в каждой руке. Это святилище Баласта, Повелителя клинков.'
        "Люди возносили молитвы во имя его накануне битвы, как о защите, так и о том, чтобы их собственные клинки были верны."
        " Вокруг статуи были монеты, оставленные в качестве подношений теми, кто надеялся заслужить благосклонность Бога."
    elif shrine_on_hill_god == 'Nerios':
        $ released_fix_rollback()
#~         $ unlock_codex('nerios')
        'Когда Роуэн приблизился к святилищу, он обнаружил, что он очень прост.'
        "Алтарь, так сказать, представлял собой старый деревянный ящик, начавший гнить от воздействия стихии, а на нем стояла простая черная чаша, до краев наполненная дождевой водой."
        "По бокам чаши белыми красками были нарисованы грубые изображения."
        "Волнистая линия изображала море, несомненно неспокойное, судя по кривизне линии, над ним было несколько лодок разных размеров."
        "Под линией была морская жизнь, включая одного довольно страшного зверя, который мог быть только Кракеном."
        "Это было святилище Нериоса, Владыки под волнами, которому обычно поклонялись пираты и более законопослушные моряки."
        "И судя по количеству золотых монет, которыми была усеяна верхняя часть ящика, он также нашел здесь некоторую популярность."
    elif shrine_on_hill_god == 'Nessa':
        $ released_fix_rollback()
#~         $ unlock_codex('nessa')
        'Первой мыслью Роуэна, увидев статую, покоящуюся на импровизированном алтаре, было то, что холм посреди пустыни-странное место, для святилища повелительницы теней.'
        'Он и раньше видел святилище Нессы, но оно находилось на углу переулка в самой бедной части Артдейла, где собиралось несколько мальчишек, просящих благословения, чтобы помочь в их злодеянии.'
        'Судя по тому, как фигура закуталась в плащ и спрятала лицо за маской с капюшоном, Несса была богиней, которой поклонялись воры, убийцы и все остальные, кто работал под покровом ночи.'
        'Вокруг лежало несколько куч золотых монет, оставленных в качестве подношений.'
        'Вероятно, подумал Роуэн, от тех, кто надеялся, что это приведет к возможности получить гораздо большую кучу монет в дальнейшем.'
    elif shrine_on_hill_god == 'Cessair':
        $ released_fix_rollback()
#~         $ unlock_codex('cessair')
        "На лице Роуэна отразилось удивление, когда он приблизился к святилищу. Он никак не ожидал найти здесь святилище матери скорби."
        "Эльфийские боги были редкостью в Розарии, да и в большинстве других королевств."
        'Эльфы, те из них, что еще остались, не хотели покидать святость великого леса Элоэна, так, что встреча с одним из них была чем-то вроде новизны.'
        'Он узнал Сессэйр из-за вуали, которую носила ее статуя, той самой, которую в эльфийских легендах она носила, чтобы прикрыть свои заплаканные глаза со дня раскола.'
        'Перед статуей лежало несколько разрозненных монет, а также коллекция других трагически выглядящих мелких предметов.'
        "Меч, отломившийся у рукояти, сломанная детская игрушка, прядь волос, обернутая вокруг простой железной ленты;"
        "Роуэн представил себе, что у каждого из них есть своя печальная маленькая история."
    # The Sisters
    else:
        $ released_fix_rollback()
#~         $ unlock_codex('the_sisters')
        'Любой мужчина или женщина в шести Королевствах узнал бы фигуры на алтаре перед Роуэном. Две женщины, одна светлая и одна темная, которые держали судьбу народа в своих руках.'
        'Обе обладали поразительной красотой, сестры управляли двумя полюсами удачи, которые одинаково влияли на жизнь людей и животных, хороших и плохих.'
        "Лейрен, более светлая из них двоих, была той, у которой люди искали благословения, в то время для Уулы, нужно было надеяться, что ты никогда не попадался ей на глаза вообще."
        "Это никогда не заканчивалось особенно хорошо для бедных людей, которыми она интересовалась."
        'Вокруг алтаря были разбросаны золотые монеты, которые несли сестрам с разных сторон, людьми, которые надеялись, что их удача изменится.'
    menu:
        'Помолиться в храме':
            $ released_fix_rollback()
            'Роуэн нуждался во всей помощи, которую он мог получить, поэтому он решил вознести тихую молитву [shrine_on_hill_god] в надежде, что он получит благословение.'
            'Покончив с подношением под дождем, он отвернулся от алтаря и направился вниз по склону в том направлении, откуда пришел.'
            if check_dc(7, 'luck'):
                if shrine_on_hill_god == 'Solansia':
                    $ avatar.base_luck += 1
                    $ avatar.base_corruption -= 2
                elif shrine_on_hill_god == 'Balast':
                    $ avatar.base_strength += 2
                elif shrine_on_hill_god == 'Nerios':
                    $ avatar.base_vitality += 2
                elif shrine_on_hill_god == 'Nessa':
                    $ avatar.base_reflexes += 2
                elif shrine_on_hill_god == 'Cessair':
                    $ avatar.base_intelligence += 2
                # The Sisters
                else:
                    $ avatar.base_luck += 2
        'Забрать подношения и уничтожить святыню':
            $ released_fix_rollback()
            'Убедившись, что вокруг никого нет, Роуэн собрал из алтаря как можно больше монет, прежде чем вытащить оружие и разбить его вдребезги.'
            'Закончив работу, он отвернулся от алтаря и направился вниз по склону холма в том направлении, откуда пришел.'
            $ avatar.base_corruption += 3
            $ change_personal_gold(dice(20, 2) + 10)
    # in any case, give exp reward
    $ add_exp(10)
    return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label slipped:
#Slipped (malediction)
#no requirements

scene bg43 with fade

"Внезапный порыв ветра застал Роуэна врасплох, заставив его быстро искать точку опоры, чтобы не упасть. Но его нога соскальзнула вместе с остальным телом."
"Короткий Утес казался ему детской забавой, после всех тех раз, когда он взбирался на стены замка, во много раз превышающие его высоту."
"Он был смонадеян, забыв быть более осторожным. Теперь он расплачивался за это."

#reflex test: DC12
$ temp1, temp2 = check_stat(12, 'reflexes')
$ released_fix_rollback()

if temp1:
    #Test reflexes: Success.
    "Мужчина раскинул руки в стороны, ища что-нибудь, за что можно ухватиться.  Он ухитрился ухватиться рукой за корень дерева,отчего его тело с силой ударилось о скалу."
    "От удара с его губ сорвалось проклятие. Это было чертовски больно, но гораздо лучше, чем могло бы быть."
    # 10 xp, small vitality injury for the rest of the week.
    $ add_exp(10)
    $ add_effect(Injury('Wounds', 'vitality', -1))
    return
elif temp2 >= 4:
    #Test reflexes: Partial failure (4-11).
    "Он пролетел мимо корня дерева, он попытался вытянуть руки, чтобы схватить его, но не успел. Инстинктивно он попытался приготовиться к удару и расслабил тело."
    "Вскоре после этого он поднялся с земли.  Он был разбит, покрыт синяками, но жив. Проклятие сорвалось с его губ, когда он посмотрел на утес, который победил его."
    "Он не сможет лазить по скалам теперь некоторое время."
    #5 xp, Vitality injury for three weeks.
    $ add_exp(5)
    $ add_effect(Injury('Fall damage', 'vitality', -2, 3))
    return
else:
    #Test reflexes: Total failure.
    "Он пролетел мимо корня дерева, он попытался вытянуть руки, чтобы схватить его, но был слишком медлителен."
    "Он попытался приготовиться к приземлению, которое скоро наступит, но его тело все еще было напряжено."
    "Громкий крик боли сопровождал глухой стук и влажный треск от удара его тела о землю. Он упал под странным углом и сломал одну ногу."
    "Только через час ему удалось подняться и вытащить из-под тела сломанную конечность."
    "Понимая, что дальше так дело не пойдет, Роуэн был вынужден связаться со своими хозяевами и потребовать возвращения в замок."
    "Похоже, его исследования на эту неделю были закончены."
    #End week's exploration. Vitality injury for three weeks.
    $ add_effect(Injury('Fall damage', 'vitality', -3, 3))
    jump end_map_exploration
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label friends_in_high_places:
#Friends in high places (worldbuilding)
scene bg43 with fade

"Роуэн с удивлением обнаружила маленький домик, окруженный стадом коз, высоко в горах, вдали от людей."
"Там он еще больше удивился, когда его встретил старый друг с войны."
"Герой был не единственным командиром, которого обстоятельства сделали из крестьянина."
"Пока он не присоединился к Данаре, два других 'грязных генерала'' были его единственными товарищами среди офицеров."
"В то время как Роуэн имел честь быть одним из шести героев, которые победили Карнаса, чтобы защитить его, двое других не сделали этого."
"Дворяне не любили нарушать естественный порядок вещей, они ненавидели угрозу, которую представляли для них грязные генералы."
"Один из них нашел свой путь в холмах и спрятался от остальной цивилизации, чтобы прожить свою жизнь отшельником, не желая участвовать в политике."
"Какое-то время они предавались воспоминаниям, вспоминая то, что делали во время войны."
"Никто из них не знал, что стало с их третьим товарищем."
# 5xp, end scene
$ add_exp(5)
$ codex_add('dirt_general_starting')
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label harsh_valley:
#### Hills Event: Harsh Valley ####
$ released_block_rollback()

scene bg43 with fade

"Пройдя через гористую местность, Роуэн оказался у подножия длинной долины, по обе стороны которой возвышались крутые скалистые вершины."
"Пастух, мимо которого он проходил накануне, предупредил его о свирепой погоде, которая может пронестись по таким долинам, заманивая в ловушку и убивая неподготовленных."
"За вершинами гор Роуэн видел громоздящиеся и угрожающе нависающие облака."
"И все же-сама природа ее враждебности и отдаленности, несомненно, делала эту территорию достойной изучения."
"Кто знает, что лежит дальше, не тронутое большинством падальщиков. Развалины гномов? Богатые железные руды? Заброшенные сокровища?"
"Роуэн должен был решить, стоит ли рисковать с этими тяжелыми тучами, собирающимися за горами, чтобы потратить время на изучение долины."

menu:
    "Исследовать.":
        $ released_fix_rollback()
        "Роуэн схватил свой рюкзак, подставил лицо ветру и зашагал вперед, внимание его было разделено между прочесыванием скал в поисках чего-нибудь интересного и облаками наверху."
        #dice roll - weather
        $ event_tmp['weather'] = 'weather holds' if dice(5) == 1 else 'weather breaks'
        #weather holds (20% chance)
        if event_tmp['weather'] == 'weather holds':
            "Погода в горах действительно переменчива, и не всегда к худшему. По мере того как день продвигался вперед, тучи, обещавшие метель, рассеялись под сияющим солнцем."
            "В такие дни воздух в горах подобен вину, и Роуэн с радостным сердцем иследовал долину."
            #go to loot rolls
            jump .loot_rolls
        #weather breaks (80% chance)
        else:
            "С наступлением дня вершины исчезли за серым одеялом, и толстые хлопья снега начали целовать лицо Роуэна."
            "Ветер дул с обратной стороны долины, и вскоре Роуэн поднял руку, защищаясь от сильной метели."
            "Прогресс обещал быть трудным."
            # survival check dc12
            #pass
            if check_skill(12, 'survival')[0]:
                "Однако Роуэн был достаточно опытен на открытом воздухе, чтобы выдержать метель."
                "Он держался той стороны долины, где было больше всего укрытий от шторма, и жался между камнями, разводя небольшие костры, всякий раз, когда чувствовал, что температура падает."
                "Сбитый с толку, но не испуганный, он двигался дальше."
                #go to loot rolls
                jump .loot_rolls
            #fail
            else:
                "Сквозь вздымающиеся снежные завесы стало невозможно ориентироваться на местности, и Роуэн заблудился в завывающем шторме."
                "Паника охватила его, когда он почувствовал, что пальцы на руках и ногах начинают неметь, и ему пришлось повернуться в единственном направлении, куда он мог пойти - туда, куда его толкал ветер."
                "После нескольких нескольких часов он вернулся в начало долины, живой, но измученный."
                #gain 1 wound, lose 3 move points
                $ take_damage(1)
                $ change_mp(-3)
                return
    "Отказатся.":
        $ released_fix_rollback()
        "Роуэн не был ни альпинистом, ни тем более знатоком этих вершин. Он не стал тратить драгоценное время на опасную, неизвестную долину и, отдохнув недолго, двинулся дальше."
        #no effect
        return

label .loot_rolls:
#loot rolls - choose one of the following at random
$ event_tmp['loot'] = dice(4)
if event_tmp['loot'] == 1:
    #1 - Nothing
    "К сожалению, несмотря на усердные часы, потраченные на обыск каждого закоулка в этом мрачном каменном коридоре, Роуэн не нашел ничего интересного."
    "Иногда, уныло размышлял он, возвращаясь тем же путем, что и пришел, места, которые кажутся необитаемыми с начала времен, действительно такие."
    #gain 10xp, lose 2 move points
    $ add_exp(10)
    $ change_mp(-2)
    return
elif event_tmp['loot'] == 2:
    #2 - Chest
    "В глубине долины Роуэн обнаружил груду обломков-похоже, это было все, что осталось от тяжело нагруженного фургона."
    "Как его владельцы так заблудились, и какой мрачный конец они встретили?"
    "Запечатанный сундук, наполовину погребенный среди гниющего дерева, привлек его внимание. Это была минутная работа, чтобы открыть его, и получить прекрасный образец мастерства внутри."
    "Более чем довольный своей добычей, Роуэн вернулся обратно."
    # gain 10 xp, random piece of armor gained, lose 2 move points
    $ add_exp(10)
    $ change_mp(-2)
    $ get_rnd_item(0, 500, 'armour')
    return
elif event_tmp['loot'] == 3:
    #3 - Orc Standard
    "Роуэну не потребовалось много времени, чтобы найти следы сражения, которое здесь велось: копья, воткнутые в каменистую землю, ржавые доспехи, разорванные останки орков и людей."
    "Расхаживая вокруг жалких, гниющих обломков, Роуэн мысленно видел, как стояли шахматные фигуры..."
    "...как командир-человек, должно быть, заставил батальон орков отступить в долину, чтобы не было спасения от последовавшей за этим резни."
    "Один предмет привлек его внимание. Высокий штандарт воткнутый в землю, его знамя давно исчезло, но грубые и мощные символы на его шесте все еще оставались нетронутыми и узнаваемыми."
    "Роуэн вытащил его и задумчиво осмотрел."
    "Возможно, для него это бесполезно, но орки под его командованием были бы рады иметь этот символ своего старого хозяина, чтобы маршировать под ним. Довольный своей добычей, Роуэн вернулся обратно."
    #gain 10 xp, gain morale, lose 2 move points
    $ add_exp(10)
    $ change_mp(-2)
    $ change_morale(5)
    return
else:
    #4 - Dwarven Ruin
    "В глубине долины Роуэн нашел то, что, как он подозревал, и должен был найти."
    "Квадратные Черные ворота, ведущие в гору, охранялись с обеих сторон статуями дварфов, гордых и суровых, несмотря на то, что они рассыпались в основании."
    "Роуэн хотел бы, чтобы группа опытных наемников была у него за спиной, чтобы должным образом исследовать развалины дварфов - была бы причина, почему они были оставлены."
    "Дварфы , как известно, копали глубоко и непредусмотрительно - поэтому он решил тщательно разведать только входные коридоры."
    "Этого было достаточно, чтобы обнаружить тайник из полированного топаза и искусно сделанное оружие, оставленное в караульном помещения."
    "Более чем довольный своей добычей, Роуэн вернулся обратно."
    #gain 10 xp, gain treasury, gain random weapon, lose 3 move points
    $ add_exp(10)
    $ change_mp(-3)
    $ change_treasury(20)
    $ get_rnd_item(0, 500, 'weapon')
    return


#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label the_bridge:
#### Hills Event: The Bridge ####
$ released_block_rollback()

scene bg43 with fade

#50% the bridge is safe, 50% it will not hold
$ temp = dice(2)

"Когда Роуэн взобрался на вершину холма, он не был готов к тому, что его ожидало."
"В середине вершины холма длинная трещина разделяла холм надвое, словно длинный шрам ландшафта."
"Он задавался вопросом, что могло бы сделать это, поскольку казалось очень маловероятным, что это естественно."
"Глядя вниз, в пропасть, не видя дна,и он мог только предполагать, что она слишком глубока для подъема."
"По краям он мог увидеть, что камень был оплавлен. Была ли это дуэль великого мага или битва между богами?"
"Теперь его мысли обратились к тому, как бы пересечь ее, чтобы не повернуть назад и не потерять еще больше времени."
"Разрыв был слишком велик, чтобы прыгать, в этом он был уверен."
"Пройдя по краю обрыва, он в конце концов наткнулся на старый веревочный мост."
"Судя по состоянию ветхости, прошли годы, а может быть, и десятилетия с тех пор, как кто-либо проводил здесь ремонт."
"Вопрос, стоявший перед ним сейчас, был прост, рискнуть мостом или повернуть назад?"

menu:
    "Пересечь мост.":
        $ released_fix_rollback()
        "Роуэн положил руку на веревку, которая подвешивала деревянные перекладины над пропастью, и она показалась ему достаточно прочной, несмотря на рваный вид."
        "Осторожно он начал пробираться по старому мосту, проверяя каждую деревянную перекладину, чтобы убедиться, что она выдержит его вес."
        #if the bridge is safe
        if temp == 1:
            "Несколько раз он был уверен, что прогнившая доска прогнется под ним, но не успел он опомниться, как снова оказался на твердой земле."
            "Поблагодарив богов за свою удачу, он спустился с другой стороны холма."
            #gain 10xp
            $ add_exp(10)
            return
        #if the bridge is unsafe
        else:
            "Все шло хорошо, пока Роуэн не добрался до середины моста. Он поставил ногу на одну из прогнивших досок, и она раскололась под ним пополам."
            "Все, что он мог сделать, это отпрянуть назад, чтобы не упасть. В этот момент он услышал треск веревки, когда остальная часть моста начала проваливаться."
            #reflect check dc12
            if check_stat(12, 'reflexes')[0]:
            #pass
                "Быстро соображая, Роуэн бросился вперед, чтобы ухватиться за веревку, когда она упала от него к противоположной стороне пропасти."
                "Приготовившись к удару, он согнул колени, чтобы поглотить удар, и перемахнул через щель, столкнувшись с дальней стеной."
                "Теперь, когда он добрался до другой стороны,он начал медленно подниматься по веревке, пока не выбрался на вершину утеса."
                "Экспедиция оказалась гораздо более рискованной, чем он надеялся, но, по крайней мере, он добрался до другой стороны и больше не будет терять времени."
                #gain 10xp
                $ add_exp(10)
                return
            #fail
            else:
                "Роуэн едва успел подумать, как схватился за ближайшую к нему веревку и качнулся назад, в ту сторону, откуда начал движение."
                "Боль пронзила его насквозь, когда он ударился спиной о скалу, и ему потребовалось все силы, чтобы удержаться за веревку и не упасть замертво."
                "Стиснув зубы, он медленно подтягивался вверх по веревке, пока снова не оказался там, откуда начал."
                "Не имея другого способа пересечь пропасть, все, что он мог сделать, это спустится с холма тем же путем, каким пришел, и надеяться, что повреждения его тела от этой неудавшейся попытки не окажутся серьезными."
                #gain 5xp, lose 3 move points
                $ add_exp(5)
                $ avatar.mp -= 3
                return
    "Вернуться тем же путем, каким пришел.":
        $ released_fix_rollback()
        "Решив, что мост выглядит слишком ветхим, чтобы рисковать его пересечь, Роуэн решил вернуться тем же путем, каким пришел."
        "Да, он потеряет много времени, но это все же лучше, чем потерять свою жизнь."
        #lose 3 move points
        $ avatar.mp -= 3
        return

