# random map events that happen on plains
init python:

    # Local guide (Benediction)
    event('local_guide', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # A minor noble's gift (choice)
    event('minor_noble_s_gift', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #Bandit attack (malediction)
    event('bandit_attack', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #Snake bite
    event('snake_bite', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Monster hunter team
    event('monster_hunter_team', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Blighted farm
    # Req: First half year, already hear about famine looming.
    event('blighted_farm', triggers='map_expl', conditions=('world.cur_tile_type == "plains"', "week <= 24"), depends=('famine_looms',), run_count=1, group='map_expl', priority=pr_map_rnd)
    # Site of occult ritual
    event('site_of_occult_ritual', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #Drakken highwaywoman (malediction)
    #No requirements
    event('drakken_highwaywoman', triggers='map_expl', conditions=('world.cur_tile_type == "plains"',), run_count=1, group='map_expl', priority=pr_map_rnd)
    #Roadside incident (judgement)
    #Req: Rowan has low corruption or is pure.
    event('roadside_incident', triggers='map_expl', conditions=('world.cur_tile_type == "plains"', 'avatar.corruption < 10'), run_count=1, group='map_expl', priority=pr_map_rnd)


label local_guide:
# Local guide (Benediction)

#countryside BG
scene bg31 with fade

"Путешествуя по равнинам розарии, Роуэн наткнулся на другого путешественника, судя по одежде, охотника. Мужчина тепло поздоровался с ним и спросил о его здоровье."
"Узнав имя Роуэна и подтвердив, что он действительно великий герой войны, охотник предложил помочь Роуэну всеми возможными способами."
"Роуэн принял его предложение, попросив помощи в навигации по местности и составлении карт региона."
"Советы и информация, которые он получил от своего гида, помогли Роуэну путешествовать по этой местности гораздо быстрее, чем он мог делать это раньше."

# +3 movement points (this turn only)
#~ $ movementPoints += 3
$ change_mp(3)
# +10 xp
$ add_exp(10)
return

###################################################################################################
###################################################################################################
###################################################################################################


label minor_noble_s_gift:
# A minor noble's gift (choice)

#countryside BG
scene bg31 with fade

"В этот день Роуэн случайно столкнулся с охотничьим отрядом какого-то мелкого дворянина."
"Лидер отряда почти сразу узнал героя и тепло приветствовал его."
"После недолгих раздумий Роуэн смог вспомнить лицо этого человека, как одного из офицеров Розарианской армии во время войны."
"Они правда, никогда не были представлены друг другу."
"Аристократ не возражал, что Роуэн не знает его имени, на самом деле он почти отказался даже назвать его, когда Роуэн спросил."
"Он настоял на том, чтобы подарить часть золота доставшееся с войны герою Розарии, поскольку Роуэн заслуживал этого гораздо больше, чем он сам."


menu:
    "Дипломатично откажитесь и похвалите его дом." if ('history_of_rosaria' in castle.completed_researches): #(Req: History of Rosaria)
        $ released_fix_rollback()
        "Зная этикет и историю Розарии, которыми вооружила его Клиона, Роуэн смог изящно отказаться от подарка, одновременно восхваляя этого человека за вклад его семьи во время войны."
        "Аристократ был невероятно впечатлен тем, что Роуэн знал, что он сделал, и пообещал рассказать всем о самоотверженности героя на службе своей стране."
        # - small amount of infamy
        $ change_base_stat('f', -2)
        # +10 xp
        $ add_exp(10)

    "Принять подарок и передать его Близнецам.":
        $ released_fix_rollback()
        "Не имея другого выбора, Роуэн принял подарок, чтобы не расстраивать аристократа."
        "Позже он доложил о случившемся Изере, которая была весьма довольна тем, что Роуэн хитростью добился финансирования их операций."
        # + small amount treasury and + small amount corruption
        $ change_treasury(10)
        $ change_base_stat('c', 2)
        # + small appoint Jezera opinion
        $ change_relation('jezera', 2)
        # +10 xp
        $ add_exp(10)

    "Принять подарок, но отдать его, прежде чем вернешься в замок.":
        $ released_fix_rollback()
        "Не имея другого выбора, Роуэн принял подарок, чтобы не расстраивать аристократа."
        "Однако позже он оставил деньги возле двери фермера. Когда фермер найдет их, он, без сомнения, найдет хорошее применение подарку неизвестного благодетеля."
        # - small amount corruption
        $ change_base_stat('c', 2)
        # +10 xp
        $ add_exp(10)
return

###################################################################################################
###################################################################################################
###################################################################################################


label bandit_attack:
#Bandit attack (malediction)

#countryside BG
scene bg31 with fade

"Бандиты!  В наши дни, путешествуя по Розарии, всегда есть риск подвергнуться нападению со стороны 'ne'er-do-wells'."
"*автор здесь применяет термин обозначающий молодых дворян, которые ради развлечения занимаются грабежами. "
"И герой, ставший демоническим агентом, теперь столкнулся именно с такой группой. У него не было другого выбора, он должен был сражатся с ними."


# Combat check (d20 + str mod)
#~ $ res_roll = dice(20) + avatar.stat_mod('strength')
$ event_tmp['combat roll'] = check_combat(11)[1]
$ released_fix_rollback()

if event_tmp['combat roll'] >= 11:
    #11+ - Pass (defeat them):
    "Конечно, такие враги не представляли особого труда для такого опытного воина, как Роуэн. Хотя даже такой, как он, не мог уйти совершенно невредимым."
    "Расправившись с ними, Роуэн передал бандитов войскам барона. В благодарность за эту услугу он получил их нечестно добытые доходы и позже отдаст их в замок Бладмин."
    "С другой стороны, быстро распространился слух, что герой осады Карста внес свой вклад в решение проблемы бандитов."
    #+15 xp
    $ add_exp(15)
    #Rowan is lightly wounded (lose 1 movement point until rest), + small amount treasury, - small amount infamy
    $ change_treasury(10)
    $ take_damage(1)
    $ change_base_stat('f', -2)

elif event_tmp['combat roll'] >= 6:
    #6-10 - Partial pass (fend them off):
    "Их было так много, что они едва не завалили Роуэна."
    "Только его мастерство и опыт воина удерживали их на расстоянии достаточно долго, чтобы они решили, что он не был целью, достойной охоты, и поспешно отступили."
    "Даже с его ранами, Роуэн все еще был в состоянии сбить одного из них и передать барону."
    "Вскоре распространился слух, что герой Карста отправил домой какого-то бандита с поджатым хвостом."
    #+10 xp
    $ add_exp(10)
    #Rowan is wounded (lose 3 movement points until rest), - smaller amount of infamy.
    $ take_damage(3)
    $ change_base_stat('f', -1)

else:
    #5 or less - Fail (captured and ransomed):
    "Даже такой опытный воин, как Роуэн, должен остерегаться излишней самоуверенности, когда его превосходят."
    "В этот день он вспомнил об этом, когда обнаружил, что разбит и захвачен бандитами."
    "Они оттащили его избитое и покрытое синяками тело в свое убежище, где Роуэна держали взаперти, пока Изера не смогла выкупить его у бандитов с помощью средств из их казны."
    #+5 xp
    $ add_exp(5)
    #Rowan is wounded (lose 3 movement points until rest), - small amount treasury, end turn regardless of remaining moves.
    $ take_damage(3)
    $ change_treasury(-10)
    # TODO: make castle.treasury a limited property
    # $ if castle.treasury < 0: castle.treasury = 0
    $ renpy.set_return_stack(renpy.get_return_stack()[:1])
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label snake_bite:
#Snake bite

# Show countryside background
scene bg31 with fade
show rowan necklace shock behind bg31

"Прогуливаясь по равнинам Розарии, человек должен стараться остерегаться зеленых змей."
"Их укусы, не смертельны для большинства людей, но они причиняют адскую боль, а действие их яда вызывает раздражительность и затрудняет сон на некоторое время."
"К сожалению, обнаружить этих существ практически невозможно из-за их маскировки кожи. Однако, зеленая змея обычно никого не кусает, если только на нее не наступить."
"И иногда тебе просто не везет."

ro "ААА!  Черт возьми, гринснейп!"

# add 10 xp
# End scene, Rowan is injured, -reflex poison.
$ add_exp(10)
$ add_effect(Injury('Greensnap poison', 'reflexes', -2))
return
#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label monster_hunter_team:
# Monster hunter team

# Show countryside background
scene bg31 with fade

"Розария является домом известной гильдии охотников на монстров."
"Отсюда, почти все самые могучие убийцы являются членами или, по крайней мере, начинали начинали здесь."
"Команды гильдии являются довольно частым явлением в некоторых районах, где присутствуют заражения или вторжение других существ на земли человека."
"Сегодня Роуэн столкнулся с одной такой группой. Они мгновенно узнали героя таким, каким он был. Как это часто бывает, его репутация бежала впереди него."

if avatar.infamy < 10:
    # Test low infamy: Pass
    "История героя Карнаса одержали сегодня победу над истории про агента близнецов, и его встретили как друга и кумира."
    "Поскольку охотники возвращались с удачного задания, они предложили Роуэну свои оставшиеся зелья в качестве небольшого подарка."
    # End scene: add 10 xp, Rowan gains some minor stat bonuses for a couple weeks.
    $ add_exp(10)
    $ add_effect(MultiEffect('Potions bonuses', 'pos', (('strength', 1), ('reflexes', 1), ('vitality', 1)), 5))
    return
else:
    # Test low infamy: Fail
    "Рассказы о зверствах, которые Роуэн была вынужден совершить, становились все сильнее."
    "Настолько, что группа фактически решила напасть на Роуэна, очевидно, надеясь либо победить зло, либо получить какую-то награду."
    if check_skill(10, 'escape')[0]:
        $ renpy.fix_rollback()
        # Escape challenge: Success
        "Так как он был в меньшинстве против опытных врагов, агент близнецов решил сбежать, а не встретиться с группой лицом к лицу."
        # End scene, add 10 xp, no effect.
        $ add_exp(10)
        return
    else:
        $ renpy.fix_rollback()
        # Escape challenge: Fail
        "Когда их превосходят по численности опытные враги, лучший способ действия-бежать. Агент близнеца знал это, как и его противники."
        "Только после затяжной оборонительной схватки Роуэн наконец нашел лазейку и сумел ускользнуть."
        "Он получил несколько травм во время боя и был вынужден убить одного из охотников. Это никак не поможет его репутации."
        # End scene, add 5 xp, +infamy, Rowan is injured.
        $ add_exp(5)
        $ change_base_stat('f', 2)
        $ add_effect(Injury('Wounds', 'strength', -2))
        return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label blighted_farm:
# Blighted farm
# Req: First half year, already hear about famine looming.

# Show countryside background
scene bg31 with fade

"Еще одна заброшенная ферма. За последние несколько дней Роуэн уже нашел несколько таких в этом районе."
"Он решил взглянуть поближе и посмотреть, сможет ли он установить причину этого."
"Проверив два из них, он решил, что у него есть одна идея, почему."
"Все амбары были пусты, так что еды не осталось. Вдобавок ко всему, посевы на полях были покрыты странными пятнами, какой-то болезни."
"Он сделал еще несколько исследований и обнаружил, что та же самая чума растений была ответственна за плохой урожай в прошлом году."
"С голодом на горизонте, урожай года, уже выглядящий чрезвычайно бедным, не предвещал ничего хорошего для людей здесь."
"Фермеры покидали свои земли, чтобы стать беженцами, надеясь найти еду в другом месте."
# add 10 xp
# end scene.
$ add_exp(10)
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label site_of_occult_ritual:
# Site of occult ritual
# No req.

# Show countryside background
scene bg31 with fade

"Следы пожара, странные царапины на земле, два обескровленных игниющих трупа животных."
"Сначала Роуэн подумал, что он нашел место лагеря, но чем больше он смотрел, тем больше убеждался, что это что-то другое."
"Это не был ритуал Солансии, это было либо что-то рожденное оккультным суеверием, либо очарование хаоса."
"Если только он не найдет кого - то более настроенного на магию, чем он сам, то не будет никакого способа узнать наверняка, произошло ли здесь что-то большее, чем ритуальный танец."
"Однако сам факт того, что люди прибегали к подобным вещам на его родине, тревожил героя."
"Он будет следить за новыми признаками оккультизма и поклонения хаосу, пока его разведка продолжится."
# add 10 xp
# End scene.
$ add_exp(10)
return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label drakken_highwaywoman:
#Drakken highwaywoman (malediction)
#No requirements

#Show countryside background.
scene bg31 with fade

"Одна из самых малоизвестных рас в Розарии - драккены, потомки людей и драконов."
"Случайно наткнуться на одного из них на дороге-это почти неслыханно. А тот, который превратился в бандитита, было еще более редким."
"Роуэн только что столкнулся лицом к лицу с одним из таких разбойников. Или разбойницей с большой дороги, так сказать."
"Она потребовала у него деньги, угрожая силой забрать их, если он не подчинится. Очень распространенное требование от преступников, но ни в коем случае не общее требование."

#If treasury has less than 30 gold.
if castle.treasury < 30:
    $ released_block_rollback()
    "Герой посмеялся над ней и небрежно сообщил, что она выбрала плохую цель для ограбления, так как у него не было денег."
    "Он не стал объяснять, что казна настолько мала, что он просто не может позволить себе носить ее с собой."
    "Он даже позволил ей пощупать себя, несколько тщательно, пока она, наконец, не ушла с раздражениеи. Роуэну немного повезло, что она украла у него только время."
    #End scene. 5xp,  no effect.
    $ add_exp(5)
    return

#else
else:
    "Это будет нелегкая битва, герой должен быть в отличной форме, чтобы сразиться с драккеном в честном бою."
    menu:
        #Choice: Pay her 30 gold.
        'Заплатить ей 30 золотых':
            $ released_fix_rollback()
            "Он решил, что не стоит рисковать в бою, и отдал деньги, которые нес с собой."
            "Довольная 'подарком', драконья кровь улыбнулась и послала Роуэну воздушный поцелуй, прежде чем улететь."
            #End scene. 10xp, lose 30 treasury.
            $ add_exp(10)
            $ change_treasury(-30)
            return
        #Choice: Fight her.
        'Сразится с ней':
            $ released_fix_rollback()
            $ temp1 = current_weapon()
            "Роуэн вытащил свой [temp 1], готовясь сразиться с драконьей кровью.  Прежде чем началась их битва, он попытался разыграть карту героя, чтобы заставить ее отступить."
            "Ответом было то, что быть великим героем означало только то, что он был на вершине кучи человеческой грязи, но все еще был человеческой грязью."

            #combat DC13
            #Fight challenge, success:
            if check_combat(13)[0]:
                $ released_fix_rollback()
                "Драккен оказалась чересчур самоуверенной. Она стремилась быстро одолеть своего противника одной лишь силой, которую Роуэн быстро сумел обратить против нее."
                "Вскоре последовал сильный удар о землю и большая рана на ноге."
                "Осознав свою глупость, крылатая женщина поспешно отступила, улетая от того, что считала легкой мишенью всего несколько мгновений назад."
                "Роуэн проводил ее взглядом, потом вытер и убрал меч."
                #End scene.  20xp.
                $ add_exp(20)
                return
            #Fight Challenge, failure:
            else:
                $ released_fix_rollback()
                "Драккен набросилась на него с удвоенной силой. Она стремилась быстро одолеть своего противника с помощью чистой силы, которую Роуэн обычно мог бы обратить против."
                "Однако в этот день он был не в лучшей форме."
                "Она просто опрокинула его попытку опрокинуть ее на землю и впилась когтями в его тело."
                "Все было кончено в одной атаке, когда крылатая женщина торжествующе оседлала своего врага."
                "Разорвав его рюкзак и стащив тридцать золотых монет, которые были ее целью, она поцеловала его в губы и улетела."
                "Роуэн смотрел ей вслед, потом попытался встать, чтобы перевязать раны, которые она оставила ему. Все прошло не очень хорошо."
                #End scene. 10xp, rowan is injured.
                $ add_exp(10)
                $ add_effect(Injury('Wounds', 'strength', -1))
                return

#############################################################################################################################################################
#############################################################################################################################################################
#############################################################################################################################################################


label roadside_incident:
#Roadside incident (judgement)
#Req: Rowan has low corruption or is pure.

#Show countryside background.
scene bg31 with fade

"На одной из боковых дорог, пересекающих равнину, Роуэн столкнулся с ДТП."
"Две повозки, запряженные лошадьми, столкнулись друг с другом, и их владельцы вступили в жаркий спор."
"Эти двое были очень взволнованы, и, увидев Роуэна, один из них попросил его выступить посредником в их споре, потому что он был чужаком и героем."
"Один считал, что другой должен заплатить за причиненный ущерб, другой не считал это очень справедливым."
"Немного осмотрев место происшествия и выслушав обе истории, Роуэн понял, что виноват был человек, пытавшийся избежать возмещения ущерба."
"Не было никаких причин, кроме неосторожности или злонамеренности, которые могли бы вызвать столкновение."
"Однако незадолго до того, как он принял решение, виновный спокойно сказал ему, что он готов отдать герою половину денег, которые ему придется заплатить, если решение будет принято в его пользу."
"*Что то мне все больше и больше, Розария напоминает одну страну."
menu:
    #Choice: Accept the bribe and side with the man at fault.
    'Принять взятку и встать на сторону виновного человека':
        $ released_block_rollback()
        "После вынесения решения и наблюдения за тем, как один торговец унесся прочь, разглагольствуя о том, что его обманули, Роуэн принял оплату от другого."
        "Он чувствовал себя виноватым, но ему нужно было собрать деньги, чтобы защитить себя и свою жену."
        #End scene.  Gain 10xp, 30 gold, gain little guilt, gain little corruption, gain little infamy.  This is nowhere near as bad as attacking villages.
        $ add_exp(10)
        $ change_treasury(30)
        $ change_base_stat('g', 1)
        $ change_base_stat('f', 1)
        $ change_base_stat('c', 1)
        return
    #Choice: Side with the man in the right.
    'Принять справедливое решение':
        $ released_block_rollback()
        "Чувство справедливости Роуэна не мог поколебать мешочек с монетами, он встал на сторону пострадавшего."
        "Другой торговец обозвал его дураком, прежде чем расплатиться с другим и умчаться прочь."
        "Человек, которого обидели, похвалил героя за его высокую мораль, узнав, что его попытались подкупить его."
        "Он назвал его хорошим другом и сказал, что все, кого он встретит, услышат об этом правосудии."
        #End scene. Gain 10xp.  Lose a little corruption, lose a little infamy.
        $ add_exp(10)
        $ change_base_stat('c', -1)
        $ change_base_stat('f', -1)
return
